
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Автор			= ПараметрыСеанса.глТекПользователь;
		ДатаСоздания	= ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_();
	Иначе
		АвторИзменения	= ПараметрыСеанса.глТекПользователь;
		ДатаИзменения	= ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_(); 
	КонецЕсли;	
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	Если Не ЭтоНовый()
		 И ЭтотОбъект.ДополнительныеСвойства.Свойство("ДанныеМодифицированы")
		 И ЭтотОбъект.ДополнительныеСвойства.ДанныеМодифицированы
		 И Справочники.Аук_Аукционы.ТребуетсяЗаписьВИсторию(Ссылка)
	Тогда
			
		Справочники.Аук_Аукционы.СоздатьСнимокТекущегоАукциона(Ссылка.ПолучитьОбъект());
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.ЭтоНовый И Не ЭтотОбъект.ДополнительныеСвойства.Свойство("ЭтоСнимок") Тогда
		сткПараметрыЗаписи = Новый Структура;
		сткПараметрыЗаписи.Вставить("Период",	ТекущаяДатаСеанса());
		сткПараметрыЗаписи.Вставить("Аукцион",	Ссылка);
		сткПараметрыЗаписи.Вставить("Статус",	ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.Новый"));
		
		сткРез = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ДобавитьЗапись(сткПараметрыЗаписи);
		
		Если Не сткРез.Успех Тогда
			ВызватьИсключение сткРез.Ошибка;	
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти


