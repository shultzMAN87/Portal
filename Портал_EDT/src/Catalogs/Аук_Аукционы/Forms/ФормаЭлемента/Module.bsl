
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальнаяИнициализация();
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЗаполнитьСписокВыбораУчастников();
	
	НастроитьИнтерфейсПоУсловиям();
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Если включена автоматика (хоть один чекбокс) то делаем весь перечень проверок 
	// Если автоматика выключена, тогда проверки будут только при старте!!!
	
	//Если ПроверитьЗаполнение() И Модифицированность Тогда	
		ПерезаполнитьНачальныеДанныеОбъектаИспользуяДанныеФормы();
		
	//	Если Аук_ОбщийКлиентСервер.АвтоматическийРежим(Объект) Тогда
	//		
	//		сткДанныеФормы = Аук_ОбщийКлиентСервер.ДанныеАукционаДляПроверки(Объект);
	//		
	//		Оповещение = Новый ОписаниеОповещения("ПроверкаПередЗакрытием_Продолжить", ЭтотОбъект);
	//		
	//		Аук_ОбщийКлиент.ВыполнитьПроверкиНастроекАукциона(сткДанныеФормы, Оповещение, Отказ);
	//		
	//	КонецЕсли;	
	//КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ДанныеМодифицированы", Модифицированность);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЭтотОбъект.КоличествоСнимков = Справочники.Аук_Аукционы.КоличествоСнимковТекущегоАукциона(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьГиперссылкуИсторииИзменений();
	
	ОбновитьГиперссылкиАвторов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьКонтактныеЛица" Тогда
		
		масСтрокиТч = Объект.Участники.НайтиСтроки(Новый Структура("Участник", Параметр));
		
		Аук_ОбщийКлиентСервер.ОбновитьОтборКонтактныхЛицДляВсехУчастников(масСтрокиТч, Объект.ВидАукциона);
			
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура ВидАукционаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	сткРез = ПроверитьЗаполнениеДанныхПередИзменениемВидаАукциона();
	
	Если Не сткРез.Успех Тогда 
		
		Оповещение = Новый ОписаниеОповещения("ВидАукционаНачалоВыбораВопрос_Продолжить", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, сткРез.Результат, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));
		
	Иначе	
		
		ОткрытьФормуВыбораВидаАукциона();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидАукционаНачалоВыбораВопрос_Продолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ОткрытьФормуВыбораВидаАукциона();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидАукционаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	//СтандартнаяОбработка = Ложь;
	
	ОбработкаВыбораВидаАукциона(СтандартнаяОбработка, ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидАукционаОчистка(Элемент, СтандартнаяОбработка)
	ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами = Новый ФиксированноеСоответствие(Новый Соответствие);
	
	ОбработкаВыбораВидаАукциона(СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура тзНачальныеДанныеДинамическаяПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	//Если НоваяСтрока Тогда
		НовСтрока = ЭтотОбъект.тзНачальныеДанныеДинамическая.НайтиПоИдентификатору(Элемент.ТекущаяСтрока);
		
		Для Каждого ЭлСоо Из ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами Цикл
			НовСтрока["Характеристика" + ЭлСоо.Ключ] = ЭлСоо.Значение;	
		КонецЦикла;
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тзНачальныеДанныеДинамическаяПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура тзНачальныеДанныеДинамическаяУчастникНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элементы.тзНачальныеДанныеДинамическая.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Ссылка", СписокУникальныхУчастников()));
		
		Оповещение = Новый ОписаниеОповещения("НачальныеДанныеВыборУчастника_Продолжить", ЭтотОбъект, ТекДанные);
		
		ОткрытьФорму("Справочник.Аук_УчастникиАукционов.ФормаВыбора", ПараметрыФормы, ЭтотОбъект,,,, Оповещение);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НачальныеДанныеВыборУчастника_Продолжить(Результат, ТекДанные) Экспорт

	Если Результат <> Неопределено Тогда
		
		ТекДанные.Участник = Результат;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиИКонтактыУчастникПриИзменении(Элемент)
	
	ОбновитьОтборКонтактныхЛиц();	
			
КонецПроцедуры

&НаКлиенте
Процедура УчастникиИКонтактыУчастникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	ТекДанные = Элементы.УчастникиИКонтакты.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И ТекДанные.Участник <> ВыбранноеЗначение Тогда 
		
		ТекДанные.КонтактноеЛицо = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаИсторияИзмененийНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура("ТекущийАукцион", Объект.Ссылка));
	
	ОткрытьФорму("Справочник.Аук_Аукционы.ФормаСписка", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиИКонтактыКонтактноеЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	сткОтбор = Новый Структура("КонтактноеЛицо", ВыбранноеЗначение); 
	
	Если Объект.Участники.НайтиСтроки(сткОтбор).Количество() <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница.Имя = "ГруппаНачальныеДанные" Тогда
		
		ЗаполнитьСписокВыбораУчастников();
		
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура УчастникиИКонтактыКонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекДанные = Элементы.УчастникиИКонтакты.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("МожноСоздаватьЭлементы", Ложь);
		
		НастройкиКД = Новый НастройкиКомпоновкиДанных;
		
		ЭлементОтбора = НастройкиКД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ПравоеЗначение	= ТекДанные.КонтактныеЛицаОтбор;
		ЭлементОтбора.Использование		= Истина; 
		
		ПараметрыФормы.Вставить("ФиксированныеНастройки", НастройкиКД);
		
		Оповещение = Новый ОписаниеОповещения("КонтактноеЛицоВыбор_Продолжить", ЭтотОбъект, ТекДанные);
		
		ОткрытьФорму("Справочник.Аук_КонтактныеЛицаУчастниковАукционов.ФормаВыбора", ПараметрыФормы, Элемент,,,, Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоВыбор_Продолжить(Результат, пТекДанные) Экспорт 
	
	Если Результат <> Неопределено Тогда
		
		сткОтбор = Новый Структура("КонтактноеЛицо", Результат); 
		
		Если Объект.Участники.НайтиСтроки(сткОтбор).Количество() = 0 Тогда
			
			пТекДанные.КонтактноеЛицо = Результат;
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиИКонтактыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЭтотОбъект.БлокироватьИзмененияДанных Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекДанные = Объект.Участники.НайтиПоИдентификатору(ВыбраннаяСтрока);
		
		Если Поле.Имя = "УчастникиИКонтактыУчастник" Тогда
			
			ПоказатьЗначение(, ТекДанные.Участник); 
			
		ИначеЕсли Поле.Имя = "УчастникиИКонтактыКонтактноеЛицо" Тогда 
			
			ПоказатьЗначение(, ТекДанные.КонтактноеЛицо);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура УстановитьТекущееВремя(Команда)
	
	Объект.ДатаНачала = НачалоМинуты(ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_() + 60);
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьУчастниковПоВиду(Команда)
	ЗаполнитьУчастниковПоВиду();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНачальныеДанныеПоУчастникам(Команда)
	ЗаполнитьНачальныеДанныеПоСпискуУчастников();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрытьКастомная(Команда)
	ПроверитьДанныеПередЗаписью(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьКастомная(Команда)
	ПроверитьДанныеПередЗаписью(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура ПроверитьДанныеПередЗаписью(пБулЭтоЗакрытие)
	// Если включена автоматика (хоть один чекбокс) то делаем весь перечень проверок 
	// Если автоматика выключена, тогда проверки будут только при старте!!!
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда	
		//ПерезаполнитьНачальныеДанныеОбъектаИспользуяДанныеФормы();
		
		Если Аук_ОбщийКлиентСервер.АвтоматическийРежим(Объект) Тогда
			
			сткДанныеФормы = Аук_ОбщийКлиентСервер.ДанныеАукционаДляПроверки(Объект);
			
			Оповещение = Новый ОписаниеОповещения("ПроверкаПередЗаписью_Продолжить", ЭтотОбъект, пБулЭтоЗакрытие);
			
			Аук_ОбщийКлиент.ВыполнитьПроверкиНастроекАукциона(сткДанныеФормы, Оповещение);
		Иначе	
			НачатьЗаписьИЗакрыть(пБулЭтоЗакрытие);			
		КонецЕсли;
	Иначе
		НачатьЗаписьИЗакрыть(пБулЭтоЗакрытие)	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаписьИЗакрыть(пБулЭтоЗакрытие)
	Записать();
	
	Если пБулЭтоЗакрытие Тогда
		Закрыть();	
	КонецЕсли;	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверкаПередЗаписью_Продолжить(Результат, пБулЭтоЗакрытие) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		//Модифицированность = Ложь;
		
		НачатьЗаписьИЗакрыть(пБулЭтоЗакрытие);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНачальныеДанныеПоСпискуУчастников()
	масУчастники = Аук_ОбщийКлиентСервер.МассивУникальныхЗначенийИзКолонкиТаблицы(Объект.Участники, "Участник", Истина);
		
	Аук_ОбщийКлиент.ЗаполнитьТаблицуДаннымиМассиваСтруктур(ЭтотОбъект.тзНачальныеДанныеДинамическая, масУчастники, "Участник");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУчастниковПоВиду()
	Если ЗначениеЗаполнено(Объект.ВидАукциона) Тогда
		масУчастникиПоВиду = Аук_ОбщийВызовСервера.УчастникиАукционаИИхКонтактныеЛицаПоВидуАукциона(Объект.ВидАукциона);
		
		Аук_ОбщийКлиент.ЗаполнитьТаблицуДаннымиМассиваСтруктур(Объект.Участники, масУчастникиПоВиду, "Участник, КонтактноеЛицо");
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Заполните вид аукциона'"));	
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтборКонтактныхЛиц()
	
	ТекДанные = Элементы.УчастникиИКонтакты.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И ЗначениеЗаполнено(ТекДанные.Участник) Тогда
		
		Если ЗначениеЗаполнено(Объект.ВидАукциона) Тогда	
			
			сооКонтактныеЛицаУчастника = Аук_ОбщийВызовСервера.КонтактныеЛицаУчастникаПоВидуАукциона(ТекДанные.Участник, Объект.ВидАукциона);
			
			Аук_ОбщийКлиентСервер.ЗаполнитьКонтактныеЛицаОтборПоСтроке(ТекДанные, сооКонтактныеЛицаУчастника);
			
		КонецЕсли;	
	
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаВыбораВидаАукциона(пСтандартнаяОбработка, пНовыйВидАукциона = Неопределено)
	
	Если ЗначениеЗаполнено(пНовыйВидАукциона) И Объект.ВидАукциона <> пНовыйВидАукциона Тогда 
		
		сткРез = ПроверитьЗаполнениеДанныхПередИзменениемВидаАукциона();
		
		Если Не сткРез.Успех Тогда
			
			пСтандартнаяОбработка = Ложь; 
			
			Оповещение = Новый ОписаниеОповещения("ВидАукционаОбработкаВыбораВопрос_Продолжить", ЭтотОбъект, пНовыйВидАукциона);
			
			ПоказатьВопрос(Оповещение, сткРез.Результат, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));
			
		Иначе	
			
			ОбновитьИнтефейсПриИзмененииВидаАукциона(пНовыйВидАукциона);
			
		КонецЕсли;
		
	ИначеЕсли Не ЗначениеЗаполнено(пНовыйВидАукциона) Тогда	
		масТчНаОчистку = Новый Массив;
		Если Объект.Участники.Количество() <> 0 Тогда
			масТчНаОчистку.Добавить("""Участники""");	
		КонецЕсли;
		
		Если ЭтотОбъект.тзНачальныеДанныеДинамическая.Количество() <> 0 Тогда // тч которая на форме это не ТЧ которая объекта
			масТчНаОчистку.Добавить("""НачальныеДанные""");	
		КонецЕсли;
		
		Если Объект.Этапы.Количество() <> 0 Тогда
			масТчНаОчистку.Добавить("""Этапы""");	
		КонецЕсли;
		
		Если масТчНаОчистку.Количество() <> 0 Тогда
			пСтандартнаяОбработка = Ложь; 
			
			Оповещение = Новый ОписаниеОповещения("ВидАукционаОбработкаВыбораВопрос_Продолжить", ЭтотОбъект, пНовыйВидАукциона);
			
			стрВопрос = СтрШаблон(НСтр("ru = 'Табличные части %1 будет очищена. Продолжить?'"), СтрСоединить(масТчНаОчистку, ", "));
			
			ПоказатьВопрос(Оповещение, стрВопрос, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));		
		Иначе	
			ОбновитьИнтефейсПриИзмененииВидаАукциона(пНовыйВидАукциона);	
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеДанныхПередИзменениемВидаАукциона()
	
	ПустКонтактныеЛицаКол	= Объект.Участники.НайтиСтроки(Новый Структура("КонтактноеЛицо", ПредопределенноеЗначение("Справочник.Аук_КонтактныеЛицаУчастниковАукционов.ПустаяСсылка"))); 
	УчастникиКол			= Объект.Участники.Количество();
	НачальныеДанныеКол		= ЭтотОбъект.тзНачальныеДанныеДинамическая.Количество(); // тч которая на форме это не ТЧ которая объекта
	
	Если (УчастникиКол <> 0 И УчастникиКол <> ПустКонтактныеЛицаКол) И НачальныеДанныеКол <> 0 Тогда
		
		стрТекстВопроса = НСтр("ru = 'При изменении вида аукциона начальные данные будут очищены, таблица перестроена, перезаполнятся участники. Продолжить?'");
		
	ИначеЕсли УчастникиКол <> 0 И УчастникиКол <> ПустКонтактныеЛицаКол Тогда 
		
		стрТекстВопроса = НСтр("ru = 'При изменении вида аукциона перезаполнятся участники. Продолжить?'");
		
	ИначеЕсли НачальныеДанныеКол <> 0 Тогда 
		
		стрТекстВопроса = НСтр("ru = 'При изменении вида аукциона начальные данные будут очищены, таблица перестроена. Продолжить?'");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(стрТекстВопроса) Тогда
		
		Возврат ЭдоОбщий._Ошибка(стрТекстВопроса);
		
	Иначе
		
		Возврат ЭдоОбщий._Успех();
		
	КонецЕсли;	
	
КонецФункции	

&НаКлиенте
Процедура ВидАукционаОбработкаВыбораВопрос_Продолжить(Результат, ВыбранноеЗначение) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
				
		ОбновитьИнтефейсПриИзмененииВидаАукциона(ВыбранноеЗначение);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтефейсПриИзмененииВидаАукциона(пНовыйВидАукциона)
	
	ЭтотОбъект.тзНачальныеДанныеДинамическая.Очистить(); // тч которая на форме это не ТЧ которая объекта
	
	ОбновитьИнтефейсПриИзмененииВидаАукционаНаСервере(Объект.ВидАукциона, пНовыйВидАукциона);
	
	УстановитьВидимостьВкладкиНачальныхДанных();
	
	Объект.ВидАукциона = пНовыйВидАукциона;
	
	Объект.Участники.Очистить();
		
	Аук_ОбщийКлиентСервер.ОбновитьОтборКонтактныхЛицДляВсехУчастников(Объект.Участники, Объект.ВидАукциона);
	
	Если Не ЗначениеЗаполнено(Объект.ВидАукциона) Тогда
		Объект.Этапы.Очистить();	
	КонецЕсли;	
	
	Модифицированность = Истина;
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.ВидАукциона;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораВидаАукциона()

	Оповещение = Новый ОписаниеОповещения("ВыборВидаАукциона_Продолжить", ЭтотОбъект);
		
	ОткрытьФорму("Справочник.Аук_ВидыАукционов.ФормаВыбора",, ЭтотОбъект,,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборВидаАукциона_Продолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ОбновитьИнтефейсПриИзмененииВидаАукциона(Результат);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьИнтерфейсПоУсловиям()
	
	ОбновитьГиперссылкуИсторииИзменений();
	
	ОбновитьГиперссылкиАвторов();
	
	Если ЭтотОбъект.БлокироватьИзмененияДанных Или ЗначениеЗаполнено(Объект.ТекущийАукцион) Тогда
	
		Аук_ОбщийКлиент.УставновитьРежимТолькоПросмотрУВсехЭлементов(ЭтотОбъект);
		
		Аук_ОбщийКлиент.УстановитьВидимостьКоманд(ЭтотОбъект);
	
	ИначеЕсли ЭтотОбъект.ТекущийСтатус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.НовыйЭтап")
		      Или ЭтотОбъект.ТекущийСтатус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов")
	Тогда		  
	
		Элементы.ВидАукциона.ТолькоПросмотр = Истина;
		
		Аук_ОбщийКлиент.УставновитьРежимТолькоПросмотрУВсехЭлементов(Элементы.ГруппаДатаИВремяНачала);
		
		Аук_ОбщийКлиент.УставновитьРежимТолькоПросмотрУВсехЭлементов(Элементы.ГруппаНастройкаАвтоматическогоПроведения);
		
		Элементы.ГруппаУчастникиИКонтакты.ТолькоПросмотр = Истина;
		Элементы.Этапы.ТолькоПросмотр = Истина;
		
		Элементы.тзНачальныеДанныеДинамическая.ИзменятьСоставСтрок = Ложь;
		
		Аук_ОбщийКлиент.УстановитьВидимостьКоманд(ЭтотОбъект,, "ЗаписатьИЗакрытьКастомная,ЗаписатьКастомная");
			  
	КонецЕсли;
	
	УстановитьВидимостьВкладкиНачальныхДанных();
	
	Если Параметры.Ключ.Пустая() Тогда
		ЭтотОбъект.ТекущийЭлемент = Элементы.Наименование;	
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьВидимостьВкладкиНачальныхДанных()
	
	Элементы.ГруппаНачальныеДанные.Видимость = ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами.Количество() <> 0;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьГиперссылкиАвторов() 
	
	Если ЗначениеЗаполнено(Объект.Автор) И ЗначениеЗаполнено(Объект.ДатаСоздания) Тогда
		Элементы.НадписьСоздан.Заголовок = "Создана: " + Строка(Объект.Автор) + " " + Строка(Объект.ДатаСоздания);
		
		Если ЗначениеЗаполнено(Объект.АвторИзменения) И ЗначениеЗаполнено(Объект.ДатаИзменения) Тогда
			Элементы.НадписьИзменен.Заголовок = "Изменена: " + Строка(Объект.АвторИзменения) + " " + Строка(Объект.ДатаИзменения);	
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьГиперссылкуИсторииИзменений()

	Если ЭтотОбъект.КоличествоСнимков = 0 Тогда
		
		Элементы.ГиперссылкаИсторияИзменений.Доступность = Ложь;
		
		Элементы.ГиперссылкаИсторияИзменений.ЦветТекста = WebЦвета.Серый;
		
	Иначе
		
		Элементы.ГиперссылкаИсторияИзменений.Доступность = Истина;
		
		Элементы.ГиперссылкаИсторияИзменений.ЦветТекста = Новый Цвет();
		
		Элементы.ГиперссылкаИсторияИзменений.Заголовок = СтрШаблон(НСтр("ru = 'История изменений (%1)'"), ЭтотОбъект.КоличествоСнимков);
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьСписокВыбораУчастников()
	
	СписокВыбораУчастников = Элементы.тзНачальныеДанныеДинамическаяУчастник.СписокВыбора;
	
	СписокВыбораУчастников.Очистить();
	
	СписокУчастников = СписокУникальныхУчастников();
	
	Для Каждого ЭлУчастник Из СписокУчастников Цикл
		
		СписокВыбораУчастников.Добавить(ЭлУчастник);	
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция СписокУникальныхУчастников()

	Возврат Аук_ОбщийКлиентСервер.МассивУникальныхЗначенийИзКолонкиТаблицы(Объект.Участники, "Участник");
	
КонецФункции

&НаКлиенте
Процедура ПерезаполнитьНачальныеДанныеОбъектаИспользуяДанныеФормы()
	
	Объект.НачальныеДанные.Очистить();
	
	масУчастники = Аук_ОбщийКлиентСервер.МассивУникальныхЗначенийИзКолонкиТаблицы(ЭтотОбъект.тзНачальныеДанныеДинамическая, "Участник");
	
	Для Сч = 0 По масУчастники.ВГраница() Цикл
		
		масСтрокиТз = ЭтотОбъект.тзНачальныеДанныеДинамическая.НайтиСтроки(Новый Структура("Участник", масУчастники[Сч]));
		
		Для Сч_ = 0 По масСтрокиТз.ВГраница() Цикл
			
			Для Каждого ЭлСоо Из ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами Цикл
				НовСтрока = Объект.НачальныеДанные.Добавить();
				
				НовСтрока.Участник = масУчастники[Сч];
				
				НовСтрока.СтрокаНачальныхДанных = Сч_;
				
				НовСтрока.ЭлементНачальныхДанных	= масСтрокиТз[Сч_]["Характеристика" + ЭлСоо.Ключ];
				
				НовСтрока.ЗначениеНачальныхДанных	= масСтрокиТз[Сч_][ЭлСоо.Ключ];
			КонецЦикла;
			
		КонецЦикла;	
		
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура НачальнаяИнициализация()
	
	ТекСостояние = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ТекущееСостояниеАукциона(Объект.Ссылка);
	
	ЭтотОбъект.ТекущийСтатус = ТекСостояние.Статус;
	
	ЭтотОбъект.БлокироватьИзмененияДанных = Справочники.Аук_Аукционы.АукционИмеетСтатусБлокирующийИзменения(ЭтотОбъект.ТекущийСтатус); 
	
	Если Не Параметры.Ключ.Пустая() Тогда
		
		ЭтотОбъект.КоличествоСнимков = Справочники.Аук_Аукционы.КоличествоСнимковТекущегоАукциона(Объект.Ссылка);
		
	ИначеЕсли ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		ОчиститьРеквизитыПриКопировании();
		
	КонецЕсли;
	
	СоздатьКолонкиНачальныхДанныхДинамическиИЗаполнить(Объект.ВидАукциона);
		
КонецПроцедуры

&НаСервере
Процедура СоздатьКолонкиНачальныхДанныхДинамическиИЗаполнить(пВидАукциона, пБулТолькоСоздатьКолонки = Ложь)
	
	ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами = Новый ФиксированноеСоответствие(Новый Соответствие);
	
	ОбновитьКоллекцииВспомогательныхДанных(пВидАукциона);
	
	Если ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами.Количество() <> 0 Тогда
		
		Аук_ОбщийСервер.ДобавитьКолонкиНачальныхДанныхПоАукциону(ЭтотОбъект, пВидАукциона, "тзНачальныеДанныеДинамическая");
		
		Если Не пБулТолькоСоздатьКолонки Тогда
			
			ЗаполнитьТаблицуНачальныхДанных(Объект.Ссылка, пВидАукциона);
			
		КонецЕсли;
		
		Аук_ОбщийКлиентСервер.ОбновитьОтборКонтактныхЛицДляВсехУчастников(Объект.Участники, пВидАукциона);
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ОчиститьРеквизитыПриКопировании()

	Объект.АвторИзменения	= Неопределено;
	Объект.ДатаИзменения	= Дата("00010101");
	Объект.Автор			= Неопределено;
	Объект.ДатаСоздания		= Дата("00010101");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнтефейсПриИзмененииВидаАукционаНаСервере(Знач пТекВидАукциона, Знач пНовВидАукциона)
	
	Если ЗначениеЗаполнено(пНовВидАукциона) Тогда
		
		Аук_ОбщийСервер.УдалитьКолонкиНачальныхДанныхПоАукциону(ЭтотОбъект, пТекВидАукциона, "тзНачальныеДанныеДинамическая");
		
		СоздатьКолонкиНачальныхДанныхДинамическиИЗаполнить(пНовВидАукциона, Истина);
		
		ЗаполнитьЭтапыПоУмолчанию(пНовВидАукциона);
		
	Иначе
		
		Аук_ОбщийСервер.УдалитьКолонкиНачальныхДанныхПоАукциону(ЭтотОбъект, пТекВидАукциона, "тзНачальныеДанныеДинамическая");
	
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭтапыПоУмолчанию(пНовВидАукциона)
	
	Объект.Этапы.Очистить();
	
	Если ЗначениеЗаполнено(пНовВидАукциона) Тогда
		
		масСписокЭтапов = Аук_ОбщийВызовСервера.СписокЭтаповПоУмолчаниюПоВидуАукциона(пНовВидАукциона);
		
		Для Каждого ЭлМас Из масСписокЭтапов Цикл
			
			ЗаполнитьЗначенияСвойств(Объект.Этапы.Добавить(), ЭлМас);
			
		КонецЦикла;	
		
	КонецЕсли;	
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуНачальныхДанных(пАукцион, пВидАукциона)
	
	Если ЗначениеЗаполнено(пВидАукциона) Тогда
		тзТемп = РеквизитФормыВЗначение("тзНачальныеДанныеДинамическая");
		
		Если ЗначениеЗаполнено(пАукцион) Тогда
			тзТекДанныеНачДанных = Справочники.Аук_Аукционы.ЗначенияНачальныхДанных(пАукцион, пВидАукциона);
		Иначе	
			тзТекДанныеНачДанных = Справочники.Аук_Аукционы.ЗначенияНачальныхДанныхНезаписанногоОбъекта(Объект.НачальныеДанные.Выгрузить(), пВидАукциона);
		КонецЕсли;
			
		Для Каждого СтрокаТз Из тзТекДанныеНачДанных Цикл
			
			НовСтрока = тзТемп.Добавить();
			
			ЗаполнитьЗначенияСвойств(НовСтрока, СтрокаТз);
			
			Для Каждого ЭлСоо Из ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами Цикл
				НовСтрока["Характеристика" + ЭлСоо.Ключ] = ЭлСоо.Значение;	
			КонецЦикла;
			
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(тзТемп, "тзНачальныеДанныеДинамическая");		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьКоллекцииВспомогательныхДанных(пВидАукциона)
	
	Если ЗначениеЗаполнено(пВидАукциона) Тогда
		
		ОбновитьЗначениеСвязиЭлементовСтавкиСРеквизитами(пВидАукциона);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначениеСвязиЭлементовСтавкиСРеквизитами(пВидАукциона)
	
	масСтруктураНачДанных = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(пВидАукциона);
	
	сооСвязьЭлементовСтавкиСРеквизитами = Новый Соответствие;
	
	Для Каждого сткЭлемент Из масСтруктураНачДанных Цикл
		
		сооСвязьЭлементовСтавкиСРеквизитами.Вставить(сткЭлемент.ТехническоеНаименование, сткЭлемент.Элемент);
		
	КонецЦикла;
	
	ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами = Новый ФиксированноеСоответствие(сооСвязьЭлементовСтавкиСРеквизитами);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭтапыПоУмолчанию1(Команда)
	Если ЗначениеЗаполнено(Объект.ВидАукциона) Тогда
		ЗаполнитьЭтапыПоУмолчанию(Объект.ВидАукциона);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Заполните вид аукциона'"));	
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти
