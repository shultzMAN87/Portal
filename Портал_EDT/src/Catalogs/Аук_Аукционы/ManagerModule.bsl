
#Область ПрограммныйИнтерфейс

Функция СведенияПоЭтапуАукциона(пАукцион, пНомерЭтапа = 1) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_АукционыЭтапы.Этап КАК Этап,
	|	Аук_АукционыЭтапы.ДлительностьДни КАК ДлительностьДни,
	|	Аук_АукционыЭтапы.ДлительностьЧасы КАК ДлительностьЧасы,
	|	Аук_АукционыЭтапы.НомерСтроки КАК НомерЭтапа
	|ИЗ
	|	Справочник.Аук_Аукционы.Этапы КАК Аук_АукционыЭтапы
	|ГДЕ
	|	Аук_АукционыЭтапы.Ссылка = &Аукцион
	|	И Аук_АукционыЭтапы.НомерСтроки = &НомерЭтапа";
	
	Запрос.УстановитьПараметр("Аукцион",	пАукцион);
	Запрос.УстановитьПараметр("НомерЭтапа",	пНомерЭтапа);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
КонецФункции

Функция ЭтапыАукциона(пАукцион, пБулВТаблицуЗначений = Ложь) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_АукционыЭтапы.Этап КАК Этап,
	|	Аук_АукционыЭтапы.ДлительностьДни КАК ДлительностьДни,
	|	Аук_АукционыЭтапы.ДлительностьЧасы КАК ДлительностьЧасы,
	|	Аук_АукционыЭтапы.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Справочник.Аук_Аукционы.Этапы КАК Аук_АукционыЭтапы
	|ГДЕ
	|	Аук_АукционыЭтапы.Ссылка = &Аукцион";
	
	Запрос.УстановитьПараметр("Аукцион",	пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если пБулВТаблицуЗначений Тогда
		Возврат РезультатЗапроса.Выгрузить();	
	Иначе	
		Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	КонецЕсли;
КонецФункции

// Если текущий статус аукциона "НовыйЭтап" или "ПодведениеПромежуточныхИтогов" тогда создаем снимок
//
Функция ТребуетсяЗаписьВИсторию(пАукцион) Экспорт
	булРезультат = Ложь;
	
	сткТекСтатусАукциона = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ТекущееСостояниеАукциона(пАукцион);
	
	Если сткТекСтатусАукциона.Статус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.НовыйЭтап")
		 Или сткТекСтатусАукциона.Статус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов")
	Тогда	 
		булРезультат = Истина;
	КонецЕсли;
		
	Возврат булРезультат;	
КонецФункции

Функция КлючевыеДанныеАукциона(пАукцион) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.ВидАукциона КАК ВидАукциона,
	|	Аук_Аукционы.Сумма КАК Сумма,
	|	Аук_Аукционы.Срок КАК Срок,
	|	Аук_Аукционы.ДатаНачала КАК ДатаНачала,
	|	Аук_Аукционы.Наименование КАК Наименование,
	|	Аук_Аукционы.Код КАК Код
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|ГДЕ
	|	Аук_Аукционы.Ссылка = &Аукцион";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
	
КонецФункции

Функция НастройкиАвтоматическогоВыполненияАукциона(пАукцион) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.АвтоматическийСтарт КАК АвтоматическийСтарт,
	|	Аук_Аукционы.АвтоматическийПереходПоЭтапам КАК АвтоматическийПереходПоЭтапам,
	|	Аук_Аукционы.АвтоматическоеПодведениеПромежуточныхИтогов КАК АвтоматическоеПодведениеПромежуточныхИтогов,
	|	Аук_Аукционы.АвтоматическоеПодведениеОкончательныхИтогов КАК АвтоматическоеПодведениеОкончательныхИтогов
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|ГДЕ
	|	Аук_Аукционы.Ссылка = &Аукцион";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
	
КонецФункции

Процедура СоздатьСнимокТекущегоАукциона(пТекОбъектАукцион) Экспорт

	НовОбъект = Аук_ОбщийСервер.СоздатьКопиюСуществующегоЭлементаСправочника(пТекОбъектАукцион);
	
	НовОбъект.ТекущийАукцион	= пТекОбъектАукцион.Ссылка;
	НовОбъект.Наименование		= СтрШаблон("%1 <%2>", пТекОбъектАукцион.Наименование, ПорядковыйНомерСнимка(пТекОбъектАукцион.Ссылка));
	
	сткРез = ОбщегоНазначения.ЗаписатьОбъектВБазу(НовОбъект);
	
	Если Не сткРез.Успех Тогда
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;	
	
КонецПроцедуры

Функция КоличествоЭтаповАукциона(пАукцион) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(Аук_АукционыЭтапы.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	Справочник.Аук_Аукционы.Этапы КАК Аук_АукционыЭтапы
	|ГДЕ
	|	Аук_АукционыЭтапы.Ссылка = &Аукцион";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат 0;
		
	Иначе	
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий();
		
		Возврат Выборка.НомерСтроки;
	КонецЕсли;	
	
КонецФункции	

Функция КонтактныеЛицаСПривязкойКУчастникам(пАукцион, пУчасники = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_АукционыУчастники.Участник КАК Участник,
	|	Аук_АукционыУчастники.КонтактноеЛицо КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.Аук_Аукционы.Участники КАК Аук_АукционыУчастники
	|ГДЕ
	|	Аук_АукционыУчастники.Ссылка = &Аукцион
	|	И &ОтборПоУчастникам";
	
	Если пУчасники <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоУчастникам", "Аук_АукционыУчастники.Участник В (&Участники)");
		
		Запрос.УстановитьПараметр("Участники", пУчасники);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоУчастникам", "ИСТИНА");	
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция КонтактныеЛицаУчастникаАукциона(пАукцион, пУчастник) Экспорт
	
	тзТемп = КонтактныеЛицаСПривязкойКУчастникам(пАукцион);
	
	масСтроки = тзТемп.НайтиСтроки(Новый Структура("Участник", пУчастник));
	
	Возврат Аук_ОбщийКлиентСервер.МассивУникальныхЗначенийИзКолонкиТаблицы(масСтроки, "КонтактноеЛицо");
	
КонецФункции

Функция КонтактныеЛицаАукциона(пАукцион, пУчасники = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_АукционыУчастники.КонтактноеЛицо КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.Аук_Аукционы.Участники КАК Аук_АукционыУчастники
	|ГДЕ
	|	Аук_АукционыУчастники.Ссылка = &Аукцион
	|	И &ОтборПоУчастникам";
	
	Если пУчасники <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоУчастникам", "Аук_АукционыУчастники.Участник В (&Участники)");
		
		Запрос.УстановитьПараметр("Участники", пУчасники);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоУчастникам", "ИСТИНА");	
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КонтактноеЛицо");
	
КонецФункции

Функция УчастникиАукциона(пАукцион) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аук_АукционыУчастники.Участник КАК Участник
	|ИЗ
	|	Справочник.Аук_Аукционы.Участники КАК Аук_АукционыУчастники
	|ГДЕ
	|	Аук_АукционыУчастники.Ссылка = &Аукцион";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Участник");
	
КонецФункции

Функция ЗначенияНачальныхДанных(пАукцион, пВидАукциона = Неопределено) Экспорт
	
	Если пВидАукциона = Неопределено Тогда
		
		пВидАукциона = Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион).ВидАукциона;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_АукционыНачальныеДанные.Участник КАК Участник,
	|	Аук_АукционыНачальныеДанные.СтрокаНачальныхДанных КАК СтрокаДанных,
	|	Аук_АукционыНачальныеДанные.ЭлементНачальныхДанных КАК ЭлементДанных,
	|	Аук_АукционыНачальныеДанные.ЗначениеНачальныхДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	Справочник.Аук_Аукционы.НачальныеДанные КАК Аук_АукционыНачальныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_АукционыНачальныеДанные.ЭлементНачальныхДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|ГДЕ
	|	Аук_АукционыНачальныеДанные.Ссылка = &Аукцион
	|ИТОГИ ПО
	|	Участник,
	|	СтрокаДанных";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	масСтруктураНачДанных = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(пВидАукциона);
	
	масОбязательныеКолонки = Аук_ОбщийСервер.СоставОбязательныхКолонок("Участник");
	
	тзРезультат = Аук_ОбщийСервер.ТекущиеДанныеУчастниковАукционаСоздатьТаблицу(масОбязательныеКолонки, масСтруктураНачДанных);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Аук_ОбщийСервер.ЗаполнитьДанныеУчастниковПоРезультатуЗапроса(тзРезультат, РезультатЗапроса);
	
	Возврат тзРезультат;
	
КонецФункции

Функция ЗначенияНачальныхДанныхНезаписанногоОбъекта(пТзТабличнаяЧасть, пВидАукциона) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тзТабличнаяЧасть.Участник КАК Участник,
	|	тзТабличнаяЧасть.СтрокаНачальныхДанных КАК СтрокаДанных,
	|	тзТабличнаяЧасть.ЭлементНачальныхДанных КАК ЭлементДанных,
	|	тзТабличнаяЧасть.ЗначениеНачальныхДанных КАК ЗначениеЭлементаДанных
	|ПОМЕСТИТЬ втТабличнаяЧасть
	|ИЗ
	|	&тзТабличнаяЧасть КАК тзТабличнаяЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТабличнаяЧасть.Участник КАК Участник,
	|	втТабличнаяЧасть.СтрокаДанных КАК СтрокаДанных,
	|	втТабличнаяЧасть.ЭлементДанных КАК ЭлементДанных,
	|	втТабличнаяЧасть.ЗначениеЭлементаДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	втТабличнаяЧасть КАК втТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО втТабличнаяЧасть.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|ИТОГИ ПО
	|	Участник,
	|	СтрокаДанных";
	
	Запрос.УстановитьПараметр("тзТабличнаяЧасть", пТзТабличнаяЧасть);
	
	масСтруктураНачДанных = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(пВидАукциона);
	
	масОбязательныеКолонки = Аук_ОбщийСервер.СоставОбязательныхКолонок("Участник");
	
	тзРезультат = Аук_ОбщийСервер.ТекущиеДанныеУчастниковАукционаСоздатьТаблицу(масОбязательныеКолонки, масСтруктураНачДанных);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Аук_ОбщийСервер.ЗаполнитьДанныеУчастниковПоРезультатуЗапроса(тзРезультат, РезультатЗапроса);
	
	Возврат тзРезультат;
	
КонецФункции

Функция ТекущийСписокСнимковАукциона(пАукцион) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|ГДЕ
	|	Аук_Аукционы.ТекущийАукцион = &ТекущийАукцион";
	
	Запрос.УстановитьПараметр("ТекущийАукцион", пАукцион);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Аукцион");
	
КонецФункции	

Функция КоличествоСнимковТекущегоАукциона(пАукцион) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(Аук_Аукционы.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|ГДЕ
	|	Аук_Аукционы.ТекущийАукцион = &ТекущийАукцион";
	
	Запрос.УстановитьПараметр("ТекущийАукцион", пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат 0;
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий();
		
		Возврат Выборка.Количество;
		
	КонецЕсли;	
	
КонецФункции

Функция ЕстьАукционыСВидом(пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	 "ВЫБРАТЬ ПЕРВЫЕ 1
	 |	Аук_Аукционы.Ссылка КАК Ссылка
	 |ИЗ
	 |	Справочник.Аук_Аукционы КАК Аук_Аукционы
	 |ГДЕ
	 |	Аук_Аукционы.ВидАукциона = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции	

Функция АукционИмеетСтатусБлокирующийИзменения(пТекущийСтатус) Экспорт

	Возврат пТекущийСтатус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.ПодведениеИтогов")
		 	Или пТекущийСтатус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.Завершен")
		 	Или пТекущийСтатус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.Архив");
	
КонецФункции	

// Аукционы, ожидающие запуска
//    Критерии:
//		1. Текущая дата > даты начала (реквизит "ДатаНачала" спр Аук_Аукционы)
//		2. Статус = Перечисление.Аук_СтатусыАукционов.Новый
//		3. Нет ранее созданных но невыполненных команд ПроверитьВозможностьНачалаАукциона И НачатьАукцион
//			(т.е. Результат = Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка) 
//		4.1 Аукционы с автоматическим стартом (реквизит АвтоматическийСтарт = Истина) отбираются в любом случае при выполнении условий 1, 2 и 3
//		4.2 Аукционы с ручным стартом (реквизит АвтоматическийСтарт = Истина) отбираются при выполнении условий 1, 2 и 3,
//		И доп. условий:
//			а) по команде было не более 2-х оповещений сотрудников;
//			б) последнее оповещение было более 15 минут назад 
//
Функция АукционыОжидающиеДействия_Запуск(пТекДата = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион,
	|	Аук_Аукционы.АвтоматическийСтарт КАК АвтоматическийСтарт,
	|	ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьНачалаАукциона) КАК Команда
	|ПОМЕСТИТЬ втАукционы
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.Новый))
	|ГДЕ
	|	Аук_Аукционы.ДатаНачала < &ТекДата
	|	И НЕ Аук_Аукционы.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда,
	|	втАукционы.АвтоматическийСтарт КАК АвтоматическийСтарт,
	|	Аук_ИсторияКомандАукционов.Результат КАК Результат,
	|	Аук_ИсторияКомандАукционов.КлючКоманды КАК КлючКоманды
	|ПОМЕСТИТЬ втИсторияВыполнения
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втАукционы.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И (Аук_ИсторияКомандАукционов.Команда В (ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьНачалаАукциона), ЗНАЧЕНИЕ(Перечисление.Аук_Команды.НачатьАукцион)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсторияВыполнения.Аукцион КАК Аукцион,
	|	МАКСИМУМ(Аук_ИсторияКомандАукционов.Период) КАК ПоследнееОповещение,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Аук_ИсторияКомандАукционов.КлючРодителя) КАК КоличествоОповещений
	|ПОМЕСТИТЬ втИсторияВыполненияОповещенийПоКоманде
	|ИЗ
	|	втИсторияВыполнения КАК втИсторияВыполнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втИсторияВыполнения.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И втИсторияВыполнения.КлючКоманды = Аук_ИсторияКомандАукционов.КлючРодителя
	|			И (Аук_ИсторияКомандАукционов.Команда = ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ВыполнитьСлужебноеОповещение))
	|ГДЕ
	|	НЕ втИсторияВыполнения.АвтоматическийСтарт
	|
	|СГРУППИРОВАТЬ ПО
	|	втИсторияВыполнения.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И втАукционы.АвтоматическийСтарт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втАукционы.Аукцион,
	|	втАукционы.Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|			И (втИсторияВыполнения.Результат = ЗНАЧЕНИЕ(Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполненияОповещенийПоКоманде КАК втИсторияВыполненияОповещенийПоКоманде
	|		ПО втАукционы.Аукцион = втИсторияВыполненияОповещенийПоКоманде.Аукцион
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И НЕ втАукционы.АвтоматическийСтарт
	|	И ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.КоличествоОповещений, 0) < &МаксКоличествоОповещений
	|	И РАЗНОСТЬДАТ(ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.ПоследнееОповещение, ДАТАВРЕМЯ(1, 1, 1)), &ТекДата, МИНУТА) > &ДельтаМеждуОповещениями";
	
	Запрос.УстановитьПараметр("ТекДата",		?(пТекДата <> Неопределено, пТекДата, ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_()));
	Запрос.УстановитьПараметр("МаксКоличествоОповещений",		Аук_УправлениеАукционамиКлиентСервер.КоличествоСлужебныхОповещений());
	Запрос.УстановитьПараметр("ДельтаМеждуОповещениями",		Аук_УправлениеАукционамиКлиентСервер.ПромежутокВремениМеждуОповещениямиМинут());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции	

// Аукционы, ожидающие перехода на следующий этап
//    Критерии:
//		1. К дате последнего статуса "НовыйЭтап" прибавляется длительность этапа, текущая дата должна быть больше
//		2. Статус = Перечисление.Аук_СтатусыАукционов.НовыйЭтап
//		3. Нет ранее созданных но невыполненных команд ПроверитьВозможностьПеревестиАукционНаСледующийЭтап И ПеревестиАукционНаСледующийЭтап
//			(т.е. Результат = Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка) 
//		4.1 Аукционы с автоматическим стартом (реквизит АвтоматическийПереходПоЭтапам = Истина) отбираются в любом случае при выполнении условий 1, 2 и 3
//		4.2 Аукционы с ручным стартом (реквизит АвтоматическийПереходПоЭтапам = Истина) отбираются при выполнении условий 1, 2 и 3,
//		И доп. условий:
//			а) по команде было не более 2-х оповещений сотрудников. Причем счет ведется от последнего статуса (их может быть несколько);
//			б) последнее оповещение было более 15 минут назад 
//
Функция АукционыОжидающиеДействия_ПереводНаСледующийЭтап(пТекДата = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион,
	|	Аук_Аукционы.АвтоматическийПереходПоЭтапам КАК АвтоматическийПереходПоЭтапам,
	|	ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьПеревестиАукционНаСледующийЭтап) КАК Команда,
	|	Аук_ИсторияСтатусовАукционовСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ втАукционы
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних(, ) КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.НовыйЭтап))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_Аукционы.Этапы КАК Аук_АукционыЭтапы
	|		ПО Аук_Аукционы.Ссылка = Аук_АукционыЭтапы.Ссылка
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.НомерЭтапа = Аук_АукционыЭтапы.НомерСтроки)
	|ГДЕ
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(Аук_ИсторияСтатусовАукционовСрезПоследних.Период, ДЕНЬ, Аук_АукционыЭтапы.ДлительностьДни), СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), Аук_АукционыЭтапы.ДлительностьЧасы, СЕКУНДА)) < &ТекДата
	|	И НЕ Аук_Аукционы.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда,
	|	втАукционы.АвтоматическийПереходПоЭтапам КАК АвтоматическийПереходПоЭтапам,
	|	Аук_ИсторияКомандАукционов.Результат КАК Результат,
	|	Аук_ИсторияКомандАукционов.КлючКоманды КАК КлючКоманды
	|ПОМЕСТИТЬ втИсторияВыполнения
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втАукционы.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И втАукционы.Период <= Аук_ИсторияКомандАукционов.Период
	|			И (Аук_ИсторияКомандАукционов.Команда В (ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьПеревестиАукционНаСледующийЭтап), ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПеревестиАукционНаСледующийЭтап)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсторияВыполнения.Аукцион КАК Аукцион,
	|	МАКСИМУМ(Аук_ИсторияКомандАукционов.Период) КАК ПоследнееОповещение,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Аук_ИсторияКомандАукционов.КлючРодителя) КАК КоличествоОповещений
	|ПОМЕСТИТЬ втИсторияВыполненияОповещенийПоКоманде
	|ИЗ
	|	втИсторияВыполнения КАК втИсторияВыполнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втИсторияВыполнения.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И втИсторияВыполнения.КлючКоманды = Аук_ИсторияКомандАукционов.КлючРодителя
	|			И (Аук_ИсторияКомандАукционов.Команда = ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ВыполнитьСлужебноеОповещение))
	|ГДЕ
	|	НЕ втИсторияВыполнения.АвтоматическийПереходПоЭтапам
	|
	|СГРУППИРОВАТЬ ПО
	|	втИсторияВыполнения.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И втАукционы.АвтоматическийПереходПоЭтапам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втАукционы.Аукцион,
	|	втАукционы.Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|			И (втИсторияВыполнения.Результат = ЗНАЧЕНИЕ(Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполненияОповещенийПоКоманде КАК втИсторияВыполненияОповещенийПоКоманде
	|		ПО втАукционы.Аукцион = втИсторияВыполненияОповещенийПоКоманде.Аукцион
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И НЕ втАукционы.АвтоматическийПереходПоЭтапам
	|	И ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.КоличествоОповещений, 0) < &МаксКоличествоОповещений
	|	И РАЗНОСТЬДАТ(ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.ПоследнееОповещение, ДАТАВРЕМЯ(1, 1, 1)), &ТекДата, МИНУТА) > &ДельтаМеждуОповещениями";
	
	Запрос.УстановитьПараметр("ТекДата",		?(пТекДата <> Неопределено, пТекДата, ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_()));
	Запрос.УстановитьПараметр("МаксКоличествоОповещений",	Аук_УправлениеАукционамиКлиентСервер.КоличествоСлужебныхОповещений());
	Запрос.УстановитьПараметр("ДельтаМеждуОповещениями",	Аук_УправлениеАукционамиКлиентСервер.ПромежутокВремениМеждуОповещениямиМинут());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции

// Аукционы, ожидающие подведения промежуточных итогов
//    Критерии:
//		1. Статус = Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов
//		2. Нет ранее созданных но невыполненных команд АвтоматическоеПодведениеПромежуточныхИтогов И ЗафиксироватьПромежуточныеИтогиАукциона
//			(т.е. Результат = Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка) 
//		3.1 Аукционы с автоматическим стартом (реквизит ПроверитьВозможностьПодведенияПромежуточныхИтогов = Истина) отбираются в любом случае при выполнении условий 1 и 2
//		3.2 Аукционы с ручным стартом (реквизит ПроверитьВозможностьПодведенияПромежуточныхИтогов = Истина) отбираются при выполнении условий 1 и 2,
//		И доп. условий:
//			а) по команде было не более 2-х оповещений сотрудников. Причем счет ведется от последнего статуса (их может быть несколько);
//			б) последнее оповещение было более 15 минут назад 
//
Функция АукционыОжидающиеДействия_ПодведениеПромежуточныхИтогов(пТекДата = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион,
	|	Аук_Аукционы.АвтоматическоеПодведениеПромежуточныхИтогов КАК АвтоматическоеПодведениеПромежуточныхИтогов,
	|	ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьПодведенияПромежуточныхИтогов) КАК Команда,
	|	Аук_ИсторияСтатусовАукционовСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ втАукционы
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов))
	|ГДЕ
	|	НЕ Аук_Аукционы.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда,
	|	втАукционы.АвтоматическоеПодведениеПромежуточныхИтогов КАК АвтоматическоеПодведениеПромежуточныхИтогов,
	|	Аук_ИсторияКомандАукционов.Результат КАК Результат,
	|	Аук_ИсторияКомандАукционов.КлючКоманды КАК КлючКоманды
	|ПОМЕСТИТЬ втИсторияВыполнения
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втАукционы.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И втАукционы.Период <= Аук_ИсторияКомандАукционов.Период
	|			И (Аук_ИсторияКомандАукционов.Команда В (ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьПодведенияПромежуточныхИтогов), ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ЗафиксироватьПромежуточныеИтогиАукциона)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсторияВыполнения.Аукцион КАК Аукцион,
	|	МАКСИМУМ(Аук_ИсторияКомандАукционов.Период) КАК ПоследнееОповещение,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Аук_ИсторияКомандАукционов.КлючРодителя) КАК КоличествоОповещений
	|ПОМЕСТИТЬ втИсторияВыполненияОповещенийПоКоманде
	|ИЗ
	|	втИсторияВыполнения КАК втИсторияВыполнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втИсторияВыполнения.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И втИсторияВыполнения.КлючКоманды = Аук_ИсторияКомандАукционов.КлючРодителя
	|			И (Аук_ИсторияКомандАукционов.Команда = ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ВыполнитьСлужебноеОповещение))
	|ГДЕ
	|	НЕ втИсторияВыполнения.АвтоматическоеПодведениеПромежуточныхИтогов
	|
	|СГРУППИРОВАТЬ ПО
	|	втИсторияВыполнения.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И втАукционы.АвтоматическоеПодведениеПромежуточныхИтогов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втАукционы.Аукцион,
	|	втАукционы.Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|			И (втИсторияВыполнения.Результат = ЗНАЧЕНИЕ(Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполненияОповещенийПоКоманде КАК втИсторияВыполненияОповещенийПоКоманде
	|		ПО втАукционы.Аукцион = втИсторияВыполненияОповещенийПоКоманде.Аукцион
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И НЕ втАукционы.АвтоматическоеПодведениеПромежуточныхИтогов
	|	И ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.КоличествоОповещений, 0) < &МаксКоличествоОповещений
	|	И РАЗНОСТЬДАТ(ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.ПоследнееОповещение, ДАТАВРЕМЯ(1, 1, 1)), &ТекДата, МИНУТА) > &ДельтаМеждуОповещениями";
	
	Запрос.УстановитьПараметр("ТекДата",		?(пТекДата <> Неопределено, пТекДата, ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_()));
	Запрос.УстановитьПараметр("МаксКоличествоОповещений",	Аук_УправлениеАукционамиКлиентСервер.КоличествоСлужебныхОповещений());
	Запрос.УстановитьПараметр("ДельтаМеждуОповещениями",	Аук_УправлениеАукционамиКлиентСервер.ПромежутокВремениМеждуОповещениямиМинут());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции

// Аукционы, ожидающие подведения окончательных итогов
//    Критерии:
//		1. Статус = Перечисление.Аук_СтатусыАукционов.ПодведениеИтогов
//		2. Нет ранее созданных но невыполненных команд ПроверитьВозможностьЗавершенияАукциона И ЗавершитьАукцион
//			(т.е. Результат = Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка) 
//		3.1 Аукционы с автоматическим стартом (реквизит АвтоматическоеПодведениеОкончательныхИтогов = Истина) отбираются в любом случае при выполнении условий 1 и 2
//		3.2 Аукционы с ручным стартом (реквизит АвтоматическоеПодведениеОкончательныхИтогов = Истина) отбираются при выполнении условий 1 и 2,
//		И доп. условий:
//			а) по команде было не более 2-х оповещений сотрудников;
//			б) последнее оповещение было более 15 минут назад 
//
Функция АукционыОжидающиеДействия_Завершение(пТекДата = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион,
	|	Аук_Аукционы.АвтоматическоеПодведениеОкончательныхИтогов КАК АвтоматическоеПодведениеОкончательныхИтогов,
	|	ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьЗавершенияАукциона) КАК Команда
	|ПОМЕСТИТЬ втАукционы
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеИтогов))
	|ГДЕ
	|	НЕ Аук_Аукционы.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда,
	|	втАукционы.АвтоматическоеПодведениеОкончательныхИтогов КАК АвтоматическоеПодведениеОкончательныхИтогов,
	|	Аук_ИсторияКомандАукционов.Результат КАК Результат,
	|	Аук_ИсторияКомандАукционов.КлючКоманды КАК КлючКоманды
	|ПОМЕСТИТЬ втИсторияВыполнения
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втАукционы.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И (Аук_ИсторияКомандАукционов.Команда В (ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ПроверитьВозможностьЗавершенияАукциона), ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ЗавершитьАукцион)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсторияВыполнения.Аукцион КАК Аукцион,
	|	МАКСИМУМ(Аук_ИсторияКомандАукционов.Период) КАК ПоследнееОповещение,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Аук_ИсторияКомандАукционов.КлючРодителя) КАК КоличествоОповещений
	|ПОМЕСТИТЬ втИсторияВыполненияОповещенийПоКоманде
	|ИЗ
	|	втИсторияВыполнения КАК втИсторияВыполнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|		ПО втИсторияВыполнения.Аукцион = Аук_ИсторияКомандАукционов.Аукцион
	|			И втИсторияВыполнения.КлючКоманды = Аук_ИсторияКомандАукционов.КлючРодителя
	|			И (Аук_ИсторияКомандАукционов.Команда = ЗНАЧЕНИЕ(Перечисление.Аук_Команды.ВыполнитьСлужебноеОповещение))
	|ГДЕ
	|	НЕ втИсторияВыполнения.АвтоматическоеПодведениеОкончательныхИтогов
	|
	|СГРУППИРОВАТЬ ПО
	|	втИсторияВыполнения.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАукционы.Аукцион КАК Аукцион,
	|	втАукционы.Команда КАК Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И втАукционы.АвтоматическоеПодведениеОкончательныхИтогов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втАукционы.Аукцион,
	|	втАукционы.Команда
	|ИЗ
	|	втАукционы КАК втАукционы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполнения КАК втИсторияВыполнения
	|		ПО втАукционы.Аукцион = втИсторияВыполнения.Аукцион
	|			И втАукционы.Команда = втИсторияВыполнения.Команда
	|			И (втИсторияВыполнения.Результат = ЗНАЧЕНИЕ(Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияВыполненияОповещенийПоКоманде КАК втИсторияВыполненияОповещенийПоКоманде
	|		ПО втАукционы.Аукцион = втИсторияВыполненияОповещенийПоКоманде.Аукцион
	|ГДЕ
	|	втИсторияВыполнения.Аукцион ЕСТЬ NULL
	|	И НЕ втАукционы.АвтоматическоеПодведениеОкончательныхИтогов
	|	И ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.КоличествоОповещений, 0) < &МаксКоличествоОповещений
	|	И РАЗНОСТЬДАТ(ЕСТЬNULL(втИсторияВыполненияОповещенийПоКоманде.ПоследнееОповещение, ДАТАВРЕМЯ(1, 1, 1)), &ТекДата, МИНУТА) > &ДельтаМеждуОповещениями";
	
	Запрос.УстановитьПараметр("ТекДата",		?(пТекДата <> Неопределено, пТекДата, ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_()));
	Запрос.УстановитьПараметр("МаксКоличествоОповещений",	Аук_УправлениеАукционамиКлиентСервер.КоличествоСлужебныхОповещений());
	Запрос.УстановитьПараметр("ДельтаМеждуОповещениями",	Аук_УправлениеАукционамиКлиентСервер.ПромежутокВремениМеждуОповещениямиМинут());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции

Функция АукционыОжидающиеЗапуска(пМасАукционы = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних(, ) КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.Новый))
	|ГДЕ
	|	Аук_Аукционы.ДатаНачала < &ТекДата
	|	И &ОтборПоСпискуАукционов";
	
	Если пМасАукционы <> Неопределено Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "Аук_Аукционы.Ссылка В(&Аукционы)");
		
		Запрос.УстановитьПараметр("Аукционы", пМасАукционы);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "ИСТИНА");	
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТекДата", ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Аукцион");
	
КонецФункции

Функция АукционыОжидающиеПереходаНаСледующийЭтап(пМасАукционы = Неопределено) Экспорт
	
	// ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(Аук_ИсторияСтатусовАукционовСрезПоследних.Период, ДЕНЬ, Аук_АукционыЭтапы.ДлительностьДни), СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), Аук_АукционыЭтапы.ДлительностьЧасы, СЕКУНДА))
	// сначала высчитываем ДОБАВИТЬКДАТЕ(Аук_ИсторияСтатусовАукционовСрезПоследних.Период, ДЕНЬ, Аук_АукционыЭтапы.ДлительностьДни)
	// Затем прибавляем количество секунд
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних(, ) КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.НовыйЭтап))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_Аукционы.Этапы КАК Аук_АукционыЭтапы
	|		ПО Аук_Аукционы.Ссылка = Аук_АукционыЭтапы.Ссылка
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.НомерЭтапа = Аук_АукционыЭтапы.НомерСтроки)
	|ГДЕ
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(Аук_ИсторияСтатусовАукционовСрезПоследних.Период, ДЕНЬ, Аук_АукционыЭтапы.ДлительностьДни), СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), Аук_АукционыЭтапы.ДлительностьЧасы, СЕКУНДА)) < &ТекДата
	|	И &ОтборПоСпискуАукционов";
	
	Если пМасАукционы <> Неопределено Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "Аук_Аукционы.Ссылка В(&Аукционы)");
		
		Запрос.УстановитьПараметр("Аукционы", пМасАукционы);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "ИСТИНА");	
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТекДата", ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Аукцион");
	
КонецФункции

Функция АукционыОжидающиеПодведенияПромежуточныхИтогов(пМасАукционы = Неопределено) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних(, ) КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов))
	|ГДЕ
	|	&ОтборПоСпискуАукционов";
	
	Если пМасАукционы <> Неопределено Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "Аук_Аукционы.Ссылка В(&Аукционы)");
		
		Запрос.УстановитьПараметр("Аукционы", пМасАукционы);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "ИСТИНА");	
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТекДата", ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Аукцион");
	
КонецФункции

Функция АукционыОжидающиеЗавершения(пМасАукционы = Неопределено) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних(, ) КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|		ПО Аук_Аукционы.Ссылка = Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион
	|			И (Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеИтогов))
	|ГДЕ
	|	&ОтборПоСпискуАукционов";
	
	Если пМасАукционы <> Неопределено Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "Аук_Аукционы.Ссылка В(&Аукционы)");
		
		Запрос.УстановитьПараметр("Аукционы", пМасАукционы);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСпискуАукционов", "ИСТИНА");	
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТекДата", ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Аукцион");
	
КонецФункции

Функция ОтветственныеЛицаПоАукциону(пАукцион) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Автор КАК Автор,
	|	Аук_Аукционы.АвторИзменения КАК АвторИзменения
	|ПОМЕСТИТЬ втТемп
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|ГДЕ
	|	Аук_Аукционы.Ссылка = &Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТемп.Автор КАК Ответственный
	|ИЗ
	|	втТемп КАК втТемп
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втТемп.АвторИзменения
	|ИЗ
	|	втТемп КАК втТемп
	|ГДЕ
	|	втТемп.АвторИзменения <> ЗНАЧЕНИЕ(Справочник.УБД_Пользователи.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ответственный");
	
КонецФункции	

Функция НачальныеДанные(пАукцион) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_АукционыНачальныеДанные.Участник КАК Участник,
	|	Аук_АукционыНачальныеДанные.СтрокаНачальныхДанных КАК СтрокаНачальныхДанных,
	|	Аук_АукционыНачальныеДанные.ЭлементНачальныхДанных КАК ЭлементНачальныхДанных,
	|	Аук_АукционыНачальныеДанные.ЗначениеНачальныхДанных КАК ЗначениеНачальныхДанных
	|ИЗ
	|	Справочник.Аук_Аукционы.НачальныеДанные КАК Аук_АукционыНачальныеДанные
	|ГДЕ
	|	Аук_АукционыНачальныеДанные.Ссылка = &Аукцион";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции	

Функция ДанныеАукционаДляПроверки(пАукцион) Экспорт
	
	сткРез = КлючевыеДанныеАукциона(пАукцион);
	
	сткРез.Вставить("Этапы",			ЭтапыАукциона(пАукцион));
	сткРез.Вставить("Участники",		УчастникиАукциона(пАукцион));
	сткРез.Вставить("НачальныеДанные",	НачальныеДанные(пАукцион));
	
	сткНастройкиАвтоматики = НастройкиАвтоматическогоВыполненияАукциона(пАукцион);
	
	сткРез.Вставить("АвтоматическийРежим",	Аук_ОбщийКлиентСервер.АвтоматическийРежим(сткНастройкиАвтоматики));
	
	Возврат сткРез;
	
КонецФункции	

Функция ПланируемоеВремяЗавершения(пАукцион) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ИсторияСтатусовАукционовСрезПоследних.Период КАК Период,
	|	Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион КАК Аукцион
	|ПОМЕСТИТЬ втРеальноеВремяНачала
	|ИЗ
	|	РегистрСведений.Аук_ИсторияСтатусовАукционов.СрезПоследних КАК Аук_ИсторияСтатусовАукционовСрезПоследних
	|ГДЕ
	|	Аук_ИсторияСтатусовАукционовСрезПоследних.Аукцион = &Аукцион
	|	И Аук_ИсторияСтатусовАукционовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.НовыйЭтап)
	|	И Аук_ИсторияСтатусовАукционовСрезПоследних.НомерЭтапа = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Аукцион,
	|	Аук_Аукционы.ДатаНачала КАК ДатаНачала
	|ПОМЕСТИТЬ втПлановоеВремяНачала
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|ГДЕ
	|	Аук_Аукционы.Ссылка = &Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПлановоеВремяНачала.Аукцион КАК Аукцион,
	|	ВЫБОР
	|		КОГДА втРеальноеВремяНачала.Период ЕСТЬ NULL
	|			ТОГДА втПлановоеВремяНачала.ДатаНачала
	|		ИНАЧЕ втРеальноеВремяНачала.Период
	|	КОНЕЦ КАК ВремяНачала
	|ПОМЕСТИТЬ втВремяНачала
	|ИЗ
	|	втПлановоеВремяНачала КАК втПлановоеВремяНачала
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеальноеВремяНачала КАК втРеальноеВремяНачала
	|		ПО втПлановоеВремяНачала.Аукцион = втРеальноеВремяНачала.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВремяНачала.Аукцион КАК Аукцион,
	|	СУММА(Аук_АукционыЭтапы.ДлительностьДни) КАК ДлительностьДни,
	|	СУММА(РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), Аук_АукционыЭтапы.ДлительностьЧасы, СЕКУНДА)) КАК ДлительностьСекунды
	|ПОМЕСТИТЬ втДлительность
	|ИЗ
	|	втВремяНачала КАК втВремяНачала
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_Аукционы.Этапы КАК Аук_АукционыЭтапы
	|		ПО втВремяНачала.Аукцион = Аук_АукционыЭтапы.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	втВремяНачала.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВремяНачала.Аукцион КАК Аукцион,
	|	ДОБАВИТЬКДАТЕ(втВремяНачала.ВремяНачала, СЕКУНДА, втДлительность.ДлительностьДни * 86400 + втДлительность.ДлительностьСекунды) КАК ВремяЗавершения
	|ИЗ
	|	втВремяНачала КАК втВремяНачала
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДлительность КАК втДлительность
	|		ПО втВремяНачала.Аукцион = втДлительность.Аукцион";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка.ВремяЗавершения;
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПорядковыйНомерСнимка(пТекущийАукцион)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(Аук_Аукционы.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|ГДЕ
	|	Аук_Аукционы.ТекущийАукцион = &ТекущийАукцион";
	
	Запрос.УстановитьПараметр("ТекущийАукцион", пТекущийАукцион);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Количество + 1;	
	Иначе	
		Возврат 1;	
	КонецЕсли;	
	
КонецФункции	

#КонецОбласти

