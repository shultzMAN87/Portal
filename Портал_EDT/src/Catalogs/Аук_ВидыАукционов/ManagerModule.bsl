
#Область ПрограммныйИнтерфейс

Функция ОписаниеНастроекСтавокПоВидуАукциона(пВидАукциона) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционов.НесколькоЗначенийСтруктурыСтавки КАК НесколькоЗначенийСтруктурыСтавки
	|ИЗ
	|	Справочник.Аук_ВидыАукционов КАК Аук_ВидыАукционов
	|ГДЕ
	|	Аук_ВидыАукционов.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
	
КонецФункции

Функция ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционовСтруктураСтавки.ЭлементСтавки КАК Элемент,
	|	Аук_ВидыАукционовСтруктураСтавки.НеОбязательный КАК НеОбязательный,
	|	Аук_ЭлементыДанныхАукционов.Описание КАК Описание,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование,
	|	Аук_ЭлементыДанныхАукционов.ТипЗначения КАК ТипЗначения,
	|	Аук_ЭлементыДанныхАукционов.Представление КАК ЭлементПредставление,
	|	Аук_ЭлементыДанныхАукционов.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Аук_ВидыАукционов.СтруктураСтавки КАК Аук_ВидыАукционовСтруктураСтавки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ВидыАукционовСтруктураСтавки.ЭлементСтавки = Аук_ЭлементыДанныхАукционов.Ссылка
	|ГДЕ
	|	Аук_ВидыАукционовСтруктураСтавки.Ссылка = &ВидАукциона
	|
	|УПОРЯДОЧИТЬ ПО
	|	Аук_ВидыАукционовСтруктураСтавки.НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции

Функция ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(пВидАукциона) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционовСтруктураНачальныхДанных.ЭлементДанных КАК Элемент,
	|	Аук_ЭлементыДанныхАукционов.Описание КАК Описание,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование,
	|	Аук_ЭлементыДанныхАукционов.ТипЗначения КАК ТипЗначения,
	|	Аук_ЭлементыДанныхАукционов.Представление КАК ЭлементПредставление,
	|	Аук_ЭлементыДанныхАукционов.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Аук_ВидыАукционов.СтруктураНачальныхДанных КАК Аук_ВидыАукционовСтруктураНачальныхДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ВидыАукционовСтруктураНачальныхДанных.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|ГДЕ
	|	Аук_ВидыАукционовСтруктураНачальныхДанных.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции

Функция ОписаниеРасчетаПромежуточныхИтоговПоВидуАукциона(пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционов.ПромежуточныйИтогВариантВыбора КАК ПромежуточныйИтогВариантВыбора,
	|	Аук_ВидыАукционов.ПромежуточныйИтогФормула КАК ПромежуточныйИтогФормула,
	|	Аук_ВидыАукционов.ПромежуточныйИтогШаблонРезультата КАК ПромежуточныйИтогШаблонРезультата
	|ИЗ
	|	Справочник.Аук_ВидыАукционов КАК Аук_ВидыАукционов
	|ГДЕ
	|	Аук_ВидыАукционов.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
	
КонецФункции	

Функция ФормулаРасчетаПромежуточногоИтогаСУчетомТехническихНаименований(пВидАукциона) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционов.ПромежуточныйИтогФормула КАК ПромежуточныйИтогФормула
	|ИЗ
	|	Справочник.Аук_ВидыАукционов КАК Аук_ВидыАукционов
	|ГДЕ
	|	Аук_ВидыАукционов.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Возврат ЗаменитьЭлементыНаТехническиеНаименования(Выборка.ПромежуточныйИтогФормула);
	
КонецФункции

Функция ШаблонРезультатПромежуточногоИтогаСУчетомТехническихНаименований(пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционов.ПромежуточныйИтогШаблонРезультата КАК ПромежуточныйИтогШаблонРезультата
	|ИЗ
	|	Справочник.Аук_ВидыАукционов КАК Аук_ВидыАукционов
	|ГДЕ
	|	Аук_ВидыАукционов.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Возврат ЗаменитьЭлементыНаТехническиеНаименования(Выборка.ПромежуточныйИтогШаблонРезультата);
	
КонецФункции

Функция СписокЭтаповПоУмолчаниюПоВидуАукциона(пВидАукциона) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционовЭтапыПоУмолчанию.Этап КАК Этап,
	|	Аук_ВидыАукционовЭтапыПоУмолчанию.ДлительностьДни КАК ДлительностьДни,
	|	Аук_ВидыАукционовЭтапыПоУмолчанию.ДлительностьЧасы КАК ДлительностьЧасы
	|ИЗ
	|	Справочник.Аук_ВидыАукционов.ЭтапыПоУмолчанию КАК Аук_ВидыАукционовЭтапыПоУмолчанию
	|ГДЕ
	|	Аук_ВидыАукционовЭтапыПоУмолчанию.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции	

Функция КлючевыеДанныеВидаАукциона(пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционов.ВидОперации КАК ВидОперации,
	|	Аук_ВидыАукционов.Продукт КАК Продукт,
	|	Аук_ВидыАукционов.СРаспределениемСуммы КАК СРаспределениемСуммы
	|ИЗ
	|	Справочник.Аук_ВидыАукционов КАК Аук_ВидыАукционов
	|ГДЕ
	|	Аук_ВидыАукционов.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);	
	
КонецФункции	

Функция ОписаниеРасчетаОкончательныхИтоговПоВидуАукциона(пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционов.ОкончательныйИтогФормула КАК Формула,
	|	Аук_ВидыАукционов.ОкончательныйИтогРаспределяемаяСуммаОсновная КАК РаспределяемаяСуммаОсновная,
	|	Аук_ВидыАукционов.ОкончательныйИтогРаспределяемаяСуммаДополнительная КАК РаспределяемаяСуммаДополнительная
	|ИЗ
	|	Справочник.Аук_ВидыАукционов КАК Аук_ВидыАукционов
	|ГДЕ
	|	Аук_ВидыАукционов.Ссылка = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
	
КонецФункции	

Функция ВидыАукционовСписком() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыАукционов.Ссылка КАК ВидАукциона
	|ИЗ
	|	Справочник.Аук_ВидыАукционов КАК Аук_ВидыАукционов
	|ГДЕ
	|	НЕ Аук_ВидыАукционов.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции	

Функция ЗаменитьЭлементыНаТехническиеНаименования(пСтрокаДанных)
	
	Результат = "";
	
	Если ЗначениеЗаполнено(пСтрокаДанных) Тогда
		
		// 1. Получить имена всех полей
		масРезультаты = СтрНайтиВсеПоРегулярномуВыражению(пСтрокаДанных, "(?<=\[).+(?=\])"); 
		
		Если масРезультаты.Количество() <> 0 Тогда
			
			масЭлементыФормулы = Новый Массив;
			
			Для Каждого ЭлРез Из масРезультаты Цикл
				
				масЭлементыФормулы.Добавить(ЭлРез.Значение);
				
			КонецЦикла;	
			
			// 2. Получить технические наименования по списку наименований
			сооРез = ПланыВидовХарактеристик.Аук_ЭлементыДанныхАукционов.ЭлементыДанныхАукционовПоНаименованиям(масЭлементыФормулы);
			
			Если сооРез.Количество() <> 0 Тогда
				Результат = пСтрокаДанных;
				
				// 3. Заменить
				Для Каждого ЭлСоо Из сооРез Цикл
					
					Результат = СтрЗаменить(Результат, "[" + ЭлСоо.Ключ + "]", ЭлСоо.Значение.ТехническоеНаименование);	
					
				КонецЦикла;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

#КонецОбласти


