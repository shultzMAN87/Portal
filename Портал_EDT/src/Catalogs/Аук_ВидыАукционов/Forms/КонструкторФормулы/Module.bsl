
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальнаяИнициализация(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокОперандовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЭтотОбъект.ИтоговаяФормула = СтрШаблон("%1 [%2]", ЭтотОбъект.ИтоговаяФормула, ЭтотОбъект.СписокОперандов.НайтиПоИдентификатору(ВыбраннаяСтрока).Операнд);
	
	ИтоговаяФормулаПриИзмененииСвязанныеДействия();
	
КонецПроцедуры

&НаКлиенте
Процедура ИтоговаяФормулаПриИзменении(Элемент)
	
	ИтоговаяФормулаПриИзмененииСвязанныеДействия();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ПроверитьФормулу(Команда)
	
	ОчиститьСообщения();
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ИтоговаяФормула) Тогда
		
		сткРез = ВыполнитьПроверкуФормулы();
		
		Если сткРез.Успех Тогда
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Формула корректная!'"));
			
			ОбновитьЦветФонаПоляВводаФормулы(Ложь);
			
		Иначе
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Формула имеет ошибки!'"));
			
			ОбновитьЦветФонаПоляВводаФормулы(Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ИтоговаяФормула) Тогда
		
		Если ЭтотОбъект.ФормулаПроверена Тогда			
			Закрыть(ЭтотОбъект.ИтоговаяФормула);			
		Иначе			
			сткРез = ВыполнитьПроверкуФормулы();
			
			Если сткРез.Успех Тогда
				Закрыть(Аук_ОбщийКлиент.НормализоватьСтрокуФормулы(ЭтотОбъект.ИтоговаяФормула));	
			Иначе
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Формула имеет ошибки!'"));
				
			КонецЕсли;	
		КонецЕсли;
		
	Иначе	
		стрТекстВопроса = НСтр("ru = 'Формула не заполнена. Прдолжить?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытием_Продолжить", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, стрТекстВопроса, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'")); 	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытием_Продолжить(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Закрыть(ЭтотОбъект.ИтоговаяФормула);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИтоговаяФормулаПриИзмененииСвязанныеДействия()  

	ЭтотОбъект.ФормулаПроверена = Ложь;
	
	ОбновитьЦветФонаПоляВводаФормулы();
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьЦветФонаПоляВводаФормулы(пЕстьОшибка = Неопределено)
		
	Если пЕстьОшибка = Ложь Тогда
		
		Элементы.ИтоговаяФормула.ЦветФона = WebЦвета.СветлоЗеленый;
		
	ИначеЕсли пЕстьОшибка = Истина Тогда 
		
		Элементы.ИтоговаяФормула.ЦветФона = WebЦвета.СветлоРозовый;
		
	Иначе
		
		Элементы.ИтоговаяФормула.ЦветФона = Новый Цвет();
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура НачальнаяИнициализация()
	
	Для Каждого ЭлМас Из Параметры.Операнды Цикл
		
		НовСтрока = ЭтотОбъект.СписокОперандов.Добавить();
		
		НовСтрока.Операнд = ЭлМас;
		
	КонецЦикла;
	
	ЭтотОбъект.ИтоговаяФормула = Параметры.ИсходнаяФормула;
	
	Если Параметры.Свойство("ЭтоФормулаРасчетаОкончательныхИтогов") Тогда
		
		ЭтотОбъект.ЭтоФормулаРасчетаОкончательныхИтогов = Параметры.ЭтоФормулаРасчетаОкончательныхИтогов;
		
	КонецЕсли;	 
	
КонецПроцедуры

&НаКлиенте
Функция ВыполнитьПроверкуФормулы()
	
	сткПараметры = Аук_ОбщийКлиент.ПараметрыПроверкиФормулы();
	
	сткПараметры.ИсходнаяФормула 			= ЭтотОбъект.ИтоговаяФормула;
	сткПараметры.СписокВозможныхОперандов	= Аук_ОбщийКлиент.КолонкаТабличнойЧастиВМассив(ЭтотОбъект, "СписокОперандов", "Операнд");
	
	Возврат Аук_ОбщийКлиент.ПроверитьФормулуРасчета(сткПараметры);
	
КонецФункции

#КонецОбласти
	






	


