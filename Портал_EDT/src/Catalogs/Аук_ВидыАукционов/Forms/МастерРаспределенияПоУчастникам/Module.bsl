
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	НачальнаяИнициализация();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УчастникиПриАктивизацииСтроки(Элемент)
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ТекДанные.Участник) Тогда	
			УстановитьОтборПоВыделенномуУчастнику(ТекДанные.Участник);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УчастникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Копирование;
КонецПроцедуры

&НаКлиенте
Процедура УчастникиПередУдалением(Элемент, Отказ)
	УдалитьКонтактныхЛицПоУчастнику(Элемент.ТекущиеДанные.Участник);
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаУчастниковПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		
		ТекДанные = Элемент.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено Тогда
			
			Если Элемент.ОтборСтрок <> Неопределено И Элемент.ОтборСтрок.Свойство("Участник") Тогда
				
				ТекДанные.Участник = Элемент.ОтборСтрок.Участник;
				
			КонецЕсли;	
	
		КонецЕсли;	
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаУчастниковКонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элементы.КонтактныеЛицаУчастников.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		Оповещение = Новый ОписаниеОповещения("ВыборКонтактногоЛица_Продолжить", ЭтотОбъект, ТекДанные);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор",				Новый Структура("Владелец", ТекДанные.Участник));
		ПараметрыФормы.Вставить("МножественныйВыбор",	Истина);
		
		ОткрытьФорму("Справочник.Аук_КонтактныеЛицаУчастниковАукционов.ФормаВыбора", ПараметрыФормы,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыборКонтактногоЛица_Продолжить(Результат, пТекДанные) Экспорт

	Если ЗначениеЗаполнено(Результат) Тогда
		Если ТипЗнч(Результат) = Тип("Массив") Тогда
			
			ОбработатьМножественныйВыборКонтактныхЛиц(Результат, пТекДанные);
			
		ИначеЕсли ТипЗнч(Результат) = Тип("СправочникСсылка.Аук_КонтактныеЛицаУчастниковАукционов") Тогда 	
			
			ОбработатьЕдиничныйВыборКонтактногоЛица(Результат, пТекДанные);	
			
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаУчастниковКонтактноеЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элементы.КонтактныеЛицаУчастников.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		ОбработатьЕдиничныйВыборКонтактногоЛица(ВыбранноеЗначение, ТекДанные);
			
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиУчастникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ЭтотОбъект.Участники.НайтиСтроки(Новый Структура("Участник", ВыбранноеЗначение)).Количество() = 0 Тогда
		ТекДанные = Элементы.Участники.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено Тогда
			ТекДанные.Участник = ВыбранноеЗначение;	
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	сткРез = ОбновитьДанныеУчастников(ПараметрыЗаписиИзДанныхФормы());
	
	Если Не сткРез.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(сткРез.Результат);
	Иначе
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаДобавить(Команда)
	УчастникиТекДанные = Элементы.Участники.ТекущиеДанные;
	
	Если УчастникиТекДанные = Неопределено Или Не ЗначениеЗаполнено(УчастникиТекДанные.Участник) Тогда 

		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите вид аукциона'"));
		
	Иначе	
		
		ДобавитьСтрокиКонтактныхЛиц(УчастникиТекДанные.Участник);
		
		УстановитьОтборПоВыделенномуУчастнику(УчастникиТекДанные.Участник);
											   
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура ОбработатьЕдиничныйВыборКонтактногоЛица(пВыбранноеЗначение, пТекДанные)
	
	Если ЭтотОбъект.КонтактныеЛицаУчастников.НайтиСтроки(Новый Структура("КонтактноеЛицо", пВыбранноеЗначение)).Количество() = 0 Тогда
		пТекДанные.КонтактноеЛицо = пВыбранноеЗначение;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьМножественныйВыборКонтактныхЛиц(пМасВыбранныеЗначения, пТекДанные)
	масДобавление = Новый Массив;
	Для Каждого ЭлРезультат Из пМасВыбранныеЗначения Цикл
		Если ЭтотОбъект.КонтактныеЛицаУчастников.НайтиСтроки(Новый Структура("Участник, КонтактноеЛицо", пТекДанные.Участник, ЭлРезультат)).Количество() = 0 Тогда
			масДобавление.Добавить(ЭлРезультат);	
		КонецЕсли;	
	КонецЦикла;
	
	Если масДобавление.Количество() <> 0 Тогда
		
		Для Сч = 0 По масДобавление.ВГраница() Цикл
			Если Сч = 0 Тогда
				пТекДанные.КонтактноеЛицо = масДобавление[Сч];	
			Иначе
				НовСтрока = ЭтотОбъект.КонтактныеЛицаУчастников.Добавить();
				
				НовСтрока.Участник = пТекДанные.Участник;
				НовСтрока.КонтактноеЛицо = масДобавление[Сч];
			КонецЕсли;	
		КонецЦикла;	
		
	КонецЕсли;	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ОбновитьДанныеУчастников(Знач пСткПараметры)
	Возврат Справочники.Аук_УчастникиАукционов.ОбновитьВидыАукционовВЭлементахУчастников(пСткПараметры);	
КонецФункции	

&НаКлиенте
Функция ПараметрыЗаписиИзДанныхФормы()
    сткПараметры = Новый Структура;
	
	сткПараметры.Вставить("ВидАукциона",	ЭтотОбъект.ВидАукциона);
	
	сооУчастники = Новый Соответствие;
	
	Для Каждого СтрокаТзУчастники Из ЭтотОбъект.Участники Цикл
		масСтрокиТзКонтЛица = ЭтотОбъект.КонтактныеЛицаУчастников.НайтиСтроки(Новый Структура("Участник", СтрокаТзУчастники.Участник));
		
		Если масСтрокиТзКонтЛица.Количество() <> 0 Тогда
			масКонтЛица = Новый Массив;
			
			Для Каждого СтрокаТзКонтЛицо Из масСтрокиТзКонтЛица Цикл
				Если ЗначениеЗаполнено(СтрокаТзКонтЛицо.КонтактноеЛицо) Тогда
					масКонтЛица.Добавить(СтрокаТзКонтЛицо.КонтактноеЛицо);	     
				КонецЕсли;
			КонецЦикла;	
			
			сооУчастники.Вставить(СтрокаТзУчастники.Участник, масКонтЛица);
		КонецЕсли;	
	КонецЦикла;
	
	сткПараметры.Вставить("УчастникиИКонтактныеЛица", сооУчастники);
	
	Возврат сткПараметры;
КонецФункции

&НаКлиенте
Процедура УстановитьОтборПоВыделенномуУчастнику(пУчастник)
	Элементы.КонтактныеЛицаУчастников.ОтборСтрок = Новый ФиксированнаяСтруктура("Участник", пУчастник);	
КонецПроцедуры

&НаСервере
Процедура НачальнаяИнициализация()
	РезультатЗапроса = Справочники.Аук_УчастникиАукционов.УчастникиИИхКонтактныеЛица();
	
    тзУчастникиТемп = РеквизитФормыВЗначение("Участники"); 
	
	тзКонтактныеЛицаУчастников = РеквизитФормыВЗначение("КонтактныеЛицаУчастников");
	
	ВыборкаУчастники = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаУчастники.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(тзУчастникиТемп.Добавить(), ВыборкаУчастники); 
		
		ВыборкаДетальная = ВыборкаУчастники.Выбрать();
		
		Пока ВыборкаДетальная.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(тзКонтактныеЛицаУчастников.Добавить(), ВыборкаДетальная);	
		КонецЦикла;	
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(тзУчастникиТемп, "Участники");
	ЗначениеВРеквизитФормы(тзКонтактныеЛицаУчастников, "КонтактныеЛицаУчастников");
	
	ЭтотОбъект.ВидАукциона = Параметры.ВидАукциона;
	
КонецПроцедуры	

&НаКлиенте
Процедура УдалитьКонтактныхЛицПоУчастнику(пУчастник)
	
	масСтрокиНаУдаление = ЭтотОбъект.КонтактныеЛицаУчастников.НайтиСтроки(Новый Структура("Участник", пУчастник));
	
	Для Каждого ЭлСтрока Из масСтрокиНаУдаление Цикл
		
		ЭтотОбъект.КонтактныеЛицаУчастников.Удалить(ЭлСтрока);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокиКонтактныхЛиц(пУчастник)
		
	НовСтрока = ЭтотОбъект.КонтактныеЛицаУчастников.Добавить();
	
	НовСтрока.Участник = пУчастник;
		
КонецПроцедуры



#КонецОбласти


