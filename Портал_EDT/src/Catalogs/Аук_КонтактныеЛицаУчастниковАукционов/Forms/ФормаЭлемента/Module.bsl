
#Область Перемнные

&НаКлиенте
Перем УдаленныйЭлементНастройки;

&НаКлиенте
Перем глКэшНастроекКаналовВзаимодействия;

#КонецОбласти

#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Параметры;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СпозиционироватьсяНаТекущемКанале();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Наименование = Объект.Фамилия + " " + Объект.Имя + " " + Объект.Отчество;
	
	ПроверитьДанныеФормыПередЗаписью(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ЭтотОбъект.ВладелецФормы <> Неопределено Тогда
		
		Закрыть(Объект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КаналыВзаимодействия1ПередУдалением(Элемент, Отказ)
	
	УдалитьНастройкиКаналовВзаимодействияПоВидуКаналовВзаимодействия(Элемент.ТекущиеДанные.КаналВзаимодействия);
	
КонецПроцедуры

&НаКлиенте
Процедура КаналыВзаимодействия1ПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ТекДанные.КаналВзаимодействия) Тогда
			
			УстановитьОтборПоВыделенномуКаналу(ТекДанные.КаналВзаимодействия);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиКаналовВзаимодействия1ПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		КаналыВзаимодействияТекДанные = Элементы.КаналыВзаимодействия1.ТекущиеДанные;
		
		Элемент.ТекущиеДанные.КаналВзаимодействия = КаналыВзаимодействияТекДанные.КаналВзаимодействия;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КаналыВзаимодействия1КаналВзаимодействияОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
		
	сткОтбор = Новый Структура("КаналВзаимодействия", ВыбранноеЗначение); 
	
	Если Объект.КаналыВзаимодействия.НайтиСтроки(сткОтбор).Количество() <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Возврат;
		
	Иначе
		
		ТекДанные = Элементы.КаналыВзаимодействия1.ТекущиеДанные;
		
		ТекДанные.Текущий = Ложь;
		
		УдалитьНастройкиКаналовВзаимодействияПоВидуКаналовВзаимодействия(ТекДанные.КаналВзаимодействия);
		
		ДобавитьСтрокиНастроекКаналаВзаимодействия(ВыбранноеЗначение);
		
	КонецЕсли;
	
	УстановитьОтборКонтактныхЛиц(ВыбранноеЗначение);
		
КонецПроцедуры

&НаКлиенте
Процедура КаналыВзаимодействия1ТекущийПриИзменении(Элемент)
	
	ТекДанные = Элементы.КаналыВзаимодействия1.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		Если ТекДанные.Текущий Тогда
			
			Если ОбработчикКаналаВзаимодействияСуществует(ТекДанные.КаналВзаимодействия) Тогда
				СнятьФлагСДругихСтрок(ТекДанные.КаналВзаимодействия);	
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Обрабочик канала не реализован!'"));
				
				ТекДанные.Текущий = Ложь;	
			КонецЕсли;	
				
		КонецЕсли;	
			
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура КаналыВзаимодействия1ПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Копирование;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиКаналовВзаимодействия1ПослеУдаления(Элемент)
	
	Если УдаленныйЭлементНастройки <> Неопределено Тогда
		
		МасСтрокиТч = Объект.НастройкиКаналовВзаимодействия.НайтиСтроки(УдаленныйЭлементНастройки);
		
		Для Каждого СтрокаТч Из МасСтрокиТч Цикл
			
			Объект.НастройкиКаналовВзаимодействия.Удалить(СтрокаТч);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиКаналовВзаимодействия1ПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Не Копирование Тогда
		
		ДобавитьНастройкиКаналаПоСоставуЭлементов();
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ДобавитьКаналВзаимодействия(Команда)
	
	Объект.КаналыВзаимодействия.Добавить();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНастройкуКаналаВзаимодействия1(Команда)
	
	ДобавитьНастройкиКаналаПоСоставуЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиКаналовВзаимодействия1ПередУдалением(Элемент, Отказ)
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	УдаленныйЭлементНастройки = Новый Структура("КаналВзаимодействия, НПП", ТекДанные.КаналВзаимодействия, ТекДанные.НПП);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СпозиционироватьсяНаТекущемКанале()
    масСтрокиКаналыТекущие = Объект.КаналыВзаимодействия.НайтиСтроки(Новый Структура("Текущий", Истина));
	
	Если масСтрокиКаналыТекущие.Количество() <> 0 Тогда
		Элементы.КаналыВзаимодействия1.ТекущаяСтрока = масСтрокиКаналыТекущие[0].ПолучитьИдентификатор();
		
		УстановитьОтборПоВыделенномуКаналу(масСтрокиКаналыТекущие[0].КаналВзаимодействия);
	КонецЕсли;	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьОтборПоВыделенномуКаналу(пКаналВзаимодействия)
	УстановитьОтборКонтактныхЛиц(пКаналВзаимодействия);
	
	УстановитьДоступностьКнопокДобавленияУдаленияНастроек(пКаналВзаимодействия);	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ОбработчикКаналаВзаимодействияСуществует(Знач пКаналВзаимодействия)
	Возврат Справочники.Аук_КаналыВзаимодействия.ЕстьОбработчикКаналаВзаимодействия(пКаналВзаимодействия);	
КонецФункции	

&НаКлиенте
Процедура ДобавитьНастройкиКаналаПоСоставуЭлементов()

	КаналыВзаимодействияТекДанные = Элементы.КаналыВзаимодействия1.ТекущиеДанные;
	
	Если КаналыВзаимодействияТекДанные = Неопределено Или Не ЗначениеЗаполнено(КаналыВзаимодействияТекДанные.КаналВзаимодействия) Тогда 

		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите канал взаимодействия'"));
		
	Иначе
		
		сткРез = ОписаниеОсновныхСвойствКаналаВзаимодействия(КаналыВзаимодействияТекДанные.КаналВзаимодействия);
		
		Если сткРез.НесколькоЗначенийЭлементовНастройки Тогда
			ДобавитьСтрокиНастроекКаналаВзаимодействия(КаналыВзаимодействияТекДанные.КаналВзаимодействия);	
		КонецЕсли;
		
		УстановитьОтборКонтактныхЛиц(КаналыВзаимодействияТекДанные.КаналВзаимодействия);
											   
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьДоступностьКнопокДобавленияУдаленияНастроек(пКаналВзаимодействия)

	сткРез = ОписаниеОсновныхСвойствКаналаВзаимодействия(пКаналВзаимодействия);
	
	Элементы.НастройкиКаналовВзаимодействия1ДобавитьНастройкуКаналаВзаимодействия.Доступность	= сткРез.НесколькоЗначенийЭлементовНастройки;
	Элементы.НастройкиКаналовВзаимодействия1КонтекстноеМенюДобавить.Доступность					= сткРез.НесколькоЗначенийЭлементовНастройки;
	Элементы.НастройкиКаналовВзаимодействия1КонтекстноеМенюУдалить.Доступность					= сткРез.НесколькоЗначенийЭлементовНастройки;
	
КонецПроцедуры	

&НаКлиенте
Процедура ДобавитьСтрокиНастроекКаналаВзаимодействия(пКаналВзаимодействия)
	
	масСписокНастроек = СписокСвязанныхНастроекКаналаВзаимодействия(пКаналВзаимодействия);
	
	чслНПП = НовыйНПП(масСписокНастроек, пКаналВзаимодействия);// сделал так, чтобы не было косяков с нумерацией НПП
	
	Для Каждого ЭлНастройка Из масСписокНастроек Цикл
		
		НовСтрока = Объект.НастройкиКаналовВзаимодействия.Добавить();
		
		НовСтрока.НПП					= чслНПП;
		НовСтрока.КаналВзаимодействия	= пКаналВзаимодействия;
		НовСтрока.ЭлементНастройки		= ЭлНастройка;
		
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиенте
Функция НовыйНПП(пМасСписокНастроек, пКаналВзаимодействия)
	
	масЗанятыеНПП = ЗанятыеНПП(пМасСписокНастроек, пКаналВзаимодействия);
	
	чслРезультат = 0;
	
	Пока Истина Цикл
		
		Если масЗанятыеНПП.Найти(чслРезультат) = Неопределено Тогда
			
			Прервать;
			
		КонецЕсли;	
		
		чслРезультат = чслРезультат + 1;
		
	КонецЦикла;	
	
	Возврат чслРезультат;
	
КонецФункции	

&НаКлиенте
Функция ЗанятыеНПП(пМасСписокНастроек, пКаналВзаимодействия)

	масРезультат = Новый Массив;
	
	Если пМасСписокНастроек.Количество() <> 0 Тогда
		
		сткОтбор = Новый Структура("КаналВзаимодействия, ЭлементНастройки", пКаналВзаимодействия, пМасСписокНастроек[0]);
	
		масСтрокиТч = Объект.НастройкиКаналовВзаимодействия.НайтиСтроки(сткОтбор);
		
		Для Каждого ЭлСтрокаТч Из масСтрокиТч Цикл
			
			масРезультат.Добавить(ЭлСтрокаТч.НПП);
			
		КонецЦикла;	
		
	КонецЕсли;	
	
	Возврат масРезультат;
	
КонецФункции	

&НаКлиенте
Функция ОписаниеОсновныхСвойствКаналаВзаимодействия(пКаналВзаимодействия)

	Возврат ПолныеНастройкиКаналаВзаимодействия(пКаналВзаимодействия).СвойстваКанала;   
	
КонецФункции

&НаКлиенте
Функция СписокСвязанныхНастроекКаналаВзаимодействия(пКаналВзаимодействия)

	Возврат ПолныеНастройкиКаналаВзаимодействия(пКаналВзаимодействия).СвязанныеНастройки;
	
КонецФункции

&НаКлиенте
Функция ПолныеНастройкиКаналаВзаимодействия(пКаналВзаимодействия)
	
	Если глКэшНастроекКаналовВзаимодействия = Неопределено Тогда
		
		сткНастройкиКанала = ПолноеОписаниеСвойствКаналаВзаимодействияНаСервере(пКаналВзаимодействия);
		
		глКэшНастроекКаналовВзаимодействия = Новый Соответствие;
		
		глКэшНастроекКаналовВзаимодействия.Вставить(пКаналВзаимодействия, сткНастройкиКанала);
		
	Иначе
		
		сткНастройкиКанала = глКэшНастроекКаналовВзаимодействия.Получить(пКаналВзаимодействия);
		
		Если сткНастройкиКанала = Неопределено Тогда
			
			сткНастройкиКанала = ПолноеОписаниеСвойствКаналаВзаимодействияНаСервере(пКаналВзаимодействия);
			
			глКэшНастроекКаналовВзаимодействия.Вставить(пКаналВзаимодействия, сткНастройкиКанала);
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат сткНастройкиКанала; 
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолноеОписаниеСвойствКаналаВзаимодействияНаСервере(Знач пКаналВзаимодействия)

	Возврат Справочники.Аук_КаналыВзаимодействия.ПолноеОписаниеСвойствКаналаВзаимодействия(пКаналВзаимодействия);
	
КонецФункции

&НаКлиенте
Процедура СнятьФлагСДругихСтрок(пТекКаналВзаимодействия)

	масСтрокиТч = Объект.КаналыВзаимодействия.НайтиСтроки(Новый Структура("Текущий", Истина));
	
	Для Каждого СтрокаТч Из масСтрокиТч Цикл
		
		Если СтрокаТч.КаналВзаимодействия <> пТекКаналВзаимодействия Тогда
			
			СтрокаТч.Текущий = Ложь;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьДанныеФормыПередЗаписью(пОтказ)
	
	масОшибки = Новый Массив;
	
	Для Каждого СтрокаТч Из Объект.КаналыВзаимодействия Цикл
		
		сткОтбор = Новый Структура("КаналВзаимодействия", СтрокаТч.КаналВзаимодействия);
		
		Если Объект.НастройкиКаналовВзаимодействия.НайтиСтроки(сткОтбор).Количество() = 0 Тогда
			масОшибки.Добавить(СтрШаблон(НСтр("ru = 'Для канала <%1> не указаны настройки'"), СтрокаТч.КаналВзаимодействия));	
		КонецЕсли;	
		
	КонецЦикла;
	
	EMailСсылка = Аук_ОбщийВызовСервера.ЭлементНастроек_Email();
	
	Если EMailСсылка <> Неопределено Тогда
		
		масСтрокиТч = Объект.НастройкиКаналовВзаимодействия.НайтиСтроки(Новый Структура("ЭлементНастройки", EMailСсылка));
		
		Для Каждого СтрокаТч Из масСтрокиТч Цикл
			
			Если Не Аук_ОбщийКлиентСервер.EmailКорректный(СтрокаТч.ЗначениеНастройки) Тогда
				
				масОшибки.Добавить(СтрШаблон(НСтр("ru = 'Email <%1> некорректный'"), СтрокаТч.ЗначениеНастройки));
				
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Объект.КаналыВзаимодействия.НайтиСтроки(Новый Структура("Текущий", Истина)).Количество() = 0 Тогда
		
		масОшибки.Добавить(НСтр("ru = 'Хоть один канал должен отмечен текущим'"));
		
	КонецЕсли;	
	
	Если масОшибки.Количество() <> 0 Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка. %1'"), СтрСоединить(масОшибки, Символы.ПС)),,,, пОтказ);
		
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура УдалитьНастройкиКаналовВзаимодействияПоВидуКаналовВзаимодействия(пКаналВзаимодействия)
	
	масСтрокиНаУдаление = Объект.НастройкиКаналовВзаимодействия.НайтиСтроки(Новый Структура("КаналВзаимодействия", пКаналВзаимодействия));
	
	Для Каждого ЭлСтрока Из масСтрокиНаУдаление Цикл
		
		Объект.НастройкиКаналовВзаимодействия.Удалить(ЭлСтрока);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборКонтактныхЛиц(пКаналВзаимодействия)
	
	Элементы.НастройкиКаналовВзаимодействия1.ОтборСтрок = Новый ФиксированнаяСтруктура("КаналВзаимодействия", пКаналВзаимодействия);
	
КонецПроцедуры	

#КонецОбласти
