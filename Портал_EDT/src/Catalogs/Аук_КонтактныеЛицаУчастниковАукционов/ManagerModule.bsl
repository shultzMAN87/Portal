
#Область ПрограммныйИнтерфейс

Функция ТекущиеКонтактныеДанныеКонтактныхЛиц(пМасКонтактныеЛица) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_КонтактныеЛицаУчастниковАукционов.Ссылка КАК КонтактноеЛицо,
	|	Аук_КонтактныеЛицаУчастниковАукционов.Владелец КАК Участник,
	|	КонтактныеЛицаУчастниковКаналыВзаимодействия.КаналВзаимодействия КАК КаналВзаимодействия,
	|	КонтактныеЛицаУчастниковНастройкиКаналовВзаимодействия.НПП КАК НПП,
	|	КонтактныеЛицаУчастниковНастройкиКаналовВзаимодействия.ЭлементНастройки КАК ЭлементНастройки,
	|	КонтактныеЛицаУчастниковНастройкиКаналовВзаимодействия.ЗначениеНастройки КАК ЗначениеНастройки,
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	Справочник.Аук_КонтактныеЛицаУчастниковАукционов КАК Аук_КонтактныеЛицаУчастниковАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_КонтактныеЛицаУчастниковАукционов.КаналыВзаимодействия КАК КонтактныеЛицаУчастниковКаналыВзаимодействия
	|		ПО Аук_КонтактныеЛицаУчастниковАукционов.Ссылка = КонтактныеЛицаУчастниковКаналыВзаимодействия.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_КонтактныеЛицаУчастниковАукционов.НастройкиКаналовВзаимодействия КАК КонтактныеЛицаУчастниковНастройкиКаналовВзаимодействия
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыНастроекКаналовВзаимодействия КАК Аук_ЭлементыНастроекКаналовВзаимодействия
	|			ПО КонтактныеЛицаУчастниковНастройкиКаналовВзаимодействия.ЭлементНастройки = Аук_ЭлементыНастроекКаналовВзаимодействия.Ссылка
	|		ПО (КонтактныеЛицаУчастниковКаналыВзаимодействия.КаналВзаимодействия = КонтактныеЛицаУчастниковНастройкиКаналовВзаимодействия.КаналВзаимодействия)
	|			И (КонтактныеЛицаУчастниковКаналыВзаимодействия.Ссылка = КонтактныеЛицаУчастниковНастройкиКаналовВзаимодействия.Ссылка)
	|ГДЕ
	|	Аук_КонтактныеЛицаУчастниковАукционов.Ссылка В(&КонтактныеЛица)
	|	И КонтактныеЛицаУчастниковКаналыВзаимодействия.Текущий
	|ИТОГИ ПО
	|	КонтактноеЛицо,
	|	КаналВзаимодействия,
	|	НПП";
	
	Запрос.УстановитьПараметр("КонтактныеЛица", пМасКонтактныеЛица);
	
	Возврат СформироватьКоллекциюНстроекКаналовСвязиВРазрезеУчастникаИЕгоКонтактногоЛица(Запрос); 
	
КонецФункции

Функция ТекущийКаналВзаимодействияПоСпискуКонтактныхЛиц(пМасКонтактныеЛица) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_КонтактныеЛицаУчастниковАукционовКаналыВзаимодействия.КаналВзаимодействия КАК КаналВзаимодействия,
	|	Аук_КонтактныеЛицаУчастниковАукционовКаналыВзаимодействия.Ссылка КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.Аук_КонтактныеЛицаУчастниковАукционов.КаналыВзаимодействия КАК Аук_КонтактныеЛицаУчастниковАукционовКаналыВзаимодействия
	|ГДЕ
	|	Аук_КонтактныеЛицаУчастниковАукционовКаналыВзаимодействия.Ссылка В(&КонтактныеЛица)
	|	И Аук_КонтактныеЛицаУчастниковАукционовКаналыВзаимодействия.Текущий";
	
	Запрос.УстановитьПараметр("КонтактныеЛица", пМасКонтактныеЛица);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	сооРезультат = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		сооРезультат.Вставить(Выборка.КонтактноеЛицо, Выборка.КаналВзаимодействия);	
	КонецЦикла;	
	
	Возврат сооРезультат;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьКоллекциюНстроекКаналовСвязиВРазрезеУчастникаИЕгоКонтактногоЛица(пЗапрос) 
	
	сооРезультат = Новый Соответствие;
	
	//масРезультат = Новый Массив;
	
	ВыборкаКонтактныеЛица = пЗапрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКонтактныеЛица.Следующий() Цикл
		
		сткРезультат = Новый Структура("КонтактноеЛицо, Участник, КаналВзаимодействия, НастройкиКаналаВзаимодействия",,,, Новый Массив);
		
		ЗаполнитьЗначенияСвойств(сткРезультат, ВыборкаКонтактныеЛица);
		
		ВыборкаКаналВзаимодействия = ВыборкаКонтактныеЛица.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
		Пока ВыборкаКаналВзаимодействия.Следующий() Цикл
			
			сткРезультат.КаналВзаимодействия = ВыборкаКаналВзаимодействия.КаналВзаимодействия;
			
			ВыборкаНПП = ВыборкаКаналВзаимодействия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНПП.Следующий() Цикл
				
				ВыборкаДетальная = ВыборкаНПП.Выбрать();
				
				сткНастройки = Новый Структура;
				
				Пока ВыборкаДетальная.Следующий() Цикл
					
					сткНастройки.Вставить(ВыборкаДетальная.ТехническоеНаименование, ВыборкаДетальная.ЗначениеНастройки);	
					
				КонецЦикла;
				
				сткРезультат.НастройкиКаналаВзаимодействия.Добавить(сткНастройки);
					
			КонецЦикла;
			
		КонецЦикла; 
		
		сооРезультат.Вставить(ВыборкаКонтактныеЛица.КонтактноеЛицо, сткРезультат);
		
	КонецЦикла;	
	
	Возврат сооРезультат;
	
КонецФункции	

#КонецОбласти