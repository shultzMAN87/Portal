
#Область ПрограммныйИнтерфейс

Функция ПолноеОписаниеСвойствКаналаВзаимодействия(пКаналВзаимодействия) Экспорт

	сткРезультат = Новый Структура;
	
	сткРезультат.Вставить("СвойстваКанала",		ОписаниеОсновныхСвойствКаналаВзаимодействия(пКаналВзаимодействия));
	сткРезультат.Вставить("СвязанныеНастройки",	СписокСвязанныхНастроекКаналаВзаимодействия(пКаналВзаимодействия));
	
	Возврат сткРезультат;
	
КонецФункции

Функция ОписаниеОсновныхСвойствКаналаВзаимодействия(пКаналВзаимодействия) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_КаналыВзаимодействия.НесколькоЗначенийЭлементовНастройки КАК НесколькоЗначенийЭлементовНастройки
	|ИЗ
	|	Справочник.Аук_КаналыВзаимодействия КАК Аук_КаналыВзаимодействия
	|ГДЕ
	|	Аук_КаналыВзаимодействия.Ссылка = &КаналВзаимодействия";
	
	Запрос.УстановитьПараметр("КаналВзаимодействия", пКаналВзаимодействия);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);
		
КонецФункции

Функция СписокСвязанныхНастроекКаналаВзаимодействия(пКаналВзаимодействия) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_КаналыВзаимодействияСтруктураНастроек.ЭлементНастройки КАК ЭлементНастройки
	|ИЗ
	|	Справочник.Аук_КаналыВзаимодействия.СтруктураНастроек КАК Аук_КаналыВзаимодействияСтруктураНастроек
	|ГДЕ
	|	Аук_КаналыВзаимодействияСтруктураНастроек.Ссылка = &КаналВзаимодействия";
	
	Запрос.УстановитьПараметр("КаналВзаимодействия", пКаналВзаимодействия);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЭлементНастройки");
	
КонецФункции

Функция ОтправитьСообщениеВКанал(пСткДанныеСообщения) Экспорт
	
	масНастройкиКаналаВзаимодействия = пСткДанныеСообщения.channel_settings;
	
	Если пСткДанныеСообщения.channel = XMLЗначение(Тип("СправочникСсылка.Аук_КаналыВзаимодействия"), "9fcce16a-8798-11f0-96d7-d85ed3194db9") Тогда // почтовый неавтоматический	
		сткПараметрыПисьма = ОтправитьСообщениеВКанал_ПочтовыйНеавтоматический_Параметры();
		
		// 1. Подстановка значений параметров в шаблон
		Если пСткДанныеСообщения.template_parameter_values <> Неопределено Тогда
			ЗаменитьПараметрыВШаблонеНаЗначения(пСткДанныеСообщения.subject, пСткДанныеСообщения.template_parameter_values);	
				
			ЗаменитьПереносыНаТэгиHTML(пСткДанныеСообщения.message);
			
			ЗаменитьПараметрыВШаблонеНаЗначения(пСткДанныеСообщения.message, пСткДанныеСообщения.template_parameter_values);
		КонецЕсли;
		
		сткПараметрыПисьма.Тема		= пСткДанныеСообщения.subject;
		сткПараметрыПисьма.Текст	= пСткДанныеСообщения.message;
				
		Для Каждого ЭлНастройка Из пСткДанныеСообщения.channel_settings Цикл		
			сткПараметрыПисьма.Получатели.Добавить(ЭлНастройка.EMail);		
		КонецЦикла;	
		
		Возврат ОтправитьСообщениеВКанал_ПочтовыйНеавтоматический(сткПараметрыПисьма);	
	Иначе
		
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Для канала не реализован обработчик'"));
		
	КонецЕсли;	
		
	Возврат ЭдоОбщий._Успех();
	
КонецФункции

Функция ЕстьОбработчикКаналаВзаимодействия(пСсКаналВзаимодействия) Экспорт

	Если пСсКаналВзаимодействия = XMLЗначение(Тип("СправочникСсылка.Аук_КаналыВзаимодействия"), "9fcce16a-8798-11f0-96d7-d85ed3194db9") Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКаналовВзаимодействия

Функция ОтправитьСообщениеВКанал_ПочтовыйНеавтоматический(пСткСообщение, пАккаунт = Неопределено) Экспорт
	
	Аккаунт = Аук_ОбщийСервер.АккаунтРассылкиКотировки();
	
	Попытка
		Рассылки.ОтправитьПисьмо(пСткСообщение, Аккаунт);
		
		Возврат ЭдоОбщий._Успех();
	Исключение	
		стрОшибка = ОписаниеОшибки();
		
		Возврат ЭдоОбщий._Ошибка(стрОшибка);	
	КонецПопытки;
	
КонецФункции	

Функция ОтправитьСообщениеВКанал_ПочтовыйНеавтоматический_Параметры() Экспорт
	
	сткРезультат = Новый Структура;
	
	сткРезультат.Вставить("Тема",		"");
	сткРезультат.Вставить("Текст",		"");
	сткРезультат.Вставить("Получатели", Новый Массив);
	сткРезультат.Вставить("ТипТекста",	ТипТекстаПочтовогоСообщения.HTML);
	
	Возврат сткРезультат;
	
КонецФункции
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаменитьПереносыНаТэгиHTML(пСтрСообщение)
	масСтроки = СтрРазделить(пСтрСообщение, Символы.ПС);
	
	масСтрокиТекст = Новый Массив;
	
	Для Каждого ЭлСтрока Из масСтроки Цикл
		масСтрокиТекст.Добавить(СтрШаблон("<p>%1</p>", ЭлСтрока));	
	КонецЦикла;
	
	пСтрСообщение = СтрСоединить(масСтрокиТекст, "");
КонецПроцедуры

Функция СформироватьСписокДанныхHtmlПоСтроке(пИсходнаяСтрока)
	масСтроки1 = СтрРазделить(пИсходнаяСтрока, Символы.ПС);
	
	масСтрокиТекст1 = Новый Массив;
	
	Для Каждого ЭлСтрока Из масСтроки1 Цикл
		масСтрокиТекст1.Добавить(СтрШаблон("%1<br>", ЭлСтрока));	
	КонецЦикла;;
	
	Возврат СтрСоединить(масСтрокиТекст1, "");
КонецФункции

Процедура ЗаменитьПараметрыВШаблонеНаЗначения(пСтрШаблон, пМасЗначенияПараметров)
	Для Каждого ЭлЗначениеПарам Из пМасЗначенияПараметров Цикл
		Если ЭлЗначениеПарам.NameParam = "АУКЦИОН_СТРУКТУРА_СТАВКИ" Тогда
			
			стрОбязательныеПоля = СформироватьСписокДанныхHtmlПоСтроке(ЭлЗначениеПарам.ValueParam.АУКЦИОН_СТРУКТУРА_СТАВКИ_ОБЯЗАТЕЛЬНЫЕ_ПОЛЯ);
			
			стрНеобязательныеПоля = СформироватьСписокДанныхHtmlПоСтроке(ЭлЗначениеПарам.ValueParam.АУКЦИОН_СТРУКТУРА_СТАВКИ_НЕ_ОБЯЗАТЕЛЬНЫЕ_ПОЛЯ);
			
			ИтоговыйФрагмент = стрОбязательныеПоля + "</p><p>" + стрНеобязательныеПоля;
			
			пСтрШаблон = СтрЗаменить(пСтрШаблон, "[" + ЭлЗначениеПарам.NameParam + "]", ИтоговыйФрагмент);
			
		ИначеЕсли ЭлЗначениеПарам.NameParam = "СТАВКА_ДАННЫЕ_СТАВКИ"
				  Или ЭлЗначениеПарам.NameParam = "СТАВКА_ДАННЫЕ_ПОБЕДНОЙ_СТАВКИ"
				  Или ЭлЗначениеПарам.NameParam = "АУКЦИОН_ЭТАПЫ"
		Тогда
			
			пСтрШаблон = СтрЗаменить(пСтрШаблон, "[" + ЭлЗначениеПарам.NameParam + "]", СформироватьСписокДанныхHtmlПоСтроке(ЭлЗначениеПарам.ValueParam));
			
		Иначе		
			пСтрШаблон = СтрЗаменить(пСтрШаблон, "[" + ЭлЗначениеПарам.NameParam + "]", ЭлЗначениеПарам.ValueParam);
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

#КонецОбласти

