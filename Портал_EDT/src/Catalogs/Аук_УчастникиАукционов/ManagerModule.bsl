
#Область ПрограммныйИнтерфейс

Функция УчастникиАукционаПоВидуАукциона(пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аук_УчастникиАукционовВидыАукционов.Ссылка КАК Участник
	|ИЗ
	|	Справочник.Аук_УчастникиАукционов.ВидыАукционов КАК Аук_УчастникиАукционовВидыАукционов
	|ГДЕ
	|	Аук_УчастникиАукционовВидыАукционов.ВидАукциона = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Участник");
	
КонецФункции

Функция УчастникиАукционаИИхКонтактныеЛицаПоВидуАукциона(пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_УчастникиАукционовВидыАукционов.Ссылка КАК Участник,
	|	Аук_УчастникиАукционовКонтактныеЛица.КонтактноеЛицо КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.Аук_УчастникиАукционов.ВидыАукционов КАК Аук_УчастникиАукционовВидыАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_УчастникиАукционов.КонтактныеЛица КАК Аук_УчастникиАукционовКонтактныеЛица
	|		ПО Аук_УчастникиАукционовВидыАукционов.Ссылка = Аук_УчастникиАукционовКонтактныеЛица.Ссылка
	|ГДЕ
	|	Аук_УчастникиАукционовВидыАукционов.ВидАукциона = &ВидАукциона
	|	И Аук_УчастникиАукционовКонтактныеЛица.ВидАукциона = &ВидАукциона";
	
	Запрос.УстановитьПараметр("ВидАукциона", пВидАукциона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции

Функция КонтактныеЛицаУчастникаПоВидуАукциона(пУчастники, пВидАукциона) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_УчастникиАукционовКонтактныеЛица.Ссылка КАК Участник,
	|	Аук_КонтактныеЛицаУчастниковАукционов.Ссылка КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.Аук_УчастникиАукционов.КонтактныеЛица КАК Аук_УчастникиАукционовКонтактныеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Аук_КонтактныеЛицаУчастниковАукционов КАК Аук_КонтактныеЛицаУчастниковАукционов
	|		ПО Аук_УчастникиАукционовКонтактныеЛица.КонтактноеЛицо = Аук_КонтактныеЛицаУчастниковАукционов.Ссылка
	|ГДЕ
	|	Аук_УчастникиАукционовКонтактныеЛица.Ссылка В(&Участники)
	|	И Аук_УчастникиАукционовКонтактныеЛица.ВидАукциона = &ВидАукциона
	|ИТОГИ ПО
	|	Участник";
	
	Запрос.УстановитьПараметр("Участники",		пУчастники); 
	Запрос.УстановитьПараметр("ВидАукциона",	пВидАукциона);
	
	ВыборкаУчастник = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	сооРезультат = Новый Соответствие;
	
	Пока ВыборкаУчастник.Следующий() Цикл
		
		ВыборкаДетальная = ВыборкаУчастник.Выбрать();
		
		масКонтактныеЛица = Новый Массив;
		
		Пока ВыборкаДетальная.Следующий() Цикл
			
			масКонтактныеЛица.Добавить(ВыборкаДетальная.КонтактноеЛицо);
			
		КонецЦикла;
		
		сооРезультат.Вставить(ВыборкаУчастник.Участник, масКонтактныеЛица);
		
	КонецЦикла;	
	
	Возврат сооРезультат;
	
КонецФункции

Функция УчастникиИИхКонтактныеЛица() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_УчастникиАукционов.Ссылка КАК Участник,
	|	Аук_КонтактныеЛицаУчастниковАукционов.Ссылка КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.Аук_УчастникиАукционов КАК Аук_УчастникиАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_КонтактныеЛицаУчастниковАукционов КАК Аук_КонтактныеЛицаУчастниковАукционов
	|		ПО Аук_УчастникиАукционов.Ссылка = Аук_КонтактныеЛицаУчастниковАукционов.Владелец
	|ГДЕ
	|	НЕ Аук_УчастникиАукционов.ПометкаУдаления
	|ИТОГИ ПО
	|	Участник";
	
	Возврат Запрос.Выполнить();
КонецФункции	

Функция ОбновитьВидыАукционовВЭлементахУчастников(пПараметры) Экспорт

	НачатьТранзакцию();
	Попытка
		
		Для Каждого ЭлСоо Из пПараметры.УчастникиИКонтактныеЛица Цикл
			сткДанные = Новый Структура;
			сткДанные.Вставить("ВидАукциона",		пПараметры.ВидАукциона);
			сткДанные.Вставить("Участник",			ЭлСоо.Ключ);
			сткДанные.Вставить("КонтактныеЛица",	ЭлСоо.Значение);
			
			ОбновитьВидАукционаУУчастника(сткДанные);	
		КонецЦикла;	
		
		ЗафиксироватьТранзакцию();
		
		Возврат ЭдоОбщий._Успех();
	Исключение
		ОтменитьТранзакцию();
		
		стрОшибка = ОписаниеОшибки();
		
		Возврат ЭдоОбщий._Ошибка(стрОшибка);
	КонецПопытки;	
		
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьВидАукционаУУчастника(пСткДанные)

	СпрОб = пСткДанные.Участник.ПолучитьОбъект();
	
	// 1. Добавить вид аукциона в ТЧ ВидыАукционов
	НовСтрока1 = СпрОб.ВидыАукционов.Добавить();
	
	НовСтрока1.ВидАукциона = пСткДанные.ВидАукциона;
	
	// 2. Добавить контактные лица в ТЧ КонтактныеЛица
	Для Каждого ЭлМас Из пСткДанные.КонтактныеЛица Цикл
		НовСтрока2 = СпрОб.КонтактныеЛица.Добавить();
	
		НовСтрока2.ВидАукциона		= пСткДанные.ВидАукциона;
		НовСтрока2.КонтактноеЛицо	= ЭлМас;
	КонецЦикла;
	
	сткРез = ОбщегоНазначения.ЗаписатьОбъектВБазу(СпрОб);
	
	Если Не сткРез.Успех Тогда
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти