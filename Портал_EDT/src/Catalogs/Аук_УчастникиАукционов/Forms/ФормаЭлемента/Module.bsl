
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ПараметрыЗаписи.Свойство("ЗаписатьБезПроверки") Или Не ПараметрыЗаписи.ЗаписатьБезПроверки Тогда
		
		ПроверитьДанныеПередЗаписью(Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ОбновитьКонтактныеЛица", Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидыАукционов1ПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		УстановитьОтборКонтактныхЛиц(ТекДанные.ВидАукциона);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛица1ПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	Если НоваяСтрока Тогда
		
		ТекДанные = Элемент.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено Тогда
			
			Если Элемент.ОтборСтрок <> Неопределено И Элемент.ОтборСтрок.Свойство("ВидАукциона") Тогда
				
				ТекДанные.ВидАукциона = Элемент.ОтборСтрок.ВидАукциона;
				
			КонецЕсли;	
	
		КонецЕсли;	
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидыАукционов1ПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Копирование;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыАукционов1ВидАукционаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	сткОтбор = Новый Структура("ВидАукциона", ВыбранноеЗначение); 
	
	Если Объект.ВидыАукционов.НайтиСтроки(сткОтбор).Количество() <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Возврат;
		
	Иначе
		
		ТекДанные = Элементы.ВидыАукционов1.ТекущиеДанные;
				
		УдалитьКонтактныхЛицПоВидуАукциона(ТекДанные.ВидАукциона);
		
		ДобавитьСтрокиКонтактныхЛиц(ВыбранноеЗначение);
		
	КонецЕсли;
	
	УстановитьОтборКонтактныхЛиц(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыАукционов1ПередУдалением(Элемент, Отказ)
	
	УдалитьКонтактныхЛицПоВидуАукциона(Элемент.ТекущиеДанные.ВидАукциона);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛица1КонтактноеЛицоСоздание(Элемент, СтандартнаяОбработка)
	
	ЗаписьПередСозданиеИлиВыборомНовогоКонтактногоЛица(Элемент, СтандартнаяОбработка, "ПередЗаписьюПриСозданииКонтактногоЛица");	
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛица1КонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)

	ЗаписьПередСозданиеИлиВыборомНовогоКонтактногоЛица(Элемент, СтандартнаяОбработка, "ПередЗаписьюПриВыбореКонтактногоЛица");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ДобавитьКонтактноеЛицоУчастника(Команда)
	
	ВидАукционаТекДанные = Элементы.ВидыАукционов1.ТекущиеДанные;
	
	Если ВидАукционаТекДанные = Неопределено Или Не ЗначениеЗаполнено(ВидАукционаТекДанные.ВидАукциона) Тогда 

		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите вид аукциона'"));
		
	Иначе	
		
		ДобавитьСтрокиКонтактныхЛиц(ВидАукционаТекДанные.ВидАукциона);
		
		УстановитьОтборКонтактныхЛиц(ВидАукционаТекДанные.ВидАукциона);
											   
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВсеВидыАукционов(Команда)
	ДобавитьВсеВидыАукционовВСписок();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьВсеВидыАукционовВСписок()
	
	масВидыАукционов = Аук_ОбщийВызовСервера.ВидыАукционовСписком();
	
	Аук_ОбщийКлиент.ЗаполнитьТаблицуДаннымиМассиваСтруктур(Объект.ВидыАукционов, масВидыАукционов, "ВидАукциона");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьПередСозданиеИлиВыборомНовогоКонтактногоЛица(пЭлемент, пСтандартнаяОбработка, пИмяОповещения)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		пСтандартнаяОбработка = Ложь;
		
		ТекДанные = Элементы.КонтактныеЛица1.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено Тогда
			сткДопПараметры = Новый Структура("Элемент, ТекущаяСтрока", пЭлемент, Элементы.КонтактныеЛица1.ТекущаяСтрока);
			
			Оповещение = Новый ОписаниеОповещения(пИмяОповещения, ЭтотОбъект, сткДопПараметры);
			
			ТекстВопроса = НСтр("ru = 'Для продолжения необходимо записать элемент. Продолжить?'");
			
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПередЗаписьюПриСозданииКонтактногоЛица(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда	
		
		Если ПроверитьЗаполнение() Тогда
			
			ЗаписатьДанныеВБазуБезПроверки();
			
			Оповещение = Новый ОписаниеОповещения("ВыборКонтактногоЛица_Продолжить", ЭтотОбъект, ДополнительныеПараметры);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения",	Новый Структура("Владелец", Объект.Ссылка));
			ПараметрыФормы.Вставить("ПараметрыВыбора",		Новый Структура("Владелец", Объект.Ссылка));
			
			ОткрытьФорму("Справочник.Аук_КонтактныеЛицаУчастниковАукционов.ФормаОбъекта", ПараметрыФормы,
							ДополнительныеПараметры.Элемент,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПриВыбореКонтактногоЛица(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда	
		
		Если ПроверитьЗаполнение() Тогда
			
			ЗаписатьДанныеВБазуБезПроверки();
			
			Оповещение = Новый ОписаниеОповещения("ВыборКонтактногоЛица_Продолжить", ЭтотОбъект, ДополнительныеПараметры);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Отбор",	Новый Структура("Владелец", Объект.Ссылка));
			
			ОткрытьФорму("Справочник.Аук_КонтактныеЛицаУчастниковАукционов.ФормаВыбора", ПараметрыФормы,
							ДополнительныеПараметры.Элемент,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКонтактногоЛица_Продолжить(Результат, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(Результат) Тогда	
		
		Элементы.КонтактныеЛица1.ТекущаяСтрока = ДополнительныеПараметры.ТекущаяСтрока;
		
		Объект.КонтактныеЛица.НайтиПоИдентификатору(ДополнительныеПараметры.ТекущаяСтрока).КонтактноеЛицо = Результат;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныеВБазуБезПроверки()

	Записать(Новый Структура("ЗаписатьБезПроверки", Истина));
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьДанныеПередЗаписью(пОтказ) 
	
	масОшибки = Новый Массив;
	
	Для Каждого СтрокаТч Из Объект.ВидыАукционов Цикл
		
		масСтрокиТч = Объект.КонтактныеЛица.НайтиСтроки(Новый Структура("ВидАукциона", СтрокаТч.ВидАукциона));
		
		Если масСтрокиТч.Количество() = 0 Тогда
			
			масОшибки.Добавить(СтрШаблон(НСтр("ru = 'Для вида аукциона <%1> не выбрано ни одно контактное лицо'"), СтрокаТч.ВидАукциона));
			
		Иначе
			
			Для Каждого ЭлСтрокаТч Из масСтрокиТч Цикл
				
				Если Не ЗначениеЗаполнено(ЭлСтрокаТч.КонтактноеЛицо) Тогда
					
					масОшибки.Добавить(СтрШаблон(НСтр("ru = 'Для вида аукциона <%1> не заполнено контактное лицо'"), СтрокаТч.ВидАукциона));
					
					Прервать;
					
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если масОшибки.Количество() <> 0 Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрСоединить(масОшибки, Символы.ПС),,,, пОтказ);
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьОтборКонтактныхЛиц(пВидАукциона)
	
	Элементы.КонтактныеЛица1.ОтборСтрок = Новый ФиксированнаяСтруктура("ВидАукциона", пВидАукциона);
	
КонецПроцедуры	

&НаКлиенте
Процедура УдалитьКонтактныхЛицПоВидуАукциона(пВидАукциона)
	
	масСтрокиНаУдаление = Объект.КонтактныеЛица.НайтиСтроки(Новый Структура("ВидАукциона", пВидАукциона));
	
	Для Каждого ЭлСтрока Из масСтрокиНаУдаление Цикл
		
		Объект.КонтактныеЛица.Удалить(ЭлСтрока);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокиКонтактныхЛиц(пВидАукциона)
		
	НовСтрока = Объект.КонтактныеЛица.Добавить();
	
	НовСтрока.ВидАукциона = пВидАукциона;
		
КонецПроцедуры

#КонецОбласти



