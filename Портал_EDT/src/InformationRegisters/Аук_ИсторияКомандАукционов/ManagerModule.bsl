
#Область ПрограммныйИнтерфейс

Функция ДобавитьЗапись(пСтруктураЗаписи) Экспорт
	
	Возврат Аук_ОбщийСервер.ДобавитьЗаписьВРегистрСведений(пСтруктураЗаписи, "Аук_ИсторияКомандАукционов");
	
КонецФункции

Функция ДобавитьЗаписи(пМасЗаписи) Экспорт	
	
	Возврат Аук_ОбщийСервер.ДобавитьЗаписиВРегистрСведений(пМасЗаписи, "Аук_ИсторияКомандАукционов");
	
КонецФункции

Функция УдалитьЗапись(пСтруктураЗаписи) Экспорт
	
	Возврат Аук_ОбщийСервер.УдалитьНаборЗаписейВРегистреСведений(пСтруктураЗаписи, "Аук_ИсторияКомандАукционов");
	
КонецФункции	

Функция ОбновитьЗапись(пСтруктураЗаписи) Экспорт
	
	сткРез =  Аук_ОбщийСервер.ОбновитьЗаписьВРегистрСведений(пСтруктураЗаписи, "Аук_ИсторияКомандАукционов");
	
	Если Не сткРез.Успех Тогда
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;	
	
КонецФункции	

Функция КомандыОжидающиеВыполненияВАвтоматическомРежиме() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = ДанныеКомандДляВыполнения_БазовыйФрагментЗапроса();
	
	стрОтборы =
	"	Аук_ИсторияКомандАукционов.Автоматическая
	|	И Аук_ИсторияКомандАукционов.Результат = ЗНАЧЕНИЕ(Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Отборы", стрОтборы);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);  
	
КонецФункции

Функция ДанныеКомандПоКлючам(пМасКлючиКоманд) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = ДанныеКомандДляВыполнения_БазовыйФрагментЗапроса();
	
	стрОтборы =
	"	Аук_ИсторияКомандАукционов.КлючКоманды В(&КлючиКоманд)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Отборы", стрОтборы);
	
	Запрос.УстановитьПараметр("КлючиКоманд", пМасКлючиКоманд);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если ТипЗнч(пМасКлючиКоманд) = Тип("Массив") Тогда
		Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	Иначе
		Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);	
	КонецЕсли;	
	
КонецФункции

Функция КомандыОжидающиеВыполненияПоСпискуКлючей(пМасКлючКоманд) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ИсторияКомандАукционов.Период КАК Период,
	|	Аук_ИсторияКомандАукционов.КлючКоманды КАК КлючКоманды,
	|	Аук_ИсторияКомандАукционов.Данные КАК Данные
	|ИЗ
	|	РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|ГДЕ
	|	Аук_ИсторияКомандАукционов.Автоматическая
	|	И Аук_ИсторияКомандАукционов.Результат = ЗНАЧЕНИЕ(Перечисление.Аук_РезультатыВыполненияКоманд.ПустаяСсылка)
	|	И Аук_ИсторияКомандАукционов.КлючКоманды В(&КлючиКоманд)";
	
	Запрос.УстановитьПараметр("КлючиКоманд", пМасКлючКоманд);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса);  
	
КонецФункции

Процедура ЗафиксироватьРезультатВыполненияКомандыПоКлючу(пСткДанные) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ИсторияКомандАукционов.Период КАК Период,
	|	Аук_ИсторияКомандАукционов.КлючКоманды КАК КлючКоманды,
	|	&Результат КАК Результат,
	|	&Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|ГДЕ
	|	Аук_ИсторияКомандАукционов.КлючКоманды = &КлючКоманды";
	
	Запрос.УстановитьПараметр("КлючКоманды",	пСткДанные.КлючКоманды);
	Запрос.УстановитьПараметр("Результат",		пСткДанные.Результат);
	Запрос.УстановитьПараметр("Комментарий",	пСткДанные.Комментарий);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	сткСтруктураЗаписи = ЭдоОбщийСервер.РезультатЗапросаВСтруктуру(РезультатЗапроса); 
		
	ОбновитьЗапись(сткСтруктураЗаписи);
	
КонецПроцедуры

Процедура ЗафиксироватьКомандуДляВыполнения_(пСтруктураЗаписи) Экспорт
	
	сткРез = ДобавитьЗапись(пСтруктураЗаписи);
	
	Если Не сткРез.Успех Тогда
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;
	
КонецПроцедуры

Функция БазовыеПараметрыДляСозданияКоманд() Экспорт

	сткРезультат = Новый Структура;
	
	сткРезультат.Вставить("Период",			ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	сткРезультат.Вставить("КлючКоманды",	Справочники.УБД_КлючиУникальности.ПолучитьНовуюСсылку());
	
	сткРезультат.Вставить("КлючРодителя",	Неопределено);
	
	сткРезультат.Вставить("Аукцион");
	сткРезультат.Вставить("Автоматическая");
	
	сткРезультат.Вставить("Команда");
		
	Возврат сткРезультат;
	
КонецФункции	

Функция ДанныеДочернихКоманд(пМасКлючРодительскихКоманд) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = ДанныеКомандДляВыполнения_БазовыйФрагментЗапроса();
	
	стрОтборы =
	"	Аук_ИсторияКомандАукционов.КлючРодителя В(&КлючиРодителей)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Отборы", стрОтборы);
	
	Запрос.УстановитьПараметр("КлючиРодителей", пМасКлючРодительскихКоманд);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
		
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеКомандДляВыполнения_БазовыйФрагментЗапроса()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Аук_ИсторияКомандАукционов.Период КАК Период,
	|	Аук_ИсторияКомандАукционов.КлючКоманды КАК КлючКоманды,
	|	Аук_ИсторияКомандАукционов.Данные КАК Данные,
	|	Аук_ИсторияКомандАукционов.Участник КАК Участник,
	|	Аук_ИсторияКомандАукционов.КонтактноеЛицо КАК КонтактноеЛицо,
	|	Аук_ИсторияКомандАукционов.Автоматическая КАК Автоматическая
	|ИЗ
	|	РегистрСведений.Аук_ИсторияКомандАукционов КАК Аук_ИсторияКомандАукционов
	|ГДЕ
	|	&Отборы";
	
	Возврат ТекстЗапроса;
	
КонецФункции	

#КонецОбласти


