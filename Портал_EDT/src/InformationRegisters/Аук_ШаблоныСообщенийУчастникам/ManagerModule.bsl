
#Область ПрограммныйИнтерфейс

Функция ШаблонСообщенияПоОтбору(пСткОтбор) Экспорт
	
	тз = Новый ТаблицаЗначений;
	тз.Колонки.Добавить("ВидСообщения",	Новый ОписаниеТипов("ПеречислениеСсылка.Аук_ВидыСообщенийУчастникам"));
	тз.Колонки.Добавить("ВидАукциона",	Новый ОписаниеТипов("СправочникСсылка.Аук_ВидыАукционов"));
	тз.Колонки.Добавить("Участник",		Новый ОписаниеТипов("СправочникСсылка.Аук_УчастникиАукционов"));
	
	Для Каждого ЭлУчастник Из пСткОтбор.Участники Цикл	
		НовСтрока = тз.Добавить();
		
		НовСтрока.ВидСообщения	= пСткОтбор.ВидСообщения;
		НовСтрока.ВидАукциона	= пСткОтбор.ВидАукциона;
		НовСтрока.Участник		= ЭлУчастник;	
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тзТемп.ВидСообщения КАК ВидСообщения,
	|	тзТемп.ВидАукциона КАК ВидАукциона,
	|	тзТемп.Участник КАК Участник
	|ПОМЕСТИТЬ втТемп
	|ИЗ
	|	&тзТемп КАК тзТемп
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аук_ШаблоныСообщенийУчастникам.ТекстШаблона КАК ТекстШаблона,
	|	Аук_ШаблоныСообщенийУчастникам.ТекстЗаголовка КАК ТекстЗаголовка,
	|	0 КАК Приоритет,
	|	втТемп.Участник КАК Участник
	|ПОМЕСТИТЬ втШаблонСПриоритетом
	|ИЗ
	|	втТемп КАК втТемп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ШаблоныСообщенийУчастникам КАК Аук_ШаблоныСообщенийУчастникам
	|		ПО втТемп.ВидСообщения = Аук_ШаблоныСообщенийУчастникам.ВидСообщения
	|		И Аук_ШаблоныСообщенийУчастникам.ВидАукциона = Значение(Справочник.Аук_ВидыАукционов.ПустаяСсылка)
	|		И Аук_ШаблоныСообщенийУчастникам.Участник = Значение(Справочник.Аук_УчастникиАукционов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Аук_ШаблоныСообщенийУчастникам.ТекстШаблона,
	|	Аук_ШаблоныСообщенийУчастникам.ТекстЗаголовка,
	|	1,
	|	втТемп.Участник
	|ИЗ
	|	втТемп КАК втТемп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ШаблоныСообщенийУчастникам КАК Аук_ШаблоныСообщенийУчастникам
	|		ПО втТемп.ВидСообщения = Аук_ШаблоныСообщенийУчастникам.ВидСообщения
	|		И втТемп.ВидАукциона = Аук_ШаблоныСообщенийУчастникам.ВидАукциона
	|		И Аук_ШаблоныСообщенийУчастникам.Участник = Значение(Справочник.Аук_УчастникиАукционов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Аук_ШаблоныСообщенийУчастникам.ТекстШаблона,
	|	Аук_ШаблоныСообщенийУчастникам.ТекстЗаголовка,
	|	2,
	|	втТемп.Участник
	|ИЗ
	|	втТемп КАК втТемп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ШаблоныСообщенийУчастникам КАК Аук_ШаблоныСообщенийУчастникам
	|		ПО втТемп.ВидСообщения = Аук_ШаблоныСообщенийУчастникам.ВидСообщения
	|		И втТемп.ВидАукциона = Аук_ШаблоныСообщенийУчастникам.ВидАукциона
	|			И втТемп.Участник = Аук_ШаблоныСообщенийУчастникам.Участник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втШаблонСПриоритетом.ТекстШаблона КАК Шаблон,
	|	втШаблонСПриоритетом.ТекстЗаголовка КАК Заголовок,
	|	втШаблонСПриоритетом.Участник КАК Участник
	|ИЗ
	|	втШаблонСПриоритетом КАК втШаблонСПриоритетом
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(втШаблонСПриоритетом.Приоритет) КАК Приоритет,
	|			втШаблонСПриоритетом.Участник КАК Участник
	|		ИЗ
	|			втШаблонСПриоритетом КАК втШаблонСПриоритетом
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втШаблонСПриоритетом.Участник) КАК ВложенныйЗапрос
	|		ПО втШаблонСПриоритетом.Приоритет = ВложенныйЗапрос.Приоритет
	|			И втШаблонСПриоритетом.Участник = ВложенныйЗапрос.Участник";
	
	Запрос.УстановитьПараметр("тзТемп",	тз);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда	
		Возврат Неопределено;	
	Иначе	
		Выборка = РезультатЗапроса.Выбрать();
		
		сооРезультат = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			сооРезультат.Вставить(Выборка.Участник, Новый Структура("Шаблон, Заголовок", Выборка.Шаблон, Выборка.Заголовок));	
		КонецЦикла;	
		
		Возврат сооРезультат;
	КонецЕсли;	
	
КонецФункции	

Функция ОтсутствующиеДанныеОповещенийПоВидамСообщений() Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ВидыСообщенийУчастникам.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втЗначимыеВидыСообщений
	|ИЗ
	|	Перечисление.Аук_ВидыСообщенийУчастникам КАК Аук_ВидыСообщенийУчастникам
	|ГДЕ
	|	Аук_ВидыСообщенийУчастникам.Ссылка <> ЗНАЧЕНИЕ(Перечисление.Аук_ВидыСообщенийУчастникам.ПростоеСообщение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗначимыеВидыСообщений.Ссылка КАК ВидСообщения
	|ИЗ
	|	втЗначимыеВидыСообщений КАК втЗначимыеВидыСообщений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ШаблоныСообщенийУчастникам КАК Аук_ШаблоныСообщенийУчастникам
	|		ПО втЗначимыеВидыСообщений.Ссылка = Аук_ШаблоныСообщенийУчастникам.ВидСообщения
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗначимыеВидыСообщений.Ссылка
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(Аук_ШаблоныСообщенийУчастникам.ВидСообщения) = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗначимыеВидыСообщений.Ссылка КАК ВидСообщения
	|ИЗ
	|	втЗначимыеВидыСообщений КАК втЗначимыеВидыСообщений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ШаблоныСообщенийУчастникам КАК Аук_ШаблоныСообщенийУчастникам
	|		ПО втЗначимыеВидыСообщений.Ссылка = Аук_ШаблоныСообщенийУчастникам.ВидСообщения
	|ГДЕ
	|	ЕСТЬNULL(Аук_ШаблоныСообщенийУчастникам.ТекстЗаголовка, """") = """"";
	
	масРезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	сткРезультат = Новый Структура;
	сткРезультат.Вставить("ОтсутствующиеШаблоны",	масРезультатыЗапроса[1].Выгрузить().ВыгрузитьКолонку("ВидСообщения")); 
	сткРезультат.Вставить("ОтсутствующиеЗаголовки", масРезультатыЗапроса[2].Выгрузить().ВыгрузитьКолонку("ВидСообщения"));
	
	Возврат сткРезультат;
КонецФункции	

#КонецОбласти
