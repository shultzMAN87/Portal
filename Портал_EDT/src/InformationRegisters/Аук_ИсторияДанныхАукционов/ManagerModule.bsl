
#Область ПрограммныйИнтерфейс

Функция ДобавитьЗапись(пСтруктураЗаписи) Экспорт
	
	сткРез = Аук_ОбщийСервер.ДобавитьЗаписьВРегистрСведений(пСтруктураЗаписи, "Аук_ИсторияДанныхАукционов");
	
	Если Не сткРез.Успех Тогда
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;	
	
КонецФункции

Функция ДобавитьЗаписи(пМасЗаписи) Экспорт	
	
	сткРез = Аук_ОбщийСервер.ДобавитьЗаписиВРегистрСведений(пМасЗаписи, "Аук_ИсторияДанныхАукционов");
	
	Если Не сткРез.Успех Тогда
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;
	
КонецФункции

Функция УдалитьЗапись(пСтруктураЗаписи) Экспорт
	
	Возврат Аук_ОбщийСервер.УдалитьНаборЗаписейВРегистреСведений(пСтруктураЗаписи, "Аук_ИсторияДанныхАукционов");
	
КонецФункции

Функция ТекущиеДанныеСтавокУчастниковАукциона(пАукцион, пБулРезультатВТаблицуЗначений = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ИсторияДанныхАукционовСрезПоследних.Участник КАК Участник,
	|	Аук_ИсторияДанныхАукционовСрезПоследних.СтрокаДанных КАК СтрокаДанных,
	|	Аук_ИсторияДанныхАукционовСрезПоследних.ЭлементДанных КАК ЭлементДанных,
	|	Аук_ИсторияДанныхАукционовСрезПоследних.ЗначениеЭлементаДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	РегистрСведений.Аук_ИсторияДанныхАукционов.СрезПоследних(, Аукцион = &Аукцион) КАК Аук_ИсторияДанныхАукционовСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ИсторияДанныхАукционовСрезПоследних.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка";	
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если пБулРезультатВТаблицуЗначений Тогда
		
		Возврат РезультатЗапроса.Выгрузить();
		
	Иначе	
		
		Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
		
	КонецЕсли;
	
КонецФункции

Функция ТекущиеДанныеСтавокУчастниковАукционаВСтроке(пАукцион, пВидАукциона = Неопределено, пМасОбязательныеКолонки = Неопределено) Экспорт
	
	Если пВидАукциона = Неопределено Тогда
		
		пВидАукциона = Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион).ВидАукциона;
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аук_ИсторияДанныхАукционов.Период КАК Период,
	|	Аук_ИсторияДанныхАукционов.Аукцион КАК Аукцион,
	|	Аук_ИсторияДанныхАукционов.Участник КАК Участник,
	|	Аук_ИсторияДанныхАукционов.ВидДанных КАК ВидДанных
	|ПОМЕСТИТЬ втТемп
	|ИЗ
	|	РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(Аук_ИсторияДанныхАукционов.Период) КАК Период,
	|			Аук_ИсторияДанныхАукционов.Аукцион КАК Аукцион,
	|			Аук_ИсторияДанныхАукционов.Участник КАК Участник
	|		ИЗ
	|			РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ГДЕ
	|			Аук_ИсторияДанныхАукционов.Аукцион = &Аукцион
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Аук_ИсторияДанныхАукционов.Аукцион,
	|			Аук_ИсторияДанныхАукционов.Участник) КАК ВложенныйЗапрос
	|		ПО Аук_ИсторияДанныхАукционов.Период = ВложенныйЗапрос.Период
	|			И Аук_ИсторияДанныхАукционов.Аукцион = ВложенныйЗапрос.Аукцион
	|			И Аук_ИсторияДанныхАукционов.Участник = ВложенныйЗапрос.Участник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ИсторияДанныхАукционов.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	Аук_ИсторияДанныхАукционов.Период КАК ДатаСтавки,
	|	Аук_ИсторияДанныхАукционов.Участник КАК Участник,
	|	Аук_ИсторияДанныхАукционов.СтрокаДанных КАК СтрокаДанных,
	|	Аук_ИсторияДанныхАукционов.ЭлементДанных КАК ЭлементДанных,
	|	Аук_ИсторияДанныхАукционов.ЗначениеЭлементаДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ИсторияДанныхАукционов.КонтактноеЛицо КАК КонтактноеЛицо,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТемп КАК втТемп
	|		ПО (втТемп.Период = Аук_ИсторияДанныхАукционов.Период)
	|			И (втТемп.Аукцион = Аук_ИсторияДанныхАукционов.Аукцион)
	|			И (втТемп.Участник = Аук_ИсторияДанныхАукционов.Участник)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ИсторияДанныхАукционов.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|ГДЕ
	|	Аук_ИсторияДанныхАукционов.ВидДанных = ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Ставка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Аук_ИсторияДанныхАукционов.Период,
	|	Аук_ИсторияДанныхАукционов.Участник,
	|	Аук_ИсторияДанныхАукционов.СтрокаДанных,
	|	Аук_ИсторияДанныхАукционов.ЭлементДанных,
	|	Аук_ИсторияДанныхАукционов.ЗначениеЭлементаДанных,
	|	Аук_ИсторияДанныхАукционов.КонтактноеЛицо,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование
	|ИЗ
	|	РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(Аук_ИсторияДанныхАукционов.Период) КАК Период,
	|			Аук_ИсторияДанныхАукционов.Аукцион КАК Аукцион,
	|			Аук_ИсторияДанныхАукционов.Участник КАК Участник
	|		ИЗ
	|			РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТемп КАК втТемп
	|				ПО Аук_ИсторияДанныхАукционов.Период < втТемп.Период
	|					И Аук_ИсторияДанныхАукционов.Аукцион = втТемп.Аукцион
	|					И Аук_ИсторияДанныхАукционов.Участник = втТемп.Участник
	|					И (втТемп.ВидДанных = ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Сообщение))
	|		ГДЕ
	|			Аук_ИсторияДанныхАукционов.ВидДанных <> ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Сообщение)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Аук_ИсторияДанныхАукционов.Аукцион,
	|			Аук_ИсторияДанныхАукционов.Участник) КАК ВложенныйЗапрос
	|		ПО Аук_ИсторияДанныхАукционов.Период = ВложенныйЗапрос.Период
	|			И Аук_ИсторияДанныхАукционов.Аукцион = ВложенныйЗапрос.Аукцион
	|			И Аук_ИсторияДанныхАукционов.Участник = ВложенныйЗапрос.Участник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ИсторияДанныхАукционов.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|ГДЕ
	|	Аук_ИсторияДанныхАукционов.ВидДанных = ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Ставка)
	|ИТОГИ ПО
	|	Участник,
	|	СтрокаДанных";	
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	масСтруктураСтавки = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона);
	
	Если пМасОбязательныеКолонки = Неопределено Тогда
	
		пМасОбязательныеКолонки = Аук_ОбщийСервер.СоставОбязательныхКолонок("Участник,СтрокаДанных,КонтактноеЛицо,ДатаСтавки");
	
	КонецЕсли;
	
	тзРезультат = Аук_ОбщийСервер.ТекущиеДанныеУчастниковАукционаСоздатьТаблицу(пМасОбязательныеКолонки, масСтруктураСтавки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Аук_ОбщийСервер.ЗаполнитьДанныеУчастниковПоРезультатуЗапроса(тзРезультат, РезультатЗапроса); 
	
	Возврат тзРезультат;
		
КонецФункции

Функция ПолнаяИсторияСтавокУчастниковАукционаВСтроке(пАукцион, пВидАукциона = Неопределено) Экспорт
	
	Если пВидАукциона = Неопределено Тогда
		
		пВидАукциона = Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион).ВидАукциона;
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ИсторияДанныхАукционов.Период КАК Период,
	|	Аук_ИсторияДанныхАукционов.Участник КАК Участник,
	|	Аук_ИсторияДанныхАукционов.СтрокаДанных КАК СтрокаДанных,
	|	Аук_ИсторияДанныхАукционов.ЭлементДанных КАК ЭлементДанных,
	|	Аук_ИсторияДанныхАукционов.ЗначениеЭлементаДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ИсторияДанныхАукционов.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|ГДЕ
	|	Аук_ИсторияДанныхАукционов.Аукцион = &Аукцион
	|	И Аук_ИсторияДанныхАукционов.ВидДанных = ЗНАЧЕНИЕ(перечисление.Аук_ВидыДанных.Ставка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Период,
	|	Участник,
	|	СтрокаДанных";	
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	масСтруктураСтавки = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона);
	
	масОбязательныеКолонки = Аук_ОбщийСервер.СоставОбязательныхКолонок("Участник,Период");
	
	тзРезультат = Аук_ОбщийСервер.ТекущиеДанныеУчастниковАукционаСоздатьТаблицу(масОбязательныеКолонки, масСтруктураСтавки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Аук_ОбщийСервер.ЗаполнитьДанныеУчастниковПоРезультатуЗапроса(тзРезультат, РезультатЗапроса, 3);
	
	Возврат тзРезультат;
		
КонецФункции

Функция ПолнаяИсторияСтавокУчастниковАукционаПоЭтапамВСтроке(пАукцион, пВидАукциона = Неопределено) Экспорт
	
	Если пВидАукциона = Неопределено Тогда
		
		пВидАукциона = Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион).ВидАукциона;
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ИсторияСтатусовАукционов.Период КАК Период,
	|	Аук_ИсторияСтатусовАукционов.Аукцион КАК Аукцион,
	|	Аук_ИсторияСтатусовАукционов.Статус КАК Статус,
	|	Аук_ИсторияСтатусовАукционов.НомерЭтапа КАК НомерЭтапа,
	|	Аук_ИсторияСтатусовАукционов.Этап КАК Этап
	|ПОМЕСТИТЬ втИсторияСтатусов
	|ИЗ
	|	РегистрСведений.Аук_ИсторияСтатусовАукционов КАК Аук_ИсторияСтатусовАукционов
	|ГДЕ
	|	Аук_ИсторияСтатусовАукционов.Аукцион = &Аукцион
	|	И Аук_ИсторияСтатусовАукционов.Статус В (ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов), ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеИтогов), ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.НовыйЭтап))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсторияСтатусов.Аукцион КАК Аукцион,
	|	МАКСИМУМ(втИсторияСтатусов.Период) КАК ПериодС,
	|	ЕСТЬNULL(втИсторияСтатусов1.Период, ДАТАВРЕМЯ(3999, 12, 31)) КАК ПериодПо
	|ПОМЕСТИТЬ втНачалоОкончаниеЭтапов
	|ИЗ
	|	втИсторияСтатусов КАК втИсторияСтатусов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсторияСтатусов КАК втИсторияСтатусов1
	|		ПО втИсторияСтатусов.Период < втИсторияСтатусов1.Период
	|			И втИсторияСтатусов.Аукцион = втИсторияСтатусов1.Аукцион
	|			И (втИсторияСтатусов1.Статус В (ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов), ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.ПодведениеИтогов)))
	|ГДЕ
	|	втИсторияСтатусов.Статус = ЗНАЧЕНИЕ(Перечисление.Аук_СтатусыАукционов.НовыйЭтап)
	|
	|СГРУППИРОВАТЬ ПО
	|	втИсторияСтатусов1.Период,
	|	втИсторияСтатусов.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсторияСтатусов.Этап КАК Этап,
	|	Аук_ИсторияДанныхАукционов.Участник КАК Участник,
	|	Аук_ИсторияДанныхАукционов.Период КАК Период,
	|	Аук_ИсторияДанныхАукционов.СтрокаДанных КАК СтрокаДанных,
	|	Аук_ИсторияДанныхАукционов.ЭлементДанных КАК ЭлементДанных,
	|	Аук_ИсторияДанныхАукционов.ЗначениеЭлементаДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование,
	|	ВЫБОР
	|		КОГДА Аук_ИтогиЗавершенияЭтапов.Аукцион ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоЛидирующаяСтавка
	|ИЗ
	|	втНачалоОкончаниеЭтапов КАК втНачалоОкончаниеЭтапов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсторияСтатусов КАК втИсторияСтатусов
	|		ПО втНачалоОкончаниеЭтапов.Аукцион = втИсторияСтатусов.Аукцион
	|			И втНачалоОкончаниеЭтапов.ПериодС = втИсторияСтатусов.Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ПО втНачалоОкончаниеЭтапов.Аукцион = Аук_ИсторияДанныхАукционов.Аукцион
	|			И (Аук_ИсторияДанныхАукционов.Период МЕЖДУ втНачалоОкончаниеЭтапов.ПериодС И втНачалоОкончаниеЭтапов.ПериодПо)
	|			И (Аук_ИсторияДанныхАукционов.ВидДанных = ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Ставка))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО (Аук_ИсторияДанныхАукционов.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИтогиЗавершенияЭтапов КАК Аук_ИтогиЗавершенияЭтапов
	|		ПО (Аук_ИсторияДанныхАукционов.Период = Аук_ИтогиЗавершенияЭтапов.ДатаСтавки)
	|			И (Аук_ИсторияДанныхАукционов.Аукцион = Аук_ИтогиЗавершенияЭтапов.Аукцион)
	|			И (Аук_ИсторияДанныхАукционов.Участник = Аук_ИтогиЗавершенияЭтапов.Участник)
	|			И (Аук_ИсторияДанныхАукционов.СтрокаДанных = Аук_ИтогиЗавершенияЭтапов.СтрокаДанных)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Этап,
	|	Период,
	|	Участник,
	|	СтрокаДанных";	
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	масСтруктураСтавки = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона);
	
	масОбязательныеКолонки = Аук_ОбщийСервер.СоставОбязательныхКолонок("Участник,Период,Этап,ЭтоЛидирующаяСтавка");
	
	тзРезультат = Аук_ОбщийСервер.ТекущиеДанныеУчастниковАукционаСоздатьТаблицу(масОбязательныеКолонки, масСтруктураСтавки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Аук_ОбщийСервер.ЗаполнитьДанныеУчастниковПоРезультатуЗапроса(тзРезультат, РезультатЗапроса, 4);
	
	Возврат тзРезультат;
		
КонецФункции

Функция СведенияОбАктивностиУчастниковАукциона(пАукцион) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аук_АукционыУчастники.Участник КАК Участник
	|ПОМЕСТИТЬ втВсеУчастники
	|ИЗ
	|	Справочник.Аук_Аукционы.Участники КАК Аук_АукционыУчастники
	|ГДЕ
	|	Аук_АукционыУчастники.Ссылка = &Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аук_ИсторияДанныхАукционов.Участник КАК Участник,
	|	Аук_ИсторияДанныхАукционов.ВидДанных КАК ВидДанных
	|ПОМЕСТИТЬ втТемп
	|ИЗ
	|	РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(Аук_ИсторияДанныхАукционов.Период) КАК Период,
	|			Аук_ИсторияДанныхАукционов.Участник КАК Участник,
	|			Аук_ИсторияДанныхАукционов.Аукцион КАК Аукцион
	|		ИЗ
	|			втВсеУчастники КАК втВсеУчастники
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|				ПО (Аук_ИсторияДанныхАукционов.Участник = втВсеУчастники.Участник)
	|		ГДЕ
	|			Аук_ИсторияДанныхАукционов.Аукцион = &Аукцион
	|			И Аук_ИсторияДанныхАукционов.ВидДанных <> ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Сообщение)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Аук_ИсторияДанныхАукционов.Участник,
	|			Аук_ИсторияДанныхАукционов.Аукцион) КАК ВложенныйЗапрос
	|		ПО Аук_ИсторияДанныхАукционов.Участник = ВложенныйЗапрос.Участник
	|			И Аук_ИсторияДанныхАукционов.Период = ВложенныйЗапрос.Период
	|			И Аук_ИсторияДанныхАукционов.Аукцион = ВложенныйЗапрос.Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВсеУчастники.Участник КАК Участник,
	|	ВЫБОР
	|		КОГДА втТемп.ВидДанных ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|				КОГДА втТемп.ВидДанных = ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Ставка)
	|					ТОГДА 0
	|				ИНАЧЕ 2
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУчастия
	|ИЗ
	|	втВсеУчастники КАК втВсеУчастники
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТемп КАК втТемп
	|		ПО втВсеУчастники.Участник = втТемп.Участник
	|			И (втТемп.Участник <> ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Сообщение))";
	
	Запрос.УстановитьПараметр("Аукцион", пАукцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ЭдоОбщийСервер.РезультатЗапросаВМассивСтруктур(РезультатЗапроса);
	
КонецФункции	

Функция СформироватьЗаписиДляДобавленияВИсторию_Ставка(пСткДанныеКоманды, пМасДанныеСтавки, пНомерЭтапа, пПериод = Неопределено) Экспорт
		
	ссВидДанных		= ПредопределенноеЗначение("Перечисление.Аук_ВидыДанных.Ставка");
	ссНаправление	= ПредопределенноеЗначение("Перечисление.Аук_НаправленияДанных.Входящие");
	
	Если пПериод = Неопределено Тогда
		пПериод = ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_();
	КонецЕсли;
	
	масЗаписи = Новый Массив;
	Для Сч = 0 По пМасДанныеСтавки.ВГраница() Цикл	
		сооЗначенияСтавки = пМасДанныеСтавки[Сч];
		
		Для Каждого ЭлементСтавки Из сооЗначенияСтавки Цикл
			сткЗначенияСтавки = Новый Структура;
			сткЗначенияСтавки.Вставить("ЭлементДанных",				ЭлементСтавки.Ключ);
			сткЗначенияСтавки.Вставить("ЗначениеЭлементаДанных",	ЭлементСтавки.Значение);
			
			сткЗапись = ДобавлениеДанныхВИсторию_СтруктураЗаписи(пСткДанныеКоманды, сткЗначенияСтавки, пПериод, ссВидДанных, ссНаправление, пНомерЭтапа, Сч);
			
			масЗаписи.Добавить(сткЗапись);		
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат масЗаписи;
	
КонецФункции

Функция СформироватьЗаписиДляДобавленияВИсторию_Отказ(пСткДанныеКоманды, пСтрСведенияОбОтказе, пНомерЭтапа, пПериод = Неопределено) Экспорт
	
	ссВидДанных		= ПредопределенноеЗначение("Перечисление.Аук_ВидыДанных.Отказ");
	ссНаправление	= ПредопределенноеЗначение("Перечисление.Аук_НаправленияДанных.Входящие");
	
	Если пПериод = Неопределено Тогда
		пПериод = ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_();
	КонецЕсли;
	
	масЗаписи = Новый Массив;
	
	сткЗначенияСтавки = Новый Структура;
	сткЗначенияСтавки.Вставить("ЭлементДанных",				ПланыВидовХарактеристик.Аук_ЭлементыДанныхАукционов.Сообщение);
	сткЗначенияСтавки.Вставить("ЗначениеЭлементаДанных",	пСтрСведенияОбОтказе);
	
	сткЗапись = ДобавлениеДанныхВИсторию_СтруктураЗаписи(пСткДанныеКоманды, сткЗначенияСтавки, пПериод, ссВидДанных, ссНаправление, пНомерЭтапа);
	
	масЗаписи.Добавить(сткЗапись);
	
	Возврат масЗаписи;
	
КонецФункции

Функция СформироватьЗаписиДляДобавленияВИсторию_Сообщение(пСткДанныеКоманды, пСтрСообщение, ссНаправление, пНомерЭтапа, пПериод = Неопределено, пСтрокаДанных = 0) Экспорт
	
	ссВидДанных		= ПредопределенноеЗначение("Перечисление.Аук_ВидыДанных.Сообщение");
	
	Если пПериод = Неопределено Тогда
		пПериод = ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_();
	КонецЕсли;
	
	масЗаписи = Новый Массив;
	
	сткЗначенияСтавки = Новый Структура;
	сткЗначенияСтавки.Вставить("ЭлементДанных",				ПланыВидовХарактеристик.Аук_ЭлементыДанныхАукционов.Сообщение);
	сткЗначенияСтавки.Вставить("ЗначениеЭлементаДанных",	пСтрСообщение);
	
	сткЗапись = ДобавлениеДанныхВИсторию_СтруктураЗаписи(пСткДанныеКоманды, сткЗначенияСтавки, пПериод, ссВидДанных, ссНаправление, пНомерЭтапа, пСтрокаДанных);
	
	масЗаписи.Добавить(сткЗапись);
	
	Возврат масЗаписи;
	
КонецФункции

Функция ДанныеУчастникаИзБазы(пСткОтбор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ИсторияДанныхАукционов.СтрокаДанных КАК СтрокаДанных,
	|	Аук_ИсторияДанныхАукционов.ЗначениеЭлементаДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ЭлементыДанныхАукционов.Представление КАК ЭлементДанныхПредставление
	|ИЗ
	|	РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО Аук_ИсторияДанныхАукционов.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка
	|ГДЕ
	|	Аук_ИсторияДанныхАукционов.Период = &Период
	|	И Аук_ИсторияДанныхАукционов.Аукцион = &Аукцион
	|	И Аук_ИсторияДанныхАукционов.Участник = &Участник
	|	И Аук_ИсторияДанныхАукционов.ВидДанных = &ВидДанных
	|	И Аук_ИсторияДанныхАукционов.КонтактноеЛицо = &КонтактноеЛицо
	|ИТОГИ ПО
	|	СтрокаДанных";
	
	Для Каждого ЭлСтк Из пСткОтбор Цикл
		
		Запрос.УстановитьПараметр(ЭлСтк.Ключ, ЭлСтк.Значение);
		
	КонецЦикла;
	
	ВыборкаСтрокаДанных = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	масРезультат = Новый Массив;
	Пока ВыборкаСтрокаДанных.Следующий() Цикл
		
		ВыборкаДетальная = ВыборкаСтрокаДанных.Выбрать();
		
		сооСтрокаДанных = Новый Соответствие;		
		Пока ВыборкаДетальная.Следующий() Цикл
			
			сооСтрокаДанных.Вставить(ВыборкаДетальная.ЭлементДанныхПредставление, ВыборкаДетальная.ЗначениеЭлементаДанных);
			
		КонецЦикла;	
		
		масРезультат.Добавить(сооСтрокаДанных);
		
	КонецЦикла;
	
	Возврат масРезультат;
	
КонецФункции

Функция ДанныеСтавкиУчастника(пСткОтбор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_Аукционы.Ссылка КАК Ссылка,
	|	Аук_ВидыАукционовСтруктураСтавки.НомерСтроки КАК НомерСтроки,
	|	Аук_ВидыАукционовСтруктураСтавки.ЭлементСтавки КАК ЭлементСтавки
	|ПОМЕСТИТЬ втСтруктураСтавкиПоАукциону
	|ИЗ
	|	Справочник.Аук_Аукционы КАК Аук_Аукционы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Аук_ВидыАукционов.СтруктураСтавки КАК Аук_ВидыАукционовСтруктураСтавки
	|		ПО Аук_Аукционы.ВидАукциона = Аук_ВидыАукционовСтруктураСтавки.Ссылка
	|ГДЕ
	|	Аук_Аукционы.Ссылка = &Аукцион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аук_ИсторияДанныхАукционов.СтрокаДанных КАК СтрокаДанных,
	|	Аук_ИсторияДанныхАукционов.ЗначениеЭлементаДанных КАК ЗначениеЭлементаДанных,
	|	Аук_ЭлементыДанныхАукционов.Представление КАК ЭлементДанныхПредставление
	|ИЗ
	|	втСтруктураСтавкиПоАукциону КАК втСтруктураСтавкиПоАукциону
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Аук_ИсторияДанныхАукционов КАК Аук_ИсторияДанныхАукционов
	|		ПО втСтруктураСтавкиПоАукциону.Ссылка = Аук_ИсторияДанныхАукционов.Аукцион
	|			И втСтруктураСтавкиПоАукциону.ЭлементСтавки = Аук_ИсторияДанныхАукционов.ЭлементДанных
	|			И (Аук_ИсторияДанныхАукционов.Период = &Период)
	|			И (Аук_ИсторияДанныхАукционов.Участник = &Участник)
	|			И (Аук_ИсторияДанныхАукционов.КонтактноеЛицо = &КонтактноеЛицо)
	|			И (Аук_ИсторияДанныхАукционов.ВидДанных = ЗНАЧЕНИЕ(Перечисление.Аук_ВидыДанных.Ставка))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|		ПО (Аук_ИсторияДанныхАукционов.ЭлементДанных = Аук_ЭлементыДанныхАукционов.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтрокаДанных,
	|	втСтруктураСтавкиПоАукциону.НомерСтроки УБЫВ
	|ИТОГИ ПО
	|	СтрокаДанных";
	
	Запрос.УстановитьПараметр("Период",			пСткОтбор.Период);
	Запрос.УстановитьПараметр("Аукцион",		пСткОтбор.Аукцион);
	Запрос.УстановитьПараметр("Участник",		пСткОтбор.Участник);
	Запрос.УстановитьПараметр("КонтактноеЛицо",	пСткОтбор.КонтактноеЛицо);
	
	ВыборкаСтрокаДанных = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	масРезультат = Новый Массив;
	Пока ВыборкаСтрокаДанных.Следующий() Цикл
		
		ВыборкаДетальная = ВыборкаСтрокаДанных.Выбрать();
		
		сооСтрокаДанных = Новый Соответствие;
		Пока ВыборкаДетальная.Следующий() Цикл
			
			сооСтрокаДанных.Вставить(ВыборкаДетальная.ЭлементДанныхПредставление, ВыборкаДетальная.ЗначениеЭлементаДанных);
			
		КонецЦикла;	
		масРезультат.Добавить(сооСтрокаДанных);
		
	КонецЦикла;
	
	Возврат масРезультат;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавлениеДанныхВИсторию_СтруктураЗаписи(пСткДанныеКоманды, пСткЭлементДанных, пПериод, пСсВидДанных, ссНаправление, пНомерЭтапа, пСтрокаДанных = 0)

	сткРезультат = Новый Структура;
	
	сткРезультат.Вставить("Период",					пПериод);
	сткРезультат.Вставить("Аукцион",				пСткДанныеКоманды.Аукцион);
	сткРезультат.Вставить("Участник",				пСткДанныеКоманды.Участник);
	сткРезультат.Вставить("СтрокаДанных",			пСтрокаДанных); // Число
	сткРезультат.Вставить("ЭлементДанных",			пСткЭлементДанных.ЭлементДанных);
	сткРезультат.Вставить("ЗначениеЭлементаДанных",	пСткЭлементДанных.ЗначениеЭлементаДанных);
	сткРезультат.Вставить("НаправлениеДанных",		ссНаправление); 
	сткРезультат.Вставить("ВидДанных",				пСсВидДанных);
	сткРезультат.Вставить("КонтактноеЛицо",			пСткДанныеКоманды.КонтактноеЛицо);
	сткРезультат.Вставить("НомерЭтапа",				пНомерЭтапа);
	
	Возврат сткРезультат;
	
КонецФункции

#КонецОбласти

