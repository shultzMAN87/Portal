
#Область ПрограммныйИнтерфейс

Функция ЭлементыДанныхАукционовПоТехническимНаименованиям(пМасТехническиеНаименования) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ЭлементыДанныхАукционов.Ссылка КАК ЭлементДанных,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование,
	|	Аук_ЭлементыДанныхАукционов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|ГДЕ
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование В(&ТехническиеНаименования)";
	
	Запрос.УстановитьПараметр("ТехническиеНаименования", пМасТехническиеНаименования);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	сооРезультат = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		сооРезультат.Вставить(Выборка.ТехническоеНаименование, Новый Структура("ЭлементДанных, ТипЗначения", Выборка.ЭлементДанных, Выборка.ТипЗначения));	
	КонецЦикла;	
	
	Возврат сооРезультат;
	
КонецФункции

Функция ЭлементыДанныхАукционовПоНаименованиям(пНаименования) Экспорт 
	
	Возврат СвойстваЭлементовДанныхУниверсально(пНаименования, "Наименование");
	
КонецФункции

Функция СвойстваЭлементовДанныхАукционовПоСписку(пЭлементыДанных) Экспорт 
	
	Возврат СвойстваЭлементовДанныхУниверсально(пЭлементыДанных, "Ссылка");
	
КонецФункции

Функция СуществуетЭлементСТехническимНаименованием(пТекущийОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Аук_ЭлементыДанныхАукционов.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|ГДЕ
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование = &ТехническоеНаименование
	|	И Аук_ЭлементыДанныхАукционов.Ссылка <> &ТекущийЭлемент";
	
	Запрос.УстановитьПараметр("ТехническоеНаименование",	пТекущийОбъект.ТехническоеНаименование);
	Запрос.УстановитьПараметр("ТекущийЭлемент",				пТекущийОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция СуществуетЭлементСНаименованием(пТекущийОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Аук_ЭлементыДанныхАукционов.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|ГДЕ
	|	Аук_ЭлементыДанныхАукционов.Наименование = &Наименование
	|	И Аук_ЭлементыДанныхАукционов.Ссылка <> &ТекущийЭлемент";
	
	Запрос.УстановитьПараметр("Наименование",	пТекущийОбъект.Наименование);
	Запрос.УстановитьПараметр("ТекущийЭлемент",	пТекущийОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция СписокПредставленийПоМассивуЭлементов(пЭлементыДанных, пОтборПоТипу = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ЭлементыДанныхАукционов.Представление КАК Представление,
	|	Аук_ЭлементыДанныхАукционов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|ГДЕ
	|	Аук_ЭлементыДанныхАукционов.Ссылка В(&ЭлементыДанных)";
	
	Запрос.УстановитьПараметр("ЭлементыДанных", пЭлементыДанных);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если пОтборПоТипу = Неопределено Тогда 
	
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Представление");
		
	Иначе
		
		Возврат СписокПредставленийПоМассивуЭлементовСОтборомПоТипу(РезультатЗапроса, пОтборПоТипу);	
		
	КонецЕсли;	
	
КонецФункции	

Функция СоответствиеОперандовТехническимНаименованиям(пНаименованияОперандов) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""["" + Аук_ЭлементыДанныхАукционов.Наименование + ""]"" КАК Наименование,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|ГДЕ
	|	""["" + Аук_ЭлементыДанныхАукционов.Наименование + ""]"" В(&НаименованияОперандов)";
	
	Запрос.УстановитьПараметр("НаименованияОперандов", пНаименованияОперандов);
	
	сооРезультат = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл	
		
		сооРезультат.Вставить(Выборка.Наименование, Выборка.ТехническоеНаименование);
		
	КонецЦикла;
	
	Возврат сооРезультат;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Функция СписокПредставленийПоМассивуЭлементовСОтборомПоТипу(пРезультатЗапроса, пОтборПоТипу)
	
	масРезультат = Новый Массив;
	
	Выборка = пРезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Аук_ОбщийКлиентСервер.ТипЗначенияЭлементаПвхСоответствуетТипу(Выборка.ТипЗначения, пОтборПоТипу) Тогда
			
			масРезультат.Добавить(Выборка.Представление);
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат масРезультат;
	
КонецФункции	

Функция СвойстваЭлементовДанныхУниверсально(пСписок, пРеквизитДляОтбора)

	Запрос = Новый Запрос;
	Запрос.Текст =
	СтрШаблон(
	"ВЫБРАТЬ
	|	Аук_ЭлементыДанныхАукционов.Наименование КАК Наименование,
	|	Аук_ЭлементыДанныхАукционов.ТехническоеНаименование КАК ТехническоеНаименование,
	|	Аук_ЭлементыДанныхАукционов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыДанныхАукционов КАК Аук_ЭлементыДанныхАукционов
	|ГДЕ
	|	Аук_ЭлементыДанныхАукционов.%1 В(&Список)", пРеквизитДляОтбора);
	
	Запрос.УстановитьПараметр("Список", пСписок);
	
	сооРезультат = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл	
		
		сооРезультат.Вставить(Выборка.Наименование, Новый Структура("ТехническоеНаименование, ТипЗначения", Выборка.ТехническоеНаименование, Выборка.ТипЗначения));
		
	КонецЦикла;
	
	Возврат сооРезультат;
	
КонецФункции	

#КонецОбласти


