
#Область ПрограммныйИнтерфейс

Функция СуществуетЭлементСТехническимНаименованием(пТекущийОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыНастроекКаналовВзаимодействия КАК Аук_ЭлементыНастроекКаналовВзаимодействия
	|ГДЕ
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.ТехническоеНаименование = &ТехническоеНаименование
	|	И Аук_ЭлементыНастроекКаналовВзаимодействия.Ссылка <> &ТекущийЭлемент";
	
	Запрос.УстановитьПараметр("ТехническоеНаименование",	пТекущийОбъект.ТехническоеНаименование);
	Запрос.УстановитьПараметр("ТекущийЭлемент",				пТекущийОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция СуществуетЭлементСНаименованием(пТекущийОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыНастроекКаналовВзаимодействия КАК Аук_ЭлементыНастроекКаналовВзаимодействия
	|ГДЕ
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.Наименование = &Наименование
	|	И Аук_ЭлементыНастроекКаналовВзаимодействия.Ссылка <> &ТекущийЭлемент";
	
	Запрос.УстановитьПараметр("Наименование",	пТекущийОбъект.Наименование);
	Запрос.УстановитьПараметр("ТекущийЭлемент",	пТекущийОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция ЭлементыыНастроекКаналовВзаимодействияПоНаименованиям(пНаименования) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.Наименование КАК Наименование,
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.ТехническоеНаименование КАК ТехническоеНаименование
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыНастроекКаналовВзаимодействия КАК Аук_ЭлементыНастроекКаналовВзаимодействия
	|ГДЕ
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.Наименование В(&Наименования)";
	
	Запрос.УстановитьПараметр("Наименования", пНаименования);
	
	сооРезультат = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		сооРезультат.Вставить(Выборка.Наименование, Выборка.ТехническоеНаименование);
		
	КонецЦикла;
	
	Возврат сооРезультат;
	
КонецФункции

Функция ЭлементНастроекПоТехническомуНаименованию(пТехническоеНаименование) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.Аук_ЭлементыНастроекКаналовВзаимодействия КАК Аук_ЭлементыНастроекКаналовВзаимодействия
	|ГДЕ
	|	Аук_ЭлементыНастроекКаналовВзаимодействия.ТехническоеНаименование = &ТехническоеНаименование";
	
	Запрос.УстановитьПараметр("ТехническоеНаименование", пТехническоеНаименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;
	
КонецФункции	

#КонецОбласти