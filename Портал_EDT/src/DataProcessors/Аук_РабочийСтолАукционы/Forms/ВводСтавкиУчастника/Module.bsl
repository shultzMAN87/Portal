
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальнаяИнициализация();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого ЭлМас Из ЭтотОбъект.РеквизитыСПроверкойЗаполнения Цикл
		ПроверяемыеРеквизиты.Добавить(ЭлМас);	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьИнтерфейс();
	
	СоздатьПервуюСтроку();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокЗначенийСтавкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		НовСтрока = ЭтотОбъект.СписокЗначенийСтавки.НайтиПоИдентификатору(Элемент.ТекущаяСтрока);
		
		Для Каждого ЭлСоо Из ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами Цикл
			НовСтрока["Характеристика" + ЭлСоо.Ключ] = ЭлСоо.Значение;	
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначенийСтавкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Не ЭтотОбъект.НесколькоЗначенийСтруктурыСтавки;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначенийСтавкиПередУдалением(Элемент, Отказ)
	
	Отказ = Не ЭтотОбъект.НесколькоЗначенийСтруктурыСтавки;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
		
	Аук_ОбщийКлиент.НачатьВыборУчастника(ЭтотОбъект, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Аук_ОбщийКлиент.НачатьВыборКонтактногоЛица(ЭтотОбъект, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникОчистка(Элемент, СтандартнаяОбработка)
	
	ЭтотОбъект.КонтактноеЛицо = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникПриИзменении(Элемент)

	Аук_ОбщийКлиент.УчастникПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Принять(Команда)
	ЗафиксироватьИнформациюОтУчастников();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьПервуюСтроку() 
	
	НовСтрока = ЭтотОбъект.СписокЗначенийСтавки.Добавить();
	
	Для Каждого ЭлСоо Из ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами Цикл
		НовСтрока["Характеристика" + ЭлСоо.Ключ] = ЭлСоо.Значение;	
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗафиксироватьИнформациюОтУчастников()
	
	Если ПроверитьЗаполнение() Тогда
		
		Закрыть(ДанныеСтавкиИзДанныхФормы());
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеСтавкиИзДанныхФормы()
	
	сткРезультат = Новый Структура;
	
	сткРезультат.Вставить("Аукцион",		ЭтотОбъект.Аукцион);
	сткРезультат.Вставить("Участник",		ЭтотОбъект.Участник);
	сткРезультат.Вставить("КонтактноеЛицо",	ЭтотОбъект.КонтактноеЛицо);
	
	масРеквизитыСтавки = Аук_ОбщийКлиентСервер.КлючиКоллекцииВМассив(ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами);
	сткРезультат.Вставить("Данные",	Аук_ОбщийКлиент.ДанныеСтавкиВМассивСтруктур(ЭтотОбъект.СписокЗначенийСтавки, масРеквизитыСтавки));
	
	Возврат сткРезультат;
	
КонецФункции	

&НаКлиенте
Процедура НастроитьИнтерфейс()
	
	Элементы.СписокЗначенийСтавки.КоманднаяПанель.Видимость = ЭтотОбъект.НесколькоЗначенийСтруктурыСтавки;
	//Элементы.СписокЗначенийСтавки.КоманднаяПанель.ПодчиненныеЭлементы.СписокЗначенийСтавкиПереместить.Видимость = ЭтотОбъект.НесколькоЗначенийСтруктурыСтавки;
		
КонецПроцедуры

&НаСервере
Процедура НачальнаяИнициализация()
	
	ЭтотОбъект.Аукцион	= Параметры.Аукцион;
	
	Аук_ОбщийСервер.ЗаполнитьДанныеДляОтборовНаФорме(ЭтотОбъект);
	
	Аук_ОбщийСервер.ДобавитьКолонкиДанныхСтавкиПоВидуАукциона(ЭтотОбъект, Параметры.ВидАукциона, "СписокЗначенийСтавки");
	
	ЗаполнитьДляПроверкиЗаполнения();
		
	ЭтотОбъект.НесколькоЗначенийСтруктурыСтавки	= Справочники.Аук_ВидыАукционов.ОписаниеНастроекСтавокПоВидуАукциона(Параметры.ВидАукциона).НесколькоЗначенийСтруктурыСтавки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДляПроверкиЗаполнения()
	
	масСтруктураСтавки = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(Параметры.ВидАукциона);
	
	сооСвязьЭлементовСтавкиСРеквизитами = Новый Соответствие;
	масРеквизитыСПроверкойЗаполнения = Новый Массив;
	
	Для Каждого сткЭлемент Из масСтруктураСтавки Цикл
		сооСвязьЭлементовСтавкиСРеквизитами.Вставить(сткЭлемент.ТехническоеНаименование, сткЭлемент.Элемент);
		
		Если Не сткЭлемент.НеОбязательный Тогда
			масРеквизитыСПроверкойЗаполнения.Добавить("СписокЗначенийСтавки." + сткЭлемент.ТехническоеНаименование);
		КонецЕсли;
	КонецЦикла;
	
	ЭтотОбъект.СвязьЭлементовСтавкиСРеквизитами = Новый ФиксированноеСоответствие(сооСвязьЭлементовСтавкиСРеквизитами);
	
	ЭтотОбъект.РеквизитыСПроверкойЗаполнения	= Новый ФиксированныйМассив(масРеквизитыСПроверкойЗаполнения);
		
КонецПроцедуры

#КонецОбласти

