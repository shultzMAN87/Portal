
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальнаяИнициализация();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	НастроитьВнешнийВидТаблицыПоРежимуРаспределенияСуммы();
	
	ПересчитатьИтоговыеСуммы();
	
	ОбновитьПредставлениеНаправленияСортировки();
	
	УстановкаРежимаПросмотра();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура тзИтоговыеДанныеСтавокПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборПоУчастнику(Элемент.ТекущиеДанные.Участник);
		
КонецПроцедуры

&НаКлиенте
Процедура НадписьНаправлениеСортировкиНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ВыборНаправленияСортировки_Продолжить", ЭтотОбъект);
	
	ЭтотОбъект.сзНаправленияСортировки.ПоказатьВыборЭлемента(Оповещение, НСтр("ru = 'Направление сортировки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборНаправленияСортировки_Продолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ЭтотОбъект.сзНаправленияСортировки.ЗаполнитьПометки(Ложь);
		
		Результат.Пометка = Истина;
		
		ОтсортироватьТаблицуДанныхСтавок(Результат.Значение);
		
		ОбновитьПредставлениеНаправленияСортировки();
		
	КонецЕсли;	
	
КонецПроцедуры

// 1. Если меняется Общая сумма:
//		i) пересчитать итог по распределяемой сумме, по флагу предпол. победитель 
//		ii) если <0 тогда снять флаг факт. победителя со строк, начиная с min по расчетной сумме, на каждой итерации проверяем итог
// 2. Если меняется распределяемая сумма:
//		i) пересчитать расчетную сумму, по флагу предпол. победитель
//		ii) пересчитать итог по распределяемой сумме
//		iii) если <0 флаг факт. победителя не ставим, если >0 то ставим
// 3. Установка флага предпол. победитель
//		i) перечситать итог по распределяемой сумме, по флагу предпол. победитель
//		ii) если <0 флаг факт. победителя не ставим, если >0 то ставим
// 4. Снятие флага предпол. победитель
//		i) перечситать итог по распределяемой сумме, по флагу предпол. победитель
//		ii) проверяем есть ли строки где предпол. победитель = Истина, а факт. победитель = Ложь. Если есть то устанавливаем флаги факт. победитель начиная с max 

&НаКлиенте
Процедура ОбщаяСуммаПриИзменении(Элемент)
	
	ОбновитьОтметкиПобедителейПриИзмененииОбщейСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийСрокПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура тзИтоговыеДанныеСтавокРаспределяемаяСуммаПриИзменении(Элемент)
	
	ТекДанные = Элементы.тзИтоговыеДанныеСтавок.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		Если ТекДанные.СтатусУчастия = 0 Тогда
			
			Если ТекДанные.РаспределяемаяСумма = 0 Тогда
				
				ТекДанные.ПобедительФактический		= Ложь;
				ТекДанные.ПобедительПредполагаемый	= Ложь;
				
				ТекДанные.РасчетнаяСумма = 0;
				
			Иначе	
				
				ИтоговоеВыражение = Аук_ОбщийКлиентСервер.ЗаменитьРаспределяемуюСуммуВФормуле(ТекДанные.ФормулаРасчетнойСуммы, ТекДанные.РаспределяемаяСумма);								
				ТекДанные.РасчетнаяСумма = Вычислить(ИтоговоеВыражение);
				
				Если ТекДанные.ПобедительПредполагаемый Тогда
					
					Если ТекДанные.ПобедительФактический Тогда
						
						Если ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() < 0 Тогда	
							
							ТекДанные.ПобедительФактический = Ложь;
							
						Иначе
							
							ТекДанные.ПобедительФактический = Истина;
							
						КонецЕсли;	
						
					Иначе
						
						Если (ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() - ТекДанные.РаспределяемаяСумма) < 0 Тогда
							
							ТекДанные.ПобедительФактический = Ложь;
							
						Иначе
							
							ТекДанные.ПобедительФактический = Истина;
							
						КонецЕсли;
						
					КонецЕсли;
					
					//ОбновитьИтогПоРасчетнойСумме();
					
				КонецЕсли;
				
				Если ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() > 0 Тогда
					
					УстановитьФлагиФактическихПобедителейНачинаяСНаибольшего();
					
				Иначе
					
					ОбновитьИтогПоРасчетнойСумме();
					
				КонецЕсли;	
						
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьОстатокПоРаспределяемойСуммеДляПредполагаемыхПобедителей();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура тзИтоговыеДанныеСтавокПобедительПредполагаемыйПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	Если ЭтотОбъект.СРаспределениемСуммы Тогда
		
		ТекДанные = Элементы.тзИтоговыеДанныеСтавок.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено Тогда
			
			Если ТекДанные.СтатусУчастия = 0 Тогда
				
				Если ТекДанные.РаспределяемаяСумма <> 0 Тогда	
				
				Если ТекДанные.ПобедительПредполагаемый Тогда
					
					Если ТекДанные.ПобедительФактический Тогда
						
						Если (ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() + ТекДанные.РаспределяемаяСумма) < 0 Тогда
							
							ТекДанные.ПобедительФактический = Ложь;
							
						Иначе
							
							ТекДанные.ПобедительФактический = Истина;
							
						КонецЕсли;	
						
					Иначе
						
						Если (ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() - ТекДанные.РаспределяемаяСумма) < 0 Тогда
							
							ТекДанные.ПобедительФактический = Ложь;
							
						Иначе
							
							ТекДанные.ПобедительФактический = Истина;
							
						КонецЕсли;
						
					КонецЕсли;
					
					ОбновитьИтогПоРасчетнойСумме();
					
				Иначе
					
					ТекДанные.ПобедительФактический = Ложь;
					
					УстановитьФлагиФактическихПобедителейНачинаяСНаибольшего();	
					
				КонецЕсли;
				
				КонецЕсли;
				
			ИначеЕсли ТекДанные.СтатусУчастия = 1 Тогда
				
				ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиПоИдентификатору(Элементы.тзИтоговыеДанныеСтавок.ТекущаяСтрока).ПобедительПредполагаемый = Ложь;
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Участник не делал ставки'"));
				
			Иначе
				
				ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиПоИдентификатору(Элементы.тзИтоговыеДанныеСтавок.ТекущаяСтрока).ПобедительПредполагаемый = Ложь;
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Участник отказался от участия'"));
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе	
		
		ТекДанные = Элементы.тзИтоговыеДанныеСтавок.ТекущиеДанные;
		
		Если ТекДанные.СтатусУчастия = 0 Тогда
			
			Если ТекДанные <> Неопределено Тогда
				
				ТекДанные.ПобедительФактический = ТекДанные.ПобедительПредполагаемый;
				
			КонецЕсли;
			
		ИначеЕсли ТекДанные.СтатусУчастия = 1 Тогда
			
			ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиПоИдентификатору(Элементы.тзИтоговыеДанныеСтавок.ТекущаяСтрока).ПобедительПредполагаемый = Ложь;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Участник не делал ставки'"));
			
		Иначе
			
			ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиПоИдентификатору(Элементы.тзИтоговыеДанныеСтавок.ТекущаяСтрока).ПобедительПредполагаемый = Ложь;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Участник отказался от участия'"));
			
		КонецЕсли
		
	КонецЕсли;
	
	ОбновитьОстатокПоРаспределяемойСуммеДляПредполагаемыхПобедителей();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщаяСуммаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЭтотОбъект.ОбщаяСумма = Аук_ОбщийВызовСервера.КлючевойПараметрАукциона(ЭтотОбъект.Аукцион, "Сумма");
	
	ОбновитьОтметкиПобедителейПриИзмененииОбщейСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийСрокОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЭтотОбъект.ОбщийСрок = Аук_ОбщийВызовСервера.КлючевойПараметрАукциона(ЭтотОбъект.Аукцион, "Срок");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НачальныеДанныеПоТекущемуУчастникуПриИзменении(Элемент)
	
	ОбновитьСостояниеОтбораПоТекущемуУчастнику();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура СохранитьТекущееСостояние(Команда)	
	СохранитьСвязанныеДанныеПодведенияИтогов();		
КонецПроцедуры

&НаКлиенте
Процедура Подтвердить(Команда)	
	ПодтвердитьОтправкуРезультатовАукционаУчастникамИЗакрыть();	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	НачатьЗакрытиеФормы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьЗакрытиеФормы()
	
	Если Модифицированность Тогда		
		стрТекстВопроса = НСтр("ru = 'Данные были изменены. Вы хотите сохранить текущее состояние?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытием_Продолжить", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, стрТекстВопроса, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));
	Иначе
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытием_Продолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Аук_ОбщийКлиент.ОкончательныеИтогиЗафиксированы(ДанныеИтоговИзДанныхФормы(Истина)) Тогда
			Модифицированность = Ложь;
			
			Закрыть();	
		КонецЕсли;
	Иначе
		Модифицированность = Ложь;
		
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранитьСвязанныеДанныеПодведенияИтогов()

	Если Аук_ОбщийКлиент.ОкончательныеИтогиЗафиксированы(ДанныеИтоговИзДанныхФормы(Истина)) Тогда	
		Модифицированность = Ложь;	
	КонецЕсли;	
	
КонецФункции

&НаКлиенте
Процедура ПодтвердитьОтправкуРезультатовАукционаУчастникамИЗакрыть()
	
	Если ПобедителиВыбраны() Тогда 	
		стрТекстВопроса = НСтр("ru = 'Информация об итоговых результатах будет направлена участникам. Продолжить?'");
		
		Оповещение = Новый ОписаниеОповещения("ОтправкуРезультатов_Продолжить", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, стрТекстВопроса, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));	
	Иначе	
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Победители не выбраны!'"));	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправкуРезультатов_Продолжить(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда		
		Модифицированность = Ложь;
		
		Закрыть(ДанныеИтоговИзДанныхФормы());	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ПобедителиВыбраны()
	
	Возврат ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиСтроки(Новый Структура("ПобедительФактический", Истина)).Количество() > 0;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСостояниеОтбораПоТекущемуУчастнику()

	Если ЭтотОбъект.НачальныеДанныеПоТекущемуУчастнику Тогда
		
		ТекДанные = Элементы.тзИтоговыеДанныеСтавок.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено Тогда
			
			УстановитьОтборПоУчастнику(ТекДанные.Участник);
			
		КонецЕсли;	
		
	Иначе
		
		Элементы.тзНачальныеДанные.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура);
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьОтметкиПобедителейПриИзмененииОбщейСуммы()

	ОбновитьОстатокПоРаспределяемойСуммеДляПредполагаемыхПобедителей();
	
	чслОстаток = ОстатокПоРаспределяемойСуммеДляФактическихПобедителей();
	
	Если чслОстаток < 0 Тогда
		
		СнятьФлагиФактическихПобедителейНачинаяСНаименьшего();
		
	Иначе
		
		УстановитьФлагиФактическихПобедителейНачинаяСНаибольшего();	
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьОстатокПоРаспределяемойСуммеДляПредполагаемыхПобедителей()

	ЭтотОбъект.РаспределяемаяСуммаИтог = ЭтотОбъект.ОбщаяСумма - СуммаПоКолонкеТаблицы("РаспределяемаяСумма", "ПобедительПредполагаемый"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогПоРасчетнойСумме()

	ЭтотОбъект.РасчетнаяСуммаИтог = СуммаПоКолонкеТаблицы("РасчетнаяСумма", "ПобедительФактический"); 
	
КонецПроцедуры

&НаКлиенте
Функция ОстатокПоРаспределяемойСуммеДляФактическихПобедителей()

	Возврат ЭтотОбъект.ОбщаяСумма - СуммаПоКолонкеТаблицы("РаспределяемаяСумма", "ПобедительФактический"); 
	
КонецФункции

&НаКлиенте
Функция СуммаПоКолонкеТаблицы(пИмяКолонки, пКолонкаТипПобедителя) 
	
	чслРезультат = 0;
	
	Для Каждого СтрокаТз Из ЭтотОбъект.тзИтоговыеДанныеСтавок Цикл
		
		Если СтрокаТз[пКолонкаТипПобедителя] Тогда
			
			чслРезультат = чслРезультат + СтрокаТз[пИмяКолонки];
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат чслРезультат;
	
КонецФункции

&НаКлиенте
Функция СписокЗначенийПоКолонке(пИмяКолонки, пКолонкаТипПобедителя) 
	
	сзРезультат = Новый СписокЗначений;
	
	Для Каждого СтрокаТз Из ЭтотОбъект.тзИтоговыеДанныеСтавок Цикл
		
		Если СтрокаТз[пКолонкаТипПобедителя] Тогда
			
			сзРезультат.Добавить(СтрокаТз[пИмяКолонки]);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат сзРезультат;
	
КонецФункции

&НаКлиенте
Процедура СнятьФлагиФактическихПобедителейНачинаяСНаименьшего()
	
	сзРаспрСуммы = РаспределяемаяСуммаИзТзИтоговыеДанныеВСписокЗначений(Истина, Истина);
		
	сзРаспрСуммы.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
	ДеактивироватьПобедительФактическийПоСпискуРаспределяемыхСумм(сзРаспрСуммы, Истина, Истина);
	
	ОбновитьИтогПоРасчетнойСумме();
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлагиФактическихПобедителейНачинаяСНаибольшего()
	
	сзРаспрСуммы = РаспределяемаяСуммаИзТзИтоговыеДанныеВСписокЗначений(Истина, Ложь);
		
	сзРаспрСуммы.СортироватьПоЗначению(НаправлениеСортировки.Возр);
	
	АктивироватьПобедительФактическийПоСпискуРаспределяемыхСумм(сзРаспрСуммы, Истина, Ложь);
	
	ОбновитьИтогПоРасчетнойСумме();
	
КонецПроцедуры

&НаКлиенте
Функция РаспределяемаяСуммаИзТзИтоговыеДанныеВСписокЗначений(пБулПобедительПредполагаемый, пБулПобедительФактический)

	масСтрокиТз = ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиСтроки(Новый Структура("ПобедительПредполагаемый, ПобедительФактический", пБулПобедительПредполагаемый, пБулПобедительФактический));
	
	сзРезультат = Новый СписокЗначений;
	
	Для Каждого ЭлСтрокаТз Из масСтрокиТз Цикл
		
		сзРезультат.Добавить(ЭлСтрокаТз.РаспределяемаяСумма);
		
	КонецЦикла;
	
	Возврат сзРезультат;
	
КонецФункции

&НаКлиенте
Процедура АктивироватьПобедительФактическийПоСпискуРаспределяемыхСумм(пСзРаспрСуммы, пБулПобедительПредполагаемый, пБулПобедительФактический)

	Для Каждого ЭлСз Из пСзРаспрСуммы Цикл
		
		сткОтбор = Новый Структура("ПобедительПредполагаемый, ПобедительФактический, РаспределяемаяСумма", пБулПобедительПредполагаемый, пБулПобедительФактический, ЭлСз.Значение);
		
		масСтрокиТз = ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиСтроки(сткОтбор);
		
		Для Каждого ЭлСтрокаТз Из масСтрокиТз Цикл
			
			//Если (ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() + ЭлСтрокаТз.РаспределяемаяСумма) >= 0 Тогда
			Если (ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() - ЭлСтрокаТз.РаспределяемаяСумма) >= 0 Тогда	
				
				ЭлСтрокаТз.ПобедительФактический = Истина;
				
			Иначе
				
				Возврат;	
				
			КонецЕсли;	
			
		КонецЦикла;
			
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеактивироватьПобедительФактическийПоСпискуРаспределяемыхСумм(пСзРаспрСуммы, пБулПобедительПредполагаемый, пБулПобедительФактический)

	Для Каждого ЭлСз Из пСзРаспрСуммы Цикл
		
		сткОтбор = Новый Структура("ПобедительПредполагаемый, ПобедительФактический, РаспределяемаяСумма", пБулПобедительПредполагаемый, пБулПобедительФактический, ЭлСз.Значение);
		
		масСтрокиТз = ЭтотОбъект.тзИтоговыеДанныеСтавок.НайтиСтроки(сткОтбор);
		
		Для Каждого ЭлСтрокаТз Из масСтрокиТз Цикл
			
			Если ОстатокПоРаспределяемойСуммеДляФактическихПобедителей() < 0 Тогда
				
				ЭлСтрокаТз.ПобедительФактический = Ложь;
				
			Иначе
				
				Возврат;	
				
			КонецЕсли;	
			
		КонецЦикла;
			
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьИтоговыеСуммы()

	Если ЭтотОбъект.СРаспределениемСуммы Тогда
		
		ОбновитьОстатокПоРаспределяемойСуммеДляПредполагаемыхПобедителей();
		
		ОбновитьИтогПоРасчетнойСумме();
				
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВнешнийВидТаблицыПоРежимуРаспределенияСуммы()
	
	Элементы.тзИтоговыеДанныеСтавокРаспределяемаяСумма.Видимость	= ЭтотОбъект.СРаспределениемСуммы;
	Элементы.тзИтоговыеДанныеСтавокРасчетнаяСумма.Видимость			= ЭтотОбъект.СРаспределениемСуммы;
	
	Элементы.ГруппаРаспределяемаяСумма.Видимость					= ЭтотОбъект.СРаспределениемСуммы;
	Элементы.ГруппаРаспределяемыйОстаток.Видимость					= ЭтотОбъект.СРаспределениемСуммы;
	
	Элементы.НадписьРасчетнаяСуммаИтог.Заголовок					= Символ(8721);
	Элементы.НадписьРаспределяемаяСумма.Заголовок					= СтрШаблон(НСтр("ru = 'Остаток распределяемой суммы'"));
	
	Элементы.ГруппаНачальныеДанные.Видимость						= ЭтотОбъект.тзНачальныеДанные.Количество() <> 0;
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеИтоговИзДанныхФормы(пБулТолькоСохранить = Ложь)

	сткДанные = Новый Структура;
	
	сткДанные.Вставить("Аукцион",			ЭтотОбъект.Аукцион);
	сткДанные.Вставить("ИтогиАукциона",		Аук_ОбщийКлиентСервер.ДанныеИтоговИзТаблицыИтогов(ЭтотОбъект.тзИтоговыеДанныеСтавок));
	сткДанные.Вставить("НачальныеУсловия",	НачальныеУсловия());
	сткДанные.Вставить("ТолькоСохранить",	пБулТолькоСохранить);
	
	Возврат сткДанные;
	
КонецФункции

&НаКлиенте
Функция НачальныеУсловия() 
	
	сткРез = Новый Структура("ОбщаяСумма, ОбщийСрок");
	
	ЗаполнитьЗначенияСвойств(сткРез, ЭтотОбъект);
	
	Возврат сткРез;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьПредставлениеНаправленияСортировки()
	
	Для Каждого ЭлСз Из ЭтотОбъект.сзНаправленияСортировки Цикл
		
		Если ЭлСз.Пометка Тогда
			
			ЗаголовокНадписи = ЭлСз.Представление;
			
			Прервать;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ЗаголовокНадписи) Тогда
		
		Элементы.НадписьНаправлениеСортировки.Заголовок = ЗаголовокНадписи;
		
	Иначе
		
		Элементы.НадписьНаправлениеСортировки.Заголовок = НСтр("ru = 'не задано'");
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтсортироватьТаблицуДанныхСтавок(пНастройкиСортировки)
	
	ЭтотОбъект.тзИтоговыеДанныеСтавок.Сортировать(СтрЗаменить(пНастройкиСортировки, "_", " "));
	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьОтборПоУчастнику(пУчастник)
	
	Если ЭтотОбъект.НачальныеДанныеПоТекущемуУчастнику Тогда
	
		Элементы.тзНачальныеДанные.ОтборСтрок = Новый ФиксированнаяСтруктура("Участник", пУчастник);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачальнаяИнициализация()
	
	// Заполнение реквизитов формы
	ЭтотОбъект.Аукцион				= Параметры.Аукцион;
	ЭтотОбъект.ВидАукциона			= Параметры.ВидАукциона;
	
	сткКлючевыеДанныеВида			= Справочники.Аук_ВидыАукционов.КлючевыеДанныеВидаАукциона(Параметры.ВидАукциона);
	
	ЭтотОбъект.СРаспределениемСуммы = сткКлючевыеДанныеВида.СРаспределениемСуммы;
		
	ЗаполнитьНаправленияСортировки();
	
	сткПараметры = Новый Структура;
	
	сткПараметры.Вставить("Аукцион",					Параметры.Аукцион);
	сткПараметры.Вставить("ВидАукциона",				Параметры.ВидАукциона);
	сткПараметры.Вставить("СРаспределениемСуммы",		сткКлючевыеДанныеВида.СРаспределениемСуммы);
	
	Аук_ОбщийСервер.ОкончательныеИтоги_ДополнительныеПараметры(сткПараметры);
	
	ЗаполнитьШапку(сткПараметры);
	
	СформироватьКолонкиТаблиц(сткПараметры);
		
	ЗаполнитьТаблицыДанными(сткПараметры);
			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШапку(пСткПараметры)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, пСткПараметры.НачальныеУсловия, "ОбщаяСумма,ОбщийСрок");
		
КонецПроцедуры	

&НаКлиенте
Процедура УстановкаРежимаПросмотра()
	
	Элементы.тзИтоговыеДанныеСтавокРаспределяемаяСумма.ТолькоПросмотр		= ЭтотОбъект.ТолькоПросмотр;
	Элементы.тзИтоговыеДанныеСтавокПобедительПредполагаемый.ТолькоПросмотр	= ЭтотОбъект.ТолькоПросмотр;
		
	Элементы.Подтвердить.Видимость					= Не ЭтотОбъект.ТолькоПросмотр;
	Элементы.СохранитьТекущееСостояние.Видимость	= Не ЭтотОбъект.ТолькоПросмотр;
	
	Элементы.ОбщаяСумма.ТолькоПросмотр				= ЭтотОбъект.ТолькоПросмотр;
	Элементы.ОбщаяСумма.РедактированиеТекста		= Не ЭтотОбъект.ТолькоПросмотр;
	Элементы.ОбщаяСумма.КнопкаВыбора				= Не ЭтотОбъект.ТолькоПросмотр;
	Элементы.ОбщаяСумма.КнопкаОчистки				= Не ЭтотОбъект.ТолькоПросмотр;
	Элементы.ОбщаяСумма.ОтображениеПодсказки		= ?(ЭтотОбъект.ТолькоПросмотр, ОтображениеПодсказки.Нет, ОтображениеПодсказки.Кнопка);
	
	Элементы.ОбщийСрок.ТолькоПросмотр				= ЭтотОбъект.ТолькоПросмотр;
	Элементы.ОбщийСрок.РедактированиеТекста			= Не ЭтотОбъект.ТолькоПросмотр;
	Элементы.ОбщийСрок.КнопкаОчистки				= Не ЭтотОбъект.ТолькоПросмотр;
	Элементы.ОбщийСрок.ОтображениеПодсказки			= ?(ЭтотОбъект.ТолькоПросмотр, ОтображениеПодсказки.Нет, ОтображениеПодсказки.Кнопка);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьНаправленияСортировки()

	ЭтотОбъект.сзНаправленияСортировки.Добавить("Участник_ВОЗР", "участник А-Я");
	ЭтотОбъект.сзНаправленияСортировки.Добавить("Участник_УБЫВ", "участник Я-А");
	
	Если ЭтотОбъект.СРаспределениемСуммы Тогда
		
		ЭтотОбъект.сзНаправленияСортировки.Добавить("РаспределяемаяСумма_ВОЗР", "распределяемая сумма (сначала самая низкая)");
		ЭтотОбъект.сзНаправленияСортировки.Добавить("РаспределяемаяСумма_УБЫВ", "распределяемая сумма (сначала самая высокая)");
		
	КонецЕсли;	
	
КонецФункции

&НаСервере
Процедура СформироватьКолонкиТаблиц(пСткПараметры) 
	
	Аук_ОбщийСервер.ДобавитьКолонкиДанныхСтавкиПоВидуАукциона(ЭтотОбъект, пСткПараметры.ВидАукциона, "тзИтоговыеДанныеСтавок",
																Элементы.тзИтоговыеДанныеДанныеСтавки, пСткПараметры.СтруктураСтавки, Ложь);
	
	Аук_ОбщийСервер.ДобавитьКолонкиНачальныхДанныхПоАукциону(ЭтотОбъект, пСткПараметры.ВидАукциона, "тзНачальныеДанные",
																Элементы.тзИтоговыеНачальныеДанные, пСткПараметры.СтруктураНачальныхДанных, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыДанными(пСткПараметры)
	
	ЗаполнитьНачальныеДанные(пСткПараметры);
	
	ЗаполнитьИтоговыеДанныеСтавок(пСткПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИтоговыеДанныеСтавок(пСткПараметры)

	тзИтоговыеДанныеСтавокТемп = Аук_ОбщийСервер.ОкончательныеИтоги_ДанныеСтавокУчастников(пСткПараметры);
	
	Для Каждого СтрокаТз Из тзИтоговыеДанныеСтавокТемп Цикл
		ЗаполнитьЗначенияСвойств(ЭтотОбъект.тзИтоговыеДанныеСтавок.Добавить(), СтрокаТз);	
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьНачальныеДанные(пСткПараметры)
		
	Для Каждого ЭлементМас Из пСткПараметры.НачальныеДанные Цикл
		ЗаполнитьЗначенияСвойств(ЭтотОбъект.тзНачальныеДанные.Добавить(), ЭлементМас); 	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
