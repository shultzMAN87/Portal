
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НачальнаяИнициализация();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Отправить(Команда)
	
	Если АдресатыВыбраны() И ПроверитьЗаполнение() Тогда
		
		Закрыть(ДанныеИсходящегоСообщенияИзДанныхФормы());	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ДанныеИсходящегоСообщенияИзДанныхФормы()

	сткРез = Новый Структура;
	
	// 1. Кому отправляем	
	масАдресаты = Новый Массив;
	Для Каждого СтрокаТз Из ЭтотОбъект.тзАдресаты Цикл
		
		Если СтрокаТз.Пометка Тогда
			сткАдресат = Новый Структура("Участник, КонтактноеЛицо");
			
			ЗаполнитьЗначенияСвойств(сткАдресат, СтрокаТз); 
			
			масАдресаты.Добавить(сткАдресат);
		КонецЕсли;	
			
	КонецЦикла;
	
	сткРез.Вставить("Адресаты", масАдресаты);
	
	// 2. Что отправляем
	сткРез.Вставить("ТекстСообщения", ЭтотОбъект.ТекстСообщения);
	
	// 3. Для какого аукциона
	сткРез.Вставить("Аукцион", ЭтотОбъект.Аукцион);
		
	Возврат сткРез;
	
КонецФункции	

&НаКлиенте
Функция АдресатыВыбраны()
	
	Если ЭтотОбъект.тзАдресаты.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран ни один адресат'"),, "тзАдресаты");
		
		Возврат Ложь;	
	Иначе
		Возврат Истина;		
	КонецЕсли;
	
КонецФункции	

&НаСервере
Процедура НачальнаяИнициализация()
	
	ЭтотОбъект.Аукцион	= Параметры.Аукцион;
	
	тзКонтЛицаУчастников = Справочники.Аук_Аукционы.КонтактныеЛицаСПривязкойКУчастникам(Параметры.Аукцион);
	
	Для Каждого СтрокаТз Из тзКонтЛицаУчастников Цикл		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект.тзАдресаты.Добавить(), СтрокаТз); 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
