
#Область Переменные

&НаКлиенте
Перем СписокКолонокВТаблицеПобедителей; 

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальнаяИнициализация();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьРаспределяемыйОстаток();
	
	НастроитьВнешнийВидТаблицыПоРежимуРаспределенияСуммы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ВопросПроСохранениеПередЗакрытием_Продолжить", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Данные были изменены. Сохранить из перед закрытием?'"), РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));
		
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ВопросПроСохранениеПередЗакрытием_Продолжить(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		СохранитьТекущееСостояние();
		
	Иначе
		
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	Закрыть();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура тзИтоговыеДанныеРаспределяемаяСуммаПриИзменении(Элемент)
	
	ТекДанные = Элементы.тзИтоговыеДанные.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		ТекДанные.РасчетнаяСумма = Аук_ОбщийКлиентСервер.РасчетнаяСуммаПоСтроке(ТекДанные, ЭтотОбъект.ОбщийСрок);
		
		ОбновитьРаспределяемыйОстаток();
		
		ОбновитьРаспределяемуюСуммуВТзПобедители(ТекДанные);
		
		Модифицированность = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура тзИтоговыеДанныеРаспределяемаяСуммаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ТекДанные = ЭтотОбъект.тзИтоговыеДанные.НайтиПоИдентификатору(Элементы.тзИтоговыеДанные.ТекущаяСтрока);
	
	Если ЭтотОбъект.ИтоговаяСумма - ТекДанные.РаспределяемаяСумма + Число(Текст) > ЭтотОбъект.ОбщаяСумма Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеВыбора = Новый СписокЗначений;
      	ДанныеВыбора.Добавить(ТекДанные.РаспределяемаяСумма);
		
		СообщитьПользователюОПревышенииЛимита();
		
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ОбщаяСуммаПриИзменении(Элемент)
	
	ОбновитьРаспределяемыйОстаток();
	
КонецПроцедуры

&НаКлиенте
Процедура тзПобедителиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ОчиститьСообщения();
	
	СтандартнаяОбработка = Ложь;
	
	ЗначПеретаскивания = ПараметрыПеретаскивания.Значение;
	
	Если ЗначПеретаскивания.СтатусУчастия = 0 Тогда
		
		Если ТакаяСтрокаУжеЕстьВТаблицеПобедителей(ЗначПеретаскивания) Тогда 
			
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			
		КонецЕсли;
		
		Если ЛимитСуммыПревышен(ЗначПеретаскивания.РаспределяемаяСумма) Тогда
			
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			
			СообщитьПользователюОПревышенииЛимита();
			
		КонецЕсли;	
		
	Иначе
		
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Этот участник %1'"), ?(ЗначПеретаскивания.СтатусУчастия = 1, НСтр("ru = 'не делал ставки'"), НСтр("ru = 'отказался'"))));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тзПобедителиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("ДанныеФормыЭлементКоллекции") Тогда 
		ПеренестиДанныеИзИтоговойТаблицыВТаблицуПобедителей(ПараметрыПеретаскивания.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тзПобедителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	СтандартнаяОбработка = Ложь;
	
	ЭтотОбъект.тзПобедители.Удалить(ЭтотОбъект.тзПобедители.НайтиПоИдентификатору(ВыбраннаяСтрока));
	
	ПересчитатьИтоговуюСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура тзИтоговыеДанныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя <> "тзИтоговыеДанныеРаспределяемаяСумма" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекДанные = ЭтотОбъект.тзИтоговыеДанные.НайтиПоИдентификатору(ВыбраннаяСтрока);
		
		Если ТекДанные <> Неопределено Тогда
			
			Если ТекДанные.СтатусУчастия = 0 Тогда 
				
				Если ЛимитСуммыПревышен(ТекДанные.РаспределяемаяСумма) Тогда
					
					СообщитьПользователюОПревышенииЛимита();	
					
				Иначе	
					
					Если Не ТакаяСтрокаУжеЕстьВТаблицеПобедителей(ТекДанные) Тогда
						
						ПеренестиДанныеИзИтоговойТаблицыВТаблицуПобедителей(ТекДанные);
						
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ТекДанные.СтатусУчастия = 1 Тогда
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Этот участник не делал ставки'"));
				
			Иначе
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Этот участник отказался от ставок'"));
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тзПобедителиНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	
КонецПроцедуры

&НаКлиенте
Процедура тзИтоговыеДанныеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;

КонецПроцедуры

&НаКлиенте
Процедура тзПобедителиПриИзменении(Элемент)
	
	ПересчитатьИтоговуюСумму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Асинх Процедура Подтвердить(Команда)
	
	Если ЭтотОбъект.тзПобедители.Количество() <> 0 Тогда
		
		стрТекстВопроса = НСтр("ru = 'Результаты подведены, победители буду оповещены. Продолжить?'");
		
		Ответ = Ждать ВопросАсинх(стрТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, НСтр("ru = 'Предупреждение'")); 
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			сткРез = ВыполнитьСвязанныеДанныеПередЗакрытием();
			
			Если сткРез.Успех Тогда
				
				Закрыть(Истина);
				
			Иначе
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(сткРез.Результат);
				
			КонецЕсли;	
			
		КонецЕсли;
		
	Иначе
		
		стрТекстВопроса = НСтр("ru = 'Вы не выбрали ни одного победителя, аукцион будет завершен. Продолжить?'");
		
		Ответ = Ждать ВопросАсинх(стрТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, НСтр("ru = 'Предупреждение'")); 
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			Закрыть(Истина);	
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСостояние(Команда)
	
	СохранитьТекущееСостояние();	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьВнешнийВидТаблицыПоРежимуРаспределенияСуммы()

	Элементы.РаспределяемыйОстаток.Видимость				= ЭтотОбъект.СРаспределениемСуммы;
	Элементы.ИтоговаяСумма.Видимость						= ЭтотОбъект.СРаспределениемСуммы;
	
	Элементы.тзИтоговыеДанныеРаспределяемаяСумма.Видимость	= ЭтотОбъект.СРаспределениемСуммы;
	Элементы.тзИтоговыеДанныеРасчетнаяСумма.Видимость		= ЭтотОбъект.СРаспределениемСуммы;
	
	Элементы.ТзПобедителиРаспределяемаяСумма.Видимость		= ЭтотОбъект.СРаспределениемСуммы;
	Элементы.ТзПобедителиРасчетнаяСумма.Видимость			= ЭтотОбъект.СРаспределениемСуммы;
	
КонецПроцедуры	

&НаКлиенте
Процедура СохранитьТекущееСостояние()

	Модифицированность = Ложь;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьРаспределяемуюСуммуВТзПобедители(пТекДанные) 

	сткОтбор = СтруктураОтбораВТаблицеПобедителей();
	
	ЗаполнитьЗначенияСвойств(сткОтбор, пТекДанные);
	
	масСтрокиТч = ЭтотОбъект.тзПобедители.НайтиСтроки(сткОтбор);
	
	Если масСтрокиТч.Количество() <> 0 Тогда
		
		ЗаполнитьЗначенияСвойств(масСтрокиТч[0], пТекДанные, "РаспределяемаяСумма,РасчетнаяСумма");
		
		ПересчитатьИтоговуюСумму();
		
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура СообщитьПользователюОПревышенииЛимита()

	ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Итоговая сумма превышает распределяемую сумму'"));
	
КонецПроцедуры	

&НаКлиенте
Функция ЛимитСуммыПревышен(пРаспределяемаяСумма)
	
	Если ЭтотОбъект.СРаспределениемСуммы Тогда
		
		Возврат пРаспределяемаяСумма + ЭтотОбъект.ИтоговаяСумма > ЭтотОбъект.ОбщаяСумма;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции	

&НаКлиенте
Функция ТакаяСтрокаУжеЕстьВТаблицеПобедителей(пТекДанные)

	сткОтбор = СтруктураОтбораВТаблицеПобедителей();
	
	ЗаполнитьЗначенияСвойств(сткОтбор, пТекДанные);
	
	Возврат ЭтотОбъект.тзПобедители.НайтиСтроки(сткОтбор).Количество() <> 0;
	
КонецФункции	

&НаКлиенте
Процедура ПеренестиДанныеИзИтоговойТаблицыВТаблицуПобедителей(пТекДанные)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект.тзПобедители.Добавить(), пТекДанные);
	
	ПересчитатьИтоговуюСумму();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьИтоговуюСумму()
	
	Если ЭтотОбъект.СРаспределениемСуммы Тогда
		
		ЭтотОбъект.ИтоговаяСумма = ЭтотОбъект.тзПобедители.Итог("РаспределяемаяСумма");
		
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьРаспределяемыйОстаток()

	ЭтотОбъект.РаспределяемыйОстаток = ЭтотОбъект.ОбщаяСумма - ЭтотОбъект.тзИтоговыеДанные.Итог("РаспределяемаяСумма");
	
КонецПроцедуры	

&НаКлиенте
Функция ВыполнитьСвязанныеДанныеПередЗакрытием()
	
	// 1. Записать данные в РС Аук_ДанныеШапокПодведенияИтогов
	сткДанные = ДанныеШапкиАукционаИзДанныхФормы();
	
	сткРез = Аук_ОбщийВызовСервера.ЗафиксироватьДанныеШапкиПоАукциону(сткДанные);
	
	// 2. Записать данные в РС Аук_ДанныеТаблицПодведенияИтогов
	сткДанные = СведенияИзТаблицыПодведенияИтоговДляЗаписи();
	
	сткРез = Аук_ОбщийВызовСервера.ЗафиксироватьДанныеТаблицыПодведенияИтогов(сткДанные);
	
	// 3. Оповестить победителей		
	масРез = Аук_ОбщийКлиент.ДанныеДляОтправкиСообщенийУчастникамВJSONПоСпискуКонтактныхЛиц(ПараметрыИсходящегоСообщения());
	
	Если масРез.Количество() = 0 Тогда
		
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Не найдены настройки для отправки сообщений контактным лица'"));
		
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();
	
КонецФункции

&НаКлиенте
Функция ДанныеШапкиАукционаИзДанныхФормы() 
	
	сткРез = Новый Структура("Аукцион, ОбщаяСумма, ОбщийСрок");
	
	ЗаполнитьЗначенияСвойств(сткРез, ЭтотОбъект);
	
	Возврат сткРез;
	
КонецФункции

&НаКлиенте
Функция СведенияИзТаблицыПодведенияИтоговДляЗаписи()

	сткДанные = Новый Структура;
	
	сткДанные.Вставить("Аукцион",		ЭтотОбъект.Аукцион);
	сткДанные.Вставить("ДанныеИтогов",	СписокДанныхИтогов());
	
	Возврат сткДанные;	
	
КонецФункции

&НаКлиенте
Функция СписокДанныхИтогов()
	
	сооДанные = ДанныеТаблицыПодведенияИтогов();
	
	масРез = Новый Массив;
	
	Для Каждого ЭлСоо Из сооДанные Цикл
		
		Для Каждого СткМас Из ЭлСоо.Значение Цикл
			
			сткЭлемент = Новый Структура("Участник, СтрокаДанных, РаспределяемаяСумма, ЭтоПобедитель"); 
			
			сткЭлемент.Участник = ЭлСоо.Ключ;
			
			ЗаполнитьЗначенияСвойств(сткЭлемент, СткМас); 
			
			масРез.Добавить(сткЭлемент);
			
		КонецЦикла;	
		
	КонецЦикла;
	
	Возврат масРез;
	
КонецФункции	

&НаКлиенте
Функция ДанныеТаблицыПодведенияИтогов() 
	
	сткОтбор = СтруктураОтбораВТаблицеПобедителей();
	
	// 1. Обходим таблицу
	сооРезультат = Новый Соответствие;
	
	Для Каждого СтрокаТз Из ЭтотОбъект.тзИтоговыеДанные Цикл
		
		Если СтрокаТз.СтатусУчастия = 0 Тогда
			ЭлРез = сооРезультат.Получить(СтрокаТз.Участник);
			
			Если ЭлРез = Неопределено Тогда
				
				сооРезультат.Вставить(СтрокаТз.Участник, Новый Массив);
				
			КонецЕсли;	
			
			сткСтрока = Новый Структура("СтрокаДанных, РаспределяемаяСумма, ЭтоПобедитель");
			
			сткСтрока.СтрокаДанных = сооРезультат[СтрокаТз.Участник].Количество();
			сткСтрока.РаспределяемаяСумма = СтрокаТз.РаспределяемаяСумма;
			
			ЗаполнитьЗначенияСвойств(сткОтбор, СтрокаТз);
			
			сткСтрока.ЭтоПобедитель = ЭтотОбъект.тзПобедители.НайтиСтроки(сткОтбор).Количество() <> 0;		
			
			сооРезультат[СтрокаТз.Участник].Добавить(сткСтрока);
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат сооРезультат;
	
КонецФункции

&НаКлиенте
Функция СтруктураОтбораВТаблицеПобедителей()
	
	Если СписокКолонокВТаблицеПобедителей = Неопределено Тогда 
		СписокКолонокВТаблицеПобедителейТемп = СписокКолонокТаблицыПобедителей();
		
		СписокКолонокВТаблицеПобедителей = Новый Массив;
		
		Для Каждого ЭлМас Из СписокКолонокВТаблицеПобедителейТемп Цикл
			
			Если ЭлМас <> "РаспределяемаяСумма" И ЭлМас <> "РасчетнаяСумма" Тогда
				
				СписокКолонокВТаблицеПобедителей.Добавить(ЭлМас);
				
			КонецЕсли;	
				
		КонецЦикла;	
	КонецЕсли;
	
	сткОтбор = Новый Структура;
	
	Для Каждого ЭлМас Из СписокКолонокВТаблицеПобедителей Цикл
		сткОтбор.Вставить(ЭлМас);	
	КонецЦикла;
	
	Возврат сткОтбор;
	
КонецФункции	

&НаСервере
Функция СписокКолонокТаблицыПобедителей()

	Возврат Аук_ОбщийСервер.СписокРеквизитовФормы(ЭтотОбъект, "тзПобедители");
	
КонецФункции	

&НаКлиенте
Функция ПараметрыИсходящегоСообщения()

	сткРез = Новый Структура;
	
	// 1. Кому отправляем
	сткРез.Вставить("КонтактныеЛица", Аук_ОбщийВызовСервера.СписокКонтактныхЛицАукциона(ЭтотОбъект.Аукцион));
	
	// 2. Что отправляем
	сткРез.Вставить("Сообщение", Аук_ОбщийКлиентСервер.ШаблонСообщенияПобедителям());
	
	Возврат сткРез;
	
КонецФункции

&НаСервере
Процедура НачальнаяИнициализация()
	
	ЭтотОбъект.Аукцион		= Параметры.Аукцион;
	ЭтотОбъект.ВидАукциона	= Параметры.ВидАукциона;
	
	ЭтотОбъект.СРаспределениемСуммы = Справочники.Аук_ВидыАукционов.КлючевыеДанныеВидаАукциона(Параметры.ВидАукциона).СРаспределениемСуммы;
	
	пМасСтруктураСтавки	= Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(Параметры.ВидАукциона);
	
	Аук_ОбщийСервер.ДобавитьКолонкиДанныхСтавкиПоВидуАукциона(ЭтотОбъект, Параметры.ВидАукциона, "тзИтоговыеДанные", Элементы.тзИтоговыеДанныеДанныеСтавки, пМасСтруктураСтавки, Ложь);
	Аук_ОбщийСервер.ДобавитьКолонкиДанныхСтавкиПоВидуАукциона(ЭтотОбъект, Параметры.ВидАукциона, "тзПобедители", Элементы.ТзПобедителиДанныеСтавки, пМасСтруктураСтавки, Ложь);
	
	пМасСтруктураНачДанных	= Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(Параметры.ВидАукциона);
	
	Аук_ОбщийСервер.ДобавитьКолонкиНачальныхДанныхПоАукциону(ЭтотОбъект, Параметры.ВидАукциона, "тзИтоговыеДанные", Элементы.тзИтоговыеНачальныеДанные, пМасСтруктураНачДанных, Истина);
	Аук_ОбщийСервер.ДобавитьКолонкиНачальныхДанныхПоАукциону(ЭтотОбъект, Параметры.ВидАукциона, "тзПобедители", Элементы.ТзПобедителиНачальныеДанные, пМасСтруктураНачДанных, Истина);
	
	сткДанныеАукциона = Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(Параметры.Аукцион);
	
	ЭтотОбъект.ОбщаяСумма	= сткДанныеАукциона.Сумма;	
	ЭтотОбъект.ОбщийСрок	= сткДанныеАукциона.Срок;
	
	ЗаполнитьТаблицуСИтоговымиДанными(); 
			
КонецПроцедуры

// Так как начальные данные по участнику могут быть множеством и данные ставки могут буть множеством
// решили сделать декартово произведение
// в запросе такое сделать проблематично из-за того, что список колонок динамический
// хотя... можно было бы построителем запросов
// Для начальных данных используется префикс
//
&НаСервере
Процедура ЗаполнитьТаблицуСИтоговымиДанными()
	
	тзНачДанные			= Справочники.Аук_Аукционы.ЗначенияНачальныхДанных(Параметры.Аукцион, Параметры.ВидАукциона);
	
	масСписокКолонокНачДанные = Аук_ОбщийКлиентСервер.СписокКолонокТаблицы(тзНачДанные, "Участник");
	
	масОбязательныеКолонки = Аук_ОбщийСервер.СоставОбязательныхКолонок("Участник,СтрокаДанных");
	
	тзТекДанныеСтавки	= РегистрыСведений.Аук_ИсторияДанныхАукционов.ТекущиеДанныеСтавокУчастниковАукционаВСтроке(Параметры.Аукцион, Параметры.ВидАукциона, масОбязательныеКолонки);
	
	масСписокКолонокТекДанныеСтавки = Аук_ОбщийКлиентСервер.СписокКолонокТаблицы(тзТекДанныеСтавки, "Участник");
	
	масУчастники		= РегистрыСведений.Аук_ИсторияДанныхАукционов.СведенияОбАктивностиУчастниковАукциона(Параметры.Аукцион);
	
	Для Каждого ЭлМас Из масУчастники Цикл
		
		масСтрокиТзНачДанные = тзНачДанные.НайтиСтроки(Новый Структура("Участник", ЭлМас.Участник));
		
		Если ЭлМас.СтатусУчастия = 0 Тогда
			
			масСтрокиТзТекДанныеСтавки = тзТекДанныеСтавки.НайтиСтроки(Новый Структура("Участник", ЭлМас.Участник));
			
			Если масСтрокиТзТекДанныеСтавки.Количество() <> 0 Тогда 
				
				Если масСтрокиТзНачДанные.Количество() <> 0 Тогда 
					
					Для Каждого СтрокаТзНачДанные Из масСтрокиТзНачДанные Цикл
						
						Для Каждого СтрокаТзТекДанныеСтавки Из масСтрокиТзТекДанныеСтавки Цикл
							
							НовСтрока = ЭтотОбъект.тзИтоговыеДанные.Добавить();
							
							НовСтрока.Участник		= ЭлМас.Участник;
							НовСтрока.СтатусУчастия = ЭлМас.СтатусУчастия;
							
							Для Каждого КолонкаИмя Из масСписокКолонокНачДанные Цикл
								
								НовСтрока[КолонкаИмя + "_"] = СтрокаТзНачДанные[КолонкаИмя]; 	
								
							КонецЦикла;
							
							Для Каждого КолонкаИмя Из масСписокКолонокТекДанныеСтавки Цикл
								
								НовСтрока[КолонкаИмя] = СтрокаТзТекДанныеСтавки[КолонкаИмя]; 	
								
							КонецЦикла;
							
							НовСтрока.РаспределяемаяСумма = Аук_ОбщийКлиентСервер.РаспределяемаяСуммаПоСтрокеПредварительная(НовСтрока, ЭтотОбъект.ОбщаяСумма);
							
							НовСтрока.РасчетнаяСумма = Аук_ОбщийКлиентСервер.РасчетнаяСуммаПоСтроке(НовСтрока, ЭтотОбъект.ОбщийСрок);
							
						КонецЦикла;	
						
					КонецЦикла;
					
				Иначе
					
					Для Каждого СтрокаТзТекДанныеСтавки Из масСтрокиТзТекДанныеСтавки Цикл
						
						НовСтрока = ЭтотОбъект.тзИтоговыеДанные.Добавить();
						
						НовСтрока.Участник		= ЭлМас.Участник;
						НовСтрока.СтатусУчастия = ЭлМас.СтатусУчастия;
						
						Для Каждого КолонкаИмя Из масСписокКолонокТекДанныеСтавки Цикл
							
							НовСтрока[КолонкаИмя] = СтрокаТзТекДанныеСтавки[КолонкаИмя]; 	
							
						КонецЦикла;
						
						НовСтрока.РаспределяемаяСумма = Аук_ОбщийКлиентСервер.РаспределяемаяСуммаПоСтрокеПредварительная(НовСтрока, ЭтотОбъект.ОбщаяСумма);
						
						НовСтрока.РасчетнаяСумма = Аук_ОбщийКлиентСервер.РасчетнаяСуммаПоСтроке(НовСтрока, ЭтотОбъект.ОбщийСрок);
						
					КонецЦикла;
					
				КонецЕсли;
				
			Иначе
				
				Для Каждого СтрокаТзНачДанные Из масСтрокиТзНачДанные Цикл
					
					НовСтрока = ЭтотОбъект.тзИтоговыеДанные.Добавить();
					
					НовСтрока.Участник		= ЭлМас.Участник;
					НовСтрока.СтатусУчастия = ЭлМас.СтатусУчастия;
					
					Для Каждого КолонкаИмя Из масСписокКолонокНачДанные Цикл
						
						НовСтрока[КолонкаИмя + "_"] = СтрокаТзНачДанные[КолонкаИмя]; 	
						
					КонецЦикла;	
					
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			Для Каждого СтрокаТзНачДанные Из масСтрокиТзНачДанные Цикл
				
				НовСтрока = ЭтотОбъект.тзИтоговыеДанные.Добавить();
				
				НовСтрока.Участник		= ЭлМас.Участник;
				НовСтрока.СтатусУчастия = ЭлМас.СтатусУчастия;
				
				Для Каждого КолонкаИмя Из масСписокКолонокНачДанные Цикл
					
					НовСтрока[КолонкаИмя + "_"] = СтрокаТзНачДанные[КолонкаИмя]; 	
					
				КонецЦикла;	
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЭтотОбъект.тзИтоговыеДанные.Сортировать("СтатусУчастия Возр");
		
КонецПроцедуры

#КонецОбласти




