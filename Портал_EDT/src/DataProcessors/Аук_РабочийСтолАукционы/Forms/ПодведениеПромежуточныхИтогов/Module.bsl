
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальнаяИнициализация();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура тзПромежуточныеИтогиРасчетПоФормулеРезультатПриИзменении(Элемент)
	
	ОтсортироватьТаблицуПоКолонкеРасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура тзПромежуточныеИтогиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя <> "тзПромежуточныеИтогиРасчетПоФормулеРезультат"
		 И Поле.Имя <> "тзПромежуточныеИтогиИтоговоеЗначение"
	Тогда	 
		 
		СтандартнаяОбработка = Ложь;
		
		ТекДанные = ЭтотОбъект.тзПромежуточныеИтоги.НайтиПоИдентификатору(ВыбраннаяСтрока);
		
		ПодтвердитьВыборЛидераИПродолжить(ТекДанные);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Подтвердить(Команда)
	
	Если ЭтотОбъект.тзПромежуточныеИтоги.Количество() = 0 Тогда
			
		Закрыть();
		
	Иначе	
		
		масВыделенныеСтроки = Элементы.тзПромежуточныеИтоги.ВыделенныеСтроки;
		
		Если масВыделенныеСтроки.Количество() <> 0 Тогда
			
			ПодтвердитьВыборЛидераИПродолжить(ЭтотОбъект.тзПромежуточныеИтоги.НайтиПоИдентификатору(масВыделенныеСтроки[0]));
			
		Иначе
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран участник, являющийся лидером'"));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции	

&НаКлиенте
Процедура ПодтвердитьВыборЛидераИПродолжить(пТекДанные)

	Оповещение = Новый ОписаниеОповещения("ПодтвердитьВыбор_Продолжить", ЭтотОбъект, пТекДанные);	
	
	ПоказатьВопрос(Оповещение, СтрШаблон(ШаблонТекстаВопросаПодтверждение(), пТекДанные.Участник), РежимДиалогаВопрос.ДаНет); 
	
КонецПроцедуры

&НаКлиенте
Функция ШаблонТекстаВопросаПодтверждение()

	Возврат НСтр("ru = 'Подтверждая это Вы соглашаетесь с тем, что на текущем этапе лидирует участник <%1> и то, что все участники будут оповещены. Продолжить?'");
	
КонецФункции

&НаКлиенте
Процедура ПодтвердитьВыбор_Продолжить(Результат, пТекДанные) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Закрыть(ПараметрыЗаписиДанныхСтавкиИзДанныхФормы(пТекДанные));
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЗаписиДанныхСтавкиИзДанныхФормы(пТекДанные)	
	сткПараметры = Новый Структура;
	
	сткПараметры.Вставить("ДатаСтавки",			пТекДанные.ДатаСтавки);
	сткПараметры.Вставить("Аукцион",			ЭтотОбъект.Аукцион);
	сткПараметры.Вставить("Участник",			пТекДанные.Участник);
	сткПараметры.Вставить("СтрокаДанных",		пТекДанные.СтрокаДанных);
	
	сткПараметры.Вставить("РезультатРасчета",	пТекДанные.РезультатРасчета);
	сткПараметры.Вставить("ПредставлениеРезультатаРасчета",	пТекДанные.ПредставлениеРезультатаРасчета);
		
	Возврат сткПараметры;	
КонецФункции

&НаКлиенте
Процедура ОтсортироватьТаблицуПоКолонкеРасчета()

	ЭтотОбъект.тзПромежуточныеИтоги.Сортировать(СтрШаблон("РасчетПоФормулеРезультат %1", ?(ЭтотОбъект.ВариантСортировки = 0, "ВОЗР", "УБЫВ")));
	
КонецПроцедуры	

&НаСервере
Процедура НачальнаяИнициализация()
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Аукцион,ВидАукциона");
	
	Аук_ОбщийСервер.ДобавитьКолонкиДанныхСтавкиПоВидуАукциона(ЭтотОбъект, Параметры.ВидАукциона, "тзПромежуточныеИтоги", Элементы.тзПромежуточныеИтогиДанныеСтавки);
	
	ЗаполнитьПромежуточныеИтоги();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПромежуточныеИтоги()
	
	ЭтотОбъект.ВариантСортировки = Справочники.Аук_ВидыАукционов.ОписаниеРасчетаПромежуточныхИтоговПоВидуАукциона(Параметры.ВидАукциона).ПромежуточныйИтогВариантВыбора;
	
	сткРез = Аук_ОбщийСервер.ПромежуточныеИтогиРасчетПоУмолчанию(Параметры.Аукцион, Параметры.ВидАукциона);
	
	Если сткРез.Успех Тогда	
		тзТемп = РеквизитФормыВЗначение("тзПромежуточныеИтоги");
		
		Для Каждого СтрокаТз Из сткРез.Результат Цикл
			
			ЗаполнитьЗначенияСвойств(тзТемп.Добавить(), СтрокаТз);
			
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(тзТемп, "тзПромежуточныеИтоги");	
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(сткРез.Результат);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


