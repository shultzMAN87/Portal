
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НачальнаяИнициализация();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	НастроитьОформлениеКнопокОтбораПоТекущемуПользователю();
	
	ПодключитьОбработчикОжидания("ОбновитьСписокАукционов", 20);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)		
	ОбновитьСписокАукционов();	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыдсАукционыПодведениеИтогов

&НаКлиенте
Процедура дсАукционыПодведениеИтоговПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		Отказ = Истина;
	Иначе
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНовые;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура дсАукционыПодведениеИтоговВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		
		ОткрытьКарточкуАукциона(ТекДанные.Аукцион);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыдсАукционыЗавершенные

&НаКлиенте
Процедура дсАукционыЗавершенныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		Отказ = Истина;
	Иначе
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНовые;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыдсАукционыАрхив

&НаКлиенте
Процедура дсАукционыАрхивПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		Отказ = Истина;
	Иначе
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНовые;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыдсАукционыВРаботе

&НаКлиенте
Процедура дсАукционыВРаботеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		Отказ = Истина;
	Иначе
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНовые;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура дсАукционыВРаботеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда	
		Если ТекДанные.Автор = ТекущийПользователь Тогда	
			
			ОткрытьКарточкуАукциона(ТекДанные.Аукцион);
			
		Иначе
			
			Оповещение = Новый ОписаниеОповещения("ОткрытиеКарточкиАукциона_Продолжить", ЭтотОбъект, ТекДанные.Аукцион); 
			
			стрТекстВопроса = НСтр("ru = 'Автором данного аукциона является другой пользователь. Вы точно хотите окрыть карточку аукциона для возможного редактирования?'");
			
			ПоказатьВопрос(Оповещение, стрТекстВопроса, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'")); 	
			
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура дсАукционыВРаботеПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		
		НастроитьВидимостьКомандВЗависимостиОтСтатусаАукциона(ТекДанные);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СписокНовыеПеревестиВРаботу(Команда)
	ПеревестиВРаботуНовыйАукцион();
КонецПроцедуры

&НаКлиенте
Процедура СписокНовыеОтборПоТекущемуПользователю(Команда)
	
	УстановитьСнятьОтборВСпискеПоТекущемуПользователю("дсАукционыНовые");

КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеОтборПоТекущемуПользователю(Команда)
	
	УстановитьСнятьОтборВСпискеПоТекущемуПользователю("дсАукционыВРаботе");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПодведениеИтоговОтборПоТекущемуПользователю(Команда)
	
	УстановитьСнятьОтборВСпискеПоТекущемуПользователю("дсАукционыПодведениеИтогов");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗавершенныеОтборПоТекущемуПользователю(Команда)
	
	УстановитьСнятьОтборВСпискеПоТекущемуПользователю("дсАукционыЗавершенные");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеВводНовойСтавки(Команда)
	
	ТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		Оповещение = Новый ОписаниеОповещения("ВводНовойСтавки_Продолжить", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",			ТекДанные.Аукцион);
		ПараметрыФормы.Вставить("ВидАукциона",		ТекДанные.ВидАукциона);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ВводСтавкиУчастника", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеВводИнформацииОбОтказеОтУчастия(Команда)
	
	ТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		Оповещение = Новый ОписаниеОповещения("ВводОтказа_Продолжить", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",			ТекДанные.Аукцион);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ВводИнформацииОбОтказе", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеВводИнформационногоСообщения(Команда)
	
	ТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		Оповещение = Новый ОписаниеОповещения("ВводСообщенияОтУчастника_Продолжить", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",			ТекДанные.Аукцион);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ВводВходящегоСообщения", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеПереводНаСледующийЭтап(Команда)
	
	ТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда	
		Если Аук_ОбщийКлиент.АукционПереведенНаСледующийЭтап(ТекДанные.Аукцион) Тогда
			
			ПеревестиАукционНаСледующийЭтап(ТекДанные);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеОтправитьСообщениеУчастникам(Команда)	
	ТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		Оповещение = Новый ОписаниеОповещения("ОтправитьСообщениеУчастникам_Продолжить", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",			ТекДанные.Аукцион);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ОтправкаСообщенияУчастникам", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СписокПодведениеИтоговОтправитьСообщениеУчастникам(Команда)
	ТекДанные = Элементы.дсАукционыПодведениеИтогов.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		Оповещение = Новый ОписаниеОповещения("ОтправитьСообщениеУчастникам_Продолжить", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",			ТекДанные.Аукцион);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ОтправкаСообщенияУчастникам", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеПодведениеПромежуточныхИтогов(Команда)
	
	ОткрытьФормуПромежуточногоПодведенияИтогов();
	
КонецПроцедуры

// УДАЛИТЬ!!!
&НаКлиенте
Процедура СписокПодведениеИтоговПодведениеИтогов(Команда)
	
	ОткрытьФормуПодведенияОбщихИтогов();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПодведениеИтогов2(Команда)
	
	ОткрытьФормуПодведенияОбщихИтогов2();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗавершенныеПоказатьИтоги(Команда)
		
	ПроказатьИтогиПоТекущейСтрокеСписка("дсАукционыЗавершенные");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокАрхивПоказатьИтоги(Команда)
	
	ПроказатьИтогиПоТекущейСтрокеСписка("дсАукционыАрхив");

КонецПроцедуры

&НаКлиенте
Процедура СписокЗавершенныеВАрхив(Команда)
	
	ПеревестиВАрхивЗавершенныйАукцион();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокАрхивОтборПоТекущемуПользователю(Команда)
	
	УстановитьСнятьОтборВСпискеПоТекущемуПользователю("дсАукционыАрхив");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюСтавокПоСтроке(Команда)
	// Активная страница
	стрСтраница = Элементы.Страницы.ТекущаяСтраница.Имя;
	
	ТекДанные = Элементы[СтрЗаменить(стрСтраница, "Страница", "дсАукционы")].ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		ОткрытьИсториюСтавок(ТекДанные);	
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВводОтказа_Продолжить(пСткДанныеОтказа, ДополнительныеПараметры) Экспорт
	
	Если пСткДанныеОтказа <> Неопределено Тогда
		
		Аук_ОбщийКлиент.ЗафиксироватьОтказОтУчастия(пСткДанныеОтказа);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВводСообщенияОтУчастника_Продолжить(пСткСообщение, ДополнительныеПараметры) Экспорт
	
	Если пСткСообщение <> Неопределено Тогда
		
		Аук_ОбщийКлиент.ЗафиксироватьСообщениеОтУчастника(пСткСообщение);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеКарточкиАукциона_Продолжить(Результат, пАукцион) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ОткрытьКарточкуАукциона(пАукцион);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеУчастникам_Продолжить(пСткДанныеСообщения, ДополнительныеПараметры) Экспорт
	
	Если пСткДанныеСообщения <> Неопределено Тогда
		
		Аук_ОбщийКлиент.ОтправитьСообщениеУчастникам(пСткДанныеСообщения);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВводНовойСтавки_Продолжить(пСткДанныеСтавки, ДополнительныеПараметры) Экспорт
	
	Если пСткДанныеСтавки <> Неопределено Тогда
		
		Аук_ОбщийКлиент.ЗафиксироватьДанныеСтавки(пСткДанныеСтавки);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокАукционов()
	
	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;	
	
	Элементы[СтрЗаменить(ТекСтраница.Имя, "Страница", "дсАукционы")].Обновить();
	
	Если ТекСтраница.Имя = "СтраницаВРаботе" Тогда
		ТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
			
			НастроитьВидимостьКомандВЗависимостиОтСтатусаАукциона(ТекДанные);
			
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроказатьИтогиПоТекущейСтрокеСписка(пИмяСписка)
	
	ТекДанные = Элементы[пИмяСписка].ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",			ТекДанные.Аукцион);
		ПараметрыФормы.Вставить("ВидАукциона",		ТекДанные.ВидАукциона);
		ПараметрыФормы.Вставить("ТолькоПросмотр",	Истина);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ПодведениеИтогов2", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьИсториюСтавок(пТекДанные)

	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Аукцион",		пТекДанные.Аукцион);
	ПараметрыФормы.Вставить("ВидАукциона",	пТекДанные.ВидАукциона);
	
	ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ПросмотрИсторииСтавок2", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьКарточкуАукциона(пАукцион)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", пАукцион);
	
	ОткрытьФорму("Справочник.Аук_Аукционы.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьФормуПодведенияОбщихИтогов() 
	
	ТекДанные = Элементы.дсАукционыПодведениеИтогов.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",		ТекДанные.Аукцион);
		ПараметрыФормы.Вставить("ВидАукциона",	ТекДанные.ВидАукциона);
		
		Оповещение = Новый ОписаниеОповещения("ПодведениеОбщихИтогов_Продолжить", ЭтотОбъект);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ПодведениеИтогов", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодведенияОбщихИтогов2() 
	
	ТекДанные = Элементы.дсАукционыПодведениеИтогов.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",		ТекДанные.Аукцион);
		ПараметрыФормы.Вставить("ВидАукциона",	ТекДанные.ВидАукциона);
		
		Оповещение = Новый ОписаниеОповещения("ПодведениеОбщихИтогов_Продолжить", ЭтотОбъект, ТекДанные);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ПодведениеИтогов2", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПодведениеОбщихИтогов_Продолжить(пСткДанные, пТекДанные) Экспорт  
		
	Если пСткДанные <> Неопределено Тогда		
		Если Аук_ОбщийКлиент.ОкончательныеИтогиЗафиксированы(пСткДанные) Тогда
			
			ПеревестиВЗавершенные(пТекДанные);
			
		КонецЕсли;		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПромежуточногоПодведенияИтогов() 
	
	ТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Аукцион",		ТекДанные.Аукцион);
		ПараметрыФормы.Вставить("ВидАукциона",	ТекДанные.ВидАукциона);
		ПараметрыФормы.Вставить("НомерЭтапа",	ТекДанные.НомерЭтапа);
		
		Оповещение = Новый ОписаниеОповещения("ПодведениеПромежуточныхИтогов_Продолжить", ЭтотОбъект, ТекДанные);
		
		ОткрытьФорму("Обработка.Аук_РабочийСтолАукционы.Форма.ПодведениеПромежуточныхИтогов", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПодведениеПромежуточныхИтогов_Продолжить(пСткДанные, пТекДанные) Экспорт  
		
	Если пСткДанные <> Неопределено Тогда
		пСткДанные.Вставить("НомерЭтапа", пТекДанные.НомерЭтапа);
		
		Если Аук_ОбщийКлиент.ПромежуточныйИтогЗафиксирован(пСткДанные) Тогда
			
			ПеревестиАукционНаСледующийЭтап(пТекДанные);
			
		КонецЕсли;	
		
		//ПеревестиАукционНаСледующийЭтап();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьОтборВСпискеПоТекущемуПользователю(пИмяСписка)
	ЭлСзАктивностьОтбора = ОтборПоТекущемуПользователюАктивность.НайтиПоЗначению(пИмяСписка);
	
	Если ЭлСзАктивностьОтбора.Пометка Тогда
		УдалитьОтборВСпискеПоТекущемуПользователю(пИмяСписка);
		
		Элементы[пИмяСписка + "ОтборПоТекущемуПользователю"].ЦветРамки = Новый Цвет();
		
		ЭлСзАктивностьОтбора.Пометка = Ложь;
	Иначе
		УстановитьОтборВСпискеПоТекущемуПользователю(пИмяСписка);
		
		Элементы[пИмяСписка + "ОтборПоТекущемуПользователю"].ЦветРамки = WebЦвета.НасыщенноНебесноГолубой;
		
		ЭлСзАктивностьОтбора.Пометка = Истина;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОформлениеКнопокОтбораПоТекущемуПользователю()
	Для Каждого ЭлСз Из ОтборПоТекущемуПользователюАктивность Цикл
		ОтборКомпоновкиДанных = Аук_ОбщийКлиентСервер.ЭлементПользовательскихНастроекКомпоновкиДанныхПоТипу(ЭтотОбъект[ЭлСз.Значение], Тип("ОтборКомпоновкиДанных"));
		
		ЭлементыГруппаТекПользователь = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
											ОтборКомпоновкиДанных,, "Текущий пользователь");
		
		Если ЗначениеЗаполнено(ЭлементыГруппаТекПользователь) Тогда
			Элементы[ЭлСз.Значение + "ОтборПоТекущемуПользователю"].ЦветРамки = WebЦвета.НасыщенноНебесноГолубой;
			
			ЭлСз.Пометка = Истина;												
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьОтборВСпискеПоТекущемуПользователю(пИмяСписка)
	
	ОтборКомпоновкиДанных = Аук_ОбщийКлиентСервер.ЭлементПользовательскихНастроекКомпоновкиДанныхПоТипу(ЭтотОбъект[пИмяСписка], Тип("ОтборКомпоновкиДанных"));
	
	Если ОтборКомпоновкиДанных = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Ошибка установки отбора!'"));	
	КонецЕсли;
	
	ГруппаТекПользователь = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
									ОтборКомпоновкиДанных.Элементы,
									"Текущий пользователь",
									ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
									ГруппаТекПользователь,
									"Автор",
									ВидСравненияКомпоновкиДанных.Равно,
									ТекущийПользователь,
									,
									Истина,
									РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);   
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
									ГруппаТекПользователь,
									"АвторИзменения",
									ВидСравненияКомпоновкиДанных.Равно,
									ТекущийПользователь,
									,
									Истина,
									РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
				
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОтборВСпискеПоТекущемуПользователю(пИмяСписка)
	ОтборКомпоновкиДанных = Аук_ОбщийКлиентСервер.ЭлементПользовательскихНастроекКомпоновкиДанныхПоТипу(ЭтотОбъект[пИмяСписка], Тип("ОтборКомпоновкиДанных"));
	
	ЭлементыГруппаТекПользователь = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
									ОтборКомпоновкиДанных,, "Текущий пользователь");
	
	Для Каждого ЭлементОтбора Из ЭлементыГруппаТекПользователь Цикл
		ОтборКомпоновкиДанных.Элементы.Удалить(ЭлементОтбора);
	КонецЦикла;	
КонецПроцедуры	

&НаКлиенте
Процедура ПеревестиВЗавершенные(пТекДанные)
	
	Элементы.дсАукционыПодведениеИтогов.Обновить();
	Элементы.дсАукционыЗавершенные.Обновить();
	
	ПереключитьВкладкуИСпозиционироваться(пТекДанные.Аукцион, "СтраницаЗавершенные", "дсАукционыЗавершенные");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиВРаботуНовыйАукцион()
	
	ТекДанные = Элементы.дсАукционыНовые.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		Если Не ТекДанные.ПометкаУдаления Тогда
			сткДанныеАукциона = Аук_ОбщийВызовСервера.ДанныеАукционаДляПроверки(ТекДанные.Аукцион);
			
			Оповещение = Новый ОписаниеОповещения("АукционПереведенВРаботу_Продолжить", ЭтотОбъект, ТекДанные);
			
			Аук_ОбщийКлиент.ВыполнитьПроверкиНастроекАукциона(сткДанныеАукциона, Оповещение);		
			
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Нельзя перевести в работу помеченный на удаление аукцион.'"));
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура АукционПереведенВРаботу_Продолжить(Результат, ТекДанные) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Аук_ОбщийКлиент.АукционПереведенВРаботу(ТекДанные.Аукцион) Тогда	
			Элементы.дсАукционыНовые.Обновить();
			Элементы.дсАукционыВРаботе.Обновить();
			
			ПереключитьВкладкуИСпозиционироваться(ТекДанные.Аукцион, "СтраницаВРаботе", "дсАукционыВРаботе");	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПеревестиВАрхивЗавершенныйАукцион()
	
	ТекДанные = Элементы.дсАукционыЗавершенные.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И Не ТекДанные.Свойство("ГруппировкаСтроки") Тогда
		
		Если Аук_ОбщийКлиент.АукционПомещенВАрхив(ТекДанные.Аукцион) Тогда
			
			Элементы.дсАукционыЗавершенные.Обновить();
			Элементы.дсАукционыАрхив.Обновить();
			
			ПереключитьВкладкуИСпозиционироваться(ТекДанные.Аукцион, "СтраницаАрхив", "дсАукционыАрхив");
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьВкладкуИСпозиционироваться(пТекАукцион, пИмяВкладки, пИмяСписка)

	Элементы.Страницы.ТекущаяСтраница = Элементы[пИмяВкладки];
	
	Элементы[пИмяСписка].ТекущаяСтрока = пТекАукцион;
	
КонецПроцедуры	

&НаСервере
Процедура НачальнаяИнициализация()
	
	ТекущийПользователь = ПараметрыСеанса.глТекПользователь;
		
	ОтборПоТекущемуПользователюАктивность.Добавить("дсАукционыНовые",, Ложь);
	ОтборПоТекущемуПользователюАктивность.Добавить("дсАукционыВРаботе",, Ложь);
	ОтборПоТекущемуПользователюАктивность.Добавить("дсАукционыПодведениеИтогов",, Ложь);
	ОтборПоТекущемуПользователюАктивность.Добавить("дсАукционыЗавершенные",, Ложь);
	ОтборПоТекущемуПользователюАктивность.Добавить("дсАукционыАрхив",, Ложь);
			
КонецПроцедуры	

&НаКлиенте
Процедура ПеревестиАукционНаСледующийЭтап(пТекДанные)
	
	Элементы.дсАукционыВРаботе.Обновить();
	
	НовТекДанные = Элементы.дсАукционыВРаботе.ТекущиеДанные;
	
	Если НовТекДанные <> Неопределено Тогда
		НастроитьВидимостьКомандВЗависимостиОтСтатусаАукциона(НовТекДанные);
	КонецЕсли;
	
	Если пТекДанные.Статус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.НовыйЭтап")
		И пТекДанные.НомерЭтапа = пТекДанные.КоличествоЭтапов
	Тогда	 
		
		Элементы.дсАукционыПодведениеИтогов.Обновить();
		
		ПереключитьВкладкуИСпозиционироваться(пТекДанные.Аукцион, "СтраницаПодведениеИтогов", "дсАукционыПодведениеИтогов");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВидимостьКомандВЗависимостиОтСтатусаАукциона(пТекДанные)
	
	Если пТекДанные.ПометкаУдаления Тогда
		СкрытьПоказатьЭлементыФормы(ВРаботе_ВсеКомандыУправления(), Ложь);	
	Иначе	
		Если пТекДанные.Статус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов") Тогда
			
			СкрытьПоказатьЭлементыФормы(ВидимостьЭлементов_ВРаботе(), Ложь); 
			
			СкрытьПоказатьЭлементыФормы("дсАукционыВРаботеСписокВРаботеПодведениеПромежуточныхИтогов", Истина);
			
		Иначе
			
			СкрытьПоказатьЭлементыФормы(ВидимостьЭлементов_ВРаботе(), Истина); 
			
			СкрытьПоказатьЭлементыФормы("дсАукционыВРаботеСписокВРаботеПодведениеПромежуточныхИтогов", Ложь);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВРаботе_ВсеКомандыУправления()

	масРез = Новый Массив;
	
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеПереводНаСледующийЭтап");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеВводНовойСтавки");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеВводИнформацииОбОтказеОтУчастия");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеВводИнформационногоСообщения");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеОтправитьСообщениеУчастникам");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеПодведениеПромежуточныхИтогов");
	
	Возврат масРез;
	
КонецФункции

&НаКлиенте
Функция ВидимостьЭлементов_ВРаботе()

	масРез = Новый Массив;
	
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеПереводНаСледующийЭтап");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеВводНовойСтавки");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеВводИнформацииОбОтказеОтУчастия");
	масРез.Добавить("дсАукционыВРаботеСписокВРаботеВводИнформационногоСообщения");
	масРез.Добавить("дсАукционыВРаботеКонтекстноеМенюИзменить");
	
	Возврат масРез;
	
КонецФункции	

&НаКлиенте
Процедура СкрытьПоказатьЭлементыФормы(пИменаЭлементов, пВидимость)
	
	Если ТипЗнч(пИменаЭлементов) = Тип("Строка") Тогда
		масИменаЭлементов = СтрРазделить(пИменаЭлементов, ",");
	ИначеЕсли ТипЗнч(пИменаЭлементов) = Тип("Массив") Тогда
		масИменаЭлементов = пИменаЭлементов;	
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Для Каждого ЭлМас Из масИменаЭлементов Цикл
		
		Элементы[ЭлМас].Видимость = пВидимость;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
