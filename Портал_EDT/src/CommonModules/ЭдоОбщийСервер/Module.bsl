
// Возвращает данные первой записи результата запроса в виде структуры.
// 
// Параметры:
//  РезультатЗапроса - РезультатЗапроса - Результат запроса, содержащий данные для обработки.
// 
// Возвращаемое значение:
//  Структура - структура с результатом.
//
Функция РезультатЗапросаВСтруктуру(Знач РезультатЗапроса) Экспорт
	
	Результат = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Результат.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Возврат Результат;
КонецФункции

// Возвращает результата запроса в виде массива структур.
// 
// Параметры:
//  РезультатЗапроса - РезультатЗапроса - Результат запроса, содержащий данные для обработки.
// 
// Возвращаемое значение:
//  Массив - массив структур с результатом.
//
Функция РезультатЗапросаВМассивСтруктур(Знач РезультатЗапроса) Экспорт
	масРезультат = Новый Массив;
	
	масИменаСвойствСтруктуры = Новый Массив;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		масИменаСвойствСтруктуры.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат масРезультат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Новый Структура(СтрСоединить(масИменаСвойствСтруктуры, ", "));
		
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		
		масРезультат.Добавить(Результат);
	КонецЦикла;
	
	Возврат масРезультат;
КонецФункции