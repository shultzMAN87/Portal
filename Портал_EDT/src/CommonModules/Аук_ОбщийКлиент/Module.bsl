
#Область ПрограммныйИнтерфейс

Функция СписокПредставленийПоЭлементамТабличнойЧасти(пФорма, пИмяТабличнойЧасти, пИмяРеквизита, пОтборТип = Неопределено) Экспорт

	масСписокЭлементов = КолонкаТабличнойЧастиВМассив(пФорма, пИмяТабличнойЧасти, пИмяРеквизита);
	
	Возврат Аук_ОбщийВызовСервера.СписокПредставленийПоМассивуЭлементов(масСписокЭлементов, пОтборТип);
	
КонецФункции	

Функция КолонкаТабличнойЧастиВМассив(пФорма, пИмяТаблицы, пИмяРеквизита) Экспорт

	масРезультат = Новый Массив;
	
	Для Каждого СтрокаТч Из пФорма[пИмяТаблицы] Цикл
		
		масРезультат.Добавить(СтрокаТч[пИмяРеквизита]);
		
	КонецЦикла;	
	
	Возврат масРезультат;
	
КонецФункции

Функция ПроверитьФормулуРасчета(пСткПараметры) Экспорт
	
	Если пСткПараметры.ЭтоФормулаРасчетаОкончательныхИтогов Тогда	
		масОперандыВФормуле = Аук_ОбщийВызовСервера.СтрокаНайтиВсеПоРегулярномуВыражениюТолькоЗначения(пСткПараметры.ИсходнаяФормула,
																									   Аук_ОбщийКлиентСервер.Шаблон_ОперандФормулаРасчетаИтогов());
		
		Для Каждого ЭлОперанд Из пСткПараметры.СписокВозможныхОперандов Цикл
			
			Если масОперандыВФормуле.Найти(ЭлОперанд) = Неопределено Тогда
				
				Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'В формуле нет обязательных операндов.'"));
				
			КонецЕсли;
			
		КонецЦикла;	
		
	КонецЕсли;	
	
	ГотовоеВыражение = Аук_ОбщийВызовСервера.СтрокаЗаменитьПоРегулярномуВыражению(пСткПараметры.ИсходнаяФормула, Аук_ОбщийКлиентСервер.Шаблон_ОперандФормулаРасчетаИтогов(), "1"); 
	
	Попытка
		
		чслРезультат = Вычислить(ГотовоеВыражение);
		
		Возврат ЭдоОбщий._Успех();
		
	Исключение
		
		Возврат ЭдоОбщий._Ошибка(СтрШаблон(НСтр("ru = 'Ошибка при вычислении выражения с тестовыми данными. Выражение: %1'"), ГотовоеВыражение));
		
	КонецПопытки;
	
КонецФункции

Функция ПараметрыПроверкиФормулы() Экспорт

	сткРезультат = Новый Структура;
	
	сткРезультат.Вставить("ИсходнаяФормула",						"");
	сткРезультат.Вставить("СписокВозможныхОперандов",				Новый Массив);
	сткРезультат.Вставить("ЭтоФормулаРасчетаОкончательныхИтогов",	Ложь);
	
	Возврат сткРезультат;
	
КонецФункции	

Функция ДанныеСтавкиВМассивСтруктур(пТаблица, пМасРеквизиты) Экспорт
	
	масРезультат = Новый Массив;
	
	Для Каждого СтрокаТз Из пТаблица Цикл
		
		сткЭлемент = Новый Структура;
		
		Для Каждого ЭлМас Из пМасРеквизиты Цикл
			
			сткЭлемент.Вставить(ЭлМас, СериализаторXDTO.XMLСтрока(СтрокаТз[ЭлМас]));
			
		КонецЦикла;	
		
		масРезультат.Добавить(сткЭлемент);
	КонецЦикла;
	
	Возврат масРезультат;
	
КонецФункции

Функция НормализоватьСтрокуФормулы(пСтрФормулаРасчета) Экспорт

	масЭлементыФормулы = Аук_ОбщийВызовСервера.СтрокаНайтиВсеПоРегулярномуВыражениюТолькоЗначения(пСтрФормулаРасчета,
													Аук_ОбщийКлиентСервер.Шаблон_ОперандыИАрифметическиеЗнакиВФормулеРасчетаИтогов());

	Возврат СтрСоединить(масЭлементыФормулы, " ");												
КонецФункции	

Процедура ЗаполнитьТаблицуДаннымиМассиваСтруктур(пТаблица, пМасДанные, пСтрПоля) Экспорт
	Для Каждого сткЭлМас Из пМасДанные Цикл
		сткОтбор = Новый Структура(пСтрПоля);
		
		ЗаполнитьЗначенияСвойств(сткОтбор, сткЭлМас); 
		
		Если пТаблица.НайтиСтроки(сткОтбор).Количество() = 0 Тогда	
			ЗаполнитьЗначенияСвойств(пТаблица.Добавить(), сткЭлМас); 	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры	

#КонецОбласти

#Область ИнтерактивныеКомандыПользователей

Процедура ЗафиксироватьДанныеСтавки(пСткПараметры) Экспорт
	
	пСткПараметры.Вставить("Автоматическая",			Ложь);
	
	Аук_УправлениеАукционамиКлиентСервер.ЗафиксироватьДанныеСтавки_ОбщиеПараметры(пСткПараметры);
	
	Аук_ОбщийВызовСервера.ПолучитьДанныеОтУчастника(пСткПараметры);
	
КонецПроцедуры

Процедура ЗафиксироватьОтказОтУчастия(пСткПараметры) Экспорт
	
	пСткПараметры.Вставить("Автоматическая",			Ложь);
	
	Аук_УправлениеАукционамиКлиентСервер.ЗафиксироватьОтказОтУчастия_ОбщиеПараметры(пСткПараметры);
	
	Аук_ОбщийВызовСервера.ПолучитьДанныеОтУчастника(пСткПараметры);
	
КонецПроцедуры

Процедура ЗафиксироватьСообщениеОтУчастника(пСткПараметры) Экспорт
	
	пСткПараметры.Вставить("Автоматическая",			Ложь);
	
	Аук_УправлениеАукционамиКлиентСервер.ЗафиксироватьСообщениеОтУчастника_ОбщиеПараметры(пСткПараметры);
	
	Аук_ОбщийВызовСервера.ПолучитьДанныеОтУчастника(пСткПараметры);
	
КонецПроцедуры

Процедура ОтправитьСообщениеУчастникам(пСткПараметры) Экспорт

	пСткПараметры.Вставить("Автоматическая",	Ложь);
	пСткПараметры.Вставить("ВидСообщения",		ПредопределенноеЗначение("Перечисление.Аук_ВидыСообщенийУчастникам.ПростоеСообщение"));
	
	Аук_ОбщийВызовСервера.ОтправитьСообщениеУчастникам(пСткПараметры);
	
КонецПроцедуры

Функция АукционПереведенВРаботу(пАукцион) Экспорт
	
	сткПараметры = Новый Структура;
	
	сткПараметры.Вставить("Аукцион",		пАукцион);	
	сткПараметры.Вставить("Автоматическая",	Ложь);
	
	Возврат Аук_ОбщийВызовСервера.ПеревестиАукционВРаботу(сткПараметры);
	
КонецФункции

Функция ВыполнитьПроверкиНастроекАукциона(пСткДанныеАук, пОповещение, пОтказ = Неопределено) Экспорт	
	сткРез = Аук_ОбщийВызовСервера.ВыполнитьКонтрольКритическиВажныхНастроек(пСткДанныеАук);
	
	Если сткРез.Успех Тогда
		сткРез = Аук_ОбщийКлиентСервер.ВыполнитьКонтрольОбычныхНастроек(пСткДанныеАук);
		
		Если Не сткРез.Успех Тогда
			пОтказ = Истина;
			
			ПоказатьВопрос(пОповещение, СтрШаблон(НСтр("ru = '%1. Продолжить?'"), сткРез.Результат), РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Предупреждение'"));
		Иначе
			ВыполнитьОбработкуОповещения(пОповещение, КодВозвратаДиалога.Да);
		КонецЕсли;	
	Иначе	
		ОбщегоНазначенияКлиент.СообщитьПользователю(сткРез.Результат,,,, пОтказ);	
	КонецЕсли;	
КонецФункции

Функция АукционПомещенВАрхив(пАукцион) Экспорт
	
	сткПараметры = Новый Структура;
	
	сткПараметры.Вставить("Аукцион",		пАукцион);	
	сткПараметры.Вставить("Автоматическая",	Ложь);
	
	Возврат Аук_ОбщийВызовСервера.ПоместитьАукционВАрхив(сткПараметры);
	
КонецФункции

Функция АукционПереведенНаСледующийЭтап(пАукцион) Экспорт

	сткПараметры = Новый Структура;
	
	сткПараметры.Вставить("Аукцион",		пАукцион);	
	сткПараметры.Вставить("Автоматическая",	Ложь);
	
	Возврат Аук_ОбщийВызовСервера.ПеревестиАукционНаСледующийЭтап(сткПараметры);
	
КонецФункции

Функция ПромежуточныйИтогЗафиксирован(пСткПараметры) Экспорт
	
	пСткПараметры.Вставить("Автоматическая",	Ложь);
	
	Возврат Аук_ОбщийВызовСервера.ЗафиксироватьПромежуточныеИтогиАукциона(пСткПараметры);
	
КонецФункции

Функция ОкончательныеИтогиЗафиксированы(пСткПараметры) Экспорт
	
	пСткПараметры.Вставить("Автоматическая",	Ложь);
	
	Возврат Аук_ОбщийВызовСервера.ЗафиксироватьОкончательныеИтогиАукциона(пСткПараметры);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиФормы

Процедура НачатьВыборУчастника(пФорма, пСтандартнаяОбработка) Экспорт
	
	пСтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Ссылка", пФорма.СписокУчастников));
	
	Оповещение = Новый ОписаниеОповещения("ВыборУчастника_Продолжить", Аук_ОбщийКлиент, пФорма);
	
	ОткрытьФорму("Справочник.Аук_УчастникиАукционов.ФормаВыбора", ПараметрыФормы, пФорма,,,, Оповещение);
	
КонецПроцедуры

Процедура ВыборУчастника_Продолжить(Результат, пФорма) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		пФорма.Участник = Результат;
		
		ЗаполнитьСписокВыбораКонтактныхЛиц(пФорма);
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура УчастникПриИзменении(пФорма) Экспорт

	ЗаполнитьСписокВыбораКонтактныхЛиц(пФорма);
	
КонецПроцедуры	

Процедура НачатьВыборКонтактногоЛица(пФорма, пСтандартнаяОбработка) Экспорт
	
	пСтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Ссылка, Владелец", пФорма.СписокКонтактныхЛиц, пФорма.Участник));
	
	Оповещение = Новый ОписаниеОповещения("ВыборКонтактногоЛица_Продолжить", Аук_ОбщийКлиент, пФорма);
	
	ОткрытьФорму("Справочник.Аук_КонтактныеЛицаУчастниковАукционов.ФормаВыбора", ПараметрыФормы, пФорма,,,, Оповещение);
	
КонецПроцедуры

Процедура ВыборКонтактногоЛица_Продолжить(Результат, пФорма) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		пФорма.КонтактноеЛицо = Результат;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормами

Процедура УставновитьРежимТолькоПросмотрУВсехЭлементов(пЭлементы, пБулТолькоПросмотр = Истина, пСтрИсключение = "") Экспорт
	масИсключения = СтрРазделить(пСтрИсключение, ",");
	
	Для Каждого ПодчиненныйЭлемент Из пЭлементы.ПодчиненныеЭлементы Цикл
		Если масИсключения.Найти(ПодчиненныйЭлемент.Имя) <> Неопределено Тогда
			Продолжить;		
		КонецЕсли;
		
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ПолеФормы") Тогда	
			ПодчиненныйЭлемент.ТолькоПросмотр = пБулТолькоПросмотр;	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

// Будет работать если имя команды совпадает с именем элемента
//
Процедура УстановитьВидимостьКоманд(пФорма, пБулВидимость = Ложь, пСтрИсключение = "") Экспорт
	масИсключения = СтрРазделить(пСтрИсключение, ",");
	
	Для Каждого Команда_ Из пФорма.Команды Цикл
		Если масИсключения.Найти(Команда_.Имя) <> Неопределено Тогда
			Продолжить;		
		КонецЕсли;
		
		пФорма.Элементы[Команда_.Имя].Видимость = пБулВидимость;		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодсказкуДляПоляТехническогоНаименования(пФорма) Экспорт

	пФорма.Элементы["ТехническоеНаименование"].РасширеннаяПодсказка.Заголовок = ПодсказкаДляПоляВвода_ТехническоеНаименование();	
	
КонецПроцедуры

Процедура ЗаполнитьПодсказкуДляПоляНаименования(пФорма) Экспорт

	пФорма.Элементы["Наименование"].РасширеннаяПодсказка.Заголовок = ПодсказкаДляПоляВвода_Наименование();	
	
КонецПроцедуры

Процедура ЗапускМастераПоРаспределениюУчастниковПоВидам(пФорма) Экспорт
	Если пФорма.ЭтоНовыйВидАукциона Тогда
		Оповещение = Новый ОписаниеОповещения("НовыйВидАукциона_Продолжить", Аук_ОбщийКлиент, пФорма);
		
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Вы создали новый вид аукциона. Вы желаете привязать его к участникам и контактным лицам?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции	

Процедура НовыйВидАукциона_Продолжить(Результат, пФорма) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВидАукциона", пФорма.Объект.Ссылка);
		
		Оповещение = Новый ОписаниеОповещения("МастерРаспределенияПоУчастникам_Продолжить", Аук_ОбщийКлиент, пФорма);
		
		ОткрытьФорму("Справочник.Аук_ВидыАукционов.Форма.МастерРаспределенияПоУчастникам", ПараметрыФормы,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		пФорма.ЭтоНовыйВидАукциона = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура МастерРаспределенияПоУчастникам_Продолжить(Результат, пФорма) Экспорт
	
	пФорма.ЭтоНовыйВидАукциона = Ложь;
	
КонецПроцедуры

Функция ПодсказкаДляПоляВвода_Наименование()

	ЧастиСтроки = Новый Массив;
	
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = 'Требования к наименованию:%1'"), Символы.ПС)); 
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = '1. Латинские буквы и кириллические в верхнем либо нижнем регистре, а также цифры%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = '2. Из спецсимволов допускается только: ""_"", ""."", ""("", "")"", пробелы%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = '3. В базе не должно существовать элементов с таким же наименованием%1%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = 'Пример:%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = 'Неправильно%1'"), Символы.ПС), Новый Шрифт(,,Истина), WebЦвета.Красный));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' [Сумма ставки]%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' Процент/ставки%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = 'Правильно%1'"), Символы.ПС), Новый Шрифт(,,Истина), WebЦвета.Зеленый));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' Процент_ставки_5%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' Сумма.Кредит%1'"), Символы.ПС));
	
	Возврат Новый ФорматированнаяСтрока(ЧастиСтроки);
	
КонецФункции

Функция ПодсказкаДляПоляВвода_ТехническоеНаименование()

	ЧастиСтроки = Новый Массив;
	
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = 'Требования к техническому наименованию:%1'"), Символы.ПС)); 
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = '1. Отсутствие пробелов%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = '2. Только латинские буквы и цифры%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = '3. Начинается только с буквы%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = '4. В базе не должно существовать элементов с таким же наименованием%1%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = 'Пример:%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = 'Неправильно%1'"), Символы.ПС), Новый Шрифт(,,Истина), WebЦвета.Красный));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' Сумма ставки%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' Процент%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' 5Percent%1'"), Символы.ПС)); 
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' Count of days%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = 'Правильно%1'"), Символы.ПС), Новый Шрифт(,,Истина), WebЦвета.Зеленый));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' CountOfDays%1'"), Символы.ПС));
	ЧастиСтроки.Добавить(СтрШаблон(НСтр("ru = ' For3Days%1'"), Символы.ПС));
	
	Возврат Новый ФорматированнаяСтрока(ЧастиСтроки);
	
КонецФункции	

Процедура ЗаполнитьСписокВыбораКонтактныхЛиц(пФорма)

	СписокКонтактныхЛиц = Аук_ОбщийВызовСервера.КонтактныеЛицаУчастникаАукциона(пФорма.Аукцион, пФорма.Участник);
	
	СписокВыбора = пФорма.Элементы.КонтактноеЛицо.СписокВыбора;
	
	СписокВыбора.Очистить();
	
	Для Каждого ЭлКонтЛицо Из СписокКонтактныхЛиц Цикл
		
		СписокВыбора.Добавить(ЭлКонтЛицо);	
		
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти
