
#Область ПрограммныйИнтерфейс

// Преобразует полученные данные в строку формата JSON.
// Для значений с типом, не поддерживаемым при сериализации, будет вызвана функция преобразования ПривестиЗначениеПриЗаписиJSON.
// Перед обработкой значений новых типов следует модифицировать функцию преобразования.
//
// Параметры:
//  Данные - Произвольный - данные для сериализации.
//
// Возвращаемое значение:
//   Строка - сериализованные данные в формате JSON.
//
Функция СериализоватьВСтрокуJSON(Данные) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON;
	НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписатьJSON(ЗаписьJSON, Данные, НастройкиСериализации);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

Функция КлючиКоллекцииВМассив(пСоответствие) Экспорт
	масРезультат = Новый Массив;
	
	Для Каждого ЭлСоот Из пСоответствие Цикл
		масРезультат.Добавить(ЭлСоот.Ключ);	
	КонецЦикла;
	
	Возврат масРезультат;
КонецФункции

Функция JSONВЗначение(пДанные, пПрочитатьВСоответствие = Ложь, пИменаСвойствСоЗначениямиДата = Неопределено) Экспорт
	ЧтениеJSON = Новый ЧтениеJSON;
	
	Если ТипЗнч(пДанные) = Тип("Строка") Тогда
		ЧтениеJSON.УстановитьСтроку(пДанные);
		
	ИначеЕсли ТипЗнч(пДанные) = Тип("ДвоичныеДанные") Тогда 
		Поток = пДанные.ОткрытьПотокДляЧтения();
		ЧтениеJSON.ОткрытьПоток(Поток);
		
	ИначеЕсли ТипЗнч(пДанные) = Тип("Поток") 
		  Или ТипЗнч(пДанные) = Тип("ПотокВПамяти")
		  Или ТипЗнч(пДанные) = Тип("ФайловыйПоток") 
	Тогда 
		ЧтениеJSON.ОткрытьПоток(пДанные);
	КонецЕсли;
	
	Значение = ПрочитатьJSON(ЧтениеJSON, пПрочитатьВСоответствие, пИменаСвойствСоЗначениямиДата, ФорматДатыJSON.ISO);
	
	ЧтениеJSON.Закрыть();
	
	Если ТипЗнч(пДанные) = Тип("ДвоичныеДанные") Тогда
		Поток.Закрыть();
	КонецЕсли;
	
	Возврат Значение;
КонецФункции

Функция ШаблонСообщенияПобедителям() Экспорт

	Возврат НСтр("ru = 'Вы победили'");
	
КонецФункции

Функция ДнейВГоду() Экспорт

	Возврат 365;
	
КонецФункции	

Функция ПостфиксПовторяющихсяИменРеквизитов() Экспорт

	Возврат "_";
	
КонецФункции	

Функция МассивУникальныхЗначенийИзКолонкиТаблицы(пТаблица, пКолонка, пБулВМассивСтруктур = Ложь) Экспорт
	
	масРезультат = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из пТаблица Цикл
		
		Если масРезультат.Найти(СтрокаТаблицы[пКолонка]) = Неопределено Тогда
			Если пБулВМассивСтруктур Тогда
				масРезультат.Добавить(Новый Структура(пКолонка, СтрокаТаблицы[пКолонка]));	
			Иначе	
				масРезультат.Добавить(СтрокаТаблицы[пКолонка]);
			КонецЕсли;		
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат масРезультат
	
КонецФункции	

Функция СписокКолонокТаблицы(пТаблица, пСтрокаИсключаемыеКолонки = "") Экспорт
	
	масИсключаемыеКолонки = СтрРазделить(пСтрокаИсключаемыеКолонки, ",");
	
	масРезультат = Новый Массив;
	
	Для Каждого КолонкаТз Из пТаблица.Колонки Цикл
		
		Если масИсключаемыеКолонки.Найти(КолонкаТз.Имя) = Неопределено Тогда
			
			масРезультат.Добавить(КолонкаТз.Имя); 
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат масРезультат;
	
КонецФункции

Функция ТипЗначенияЭлементаПвхСоответствуетТипу(пТипЭлементаПвх, пТип) Экспорт

	Возврат пТипЭлементаПвх.Типы().Найти(пТип) <> Неопределено;
	
КонецФункции	

Функция РабочийДиапазон() Экспорт

	Возврат Новый Структура("НачалоДиапазона, ОкончаниеДиапазона", 8, 18);
	
КонецФункции

Функция МинимальнаяДлительностьЭтапа() Экспорт

	Возврат 20;
	
КонецФункции

Функция МинимальнаяДлительностьЭтапаАвтоматическийРежим() Экспорт

	Возврат 5;
	
КонецФункции

Функция СтруктураПоМассивуИмен(пМасИменаСвойств) Экспорт

	сткРезультат = Новый Структура;
	
	Для Каждого ЭлМас Из пМасИменаСвойств Цикл
		
		сткРезультат.Вставить(ЭлМас);
		
	КонецЦикла;
	
	Возврат сткРезультат
	
КонецФункции	

Функция ЗначениеВМассив(пЗначение) Экспорт

	масРезультат = Новый Массив;
	
	масРезультат.Добавить(пЗначение);
	
	Возврат масРезультат;
	
КонецФункции	

Функция ЭлементыСтавкиПоСтрокеТабличнойЧастиВМассив(пВидАукциона, пСтрокаТабличнойЧасти) Экспорт
	
	сооРезультат = Новый Соответствие;
	
	масСтруктураСтавки = Аук_ОбщийВызовСервера.ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона);
	
	Для Каждого ЭлМас Из масСтруктураСтавки Цикл
		
		сооРезультат.Вставить(ЭлМас.ТехническоеНаименование, СериализаторXDTO.XMLСтрока(пСтрокаТабличнойЧасти[ЭлМас.ТехническоеНаименование]));
		
		//сооЭлементСтавки = Новый Соответствие;
		
		//сооЭлементСтавки.Вставить(ЭлМас.ТехническоеНаименование, СериализаторXDTO.XMLСтрока(пСтрокаТабличнойЧасти[ЭлМас.ТехническоеНаименование]));
		//
		//масРезультат.Добавить(сооЭлементСтавки);
	
	КонецЦикла;	
	
	Возврат сооРезультат;
	
КонецФункции

Функция ПреобразоватьЧислоКСтрокеДляВычисления(пЧслИсходное) Экспорт

	Возврат ?(пЧслИсходное <> 0, Формат(пЧслИсходное, "ЧРД=.; ЧГ=0"), "0"); 
	
КонецФункции

Функция ЗаменитьРаспределяемуюСуммуВФормуле(пФормула, пЧслРаспределяемаяСумма) Экспорт

	Возврат СтрЗаменить(пФормула,
						"[Распределяемая сумма]",
						ПреобразоватьЧислоКСтрокеДляВычисления(пЧслРаспределяемаяСумма))
	
КонецФункции	

Функция ДанныеИтоговИзТаблицыИтогов(пТаблица) Экспорт

	масРез = Новый Массив;
	
	Для Каждого СтрокаТз Из пТаблица Цикл
		
		масРез.Добавить(ИтоговыеДанныеСтавокДанныеПоСтроке(СтрокаТз));
		
	КонецЦикла;
	
	Возврат масРез;
	
КонецФункции	

Функция ДанныеАукционаДляПроверки(пДанныеФормы) Экспорт
	сткДанныеАукциона = Новый Структура;
	
	сткДанныеАукциона.Вставить("ДатаНачала",	пДанныеФормы.ДатаНачала);
	сткДанныеАукциона.Вставить("ВидАукциона",	пДанныеФормы.ВидАукциона); 
	сткДанныеАукциона.Вставить("Сумма",			пДанныеФормы.Сумма);	
	сткДанныеАукциона.Вставить("Срок",			пДанныеФормы.Срок);
	
	масУчастники = Новый Массив;
	
	Для Каждого ЭлУчастник Из пДанныеФормы.Участники Цикл
		масУчастники.Добавить(ЭлУчастник.Участник);	
	КонецЦикла;	
		
	сткДанныеАукциона.Вставить("Участники",		масУчастники);
	
	масЭтапы = Новый Массив;
	
	Для Каждого ЭлЭтап Из пДанныеФормы.Этапы Цикл
		сткЭтап = Новый Структура("НомерСтроки, Этап, ДлительностьДни, ДлительностьЧасы");
		
		ЗаполнитьЗначенияСвойств(сткЭтап, ЭлЭтап); 
		
		масЭтапы.Добавить(сткЭтап);	
	КонецЦикла;	
		
	сткДанныеАукциона.Вставить("Этапы",				масЭтапы);
	
	масНачДанные = Новый Массив;
	Для Каждого СтрокаТч Из пДанныеФормы.НачальныеДанные Цикл
		сткСтрокаТч = Новый Структура("Участник, СтрокаНачальныхДанных, ЭлементНачальныхДанных, ЗначениеНачальныхДанных");
		
		ЗаполнитьЗначенияСвойств(сткСтрокаТч, СтрокаТч); 
		
		масНачДанные.Добавить(сткСтрокаТч);
	КонецЦикла;	
	
	сткДанныеАукциона.Вставить("НачальныеДанные",		масНачДанные);
	сткДанныеАукциона.Вставить("АвтоматическийРежим",	АвтоматическийРежим(пДанныеФормы));
	
	Возврат сткДанныеАукциона;
КонецФункции

Функция АвтоматическийРежим(пДанныеФормы) Экспорт
	Возврат пДанныеФормы.АвтоматическийСтарт
			Или пДанныеФормы.АвтоматическийПереходПоЭтапам
			Или пДанныеФормы.АвтоматическоеПодведениеПромежуточныхИтогов
			Или пДанныеФормы.АвтоматическоеПодведениеОкончательныхИтогов;	
КонецФункции

Функция СписокЭтаповПоУмолчанию() Экспорт

	масРезультат = Новый Массив;
	
	Для Сч = 1 По 5 Цикл
		
		масРезультат.Добавить(Новый Структура("Этап, ДлительностьДни, ДлительностьЧасы", Строка(Сч), 0, Дата('00010101010000')));
		
	КонецЦикла;
	
	Возврат масРезультат;
	
КонецФункции

#КонецОбласти

#Область ПроверкиНастроекАукциона

Функция ВыполнитьКонтрольОбычныхНастроек(пСткДанныеАукциона) Экспорт
    // 1. Дата начала в рабочее время (с 00:00 до 8:00)	
	Если Не ВремяНачалаПопадаетВРабочийДиапазон(пСткДанныеАукциона.ДатаНачала) Тогда
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Время начала не входит в рабочий диапазон'"));	
	КонецЕсли;	
	
	// 2. Длительность этапа не может быть менее 20 минут
	Если Не ПродолжительностьЭтаповНеСодержитОшибки(пСткДанныеАукциона.Этапы) Тогда
		Возврат ЭдоОбщий._Ошибка(СтрШаблон(НСтр("ru = 'Длительность одного из этапов меньше %1 минут'"), МинимальнаяДлительностьЭтапа()));	
	КонецЕсли;	
	
	Возврат ЭдоОбщий._Успех();
КонецФункции	

// Предполагаем, что нерабочее время с 00:00 до 8:00
//
Функция ВремяНачалаПопадаетВРабочийДиапазон(пДатаВремя) Экспорт
	
	сткРабочийДиапазон = РабочийДиапазон();
	
	Возврат Час(пДатаВремя) >= сткРабочийДиапазон.НачалоДиапазона И Час(пДатаВремя) <= сткРабочийДиапазон.ОкончаниеДиапазона;
	
КонецФункции	

// Предполагаем, что длительность этапа не может быть менее 20 минут
//
Функция ПродолжительностьЭтаповНеСодержитОшибки(пТчЭтапы) Экспорт
	
	булРезультат = Истина;
	
	Для Каждого СтрокаТч Из пТчЭтапы Цикл
		 
		Если ДлительностьЭтапаПоСтрокеНеудовлетворительная(СтрокаТч, МинимальнаяДлительностьЭтапа()) Тогда	
			
			булРезультат = Ложь;
			
			Прервать;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат булРезультат;
	
КонецФункции	

#КонецОбласти

#Область ИтоговыеРасчеты

// 1. Если есть колонка "Сумма" (Sum) или колонка "СуммаДо" (SumTo) в ставка => берём 
// 2. Иначе если Если есть колонка "Сумма" или колонка "СуммаДо" в начальных данных => берём
// 3. Иначе берем по умолчанию общую сумму
//
Функция РаспределяемаяСуммаПоСтрокеПредварительная(пСтрокаТз, пСуммаПоУмолчанию) Экспорт 
	
	Если пСтрокаТз.Свойство("Sum") И ЗначениеЗаполнено(пСтрокаТз.Sum) Тогда
		Возврат пСтрокаТз.Sum;
	ИначеЕсли пСтрокаТз.Свойство("SumTo") И ЗначениеЗаполнено(пСтрокаТз.SumTo) Тогда
		Возврат пСтрокаТз.SumTo;
	ИначеЕсли пСтрокаТз.Свойство("Sum_") И ЗначениеЗаполнено(пСтрокаТз.Sum_) Тогда
		Возврат пСтрокаТз.Sum_;
	ИначеЕсли пСтрокаТз.Свойство("SumTo_") И ЗначениеЗаполнено(пСтрокаТз.SumTo_) Тогда
		Возврат пСтрокаТз.SumTo_;
	Иначе
		Возврат пСуммаПоУмолчанию;	
	КонецЕсли;	
			
КонецФункции

// Ставка / 100 / 365 (дней в текущем году) * распределяемая сумма * срок (количество дней)
// СтрокаТз["Percent"] / 100 / Аук_ОбщийКлиентСервер.ДнейВГоду() * СтрокаТз.РаспределяемаяСумма * СтрокаТз["CountOfDays"]
// 
Функция РасчетнаяСуммаПоСтроке(пСтрокаТз, пСрокПоУмолчанию) Экспорт
	
	Возврат ?(пСтрокаТз.Свойство("Percent"), пСтрокаТз.Percent, 0) / 100 / ДнейВГоду() * пСтрокаТз.РаспределяемаяСумма * СрокИспользованияПредложения(пСтрокаТз, пСрокПоУмолчанию); 		
			
КонецФункции

Функция ПредопределенныеОперандыДляРасчетаОкончательногоИтога() Экспорт
	
	масРезультат = Новый Массив;
		
	// "Предопределенные" операнды	
	масРезультат.Добавить("Дней в году");
	масРезультат.Добавить("Срок");
	масРезультат.Добавить("Распределяемая сумма");
	
	Возврат масРезультат;
	
КонецФункции

#КонецОбласти

#Область ВалидацияДанных

Функция EmailКорректный(пСтрEmail) Экспорт

	Возврат Аук_ОбщийВызовСервера.СтрокаПодобнаРегулярномуВыражению(пСтрEmail, Шаблон_Email());
	
КонецФункции	

#КонецОбласти

#Область ВалидацияДанныхШаблоны

Функция Шаблон_Email() Экспорт

	Возврат "([a-zA-Z0-9._-]+@[a-zA-Z0-9._-]+\.[a-zA-Z0-9_-]+)";
	
КонецФункции

Функция Шаблон_ТехническоеНаименованиеЭлементовПВХ() Экспорт

	Возврат "^[A-Za-z][A-Za-z0-9]*$";
	
КонецФункции

Функция Шаблон_НаименованиеЭлементовПВХ() Экспорт

	Возврат "^[a-zA-Zа-яА-ЯёЁ0-9_\s\.\(\)]+$";
	
КонецФункции

Функция Шаблон_ОперандФормулаРасчетаИтогов() Экспорт

	Возврат "\[[a-zA-Zа-яА-яёЁ0-9\s\.\(\)_]+\]";
	
КонецФункции

Функция Шаблон_ОперандыИАрифметическиеЗнакиВФормулеРасчетаИтогов() Экспорт

	Возврат "\[[a-zA-Zа-яА-яёЁ0-9\s\.\(\)_]+\]|\*|\/|[0-9%\.]+|-|\+";
	
КонецФункции

Функция Шаблон_ПараметрыШаблонаСообщения() Экспорт

	Возврат "(?<=\[)[А-Я0-9_]*(?=\])";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИтоговыеДанныеСтавокДанныеПоСтроке(пСтрокаТз)
		
	сткРезультат = Новый Структура;
	
	сткРезультат.Вставить("Участник",				СериализаторXDTO.XMLСтрока(пСтрокаТз.Участник));
	сткРезультат.Вставить("СтрокаДанных",			пСтрокаТз.СтрокаДанных);
	сткРезультат.Вставить("РаспределяемаяСумма",	пСтрокаТз.РаспределяемаяСумма);
	сткРезультат.Вставить("ЭтоПобедитель",			пСтрокаТз.ПобедительФактический);
	
	Возврат сткРезультат;
	
КонецФункции

Функция СрокИспользованияПредложения(пСтрокаТз, пСрокПоУмолчанию)
	
	Если пСтрокаТз.Свойство("CountOfDays") И ЗначениеЗаполнено(пСтрокаТз.CountOfDays) Тогда
		Возврат пСтрокаТз.CountOfDays;
	ИначеЕсли пСтрокаТз.Свойство("CountOfDays_") И ЗначениеЗаполнено(пСтрокаТз.CountOfDays_) Тогда
		Возврат пСтрокаТз.CountOfDays_;
	Иначе
		Возврат пСрокПоУмолчанию;	
	КонецЕсли;	
			
КонецФункции

Функция ДлительностьЭтапаПоСтрокеНеудовлетворительная(пСтрокаЭтап, пМинДлительность) Экспорт

	Возврат пСтрокаЭтап.ДлительностьДни = 0 И (пСтрокаЭтап.ДлительностьЧасы - Дата("00010101")) / 60 < пМинДлительность;
	
КонецФункции	

#КонецОбласти

#Область ДинамическийСписок

// Пользовательские настройки - это коллекция состоящая из следующих элементов:
//		- ОтборКомпоновкиДанных
//		- ПорядокКомпоновкиДанных
//		- УсловноеОформлениеКомпоновкиДанных
//		- СтруктураНастроекКомпоновкиДанных
// Чтобы не обращаться по индексу пройдемся циклом
// По-умолчанию пусть будет самый популярный
//
&НаКлиенте
Функция ЭлементПользовательскихНастроекКомпоновкиДанныхПоТипу(пСписок, пТипЭлемента) Экспорт
	ЭлементОтбора = Неопределено;
	
	Для Каждого ЭлементНастроек Из пСписок.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(ЭлементНастроек) = пТипЭлемента Тогда	
			ЭлементОтбора = ЭлементНастроек;
			
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат ЭлементОтбора;
КонецФункции

#КонецОбласти

#Область РаботаСФормой

Процедура ОбновитьОтборКонтактныхЛицДляВсехУчастников(пКоллекцияСтрокТч, пВидАукциона) Экспорт
	
	масУчастники = Аук_ОбщийКлиентСервер.МассивУникальныхЗначенийИзКолонкиТаблицы(пКоллекцияСтрокТч, "Участник");
	
	сооКонтактныеЛицаУчастника = Аук_ОбщийВызовСервера.КонтактныеЛицаУчастникаПоВидуАукциона(масУчастники, пВидАукциона);
	
	Для Каждого СтрокаТч Из пКоллекцияСтрокТч Цикл
		
		ЗаполнитьКонтактныеЛицаОтборПоСтроке(СтрокаТч, сооКонтактныеЛицаУчастника);	
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКонтактныеЛицаОтборПоСтроке(пСтрокаТч, пСооКонтактныеЛицаУчастника) Экспорт
	
	масКонтЛица = пСооКонтактныеЛицаУчастника.Получить(пСтрокаТч.Участник);
	
	Если масКонтЛица <> Неопределено Тогда
		
		пСтрокаТч.КонтактныеЛицаОтбор = Новый ФиксированныйМассив(масКонтЛица);
		
	Иначе
		
		пСтрокаТч.КонтактныеЛицаОтбор = Новый ФиксированныйМассив(Новый Массив);
		
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Область Логирование

Процедура ОтправитьСообщениеВГрейлог(пСообщение, пСобытие, пСтадия = "") Экспорт

	сткНастройкиЛога = ЭдоОбщийЛогированиеКлиентСервер.Инициализация("Котировки");
	
	сткНастройкиВывода = ЭдоОбщийЛогированиеКлиентСервер.СтруктураНастроекВыводаВГрейЛог();
	
	сткНастройкиВывода.Процесс = "Котировки";
	
	сткНастройкиВывода.ДопДанные.Вставить("event", пСобытие);
	сткНастройкиВывода.ДопДанные.Вставить("stage", пСтадия);
	
	ЭдоОбщийЛогированиеКлиентСервер.ВключитьВыводВГрейЛог(сткНастройкиЛога, сткНастройкиВывода);
	
	ЭдоОбщийЛогированиеКлиентСервер.Лог(сткНастройкиЛога, пСообщение);
	
КонецПроцедуры	

#КонецОбласти




