
// Параметры:
//   Отбор - Отбор - произвольный отбор.
//   КлючЭлемента - Строка - имя элемента отбора.
//   ЗначениеЭлемента - Произвольный - значение элемента отбора.
// 
Процедура УстановитьЗначениеЭлементаОтбора(Отбор, КлючЭлемента, ЗначениеЭлемента) Экспорт

	ЭлементОтбора = Отбор.Найти(КлючЭлемента);
	Если ЭлементОтбора <> Неопределено Тогда
		ЭлементОтбора.Установить(ЗначениеЭлемента);
	КонецЕсли;

КонецПроцедуры

#Область РаботаСРегистрамиСведений

// Добавляет одну запись в регистр сведений по переданным значениям структуры.
//
// Параметры:
//  СтруктураЗаписи - Структура - структура, по значениям которой необходимо создать набор записей и заполнить этот
//                                набор.
//  ИмяРегистра     - Строка - имя регистра сведений, в который необходимо добавить запись.
// 
Функция ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, Знач ИмяРегистра, Загрузка = Ложь) Экспорт

	НаборЗаписей = СоздатьНаборЗаписейРегистраСведений(СтруктураЗаписи, ИмяРегистра);
	
	// Добавляем только одну запись в новый набор записей.
	НоваяЗапись = НаборЗаписей.Добавить();
	
	// Заполняем значения свойств записи из переданной структуры.
	ЗаполнитьЗначенияСвойств(НоваяЗапись, СтруктураЗаписи);

	НаборЗаписей.ОбменДанными.Загрузка = Загрузка;
	
	// записываем набор записей
	Возврат ОбщегоНазначения.ЗаписатьОбъектВБазу(НаборЗаписей);

КонецФункции

Функция ДобавитьЗаписиВРегистрСведений(МассивСтруктур, Знач ИмяРегистра, Загрузка = Ложь) Экспорт
	
	МассивНаборов = Новый Массив;
	
	Для Каждого СткЗапись Из МассивСтруктур Цикл
		
		НаборЗаписей = СоздатьНаборЗаписейРегистраСведений(СткЗапись, ИмяРегистра);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СткЗапись);
		
		НаборЗаписей.ОбменДанными.Загрузка = Загрузка;
		
		МассивНаборов.Добавить(НаборЗаписей);
		
	КонецЦикла;	
	
	Возврат ОбщегоНазначения.ЗаписатьОбъектВБазу(МассивНаборов);
	
КонецФункции

// Обновляет запись в регистр сведений по переданным значениям структуры.
//
// Параметры:
//  СтруктураЗаписи - Структура - структура, по значениям которой необходимо создать менеджер записи и обновить запись.
//  ИмяРегистра     - Строка - имя регистра сведений, в котором необходимо обновить запись.
// 
Функция ОбновитьЗаписьВРегистрСведений(СтруктураЗаписи, Знач ИмяРегистра) Экспорт

	МетаданныеРегистра = Метаданные.РегистрыСведений[ИмяРегистра]; // ОбъектМетаданныхРегистрСведений
	
	// Создаем менеджер записи регистра.
	МенеджерЗаписи = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
	
	// Если регистр периодический добавим отбор по периоду
	Если МетаданныеРегистра.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда	
		МенеджерЗаписи.Период = СтруктураЗаписи.Период;	
	КонецЕсли;	
	
	// Устанавливаем отбор по измерениям регистра.
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл

		ИмяИзмерения = Измерение.Имя;
		
		// Если задано значение в структуре, то отбор устанавливаем.
		Если СтруктураЗаписи.Свойство(ИмяИзмерения) Тогда

			МенеджерЗаписи[ИмяИзмерения] = СтруктураЗаписи[ИмяИзмерения];

		КонецЕсли;

	КонецЦикла;
	
	// Считываем запись из базы данных.
	МенеджерЗаписи.Прочитать();
	
	// Заполняем значения свойств записи из переданной структуры.
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураЗаписи);
	
	// записываем менеджер записи
	Возврат ОбщегоНазначения.ЗаписатьОбъектВБазу(МенеджерЗаписи);

КонецФункции

// Удаляет набор записей в регистре по переданным значениям структуры.
//
// Параметры:
//  СтруктураЗаписи - Структура - структура, по значениям которой необходимо удалить набор записей.
//  ИмяРегистра     - Строка - имя регистра сведений, в котором необходимо удалить набор записей.
// 
Функция УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи, ИмяРегистра, Загрузка = Ложь) Экспорт

	НаборЗаписей = СоздатьНаборЗаписейРегистраСведений(СтруктураЗаписи, ИмяРегистра);

	НаборЗаписей.ОбменДанными.Загрузка = Загрузка;
	
	// записываем набор записей
	Возврат ОбщегоНазначения.ЗаписатьОбъектВБазу(НаборЗаписей);

КонецФункции

// Создает набор записей регистра сведений по переданным значениям структуры. Добавляет одну запись в набор.
//
// Параметры:
//  СтруктураЗаписи - Структура - структура по значениям которой необходимо создать набор записей и заполнить этот
//                                набор.
//  ИмяРегистра     - Строка - имя регистра сведений.
//  
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей - набор записей регистра по указанному отбору.
// 
Функция СоздатьНаборЗаписейРегистраСведений(СтруктураЗаписи, ИмяРегистра) Экспорт

	НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей
	
	// Устанавливаем отбор по измерениям регистра.
	Для Каждого КлючЗначение Из СтруктураЗаписи Цикл
		УстановитьЗначениеЭлементаОтбора(НаборЗаписей.Отбор, КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;

	Возврат НаборЗаписей;

КонецФункции

#КонецОбласти

#Область РаботаСОбъектамиБД

Функция СоздатьКопиюСуществующегоЭлементаСправочника(пТекОбъект) Экспорт
	
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(пТекОбъект));
	
	Если МетаданныеОбъекта = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Метаданные не найдены. Копирование невозможно'");	
	Иначе	
		
		Возврат СоздатьКопиюОбъектаМетаданных(МетаданныеОбъекта, пТекОбъект);
		
	КонецЕсли;
	
КонецФункции	

// Ссылка объекта по УИД и типу
//
// Параметры:
//  пТип - 	 Тип 
//  пУИД - 	 Строка - УИД в формате "00000000-0000-0000-0000-000000000000" 
// 
// Возвращаемое значение:
//   Ссылка - может быть пустая 
//
Функция СсылкаОбъектаПоУИД(пТип, пУИД) Экспорт
	ИмяТаблицы = Метаданные.НайтиПоТипу(пТип).ПолноеИмя();
	
	Попытка
		СсылкаНаОбъект = XMLЗначение(пТип, пУИД);
	Исключение
		Возврат ПредопределенноеЗначение(ИмяТаблицы + ".ПустаяСсылка");	
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат СсылкаНаОбъект;	
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	&Таблица КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ИмяТаблицы); 
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ПредопределенноеЗначение(ИмяТаблицы + ".ПустаяСсылка");
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий();
		
		Возврат Выборка.Ссылка;
	КонецЕсли;	
	
КонецФункции

#КонецОбласти

#Область РаботаСоСтатусамиАукционов

Процедура ЗафиксироватьИзменениеСтатусаАукциона_(пСсАукцион) Экспорт
	
	сткДанныеЭтапаАукциона = Справочники.Аук_Аукционы.СведенияПоЭтапуАукциона(пСсАукцион, 1);
	
	Если Не ЗначениеЗаполнено(сткДанныеЭтапаАукциона.Этап) Тогда
		
		ВызватьИсключение НСтр("ru = 'Не заполнено наименование первого этапа.'");
		
	КонецЕсли;
	
	масУчастники = Справочники.Аук_Аукционы.УчастникиАукциона(пСсАукцион);
	
	Если масУчастники.Количество() = 0 Тогда
		
		ВызватьИсключение НСтр("ru = 'Не заполнены участники'");
	
	КонецЕсли;
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Период",		ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	сткПараметры.Вставить("Аукцион",	пСсАукцион);
	сткПараметры.Вставить("Статус",		ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.НовыйЭтап")); 
	сткПараметры.Вставить("Этап",		сткДанныеЭтапаАукциона.Этап);
	сткПараметры.Вставить("НомерЭтапа",	1);
	
	сткРез = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ДобавитьЗапись(сткПараметры);
	
	Если Не сткРез.Успех Тогда
		
		ВызватьИсключение сткРез.Ошибка;
			
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПеревестиНовыйАукционВРаботу_(пСсАукцион) Экспорт
	
	сткДанныеЭтапаАукциона = Справочники.Аук_Аукционы.СведенияПоЭтапуАукциона(пСсАукцион, 1);
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Период",		ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	сткПараметры.Вставить("Аукцион",	пСсАукцион);
	сткПараметры.Вставить("Статус",		ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.НовыйЭтап")); 
	сткПараметры.Вставить("Этап",		сткДанныеЭтапаАукциона.Этап);
	сткПараметры.Вставить("НомерЭтапа",	1);
	
	сткРез = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ДобавитьЗапись(сткПараметры);
	
	Если Не сткРез.Успех Тогда		
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеревестиАукционНаНовыйЭтап_(пСсАукцион) Экспорт
	
	ТекСостояние = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ТекущееСостояниеАукциона(пСсАукцион);
	
	КолЭтапов = Справочники.Аук_Аукционы.КоличествоЭтаповАукциона(пСсАукцион);
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Период",		ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	сткПараметры.Вставить("Аукцион",	пСсАукцион);
	
	Если ТекСостояние.НомерЭтапа < КолЭтапов Тогда
		Если ТекСостояние.Статус = ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.НовыйЭтап") Тогда	
			сткПараметры.Вставить("Статус",		ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.ПодведениеПромежуточныхИтогов"));
			сткПараметры.Вставить("Этап",		ТекСостояние.Этап);
			сткПараметры.Вставить("НомерЭтапа",	ТекСостояние.НомерЭтапа);
		Иначе
			сткДанныеЭтапаАукциона = Справочники.Аук_Аукционы.СведенияПоЭтапуАукциона(пСсАукцион, ТекСостояние.НомерЭтапа + 1);
			
			сткПараметры.Вставить("Статус",		ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.НовыйЭтап"));
			сткПараметры.Вставить("Этап",		сткДанныеЭтапаАукциона.Этап);
			сткПараметры.Вставить("НомерЭтапа",	ТекСостояние.НомерЭтапа + 1);	
		КонецЕсли;	
	Иначе
		сткПараметры.Вставить("Статус",		ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.ПодведениеИтогов"));	
	КонецЕсли;
	
	сткРез = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ДобавитьЗапись(сткПараметры);
	
	Если Не сткРез.Успех Тогда		
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеревестиАукционВЗавершенные_(пСсАукцион) Экспорт
		
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Период",		ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	сткПараметры.Вставить("Аукцион",	пСсАукцион);
	сткПараметры.Вставить("Статус",		ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.Завершен"));
	
	сткРез = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ДобавитьЗапись(сткПараметры);
	
	Если Не сткРез.Успех Тогда		
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоместитьАукционВАрхив_(пСсАукцион) Экспорт
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Период",		ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_());
	сткПараметры.Вставить("Аукцион",	пСсАукцион);
	сткПараметры.Вставить("Статус",		ПредопределенноеЗначение("Перечисление.Аук_СтатусыАукционов.Архив"));
	
	сткРез = РегистрыСведений.Аук_ИсторияСтатусовАукционов.ДобавитьЗапись(сткПараметры);
	
	Если Не сткРез.Успех Тогда		
		ВызватьИсключение сткРез.Ошибка;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСИтогами

Функция ПромежуточныеИтогиРасчетПоУмолчанию(пАукцион, пВидАукциона = Неопределено, пМасСтруктураСтавки = Неопределено) Экспорт
	
	Если пВидАукциона = Неопределено Тогда
		пВидАукциона = Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион).ВидАукциона;
	КонецЕсли;
		
	тзТекДанныеСтавок = РегистрыСведений.Аук_ИсторияДанныхАукционов.ТекущиеДанныеСтавокУчастниковАукционаВСтроке(пАукцион, пВидАукциона);
	
	тзТекДанныеСтавок.Колонки.Добавить("РезультатРасчета", Новый ОписаниеТипов("Число"));
	тзТекДанныеСтавок.Колонки.Добавить("ПредставлениеРезультатаРасчета", Новый ОписаниеТипов("Строка"));
	
	Если тзТекДанныеСтавок.Количество() > 0 Тогда 
		
		сткДанныеДляРасчета = Справочники.Аук_ВидыАукционов.ОписаниеРасчетаПромежуточныхИтоговПоВидуАукциона(пВидАукциона);
		
		Если пМасСтруктураСтавки = Неопределено Тогда
			пМасСтруктураСтавки = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона);
		КонецЕсли;
		
		Попытка
			Для Каждого СтрокаТз Из тзТекДанныеСтавок Цикл
				
				СтрокаТз.РезультатРасчета	= РасчитатьПромежуточныйИтогПоФормуле(сткДанныеДляРасчета.ПромежуточныйИтогФормула, пМасСтруктураСтавки, СтрокаТз);
				
				СтрокаТз.ПредставлениеРезультатаРасчета			= РасчитатьИтоговоеЗначениеПоШаблону(сткДанныеДляРасчета.ПромежуточныйИтогШаблонРезультата, пМасСтруктураСтавки, СтрокаТз);
				
			КонецЦикла;
		Исключение	
			стрОшибка = ОписаниеОшибки();
			
			Возврат ЭдоОбщий._Ошибка(стрОшибка);
		КонецПопытки;
		
		тзТекДанныеСтавок.Сортировать(СтрШаблон("РезультатРасчета %1", ?(сткДанныеДляРасчета.ПромежуточныйИтогВариантВыбора = 0, "ВОЗР", "УБЫВ")));
		
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех(тзТекДанныеСтавок);
	
КонецФункции

Функция ПромежуточныйИтогДанныеЛидирующейСтавки(пАукцион) Экспорт
	
	ВидАукциона = Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион).ВидАукциона;
	
	масСтруктураСтавки = Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(ВидАукциона);
	
	сткРез = ПромежуточныеИтогиРасчетПоУмолчанию(пАукцион, ВидАукциона, масСтруктураСтавки);
	
	Если сткРез.Успех Тогда	
		Если сткРез.Результат.Количество() <> 0 Тогда	
			сткЛидирующаяСтавка = Новый Структура;
			
			сткЛидирующаяСтавка.Вставить("ДатаСтавки",		сткРез.Результат[0].ДатаСтавки);
			сткЛидирующаяСтавка.Вставить("Участник",		сткРез.Результат[0].Участник);
			сткЛидирующаяСтавка.Вставить("СтрокаДанных",	сткРез.Результат[0].СтрокаДанных);
			
			сткЛидирующаяСтавка.Вставить("РезультатРасчета",				сткРез.Результат[0].РезультатРасчета);
			сткЛидирующаяСтавка.Вставить("ПредставлениеРезультатаРасчета",	сткРез.Результат[0].ПредставлениеРезультатаРасчета);	
			
			Возврат ЭдоОбщий._Успех(сткЛидирующаяСтавка);	
		Иначе	
			Возврат ЭдоОбщий._Успех();	
		КонецЕсли;	
		
	Иначе	
		Возврат сткРез;	
	КонецЕсли;
	
КонецФункции

////////////
Функция НачальныеУсловияАукциона(пАукцион) Экспорт
	
	сткРезультат = Новый Структура("ОбщаяСумма, ОбщийСрок");
	
	сткТекДанныеШапкиАукциона = РегистрыСведений.Аук_ДанныеШапокПодведенияИтогов.ТекущиеДанныеШапкиПодведенияИтоговАукциона(пАукцион);
	
	Если сткТекДанныеШапкиАукциона <> Неопределено Тогда	
		ЗаполнитьЗначенияСвойств(сткРезультат, сткТекДанныеШапкиАукциона, "ОбщаяСумма,ОбщийСрок");	
	Иначе	
		сткВводныеДанныеАукциона		= Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион);
		
		сткРезультат.ОбщаяСумма			= сткВводныеДанныеАукциона.Сумма;
		сткРезультат.ОбщийСрок			= сткВводныеДанныеАукциона.Срок;	
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции	

Процедура ОкончательныеИтоги_ДополнительныеПараметры(пСткПараметры) Экспорт

	пСткПараметры.Вставить("СтруктураСтавки",			Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пСткПараметры.ВидАукциона));
	пСткПараметры.Вставить("СтруктураНачальныхДанных",	Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(пСткПараметры.ВидАукциона));
	
	пСткПараметры.Вставить("Участники",					РегистрыСведений.Аук_ИсторияДанныхАукционов.СведенияОбАктивностиУчастниковАукциона(пСткПараметры.Аукцион));
	
	пСткПараметры.Вставить("НачальныеДанные",			Справочники.Аук_Аукционы.ЗначенияНачальныхДанных(пСткПараметры.Аукцион, пСткПараметры.ВидАукциона));
	пСткПараметры.Вставить("НачальныеУсловия",			Аук_ОбщийСервер.НачальныеУсловияАукциона(пСткПараметры.Аукцион));
	
	пСткПараметры.Вставить("ДанныеСтавок",			РегистрыСведений.Аук_ИсторияДанныхАукционов.ТекущиеДанныеСтавокУчастниковАукционаВСтроке(пСткПараметры.Аукцион,
																							пСткПараметры.ВидАукциона, Аук_ОбщийСервер.СоставОбязательныхКолонок("Участник,СтрокаДанных")));
																							
	пСткПараметры.Вставить("ТекущиеДанныеИтогов",	РегистрыСведений.Аук_ДанныеТаблицПодведенияИтогов.ТекущиеДанныеТаблицПодведенияИтогов(пСткПараметры.Аукцион));																						
	
	Если пСткПараметры.СРаспределениемСуммы Тогда	
		пСткПараметры.Вставить("НастройкиРасчетаИтога", Справочники.Аук_ВидыАукционов.ОписаниеРасчетаОкончательныхИтоговПоВидуАукциона(пСткПараметры.ВидАукциона));
		
		ОкончательныеИтоги_ДанныеСтавокУчастников_ПодготовкаПараметров_ДанныеДляРасчета(пСткПараметры);	
	КонецЕсли;
	
КонецПроцедуры

Функция ОкончательныеИтогиАукциона(пСткПараметры) Экспорт 

	тзИтогиТемп = ОкончательныеИтоги_ДанныеСтавокУчастников(пСткПараметры);
		
	Возврат Аук_ОбщийКлиентСервер.ДанныеИтоговИзТаблицыИтогов(тзИтогиТемп);
		
КонецФункции

// Окончательные итоги данные ставок участников
//
// Параметры:
//  пСткПараметры
// 	 Обязательно
//		*Аукцион			- СправочникСсылка.Аук_Аукционы
//		*ВидАукциона		- СправочникСсылка.Аук_ВидыАукционов
//		*СтруктураСтавки	- Массив - Описание элементов ставки
//		*СтруктураНачальныхДанных - Массив - Описание элементов начальных данныъ
//		*Участники - Массив структур
//			**Участник - СправочникСсылка.Аук_УчастникиАукционов 
//			**СтатусУчастия - Число - 2 - Отказался, 1 - Не делал ставки, 0 - Делал ставки
//		*НачальныеДанные - Массив
//		*НачальныеУсловия - Структура
//			**ОбщаяСумма	- Число
//			**ОбщийСрок		- Число
//   Опционально:
//		*НастройкиРасчетаИтога - Структура
//			**Формула - Строка
//			**РаспределяемаяСуммаОсновная - Строка
//			**РаспределяемаяСуммаДополнительная - Строка
//		*ОперандыВТехНаименования - Соответствие
//			**Ключ - Строка - в формате "[" + Наименование + "]"
//			**Значение - Строка - Техническое наименование
// 
// Возвращаемое значение:
//   Таблица значений 
//
Функция ОкончательныеИтоги_ДанныеСтавокУчастников(пСткПараметры) Экспорт
	
	тзИтогиТемп = ОкончательныеИтоги_ДанныеСтавокУчастников_ТемпТаблица(пСткПараметры);
	
	Для Каждого ЭлМас Из пСткПараметры.Участники Цикл
		//НовСтрока = тзИтогиТемп.Добавить();
		//
		//НовСтрока.Участник		= ЭлМас.Участник;
		//НовСтрока.СтатусУчастия = ЭлМас.СтатусУчастия;
		
		Если ЭлМас.СтатусУчастия = 0 Тогда	
			масСтрокиТзДанныеСтавки = пСткПараметры.ДанныеСтавок.НайтиСтроки(Новый Структура("Участник", ЭлМас.Участник));
			
			Для Каждого СтрокаТзТекДанныеСтавки Из масСтрокиТзДанныеСтавки Цикл
				НовСтрока = тзИтогиТемп.Добавить();
		
				НовСтрока.Участник		= ЭлМас.Участник;
				НовСтрока.СтатусУчастия = ЭлМас.СтатусУчастия;
				
				// Строка данных
				НовСтрока.СтрокаДанных	= СтрокаТзТекДанныеСтавки.СтрокаДанных;
				
				// Элементы ставки
				Для Каждого ЭлементСтавки Из пСткПараметры.СтруктураСтавки Цикл
					НовСтрока[ЭлементСтавки.ТехническоеНаименование] = СтрокаТзТекДанныеСтавки[ЭлементСтавки.ТехническоеНаименование];	
				КонецЦикла;	
				
				Если пСткПараметры.СРаспределениемСуммы Тогда		
					Если пСткПараметры.ТекущиеДанныеИтогов <> Неопределено Тогда	
						ОкончательныеИтоги_ДанныеСтавокУчастников_ЗаполнитьТекущиеИтоги(НовСтрока, пСткПараметры.ТекущиеДанныеИтогов);			
					Иначе	
						ОкончательныеИтоги_ДанныеСтавокУчастников_РаспределяемаяСумма(НовСтрока, пСткПараметры);
					КонецЕсли; 
					
					ОкончательныеИтоги_ДанныеСтавокУчастников_РасчетнаяСуммаПоФормуле(НовСтрока, пСткПараметры);
					
				Иначе		
					Если пСткПараметры.ТекущиеДанныеИтогов <> Неопределено Тогда	
						ОкончательныеИтоги_ДанныеСтавокУчастников_ЗаполнитьТекущиеИтоги(НовСтрока, пСткПараметры.ТекущиеДанныеИтогов);	
					КонецЕсли;	
				КонецЕсли;	
				
			КонецЦикла;
			
		Иначе
			НовСтрока = тзИтогиТемп.Добавить();
		
			НовСтрока.Участник		= ЭлМас.Участник;
			НовСтрока.СтатусУчастия = ЭлМас.СтатусУчастия;
		
			Если пСткПараметры.ТекущиеДанныеИтогов <> Неопределено Тогда	
				ОкончательныеИтоги_ДанныеСтавокУчастников_ЗаполнитьТекущиеИтоги(НовСтрока, пСткПараметры.ТекущиеДанныеИтогов);		
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Предварительное распределение. Только для случая, когда в РС Аук_ДанныеТаблицПодведенияИтогов
	// нет записей по текущему аукциону (т.е. пСткПараметры.ТекущиеДанныеИтогов = Неопределено)
	// Если записи есть (т.е. пСткПараметры.ТекущиеДанныеИтогов <> Неопределено),
	// то принимаем, что пользователь ранее уже произвел нужные вычисления. Менять в этом случае не нужно
	Если пСткПараметры.ТекущиеДанныеИтогов = Неопределено Тогда	
		ОкончательныеИтоги_РаспределитьСуммыАвтоматически(тзИтогиТемп, пСткПараметры);		
	КонецЕсли; 
	
	Возврат тзИтогиТемп;
	
КонецФункции

Процедура ОкончательныеИтоги_РаспределитьСуммыАвтоматически(пТзИтогиТемп, пСткПараметры)
	пТзИтогиТемп.Сортировать("РасчетнаяСумма Убыв");
	
	чслОбщаяСумма = пСткПараметры.НачальныеУсловия.ОбщаяСумма;	
	
	чслЛокальныйИтог = 0;
	Для Каждого СтрокаТз Из пТзИтогиТемп Цикл
		
		Если СтрокаТз.СтатусУчастия <> 0 Тогда
			Продолжить;	
		КонецЕсли;
		
		Если СтрокаТз.РасчетнаяСумма = 0 Тогда
			Продолжить;	
		КонецЕсли;
		
		Если (чслЛокальныйИтог + СтрокаТз.РаспределяемаяСумма) > чслОбщаяСумма Тогда
			Продолжить;	
		Иначе	
			чслЛокальныйИтог = чслЛокальныйИтог + СтрокаТз.РаспределяемаяСумма;	
			
			СтрокаТз.ПобедительФактический		= Истина;
			СтрокаТз.ПобедительПредполагаемый	= Истина;
		КонецЕсли;	
					
	КонецЦикла;
КонецПроцедуры

Процедура ОкончательныеИтоги_ДанныеСтавокУчастников_РаспределяемаяСумма(пНоваяСтрока, пСткПараметры)
	чслРаспределяемаяСуммаВычисленная = 0;
	
	// 1. Считаем по основной
	Если ЗначениеЗаполнено(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаОсновная) Тогда	
		ЧислоСтрокой = пНоваяСтрока[пСткПараметры.ОперандыВТехНаименования[пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаОсновная]];
		
		стрВыражение = Аук_ОбщийКлиентСервер.ПреобразоватьЧислоКСтрокеДляВычисления(ЧислоСтрокой);
		
		чслРаспределяемаяСуммаВычисленная = Вычислить(стрВыражение);	
	КонецЕсли;	
	
	// 2. Считаем по дополнительной
	Если чслРаспределяемаяСуммаВычисленная = 0 И ЗначениеЗаполнено(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаДополнительная) Тогда	
		масСтрокиТз_ = пСткПараметры.НачальныеДанные.НайтиСтроки(Новый Структура("Участник", пНоваяСтрока.Участник));
		
		Если масСтрокиТз_.Количество() <> 0 Тогда
			ИмяКолонки = пСткПараметры.ОперандыВТехНаименования[пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаДополнительная]; 
			
			чслРаспределяемаяСуммаВычисленная = масСтрокиТз_[0][ИмяКолонки];	
		КонецЕсли;	
	КонецЕсли;
	
	пНоваяСтрока.РаспределяемаяСумма = чслРаспределяемаяСуммаВычисленная;
КонецПроцедуры

Процедура ОкончательныеИтоги_ДанныеСтавокУчастников_РасчетнаяСуммаПоФормуле(пНоваяСтрока, пСткПараметры)
	
	ИтоговоеВыражение = пСткПараметры.НастройкиРасчетаИтога.Формула;
	
	Для Каждого ЭлСоо Из пСткПараметры.ОперандыВТехНаименования Цикл
		
		Если СтрНайти(ИтоговоеВыражение, ЭлСоо.Ключ) <> 0 Тогда 
			
			ИтоговоеВыражение = СтрЗаменить(ИтоговоеВыражение, ЭлСоо.Ключ, Аук_ОбщийКлиентСервер.ПреобразоватьЧислоКСтрокеДляВычисления(пНоваяСтрока[ЭлСоо.Значение]));  
			
			//пСткПараметры.НастройкиРасчетаИтога.Формула = СтрЗаменить(пСткПараметры.НастройкиРасчетаИтога.Формула,
			//														  ЭлСоо.Ключ,
			//														  Аук_ОбщийКлиентСервер.ПреобразоватьЧислоКСтрокеДляВычисления(пНоваяСтрока[ЭлСоо.Значение]));
			
			ИтоговоеВыражение = СтрЗаменить(ИтоговоеВыражение,
											ЭлСоо.Ключ,
											Аук_ОбщийКлиентСервер.ПреобразоватьЧислоКСтрокеДляВычисления(пНоваяСтрока[ЭлСоо.Значение]));
			
		КонецЕсли;
		
	КонецЦикла;
	
	//ИтоговоеВыражение = Аук_ОбщийКлиентСервер.ЗаменитьРаспределяемуюСуммуВФормуле(пСткПараметры.НастройкиРасчетаИтога.Формула, пНоваяСтрока.РаспределяемаяСумма);
	
	ИтоговоеВыражение_ = Аук_ОбщийКлиентСервер.ЗаменитьРаспределяемуюСуммуВФормуле(ИтоговоеВыражение, пНоваяСтрока.РаспределяемаяСумма);
	
	//пНоваяСтрока.ФормулаРасчетнойСуммы = пСткПараметры.НастройкиРасчетаИтога.Формула;
	пНоваяСтрока.ФормулаРасчетнойСуммы = ИтоговоеВыражение;
	
	Попытка
		пНоваяСтрока.РасчетнаяСумма = Вычислить(ИтоговоеВыражение_);
	Исключение
		пНоваяСтрока.РасчетнаяСумма = 0;
	КонецПопытки;
	
КонецПроцедуры	

Процедура ОкончательныеИтоги_ДанныеСтавокУчастников_ЗаполнитьТекущиеИтоги(пНовСтрока, пТзТекущиеДанныеИтогов)
	
	масСтрокиТз = пТзТекущиеДанныеИтогов.НайтиСтроки(Новый Структура("Участник, СтрокаДанных", пНовСтрока.Участник, пНовСтрока.СтрокаДанных));
	
	Если масСтрокиТз.Количество() <> 0 Тогда	
		пНовСтрока.РаспределяемаяСумма		= масСтрокиТз[0].РаспределяемаяСумма;
		пНовСтрока.ПобедительФактический	= масСтрокиТз[0].ЭтоПобедитель;
		пНовСтрока.ПобедительПредполагаемый	= пНовСтрока.ПобедительФактический;	
	КонецЕсли;
	
КонецПроцедуры	

Процедура ОкончательныеИтоги_ДанныеСтавокУчастников_ПодготовкаПараметров_ДанныеДляРасчета(пСткПараметры)
	
	масОперанды = ОкончательныеИтоги_ДанныеСтавокУчастников_ПодготовкаПараметров_ДанныеДляРасчета_Операнды(пСткПараметры);
	
	сооЭлСтавки = ПланыВидовХарактеристик.Аук_ЭлементыДанныхАукционов.СоответствиеОперандовТехническимНаименованиям(масОперанды);
	
	пСткПараметры.Вставить("ОперандыВТехНаименования", сооЭлСтавки);
	
	Если Не ЗначениеЗаполнено(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаОсновная)
		И ЗначениеЗаполнено(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаДополнительная)
	Тогда
		
		пСткПараметры.НачальныеДанные.Сортировать(пСткПараметры.ОперандыВТехНаименования[пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаДополнительная] + " УБЫВ");	
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОкончательныеИтоги_ДанныеСтавокУчастников_ПодготовкаПараметров_ДанныеДляРасчета_Операнды(пСткПараметры) 
	
	масНаименованияЭлементов = Новый Массив;
	
	Если ЗначениеЗаполнено(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаОсновная) Тогда	
		масНаименованияЭлементов.Добавить(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаОсновная);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаДополнительная) Тогда		
		масНаименованияЭлементов.Добавить(пСткПараметры.НастройкиРасчетаИтога.РаспределяемаяСуммаДополнительная);
	КонецЕсли;
	
	масВхождения = ОперандыВФормуле(пСткПараметры);
		
	Для Каждого ЭлВхождение Из масВхождения Цикл		
		масНаименованияЭлементов.Добавить(ЭлВхождение.Значение);		
	КонецЦикла;
	
	Возврат масНаименованияЭлементов;
	
КонецФункции

Функция ОперандыВФормуле(Знач пСткПараметры)

	сооЗамена = ЗначенияПредопределенныхОперандов(пСткПараметры.Аукцион);								
	
	Для Каждого ЭлСооЗамена Из сооЗамена Цикл	
		пСткПараметры.НастройкиРасчетаИтога.Формула = СтрЗаменить(пСткПараметры.НастройкиРасчетаИтога.Формула, ЭлСооЗамена.Ключ, ЭлСооЗамена.Значение);	
	КонецЦикла;
	
	Возврат СтрНайтиВсеПоРегулярномуВыражению(пСткПараметры.НастройкиРасчетаИтога.Формула, Аук_ОбщийКлиентСервер.Шаблон_ОперандФормулаРасчетаИтогов());
	
КонецФункции

Функция ЗначенияПредопределенныхОперандов(пАукцион)

	сооРезультат = Новый Соответствие;
	
	сооРезультат.Вставить("[Дней в году]",	Аук_ОбщийКлиентСервер.ПреобразоватьЧислоКСтрокеДляВычисления(Аук_ОбщийКлиентСервер.ДнейВГоду()));
	сооРезультат.Вставить("[Срок]",			Аук_ОбщийКлиентСервер.ПреобразоватьЧислоКСтрокеДляВычисления(Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион).Срок));
	
	Возврат сооРезультат;
	
КонецФункции

Функция ОкончательныеИтоги_ДанныеСтавокУчастников_ТемпТаблица(пСткПараметры)

	тзИтогиТемп = Новый ТаблицаЗначений;
	
	тзИтогиТемп.Колонки.Добавить("Участник",				Новый ОписаниеТипов("СправочникСсылка.Аук_УчастникиАукционов"));
	тзИтогиТемп.Колонки.Добавить("СтрокаДанных",			Новый ОписаниеТипов("Число"));
	тзИтогиТемп.Колонки.Добавить("СтатусУчастия",			Новый ОписаниеТипов("Число"));
	тзИтогиТемп.Колонки.Добавить("ПобедительФактический",	Новый ОписаниеТипов("Булево"));
	тзИтогиТемп.Колонки.Добавить("ПобедительПредполагаемый",	Новый ОписаниеТипов("Булево"));
	тзИтогиТемп.Колонки.Добавить("РаспределяемаяСумма",		Новый ОписаниеТипов("Число"));
	тзИтогиТемп.Колонки.Добавить("РасчетнаяСумма",			Новый ОписаниеТипов("Число"));
	тзИтогиТемп.Колонки.Добавить("ФормулаРасчетнойСуммы",	Новый ОписаниеТипов("Строка"));
	
	Для Каждого КолонкаИмя Из пСткПараметры.СтруктураСтавки Цикл	
		тзИтогиТемп.Колонки.Добавить(КолонкаИмя.ТехническоеНаименование, КолонкаИмя.ТипЗначения);		
	КонецЦикла;
	
	Возврат тзИтогиТемп; 
	
КонецФункции	

#КонецОбласти

#Область РаботаСФормами

Процедура ДобавитьКолонкиДанныхСтавкиПоВидуАукциона(пФорма, пВидАукциона, пИмяТаблицы, пРодитель = Неопределено, пМасСтруктураДанных = Неопределено, пБулПомечатьОбязательные = Истина) Экспорт
	
	Если пМасСтруктураДанных = Неопределено Тогда
		пМасСтруктураДанных	= Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона);
	КонецЕсли;
	
	ДобавитьКолонкиДанных(пФорма, пМасСтруктураДанных, пИмяТаблицы, пРодитель,, пБулПомечатьОбязательные);
	
КонецПроцедуры

Процедура ДобавитьКолонкиНачальныхДанныхПоАукциону(пФорма, пВидАукциона, пИмяТаблицы, пРодитель = Неопределено, пМасСтруктураДанных = Неопределено, пБулИспользоватьПостфикс = Ложь) Экспорт
	
	Если пМасСтруктураДанных = Неопределено Тогда
		пМасСтруктураДанных	= Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(пВидАукциона);
	КонецЕсли;
	
	Постфикс = ?(пБулИспользоватьПостфикс, Аук_ОбщийКлиентСервер.ПостфиксПовторяющихсяИменРеквизитов(), "");
	
	ДобавитьКолонкиДанных(пФорма, пМасСтруктураДанных, пИмяТаблицы, пРодитель, Постфикс);
	
КонецПроцедуры

Процедура УдалитьКолонкиНачальныхДанныхПоАукциону(пФорма, пВидАукциона, пИмяТаблицы, пМасСтруктураДанных = Неопределено, пБулИспользоватьПостфикс = Ложь) Экспорт
	
	Если пМасСтруктураДанных = Неопределено Тогда
		пМасСтруктураДанных	= Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыНачальныхДанныхПоВидуАукциона(пВидАукциона);
	КонецЕсли;
	
	Постфикс = ?(пБулИспользоватьПостфикс, Аук_ОбщийКлиентСервер.ПостфиксПовторяющихсяИменРеквизитов(), "");
	
	УдалитьКолонкиДанных(пФорма, пМасСтруктураДанных, пИмяТаблицы, Постфикс);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеДляОтборовНаФорме(пФорма) Экспорт

	тзКонтактныеЛицаУчастников = Справочники.Аук_Аукционы.КонтактныеЛицаСПривязкойКУчастникам(пФорма.Аукцион);
	
	пФорма.СписокКонтактныхЛиц	= Новый ФиксированныйМассив(тзКонтактныеЛицаУчастников.ВыгрузитьКолонку("КонтактноеЛицо"));
	
	тзКонтактныеЛицаУчастников.Свернуть("Участник");
	
	пФорма.СписокУчастников		= Новый ФиксированныйМассив(тзКонтактныеЛицаУчастников.ВыгрузитьКолонку("Участник"));
	
	СписокВыбораУчастников = пФорма.Элементы.Участник.СписокВыбора;
	
	СписокВыбораУчастников.Очистить();
	
	Для Каждого ЭлУчастник Из пФорма.СписокУчастников Цикл
		
		СписокВыбораУчастников.Добавить(ЭлУчастник);
		
	КонецЦикла;	
	
КонецПроцедуры	

Функция СписокРеквизитовФормы(пФорма, пПуть) Экспорт

	масРезультат = Новый Массив;
	
	масРеквизиты =  пФорма.ПолучитьРеквизиты(пПуть);	
	
	Для Каждого РеквизитТз Из масРеквизиты Цикл
		
		масРезультат.Добавить(РеквизитТз.Имя);
		
	КонецЦикла;
	
	Возврат масРезультат;
	
КонецФункции	

Процедура ОбработкаПередЗаписьюФормЭлементовПВХ(пТекущийОбъект, пПараметрыЗаписи, пОтказ) Экспорт
	
	ИмяОбъектаМд = Метаданные.НайтиПоТипу(ТипЗнч(пТекущийОбъект)).Имя;
	
	Если пПараметрыЗаписи.НаименованиеБылоИзменено Тогда
		
		сткРез = Аук_ОбщийВызовСервера.НаименованиеСоответствуетТребованиям(пТекущийОбъект, ИмяОбъектаМд);
		
		Если Не сткРез.Успех Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(сткРез.Результат,,, "Объект.Наименование", пОтказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если пПараметрыЗаписи.ТехническоеНаименованиеБылоИзменено Тогда
		
		сткРез = Аук_ОбщийВызовСервера.ТехническоеНаименованиеСоответствуетТребованиям(пТекущийОбъект, ИмяОбъектаМд);
		
		Если Не сткРез.Успех Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(сткРез.Результат,,, "Объект.ТехническоеНаименование", пОтказ);
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры	

Функция СписокПодчиненныхПолейВводаЭлементаФормы(пЭлементФормы, пБулУчитыватьПодчиненных = Истина) Экспорт
	
	масПоля = Новый Массив;
	
	СписокПодчиненныхПолейВводаЭлементаФормыРекурсивно(пЭлементФормы, пБулУчитыватьПодчиненных, масПоля);
	
	Возврат масПоля;
	
КонецФункции	

#КонецОбласти

#Область ОбработкаДанныхАукционов

Функция ТекущиеДанныеУчастниковАукционаСоздатьТаблицу(пМасОбязательныеКолонки, пМасСоставДанных) Экспорт
	
	тзРезультат = Новый ТаблицаЗначений;
	
	Для Каждого ЭлМас Из пМасОбязательныеКолонки Цикл
		
		тзРезультат.Колонки.Добавить(ЭлМас.ИмяКолонки, ЭлМас.ОписаниеТипов);
		
	КонецЦикла;
	
	Для Каждого ЭлСтк Из пМасСоставДанных Цикл
		
		тзРезультат.Колонки.Добавить(ЭлСтк.ТехническоеНаименование, ЭлСтк.ТипЗначения);
		
	КонецЦикла;
	
	Возврат тзРезультат;
	
КонецФункции	

Процедура ЗаполнитьДанныеУчастниковПоРезультатуЗапроса(пТзРезультат, пРезультатЗапроса, пКоличествоГруппировок = 2) Экспорт
	
	дзРезультат = пРезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для Каждого СтрокаДз Из дзРезультат.Строки Цикл
		
		ЗаполнитьДанныеУчастниковПоРезультатуЗапросаРекурсивно(пТзРезультат, СтрокаДз, пКоличествоГруппировок);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоставОбязательныхКолонок(пИменаКолонок) Экспорт
	
	масИменаКолонок = СтрРазделить(пИменаКолонок, ",");
	
	масРезультат = Новый Массив;
	
	Для Каждого ЭлИмяКолонки Из масИменаКолонок Цикл
		
		масРезультат.Добавить(ОписаниеКолонкиТаблицыПоИмени(ЭлИмяКолонки));
		
	КонецЦикла;	
	
	Возврат масРезультат;
	
КонецФункции	

#КонецОбласти

#Область КонтрольДанныхАукционаДляАвтоматическихРежимов

// Выполнить контроль данных аукциона
//
// Параметры:
//  пДанныеАукциона	 - 	Структура 
//		*ДатаНачала - Дата
//		*Участники - Массив
//		*Этапы - Массив структур
//			**НомерСтроки - Число
//			**Этап - Строка
//			**ДлительностьДни - Число
//			**ДлительностьЧасы - Дата
//		*ВидАукциона - СправочникСсылка.Аук_ВидыАукционов
//		*Сумма - Число
//		*Срок - Число
// Возвращаемое значение:
//   Структура
//		*Успех - Булево - Проверка успешна пройдена
//		*Результат - Булево - Описание ошибки в случае неуспешной проверки
//
Функция ВыполнитьКонтрольДанныхАукциона(пДанныеАукциона) Экспорт
	
	// 1.
	//		Дата начала (заполненность)
	//		Наличие участников
	сткРез = ВыполнитьКонтрольДанныхАукциона_СтартовыеДанные(пДанныеАукциона);
	
	Если Не сткРез.Успех Тогда
		Возврат сткРез; 	
	КонецЕсли;	
		
	// 2.
	//		Этапы: количество, длительность, наименование
	сткРез = ВыполнитьКонтрольДанныхАукциона_ПереходПоЭтапам(пДанныеАукциона);
	
	Если Не сткРез.Успех Тогда
		Возврат сткРез; 	
	КонецЕсли;
	
	// 3.
	//		Наличие формулы расчета и шаблона результат (из Вида аукциона)
	сткРез = ВыполнитьКонтрольДанныхАукциона_ПромежуточныйИтог(пДанныеАукциона);
	
	Если Не сткРез.Успех Тогда
		Возврат сткРез; 	
	КонецЕсли;
	
	// 4.
	//		Наличие формулы расчета и заполненные основная/дополнительная распределяемые суммы (из Вида аукциона) + Начальные данные	
	сткРез = ВыполнитьКонтрольДанныхАукциона_ОкончательныйИтог(пДанныеАукциона);
	
	Если Не сткРез.Успех Тогда
		Возврат сткРез; 	
	КонецЕсли;
	
	// 5. Наличие шаблонов оповещения
	сткРез = ВыполнитьКонтрольДанныхАукциона_ШаблоныОповещений(пДанныеАукциона);
	
	Если Не сткРез.Успех Тогда
		Возврат сткРез; 	
	КонецЕсли;
	
	// 6. Заполненность исходных данных
	//		Сумма, Срок
	сткРез = ВыполнитьКонтрольДанныхАукциона_ИсходныеДанные(пДанныеАукциона);
	
	Если Не сткРез.Успех Тогда
		Возврат сткРез; 	
	КонецЕсли;
			
	Возврат ЭдоОбщий._Успех();
	
КонецФункции	

#КонецОбласти

#Область Рассылка

Функция АккаунтРассылкиКотировки() Экспорт
	Возврат "Аккаунт";
КонецФункции

#КонецОбласти

#Область Рассылка

Функция ЗначенияПараметровШаблонов(пМасПараметрыШаблона, Знач пСткДанные) Экспорт

	сооРезультат = Новый Соответствие;

	Для Каждого ПараметрИмя Из пМасПараметрыШаблона Цикл
		Если ПараметрИмя = "АУКЦИОН_НАИМЕНОВАНИЕ" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_НАИМЕНОВАНИЕ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "АУКЦИОН_КОД" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_КОД(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "АУКЦИОН_ЭТАПЫ" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_ЭТАПЫ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "АУКЦИОН_ПЛАНИРУЕМОЕ_ВРЕМЯ_ЗАВЕРШЕНИЯ" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_ПЛАНИРУЕМОЕ_ВРЕМЯ_ЗАВЕРШЕНИЯ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "АУКЦИОН_СТРУКТУРА_СТАВКИ" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_СТРУКТУРА_СТАВКИ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "АУКЦИОН_ТЕКУЩИЙ_ЭТАП" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_ТЕКУЩИЙ_ЭТАП(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "АУКЦИОН_ВСЕГО_ЭТАПОВ" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_ВСЕГО_ЭТАПОВ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "СТАВКА_ВРЕМЯ_ПРИНЯТИЯ" Тогда
			
			ДобавитьЗначениеПараметра_СТАВКА_ВРЕМЯ_ПРИНЯТИЯ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "СТАВКА_ДАННЫЕ_СТАВКИ" Тогда
			
			ДобавитьЗначениеПараметра_СТАВКА_ДАННЫЕ_СТАВКИ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "АУКЦИОН_ДАННЫЕ_ПРОМЕЖУТОЧНОГО_ПОБЕДИТЕЛЯ" Тогда
			
			ДобавитьЗначениеПараметра_АУКЦИОН_ДАННЫЕ_ПРОМЕЖУТОЧНОГО_ПОБЕДИТЕЛЯ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "ОТКАЗ_ВРЕМЯ_ПРИНЯТИЯ" Тогда
			
			ДобавитьЗначениеПараметра_ОТКАЗ_ВРЕМЯ_ПРИНЯТИЯ(сооРезультат, ПараметрИмя, пСткДанные);
			
		ИначеЕсли ПараметрИмя = "СТАВКА_ДАННЫЕ_ПОБЕДНОЙ_СТАВКИ" Тогда
			
			ДобавитьЗначениеПараметра_СТАВКА_ДАННЫЕ_ПОБЕДНОЙ_СТАВКИ(сооРезультат, ПараметрИмя, пСткДанные);	
				
		КонецЕсли;
	КонецЦикла;	
	
	Возврат сооРезультат;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обязательно должен быть заполнен Участник и Строка данных
//
Процедура ДобавитьЗначениеПараметра_СТАВКА_ДАННЫЕ_ПОБЕДНОЙ_СТАВКИ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		масДанныеПобеднойСтавки = РегистрыСведений.Аук_ДанныеТаблицПодведенияИтогов.ДанныеСтавкиПобедителя(пСткДанные);
		
		масДанныеСтавкиСтрокой = Новый Массив;
		Для Сч = 0 По масДанныеПобеднойСтавки.ВГраница() Цикл
			Если ЗначениеЗаполнено(масДанныеПобеднойСтавки[Сч].ЗначениеЭлементаДанных) Тогда
				стрЗначение = Формат(масДанныеПобеднойСтавки[Сч].ЗначениеЭлементаДанных, "ЧРГ=.; ЧГ=3,0");	
			Иначе
				стрЗначение = НСтр("ru = 'Значение не указано'");	
			КонецЕсли;	 
			
			масДанныеСтавкиСтрокой.Добавить(СтрШаблон(НСтр("ru = '    %1. ""%2"" = %3.'"), Сч, масДанныеПобеднойСтавки[Сч].ЭлементДанныхПредставление, стрЗначение));
			
		КонецЦикла;
		
		пСооРезультат.Вставить(пСтрПараметрИмя, СтрСоединить(масДанныеСтавкиСтрокой, Символы.ПС));
	КонецЕсли;
КонецПроцедуры

// Обязательно должен быть заполнен Период
//
Процедура ДобавитьЗначениеПараметра_ОТКАЗ_ВРЕМЯ_ПРИНЯТИЯ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		пСооРезультат.Вставить(пСтрПараметрИмя, СтрШаблон(НСтр("ru = '%1 (GMT+10, Владивосток)'"), Формат(пСткДанные.Период, "ДФ=dd.MM.yyyy HH:mm; ДЛФ=DT")));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_АУКЦИОН_ДАННЫЕ_ПРОМЕЖУТОЧНОГО_ПОБЕДИТЕЛЯ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		Если Не пСткДанные.Свойство("ТекущееСостояниеАукциона") Тогда
			пСткДанные.Вставить("ТекущееСостояниеАукциона", РегистрыСведений.Аук_ИсторияСтатусовАукционов.ТекущееСостояниеАукциона(пСткДанные.Аукцион));			
		КонецЕсли;
		
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("Аукцион",	пСткДанные.Аукцион);
		сткПараметры.Вставить("НомерЭтапа",	пСткДанные.ТекущееСостояниеАукциона.НомерЭтапа);
		
		пСооРезультат.Вставить(пСтрПараметрИмя, РегистрыСведений.Аук_ИтогиЗавершенияЭтапов.ИтоговоеЗначениеЛидирующейСтавки(сткПараметры));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_СТАВКА_ДАННЫЕ_СТАВКИ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		масДанныеСтавки = РегистрыСведений.Аук_ИсторияДанныхАукционов.ДанныеСтавкиУчастника(пСткДанные);	
		
		чслКоличествоЗаписейСтавки = масДанныеСтавки.Количество(); 
		
		масДанныеСтавкиСтрокой = Новый Массив;
		Для Сч = 0 По чслКоличествоЗаписейСтавки - 1 Цикл
			масСтрокаСтавки = Новый Массив;
			
			Если чслКоличествоЗаписейСтавки > 1 Тогда 
				масСтрокаСтавки.Добавить(СтрШаблон(НСтр("ru = 'Запись №%1'"), Сч + 1));
			КонецЕсли;
			
			сооСтрокаСтавки = масДанныеСтавки[Сч];
			
			НПП = 1;
			Для Каждого ЭлСоо Из сооСтрокаСтавки Цикл
				
				Если ЗначениеЗаполнено(ЭлСоо.Значение) Тогда
					стрЗначение = Формат(ЭлСоо.Значение, "ЧРГ=.; ЧГ=3,0");	
				Иначе
					стрЗначение = НСтр("ru = 'Значение не указано'");	
				КонецЕсли;	 
				
				масСтрокаСтавки.Добавить(СтрШаблон(НСтр("ru = '    %1. ""%2"" = %3.'"), НПП, ЭлСоо.Ключ, стрЗначение));
				
				НПП = НПП + 1;
			КонецЦикла;
			
			масДанныеСтавкиСтрокой.Добавить(СтрСоединить(масСтрокаСтавки, Символы.ПС));
		КонецЦикла;
		
		пСооРезультат.Вставить(пСтрПараметрИмя, СтрСоединить(масДанныеСтавкиСтрокой, Символы.ПС + Символы.ПС));
	КонецЕсли;
КонецПроцедуры

// Обязательно должен быть заполнен период (дата записи)
//
Процедура ДобавитьЗначениеПараметра_СТАВКА_ВРЕМЯ_ПРИНЯТИЯ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		пСооРезультат.Вставить(пСтрПараметрИмя, СтрШаблон(НСтр("ru = '%1 (GMT+10, Владивосток)'"), Формат(пСткДанные.Период, "ДФ=dd.MM.yyyy HH:mm; ДЛФ=DT")));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_АУКЦИОН_СТРУКТУРА_СТАВКИ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		Если Не пСткДанные.Свойство("СтруктураСтавки") Тогда
			Если Не пСткДанные.Свойство("КлючевыеДанные") Тогда
				пСткДанные.Вставить("КлючевыеДанные", Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пСткДанные.Аукцион));			
			КонецЕсли; 
			
			пСткДанные.Вставить("СтруктураСтавки", Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пСткДанные.КлючевыеДанные.ВидАукциона));			
		КонецЕсли;
		
		// Структура ставки состоит из двух блоков: обязательные поля и необязательные поля
		сооДетальнаяСтруктура = Новый Соответствие;
			
		сооДетальнаяСтруктура.Вставить("АУКЦИОН_СТРУКТУРА_СТАВКИ_ОБЯЗАТЕЛЬНЫЕ_ПОЛЯ", ОписаниеЭлементовСтавки(пСткДанные.СтруктураСтавки, Истина));
		
		сооДетальнаяСтруктура.Вставить("АУКЦИОН_СТРУКТУРА_СТАВКИ_НЕ_ОБЯЗАТЕЛЬНЫЕ_ПОЛЯ", ОписаниеЭлементовСтавки(пСткДанные.СтруктураСтавки, Ложь));
			
		пСооРезультат.Вставить(пСтрПараметрИмя, сооДетальнаяСтруктура);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_АУКЦИОН_ПЛАНИРУЕМОЕ_ВРЕМЯ_ЗАВЕРШЕНИЯ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		ПланируемоеВремяЗавершения = Справочники.Аук_Аукционы.ПланируемоеВремяЗавершения(пСткДанные.Аукцион);
		
		пСооРезультат.Вставить(пСтрПараметрИмя, СтрШаблон(НСтр("ru = '%1 (GMT+10, Владивосток)'"), Формат(ПланируемоеВремяЗавершения, "ДФ=dd.MM.yyyy HH:mm; ДЛФ=DT")));
	КонецЕсли;
КонецПроцедуры

// [АУКЦИОН_ЭТАПЫ]
// Состоит из трёх переменных:
//	[АУКЦИОН_ТЕКУЩИЙ_ЭТАП] + [АУКЦИОН_ВСЕГО_ЭТАПОВ] + [ПРОДОЛЖИТЕЛЬНОСТЬ_ЭТАПА]
// Детально:
//	[АУКЦИОН_ТЕКУЩИЙ_ЭТАП] - данные РС Аук_ИсторияСтатусовАукционов
//	[АУКЦИОН_ВСЕГО_ЭТАПОВ] и [ПРОДОЛЖИТЕЛЬНОСТЬ_ЭТАПА] - данные ТЧ Этапы справочника Аук_Аукционы
// Примеры:
//	Этап 1 из 3. Предполагаемая продолжительность: 25 мин.
//	Этап 2 из 3. Предполагаемая продолжительность: 1 час 05 мин.
//	Этап 3 из 3. Предполагаемая продолжительность: 1 день 2 часа 30 мин.
//
Процедура ДобавитьЗначениеПараметра_АУКЦИОН_ЭТАПЫ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ДобавитьЗначениеПараметра_АУКЦИОН_ВСЕГО_ЭТАПОВ(пСооРезультат, "АУКЦИОН_ВСЕГО_ЭТАПОВ", пСткДанные);	
	
	ДобавитьЗначениеПараметра_АУКЦИОН_ТЕКУЩИЙ_ЭТАП(пСооРезультат, "АУКЦИОН_ТЕКУЩИЙ_ЭТАП", пСткДанные);	
	
	////////////
	масПродолжительностьЭтапов = Новый Массив;
	чслВсегоЭтапов = пСткДанные.Этапы.Количество();
	НомерЭтапа = 1;
	Для Каждого ЭлЭтап Из пСткДанные.Этапы Цикл	
		масВремяСтрокой = Новый Массив;
		
		Если ЭлЭтап.ДлительностьДни > 0 Тогда
			масВремяСтрокой.Добавить(СтрокаСЧислом(";%1 день;;%1 дня;%1 дней;%1 дня", ЭлЭтап.ДлительностьДни,
													ВидЧисловогоЗначения.Количественное, "Л=ru"));
		КонецЕсли;
		
		Если Час(ЭлЭтап.ДлительностьЧасы) > 0 Тогда
			масВремяСтрокой.Добавить(СтрокаСЧислом(";%1 час;;%1 часа;%1 часов;%1 часа", Час(ЭлЭтап.ДлительностьЧасы),
													ВидЧисловогоЗначения.Количественное, "Л=ru"));	
		КонецЕсли;
		
		масВремяСтрокой.Добавить(СтрокаСЧислом(";%1 минута;;%1 минуты;%1 минут;%1 минуты", Минута(ЭлЭтап.ДлительностьЧасы - Час(ЭлЭтап.ДлительностьЧасы) * 3600),
													ВидЧисловогоЗначения.Количественное, "Л=ru; ЧЦ=2; ЧВН="));
																								
		стрПродолжительностьЭтапа = СтрШаблон(НСтр("ru = 'Этап %1 из %2. Предполагаемая продолжительность: %3.'"), НомерЭтапа, чслВсегоЭтапов, СтрСоединить(масВремяСтрокой, " "));											
		
		масПродолжительностьЭтапов.Добавить(стрПродолжительностьЭтапа);
		
		НомерЭтапа = НомерЭтапа + 1;											
	КонецЦикла;
	
	пСооРезультат.Вставить(пСтрПараметрИмя, СтрСоединить(масПродолжительностьЭтапов, Символы.ПС));
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_АУКЦИОН_ТЕКУЩИЙ_ЭТАП(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда 
		Если Не пСткДанные.Свойство("ТекущееСостояниеАукциона") Тогда
			пСткДанные.Вставить("ТекущееСостояниеАукциона", РегистрыСведений.Аук_ИсторияСтатусовАукционов.ТекущееСостояниеАукциона(пСткДанные.Аукцион));			
		КонецЕсли;
		
		пСооРезультат.Вставить(пСтрПараметрИмя, Формат(пСткДанные.ТекущееСостояниеАукциона.НомерЭтапа, "ЧГ=0"));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_АУКЦИОН_ВСЕГО_ЭТАПОВ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	ЗначениеПараметра = пСооРезультат.Получить(пСтрПараметрИмя);
	
	Если ЗначениеПараметра = Неопределено Тогда
		Если Не пСткДанные.Свойство("Этапы") Тогда
			пСткДанные.Вставить("Этапы", Справочники.Аук_Аукционы.ЭтапыАукциона(пСткДанные.Аукцион, Истина));			
		КонецЕсли;
		
		пСооРезультат.Вставить(пСтрПараметрИмя, Формат(пСткДанные.Этапы.Количество(), "ЧГ=0"));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_АУКЦИОН_КОД(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	Если Не пСткДанные.Свойство("КлючевыеДанные") Тогда
		пСткДанные.Вставить("КлючевыеДанные", Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пСткДанные.Аукцион));			
	КонецЕсли;
	
	пСооРезультат.Вставить(пСтрПараметрИмя, пСткДанные.КлючевыеДанные.Код);	
КонецПроцедуры

Процедура ДобавитьЗначениеПараметра_АУКЦИОН_НАИМЕНОВАНИЕ(пСооРезультат, пСтрПараметрИмя, пСткДанные)
	Если Не пСткДанные.Свойство("КлючевыеДанные") Тогда
		пСткДанные.Вставить("КлючевыеДанные", Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пСткДанные.Аукцион));			
	КонецЕсли;
	
	пСооРезультат.Вставить(пСтрПараметрИмя, пСткДанные.КлючевыеДанные.Наименование);	
КонецПроцедуры	

Функция ОписаниеЭлементовСтавки(пМасСтруктураСтавки, пБулОбязательныеПоля)
	масПоля = Новый Массив;
	
	масПоля.Добавить(СтрШаблон(НСтр("ru = '%1:'"), ?(пБулОбязательныеПоля, "Обязательные поля ставки", "Необязательные поля ставки")));
	
	НПП = 1;
	Для Каждого ЭлементСтавки Из пМасСтруктураСтавки Цикл
		Если пБулОбязательныеПоля И ЭлементСтавки.НеОбязательный Тогда
			Продолжить;	
		ИначеЕсли Не пБулОбязательныеПоля И Не ЭлементСтавки.НеОбязательный Тогда	
			Продолжить;	
		КонецЕсли;	
		
		//НПП = масПоля.Количество() + 1;
		
		ТипЗначенияЭлементаСтавки = ЭлементСтавки.ТипЗначения.Типы()[0];
		
		Если ОбщегоНазначения.ЭтоСсылка(ТипЗначенияЭлементаСтавки) Тогда
			масЗначения = Справочники.Аук_ДопЭлементыДанныхАукционов.ЗначенияЭлементовДанныхПоКоличеству(ЭлементСтавки.Элемент);
			
			стрОкончание = СтрШаблон(НСтр("ru = 'Значение из списка: %1'"), СтрСоединить(масЗначения, ", "));	
		Иначе	
			стрОкончание = ТипЗначенияЭлементаСтавки;	
		КонецЕсли;
		
		масПоля.Добавить(СтрШаблон(НСтр("ru = '%1. ""%2"". %3.'"), НПП, ЭлементСтавки.ЭлементПредставление, стрОкончание));
		
		НПП = НПП + 1;
	КонецЦикла;
	
	Возврат СтрСоединить(масПоля, Символы.ПС);
КонецФункции	

Процедура СписокПодчиненныхПолейВводаЭлементаФормыРекурсивно(пЭлементФормы, пБулУчитыватьПодчиненных, пМасПоля)
	
	Для Каждого ПодчиненныеЭл Из пЭлементФормы.ПодчиненныеЭлементы Цикл
		Если пБулУчитыватьПодчиненных И ТипЗнч(ПодчиненныеЭл) = Тип("ГруппаФормы") Тогда
			
			СписокПодчиненныхПолейВводаЭлементаФормыРекурсивно(ПодчиненныеЭл, пБулУчитыватьПодчиненных, пМасПоля); 
			
		ИначеЕсли ТипЗнч(ПодчиненныеЭл) = Тип("ПолеФормы") Тогда
			пМасПоля.Добавить(ПодчиненныеЭл.Имя);	
		КонецЕсли;			
	КонецЦикла;
	
КонецПроцедуры

Функция ВыполнитьКонтрольДанныхАукциона_ИсходныеДанные(пДанныеАукциона)
	Если Не ЗначениеЗаполнено(пДанныеАукциона.Сумма) Тогда
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Не заполнена сумма'"));	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(пДанныеАукциона.Срок) Тогда
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Не заполнен срок'"));	
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();		
КонецФункции

Функция ВыполнитьКонтрольДанныхАукциона_ШаблоныОповещений(пДанныеАукциона)
	сткРез = РегистрыСведений.Аук_ШаблоныСообщенийУчастникам.ОтсутствующиеДанныеОповещенийПоВидамСообщений();	
	
	Если сткРез.ОтсутствующиеШаблоны.Количество() <> 0 Тогда
		Возврат ЭдоОбщий._Ошибка(СтрШаблон(НСтр("ru = 'Отсутсвуют шаблоны для видов сообщений: %1'"), СтрСоединить(сткРез.ОтсутствующиеШаблоны, ", ")));	
	КонецЕсли;
	
	Если сткРез.ОтсутствующиеЗаголовки.Количество() <> 0 Тогда
		Возврат ЭдоОбщий._Ошибка(СтрШаблон(НСтр("ru = 'Отсутсвуют заголовки для видов сообщений: %1'"), СтрСоединить(сткРез.ОтсутствующиеЗаголовки, ", ")));	
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();		
КонецФункции

Функция ВыполнитьКонтрольДанныхАукциона_СтартовыеДанные(пДанныеАукциона)
	// Начало аукциона
	Если Не ЗначениеЗаполнено(пДанныеАукциона.ДатаНачала) Тогда
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Не заполнена дата начала аукциона'"));	
	КонецЕсли;
		
	// Участники
	Если пДанныеАукциона.Участники.Количество() = 0 Тогда
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Не заполнены участники'"));	
	КонецЕсли;	
		
	Возврат ЭдоОбщий._Успех();		
КонецФункции

Функция ВыполнитьКонтрольДанныхАукциона_ПереходПоЭтапам(пДанныеАукциона)
	// Этапы: количество, длительность, наименование
	Если пДанныеАукциона.Этапы.Количество() = 0 Тогда
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Не добавлен ни один этап аукциона'")); 	
	Иначе
		масОшибки = Новый Массив;
		Для Каждого СтрокаЭтап Из пДанныеАукциона.Этапы Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаЭтап.Этап) Тогда	
				масОшибки.Добавить(СтрШаблон(НСтр("ru = 'Не заполнено имя этапа аукциона (номер строки: %1)'"), СтрокаЭтап.НомерСтроки));	
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(масОшибки) Тогда
			Возврат ЭдоОбщий._Ошибка(СтрСоединить(масОшибки, Символы.ПС));		
		КонецЕсли;	
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();
КонецФункции

Функция ВыполнитьКонтрольДанныхАукциона_ПромежуточныйИтог(пДанныеАукциона)
	Если пДанныеАукциона.АвтоматическийРежим Тогда
		// Наличие формулы расчета и шаблона результат
		сткНастройкиПромежИтога = Справочники.Аук_ВидыАукционов.ОписаниеРасчетаПромежуточныхИтоговПоВидуАукциона(пДанныеАукциона.ВидАукциона);
		
		Если Не ЗначениеЗаполнено(сткНастройкиПромежИтога.ПромежуточныйИтогФормула) Тогда
			Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Отсутсвует формула расчета промежуточного итога'"));	
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткНастройкиПромежИтога.ПромежуточныйИтогШаблонРезультата) Тогда
			Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Отсутсвует шаблон промежуточного результата'"));	
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();	
КонецФункции

Функция ВыполнитьКонтрольДанныхАукциона_ОкончательныйИтог(пДанныеАукциона)
	Если пДанныеАукциона.АвтоматическийРежим Тогда
		// Наличие формулы расчета и шаблона результат
		сткНастройкиПромежИтога = Справочники.Аук_ВидыАукционов.ОписаниеРасчетаОкончательныхИтоговПоВидуАукциона(пДанныеАукциона.ВидАукциона);
		
		Если Не ЗначениеЗаполнено(сткНастройкиПромежИтога.Формула) Тогда
			Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Отсутсвует формула расчета окончательного итога'"));	
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткНастройкиПромежИтога.РаспределяемаяСуммаОсновная) Тогда
			
			Если Не ЗначениеЗаполнено(сткНастройкиПромежИтога.РаспределяемаяСуммаДополнительная) Тогда	
				Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Не заполнены основная/дополнительная распределяемые суммы'"));	
			Иначе
				
				Если пДанныеАукциона.НачальныеДанные.Количество() = 0 Тогда
					Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Основная распределяемая сумма незаполнена, заполнена дополнительная, а значит необходимо заполнить начальные данные'"));	
				КонецЕсли;	
				
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();	
КонецФункции

Функция РасчитатьПромежуточныйИтогПоФормуле(пФормула, пМасСтруктураСтавки, пСтрокаТзИсточник)
	
	чслРезультат = 0;
	
	Если ЗначениеЗаполнено(пФормула) Тогда
		
		ПромежуточныйИтогВыражение = пФормула;
		
		Для Каждого сткЭлемент Из пМасСтруктураСтавки Цикл
			
			Если СтрНайти(ПромежуточныйИтогВыражение, "[" + сткЭлемент.Наименование + "]") <> 0 Тогда
				
				ПромежуточныйИтогВыражение = СтрЗаменить(ПромежуточныйИтогВыражение, "[" + сткЭлемент.Наименование + "]", ЧисловоеЗначениеСтрокой(пСтрокаТзИсточник, сткЭлемент));
				
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			чслРезультат = Вычислить(ПромежуточныйИтогВыражение);
		Исключение
			стрОшибка = ОписаниеОшибки();
			
			ВызватьИсключение СтрШаблон("Формула: %1. Ошибка: %2", ПромежуточныйИтогВыражение, стрОшибка);
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат чслРезультат;
	
КонецФункции

// Нужно заменить в числах "," на ".", а то при вычислении будет ошибка
//
Функция ЧисловоеЗначениеСтрокой(пСтрокаТзИсточник, пСткЭлемент)
	
	Возврат Формат(пСтрокаТзИсточник[пСткЭлемент.ТехническоеНаименование], "ЧРД=.; ЧН=0; ЧГ=0");
	
КонецФункции	

Функция РасчитатьИтоговоеЗначениеПоШаблону(пШаблон, пМасСтруктураСтавки, пСтрокаТзИсточник)
	
	Если ЗначениеЗаполнено(пШаблон) Тогда
		
		ШаблонРезультатаВыражение = пШаблон;
		
		Для Каждого сткЭлемент Из пМасСтруктураСтавки Цикл
			
			Если СтрНайти(ШаблонРезультатаВыражение, "[" + сткЭлемент.Наименование + "]") <> 0 Тогда
				
				ШаблонРезультатаВыражение = СтрЗаменить(ШаблонРезультатаВыражение, "[" + сткЭлемент.Наименование + "]", XMLСтрока(пСтрокаТзИсточник[сткЭлемент.ТехническоеНаименование]));
				
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат ШаблонРезультатаВыражение;
		
	Иначе
		
		Возврат "";
		
	КонецЕсли;	
	
КонецФункции

Функция ОписаниеКолонкиТаблицыПоИмени(пИмяКолонки)

	Если пИмяКолонки = "Участник" Тогда
		
		Возврат Новый Структура("ИмяКолонки, ОписаниеТипов", пИмяКолонки, Новый ОписаниеТипов("СправочникСсылка.Аук_УчастникиАукционов"));
		
	ИначеЕсли пИмяКолонки = "Период" Тогда
		
		Возврат Новый Структура("ИмяКолонки, ОписаниеТипов", пИмяКолонки, Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
		
	ИначеЕсли пИмяКолонки = "ДатаСтавки" Тогда
		
		Возврат Новый Структура("ИмяКолонки, ОписаниеТипов", пИмяКолонки, Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));	
		
	ИначеЕсли пИмяКолонки = "КонтактноеЛицо" Тогда
		
		Возврат Новый Структура("ИмяКолонки, ОписаниеТипов", пИмяКолонки, Новый ОписаниеТипов("СправочникСсылка.Аук_КонтактныеЛицаУчастниковАукционов"));	
		
	ИначеЕсли пИмяКолонки = "СтрокаДанных" Тогда
		
		Возврат Новый Структура("ИмяКолонки, ОписаниеТипов", пИмяКолонки, Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(3)));
		
	ИначеЕсли пИмяКолонки = "Этап" Тогда
		
		Возврат Новый Структура("ИмяКолонки, ОписаниеТипов", пИмяКолонки, Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(64)));
		
	ИначеЕсли пИмяКолонки = "ЭтоЛидирующаяСтавка" Тогда
		
		Возврат Новый Структура("ИмяКолонки, ОписаниеТипов", пИмяКолонки, Новый ОписаниеТипов("Булево"));	
		
	КонецЕсли;	
	
КонецФункции	

Процедура ЗаполнитьДанныеУчастниковПоРезультатуЗапросаРекурсивно(пТзРезультат, пСтрокаДз, пКоличествоГруппировок, пНоваяСтрока = Неопределено)
	
	Если пСтрокаДз.Уровень() = (пКоличествоГруппировок - 1) Тогда
		
		Для Каждого СтрокаДз Из пСтрокаДз.Строки Цикл
			
			пНоваяСтрока[СтрокаДз.ТехническоеНаименование] = СтрокаДз.ЗначениеЭлементаДанных;
				
			ЗаполнитьЗначенияСвойств(пНоваяСтрока, СтрокаДз);
			
		КонецЦикла;
		
	ИначеЕсли пСтрокаДз.Уровень() = (пКоличествоГруппировок - 2) Тогда 
		
		//НоваяСтрока = пТзРезультат.Добавить();
		//
		//ЗаполнитьЗначенияСвойств(НоваяСтрока, пСтрокаДз);
		
		Для Каждого СтрокаДз Из пСтрокаДз.Строки Цикл
			
			НоваяСтрока = пТзРезультат.Добавить();
		
			ЗаполнитьЗначенияСвойств(НоваяСтрока, пСтрокаДз);
			
			ЗаполнитьДанныеУчастниковПоРезультатуЗапросаРекурсивно(пТзРезультат, СтрокаДз, пКоличествоГруппировок, НоваяСтрока);
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрокаДз Из пСтрокаДз.Строки Цикл
			
			ЗаполнитьДанныеУчастниковПоРезультатуЗапросаРекурсивно(пТзРезультат, СтрокаДз, пКоличествоГруппировок);
			
		КонецЦикла;
		
	КонецЕсли;	
		
КонецПроцедуры

Процедура УдалитьКолонкиДанных(пФорма, пМасСтруктураДанных, пИмяТаблицы, пПостфикс = "")
		
	// 1. Удаление элементов формы
	Для Каждого ЭлСтк Из пМасСтруктураДанных Цикл
		
		НайденныйЭлемент = пФорма.Элементы.Найти(пИмяТаблицы + "Характеристика" + ЭлСтк.ТехническоеНаименование + пПостфикс);
		
		Если Не НайденныйЭлемент = Неопределено Тогда
			пФорма.Элементы.Удалить(НайденныйЭлемент);
		КонецЕсли;
		
		НайденныйЭлемент = пФорма.Элементы.Найти(пИмяТаблицы + ЭлСтк.ТехническоеНаименование + пПостфикс);
		
		Если Не НайденныйЭлемент = Неопределено Тогда
			пФорма.Элементы.Удалить(НайденныйЭлемент);
		КонецЕсли;
		
	КонецЦикла;
	
	// 2. Удаление реквизитов формы
	УдаляемыеРеквизиты = Новый Массив;
	
	Для Каждого ЭлСтк Из пМасСтруктураДанных Цикл		
		УдаляемыеРеквизиты.Добавить(пИмяТаблицы + "." + "Характеристика" + ЭлСтк.ТехническоеНаименование + пПостфикс);
		
		УдаляемыеРеквизиты.Добавить(пИмяТаблицы + "." +  ЭлСтк.ТехническоеНаименование + пПостфикс);
	КонецЦикла;
	
	пФорма.ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	
КонецПроцедуры

Процедура ДобавитьКолонкиДанных(пФорма, пМасСтруктураДанных, пИмяТаблицы, пРодитель, пПостфикс = "", пБулПомечатьОбязательные = Истина)
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	масОбязательные = Новый Массив;
	
	Для Каждого ЭлСтк Из пМасСтруктураДанных Цикл
		ТипЭлементаСтавки = ТипЗнч(ЭлСтк.Элемент);
		
		// 1. Колонка, отвечающая за вид характеристики (ПВХ)
		мас = Новый Массив;
		мас.Добавить(ТипЭлементаСтавки);
		
		НовыйРеквизит = Новый РеквизитФормы("Характеристика" + ЭлСтк.ТехническоеНаименование + пПостфикс, Новый ОписаниеТипов(мас));
		НовыйРеквизит.Путь			= пИмяТаблицы;
		
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
			
		// 2. Колонка, отвечающая за значение характеристики (Характеристику)
		НовыйРеквизит = Новый РеквизитФормы(ЭлСтк.ТехническоеНаименование + пПостфикс, ЭлСтк.ТипЗначения);
		НовыйРеквизит.Путь			= пИмяТаблицы;
		НовыйРеквизит.Заголовок		= ЭлСтк.ЭлементПредставление;
		
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
		
		Если пБулПомечатьОбязательные И ЭлСтк.Свойство("НеОбязательный") И Не ЭлСтк.НеОбязательный Тогда 
			масОбязательные.Добавить(НовыйРеквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	пФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// Можно указать родителя явно либо указать таблицу формы в качестве родителя
	Если пРодитель = Неопределено Тогда
		пРодитель = пФорма.Элементы[пИмяТаблицы];	
	КонецЕсли;	
	
	Для Каждого ЭлРеквизит Из ДобавляемыеРеквизиты Цикл
		// Создание элемента формы
		НовыйЭлемент = пФорма.Элементы.Добавить(пИмяТаблицы + ЭлРеквизит.Имя, Тип("ПолеФормы"), пРодитель);
		НовыйЭлемент.ПутьКДанным	= пИмяТаблицы + "." + ЭлРеквизит.Имя;
		НовыйЭлемент.Вид			= ВидПоляФормы.ПолеВвода;
		
		Если СтрНачинаетсяС(ЭлРеквизит.Имя, "Характеристика") Тогда
			НовыйЭлемент.Видимость	= Ложь;	
		Иначе	
			// 1. Создать связи параметров выбора
			Реквизит = СтрШаблон("Элементы.%1.ТекущиеДанные.%2", пИмяТаблицы, "Характеристика" + ЭлРеквизит.Имя);
				
			масСвязиПараметровВыбора = Новый Массив;
			масСвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", Реквизит, РежимИзмененияСвязанногоЗначения.Очищать));
			
			НовыйЭлемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(масСвязиПараметровВыбора);
			
			// 2. Создать связь по типу
			НовыйЭлемент.СвязьПоТипу = Новый СвязьПоТипу(Реквизит);
			
			Если масОбязательные.Найти(ЭлРеквизит.Имя) <> Неопределено Тогда
				НовыйЭлемент.АвтоОтметкаНезаполненного = Истина;	
			КонецЕсли;	
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьКопиюОбъектаМетаданных(пМетаданныеОбъекта, пОбъектИсточник)
	
	обНовыйЭлемент = Справочники[пМетаданныеОбъекта.Имя].СоздатьЭлемент();
	
	СкопироватьЗначенияРеквизитов(пМетаданныеОбъекта.Реквизиты, обНовыйЭлемент, пОбъектИсточник);
	
	СкопироватьЗначенияРеквизитовТабличныхЧастей(пМетаданныеОбъекта.ТабличныеЧасти, обНовыйЭлемент, пОбъектИсточник);
	
	обНовыйЭлемент.ДополнительныеСвойства.Вставить("ЭтоСнимок");
	
	Возврат обНовыйЭлемент;
	
КонецФункции

Процедура СкопироватьЗначенияРеквизитовТабличныхЧастей(пТабличныеЧасти, пОбъектПриемник, пОбъектИсточник)

	Для Каждого ЭлТабЧасть Из пТабличныеЧасти Цикл
		
		Для Каждого СтрокаТч Из пОбъектИсточник[ЭлТабЧасть.Имя] Цикл 
			
			СкопироватьЗначенияРеквизитов(ЭлТабЧасть.Реквизиты, пОбъектПриемник[ЭлТабЧасть.Имя].Добавить(), СтрокаТч);
		
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьЗначенияРеквизитов(пРеквизиты, пОбъектПриемник, пОбъектИсточник)

	Для Каждого ЭлРеквизит Из пРеквизиты Цикл
		
		пОбъектПриемник[ЭлРеквизит.Имя] = пОбъектИсточник[ЭлРеквизит.Имя];
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

