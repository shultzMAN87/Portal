
// Функция - Инициализация структуры для работы логирования
//
// Параметры:
//  пИсточникВывода	- СправочникСсылка.ЭдоИсточникиОпераций - Источник появления логов.
//  
//  Предполагается, что в качестве источников указываются источники операций ЭДО.
//  НО! Это не обязательно. В принципе источником может быть и любая строка текста.
//  Источник передается в доп. данных при логировании в грейлог и, главное, участвует в формировании пути для вывода логов в файлы.
// 
// Возвращаемое значение:
//   - Структура - Заполненная структура настроек для работы логирования.
//
Функция Инициализация(пИсточникВывода) Экспорт 
	сткРез = Новый Структура;

	сткРез.Вставить("ПоНулевомуGMT",  Ложь); // По умолчанию даты и время в текущем часовом поясе
	
	сткРез.Вставить("ИсточникВывода", Строка(пИсточникВывода));
	
	сткРез.Вставить("ВыводВСообщения", Ложь);		// Клиент. Сервер. Вывод в стандартные сообщения 1С.
	сткРез.Вставить("ВыводНаФорму",    Ложь);		// Клиент. 		   Вывод в поле текстового документа на форме.
	сткРез.Вставить("ВыводВФайл", 	   Ложь);		// Клиент. Сервер. Вывод в текстовый файл.
	сткРез.Вставить("ВыводВГрейЛог",   Ложь);		// Клиент. Сервер. Вывод в грейлог. !!! Вызов сервера с клиента.
	
	сткРез.Вставить("ПрефиксОшибка",   "!!!! ");	// Префикс для сообщений об ошибках
	сткРез.Вставить("ПрефиксОтладка",  "#ОТЛ# ");	// Префикс для сообщений отладки
	
	Возврат сткРез;
КонецФункции

// Функция - Структура настроек вывода логов в грейлог
// 
// Возвращаемое значение:
//   - Структура - Структура для настройки вывода логов в грейлог.
//		* Процесс   - Строка    - Имя процесса, по которому будет выполнен вывод данных в грейлог.
//		* ДопДанные - Структура - Пустая структура для добавления в нее доп. данных для передачи в грейлог вместе с сообщением.
//								  !!! Важно !!! Все ключи (имена полей) в данной структуре должны быть заданы в латинице !!!
//								  Дополнительно:
//								  Даже, если структура ДопДанные не будет заполнена, в грейлог все равно вместе с сообщением будут переданы
//								  некоторые доп. данные. Например, источник, указанный при инициализации источник логов, будет передан в поле "CreatedFrom".
//
Функция СтруктураНастроекВыводаВГрейЛог() Экспорт 
	сткРез = СтруктураНастроекВыводаБазовая();
	
	сткРез.Вставить("Процесс",   "");
	сткРез.Вставить("ДопДанные", Новый Структура);

	Возврат сткРез;
КонецФункции

// Процедура - Включает вывод логов в грейлог
//
// Если при включении еще не выполнена настройка вывода, то выполняе ее.
//
// Параметры:
//  пСткНастройкиЛога   - Структура - Структура настроек логирования.
//  пСткНастройкиВывода	- Структура - Заполненная структура настроек вывода в грейлог.
//
Процедура ВключитьВыводВГрейЛог(пСткНастройкиЛога, пСткНастройкиВывода = Неопределено) Экспорт 
	пСткНастройкиЛога.ВыводВГрейЛог = Истина;
	
	Если ЗначениеЗаполнено(пСткНастройкиВывода) Тогда
		НастроитьВыводВГрейЛог(пСткНастройкиЛога, пСткНастройкиВывода);
	КонецЕсли;	
КонецПроцедуры

// Процедура - Вывод сообщения в лог
//
// Сообщения выводятся во все места, заданные при настройке логирования.
//
// Параметры:
//  пСткНастройкиЛога - Структура - Структура настроек логирования.
//  пСтрСообщение	  - Строка    - Текст выводимого сообщения.
//
Процедура Лог(пСткНастройкиЛога, пСтрСообщение) Экспорт 
	СтрВремя = Формат(ТекущаяДатаДляЛога(пСткНастройкиЛога), "ДФ='dd.MM.yyyy HH:mm:ss'");
	
	СтрПред  = СтрЗаменить(пСтрСообщение, Символы.ПС, Символы.ПС + СтрВремя + " - ");
	СтрТекст = СтрШаблон("%1 - %2", СтрВремя, СтрПред);
	
	ВывестиЛог(пСткНастройкиЛога, СтрТекст);
КонецПроцедуры

Процедура ВывестиЛог(пСткНастройкиЛога, пСтрТекст)
	Если пСткНастройкиЛога.ВыводВСообщения = Истина Тогда
		//Лог_ВСообщения(пСткНастройкиЛога, пСтрТекст);
	КонецЕсли;
		
	Если пСткНастройкиЛога.ВыводНаФорму = Истина Тогда
		//Лог_НаФорму(пСткНастройкиЛога, пСтрТекст);
	КонецЕсли;
	
	Если пСткНастройкиЛога.ВыводВФайл = Истина Тогда
		//Лог_ВФайл(пСткНастройкиЛога, пСтрТекст);
	КонецЕсли;
	
	Если пСткНастройкиЛога.ВыводВГрейЛог = Истина Тогда
		Лог_ВГрейлог(пСткНастройкиЛога, пСтрТекст);
	КонецЕсли;
КонецПроцедуры

Процедура Лог_ВГрейлог(пСткНастройкиЛога, пСтрТекст)
	Если ЗначениеЗаполнено(пСтрТекст) Тогда
		ЛогированиеОбщий.ОтправитьСообщениеЛога_Инфо(пСткНастройкиЛога.ВыводВГрейЛогЛоггер, пСткНастройкиЛога.ВыводВГрейЛогДопДанные, пСтрТекст);
	КонецЕсли;
КонецПроцедуры

Процедура НастроитьВыводВГрейЛог(пСткНастройкиЛога, пСткНастройкиВывода) 
	пСткНастройкиЛога.Вставить("ВыводВГрейЛогНастройкиВывода", пСткНастройкиВывода);
		
	СформироватьПараметрыДляВыводаВГрейЛог(пСткНастройкиЛога);
КонецПроцедуры 

Процедура СформироватьПараметрыДляВыводаВГрейЛог(пСткНастройкиЛога)
	Логгер = ЛогированиеОбщий.ИнициироватьЛоггер(пСткНастройкиЛога.ВыводВГрейЛогНастройкиВывода.Процесс, 
											   Новый Структура("log_mess", пСткНастройкиЛога.ВыводВГрейЛогНастройкиВывода.Процесс));
											   
	пСткНастройкиЛога.Вставить("ВыводВГрейЛогЛоггер", Логгер);
											   
	ОбновитьДопДанныеДляВыводаВГрейЛог(пСткНастройкиЛога, пСткНастройкиЛога.ВыводВГрейЛогНастройкиВывода.ДопДанные);
КонецПроцедуры

// Процедура - Обновляет доп. данные для вывода в грейлог
//                                                       
// При обновлении все старые пользовательсике доп. данные заменяются на новые.
// Исключение: служебные доп. данные, добавляемые автоматически (например, источник в поле "CreatedFrom") не изменяются.
//
// Параметры:
//  пСткНастройкиЛога - Структура - Структура настроек логирования.
//  пСткДопДанные	  - Структура - Структура с доп. данными для передачи в грейлог вместе с сообщением.
//									!!! Важно !!! Все ключи (имена полей) в данной структуре должны быть заданы в латинице !!!
//
Процедура ОбновитьДопДанныеДляВыводаВГрейЛог(пСткНастройкиЛога, пСткДопДанные) Экспорт 
	сткДанные = ПолучитьДопДанныеДляВыводаВГрейЛог(пСткНастройкиЛога, пСткДопДанные);
	
	пСткНастройкиЛога.Вставить("ВыводВГрейЛогДопДанные", сткДанные);
КонецПроцедуры

Функция ПолучитьДопДанныеДляВыводаВГрейЛог(пСткНастройкиЛога, пСткДопДанные)
	сткДанные = Новый Структура;

	// Доп. данные переносим в самом начале, чтобы их (а не они!), если что, перетерли служебные данные
	Для Каждого Элт Из пСткДопДанные Цикл
		сткДанные.Вставить(Элт.Ключ, Элт.Значение);
	КонецЦикла;
	
	сткДанные.Вставить("CreatedFrom", пСткНастройкиЛога.ИсточникВывода);
	
	Возврат сткДанные;
КонецФункции

// Метода задан для стандартизации, чтобы при появлении общих полей в настройках вывода
// не пришлось бы переписывать весь код формирования настроек.
Функция СтруктураНастроекВыводаБазовая()
	сткРез = Новый Структура;
	
	Возврат сткРез;
КонецФункции

Функция ТекущаяДатаДляЛога(пСткНастройкиЛога)
	//Если пСткНастройкиЛога.ПоНулевомуGMT Тогда
	//	Возврат ЭдоОбщийВызовСервера.ТекущаяДатаПоНулевомуGMT();
	//Иначе
		Возврат ОбщегоНазначенияВызовСервера.ТекущаяДатаСервер_();
	//КонецЕсли;
КонецФункции
