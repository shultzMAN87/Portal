
// Определяет принадлежность объекта метаданных к общему типу "Документ".
//
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных - объект, для которого необходимо определить принадлежность к документам.
// 
// Возвращаемое значение:
//   Булево - Истина, если объект является документом.
//
Функция ЭтоДокумент(ОбъектМетаданных) Экспорт
	
	Возврат Метаданные.Документы.Содержит(ОбъектМетаданных);
	
КонецФункции

// Запись Объекта/МассиваОбъектов(в Транзакции) в БД 
// КомментарииЖР - набор чего то со свойствами: Событие, Данные, Комментарий
// Выход: структура: Успех - булево, признак успешности записи, Ошибка - описание ошибки при неудаче записи
Функция ЗаписатьОбъектВБазу(ОбъектИлиМассив, ЧислоПопыток = 10, ШагОжидания = 5000, ЭтоПроведение = Ложь, ЭтоОтменаПроведения = Ложь, КомментарииЖР = Неопределено) Экспорт
	
	ВремяОжидания = 0;
	
	Если ТипЗнч(ОбъектИлиМассив) = Тип("Массив") Тогда
		
		ДанныеДляЗаписи = ОбъектИлиМассив;
		
	Иначе
		
		ДанныеДляЗаписи = Новый Массив;
		ДанныеДляЗаписи.Добавить(ОбъектИлиМассив);
		
	КонецЕсли;
	
	НомерПопытки = 0;
	
	Пока Истина Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Для Каждого ТекущийОбъект Из ДанныеДляЗаписи Цикл
				
				Если ЭтоПроведение И ЭтоДокумент(ТекущийОбъект.Метаданные()) Тогда
					ТекущийОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ИначеЕсли ЭтоОтменаПроведения И ЭтоДокумент(ТекущийОбъект.Метаданные()) Тогда	
					ТекущийОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				Иначе
					ТекущийОбъект.Записать();
				КонецЕсли;					
				
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			Прервать;
			
		Исключение
			
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			инфОбОшибке = ИнформацияОбОшибке();
			текстОшибки = инфОбОшибке.Описание + ?(инфОбОшибке.Причина = Неопределено, "", " по причине: " + инфОбОшибке.Причина.Описание);
			
			Если (Найти(текстОшибки, "Конфликт блокировок при выполнении транзакции") >  0) И (НомерПопытки < ЧислоПопыток) Тогда
				// При попадании в блокировку уже не пытаемся писать дальше
				//Если ОбменДанными.ПроверитьБлокировку() Тогда
				//	Возврат Новый Структура("Успех, Ошибка, ВремяОжидания", Ложь, "На момент записи на базу данных наложена блокировка", ВремяОжидания);
				//КонецЕсли;
				
				НомерПопытки = НомерПопытки + 1;
				
				НачалоОжидания = ТекущаяУниверсальнаяДатаВМиллисекундах();
				//ОбщегоНазначения.Пауза(ШагОжидания);
				ВремяОжидания = ВремяОжидания + ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоОжидания;
				
				Продолжить;
				
			КонецЕсли;                 
			
			ОписаниеОшибки = "Возникли проблемы при записи """ + СокрЛП(ТекущийОбъект) + """. " + ?(НомерПопытки > 0, "Количество попыток записи = " + НомерПопытки, "") + Символы.ПС + текстОшибки;
			
			Возврат Новый Структура("Успех, Ошибка, ВремяОжидания", Ложь, ОписаниеОшибки, ВремяОжидания);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Новый Структура("Успех, Ошибка, ВремяОжидания", Истина, Неопределено, ВремяОжидания);
	
КонецФункции

// Преобразует таблицу значений в массив структур.
// Может использоваться для передачи на клиент данных в том случае, если таблица
// значений содержит только такие значения, которые могут
// быть переданы с сервера на клиент.
//
// Полученный массив содержит структуры, каждая из которых повторяет
// структуру колонок таблицы значений.
//
// Не рекомендуется использовать для преобразования таблиц значений
// с большим количеством строк.
//
// Параметры:
//  ТаблицаЗначений - ТаблицаЗначений - исходная таблица значений.
//
// Возвращаемое значение:
//  Массив - коллекция строк таблицы в виде структур.
//
Функция ТаблицаЗначенийВМассив(ТаблицаЗначений) Экспорт
	
	Массив = Новый Массив();
	СтруктураСтрокой = "";
	НужнаЗапятая = Ложь;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Если НужнаЗапятая Тогда
			СтруктураСтрокой = СтруктураСтрокой + ",";
		КонецЕсли;
		СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
		НужнаЗапятая = Истина;
	КонецЦикла;
	Для Каждого Строка Из ТаблицаЗначений Цикл
		НоваяСтрока = Новый Структура(СтруктураСтрокой);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Массив.Добавить(НоваяСтрока);
	КонецЦикла;
	Возврат Массив;

КонецФункции

// Проверка того, что переданный тип является ссылочным типом данных.
// Для типа "Неопределено" возвращается Ложь.
//
// Параметры:
//  ПроверяемыйТип - Тип - для проверки на ссылочный тип данных.
//
// Возвращаемое значение:
//  Булево - Истина, если это ссылка.
//
Функция ЭтоСсылка(ПроверяемыйТип) Экспорт
	
	Возврат ПроверяемыйТип <> Тип("Неопределено") И ОписаниеТипаВсеСсылки().СодержитТип(ПроверяемыйТип);
	
КонецФункции

// Проверяет физическое наличие записи в информационной базе данных о переданном значении ссылки.
//
// Параметры:
//  ПроверяемаяСсылка - ЛюбаяСсылка - значение любой ссылки информационной базы данных.
// 
// Возвращаемое значение:
//  Булево - Истина, если существует.
//
Функция СсылкаСуществует(ПроверяемаяСсылка) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	[ИмяТаблицы]
	|ГДЕ
	|	Ссылка = &Ссылка
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ИмяТаблицы]", ИмяТаблицыПоСсылке(ПроверяемаяСсылка));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ПроверяемаяСсылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает полное имя объекта метаданных по переданному значению ссылки.
//
// Параметры:
//  Ссылка - ЛюбаяСсылка - объект, для которого необходимо получить имя таблицы ИБ.
// 
// Возвращаемое значение:
//  Строка - полное имя объекта метаданных для указанного объекта. Например: "Справочник.Номенклатура".
//
Функция ИмяТаблицыПоСсылке(Ссылка) Экспорт
	
	Возврат Ссылка.Метаданные().ПолноеИмя();
	
КонецФункции

Функция ОписаниеТипаВсеСсылки() Экспорт // Надо вынести в ПовтИсп
	
	Возврат Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(
		Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(
			Справочники.ТипВсеСсылки(),
			Документы.ТипВсеСсылки().Типы()),
			ПланыОбмена.ТипВсеСсылки().Типы()),
			Перечисления.ТипВсеСсылки().Типы()),
			ПланыВидовХарактеристик.ТипВсеСсылки().Типы()),
			ПланыСчетов.ТипВсеСсылки().Типы()),
			ПланыВидовРасчета.ТипВсеСсылки().Типы()),
			БизнесПроцессы.ТипВсеСсылки().Типы()),
			БизнесПроцессы.ТипВсеСсылкиТочекМаршрутаБизнесПроцессов().Типы()),
			Задачи.ТипВсеСсылки().Типы());
	
КонецФункции
		
// Проверяет, что переданное имя ИмяПроцедуры является именем экспортной процедуры конфигурации.
// Может использоваться для проверки, что переданная строка не содержит произвольного алгоритма
// на встроенном языке 1С:Предприятия перед использованием его в операторах Выполнить и Вычислить
// при их использовании для динамического вызова методов код конфигурации.
//
// В случае если переданная строка не является именем процедуры конфигурации, генерируется исключение.
//
// Предназначена для вызова из см. процедуру ВыполнитьМетодКонфигурации.
//
// Параметры:
//   ИмяПроцедуры - Строка - проверяемое имя экспортной процедуры.
//
Процедура ПроверитьИмяПроцедурыКонфигурации(Знач ИмяПроцедуры) Экспорт 
	
	ЧастиИмени = СтрРазделить(ИмяПроцедуры, ".");
	Если ЧастиИмени.Количество() <> 2 И ЧастиИмени.Количество() <> 3 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неправильный формат параметра ИмяПроцедуры (передано значение: ""%1"") в ОбщегоНазначения.ВыполнитьМетодКонфигурации'"), ИмяПроцедуры);
	КонецЕсли;
	
	ИмяОбъекта = ЧастиИмени[0];
	Если ЧастиИмени.Количество() = 2 И Метаданные.ОбщиеМодули.Найти(ИмяОбъекта) = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неправильный формат параметра ИмяПроцедуры (передано значение: ""%1"") в ОбщегоНазначения.ВыполнитьМетодКонфигурации:
				|Не найден общий модуль ""%2"".'"),
			ИмяПроцедуры,
			ИмяОбъекта);
	КонецЕсли;
	
	Если ЧастиИмени.Количество() = 3 Тогда
		ПолноеИмяОбъекта = ЧастиИмени[0] + "." + ЧастиИмени[1];
		Попытка
			Менеджер = МенеджерОбъектаПоИмени(ПолноеИмяОбъекта);
		Исключение
			Менеджер = Неопределено;
		КонецПопытки;
		Если Менеджер = Неопределено Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Неправильный формат параметра ИмяПроцедуры (передано значение: ""%1"") в ОбщегоНазначения.ВыполнитьМетодКонфигурации:
				           |Не найден менеджер объекта ""%2"".'"),
				ИмяПроцедуры,
				ПолноеИмяОбъекта);
		КонецЕсли;
	КонецЕсли;
	
	ИмяМетодаОбъекта = ЧастиИмени[ЧастиИмени.ВГраница()];
	ВременнаяСтруктура = Новый Структура;
	Попытка
		// Проверка того, что ИмяПроцедуры является допустимым идентификатором.
		// Например: МояПроцедура.
		ВременнаяСтруктура.Вставить(ИмяМетодаОбъекта);
	Исключение
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неправильный формат параметра ИмяПроцедуры (передано значение: ""%1"") в ОбщегоНазначения.ВыполнитьМетодКонфигурации:
			           |Имя метода ""%2"" не соответствует требованиям образования имен процедур и функций.'"),
			ИмяПроцедуры, ИмяМетодаОбъекта);
	КонецПопытки;
	
КонецПроцедуры

// Возвращает менеджер объекта по имени.
// Ограничение: не обрабатываются точки маршрутов бизнес-процессов.
//
// Параметры:
//  Имя - Строка - имя например, "Справочник", "Справочники", "Справочник.Организации".
//
// Возвращаемое значение:
//  СправочникиМенеджер, СправочникМенеджер, ДокументыМенеджер, ДокументМенеджер, ...
// 
Функция МенеджерОбъектаПоИмени(Имя)
	Перем КлассОМ, ИмяОМ, Менеджер;
	
	ЧастиИмени = СтрРазделить(Имя, ".");
	
	Если ЧастиИмени.Количество() > 0 Тогда
		КлассОМ = ВРег(ЧастиИмени[0]);
	КонецЕсли;
	
	Если ЧастиИмени.Количество() > 1 Тогда
		ИмяОМ = ЧастиИмени[1];
	КонецЕсли;
	
	Если      КлассОМ = "ПЛАНОБМЕНА"
	 Или      КлассОМ = "ПЛАНЫОБМЕНА" Тогда
		Менеджер = ПланыОбмена;
		
	ИначеЕсли КлассОМ = "СПРАВОЧНИК"
	      Или КлассОМ = "СПРАВОЧНИКИ" Тогда
		Менеджер = Справочники;
		
	ИначеЕсли КлассОМ = "ДОКУМЕНТ"
	      Или КлассОМ = "ДОКУМЕНТЫ" Тогда
		Менеджер = Документы;
		
	ИначеЕсли КлассОМ = "ЖУРНАЛДОКУМЕНТОВ"
	      Или КлассОМ = "ЖУРНАЛЫДОКУМЕНТОВ" Тогда
		Менеджер = ЖурналыДокументов;
		
	ИначеЕсли КлассОМ = "ПЕРЕЧИСЛЕНИЕ"
	      Или КлассОМ = "ПЕРЕЧИСЛЕНИЯ" Тогда
		Менеджер = Перечисления;
		
	ИначеЕсли КлассОМ = "ОБЩИЙМОДУЛЬ"
	      Или КлассОМ = "ОБЩИЕМОДУЛИ" Тогда
		
		Возврат ОбщийМодуль(ИмяОМ);
		
	ИначеЕсли КлассОМ = "ОТЧЕТ"
	      Или КлассОМ = "ОТЧЕТЫ" Тогда
		Менеджер = Отчеты;
		
	ИначеЕсли КлассОМ = "ОБРАБОТКА"
	      Или КлассОМ = "ОБРАБОТКИ" Тогда
		Менеджер = Обработки;
		
	ИначеЕсли КлассОМ = "ПЛАНВИДОВХАРАКТЕРИСТИК"
	      Или КлассОМ = "ПЛАНЫВИДОВХАРАКТЕРИСТИК" Тогда
		Менеджер = ПланыВидовХарактеристик;
		
	ИначеЕсли КлассОМ = "ПЛАНСЧЕТОВ"
	      Или КлассОМ = "ПЛАНЫСЧЕТОВ" Тогда
		Менеджер = ПланыСчетов;
		
	ИначеЕсли КлассОМ = "ПЛАНВИДОВРАСЧЕТА"
	      Или КлассОМ = "ПЛАНЫВИДОВРАСЧЕТА" Тогда
		Менеджер = ПланыВидовРасчета;
		
	ИначеЕсли КлассОМ = "РЕГИСТРСВЕДЕНИЙ"
	      Или КлассОМ = "РЕГИСТРЫСВЕДЕНИЙ" Тогда
		Менеджер = РегистрыСведений;
		
	ИначеЕсли КлассОМ = "РЕГИСТРНАКОПЛЕНИЯ"
	      Или КлассОМ = "РЕГИСТРЫНАКОПЛЕНИЯ" Тогда
		Менеджер = РегистрыНакопления;
		
	ИначеЕсли КлассОМ = "РЕГИСТРБУХГАЛТЕРИИ"
	      Или КлассОМ = "РЕГИСТРЫБУХГАЛТЕРИИ" Тогда
		Менеджер = РегистрыБухгалтерии;
		
	ИначеЕсли КлассОМ = "РЕГИСТРРАСЧЕТА"
	      Или КлассОМ = "РЕГИСТРЫРАСЧЕТА" Тогда
		
		Если ЧастиИмени.Количество() < 3 Тогда
			// Регистр расчета
			Менеджер = РегистрыРасчета;
		Иначе
			КлассПодчиненногоОМ = ВРег(ЧастиИмени[2]);
			Если ЧастиИмени.Количество() > 3 Тогда
				ИмяПодчиненногоОМ = ЧастиИмени[3];
			КонецЕсли;
			Если КлассПодчиненногоОМ = "ПЕРЕРАСЧЕТ"
			 Или КлассПодчиненногоОМ = "ПЕРЕРАСЧЕТЫ" Тогда
				// Перерасчет
				Попытка
					Менеджер = РегистрыРасчета[ИмяОМ].Перерасчеты;
					ИмяОМ = ИмяПодчиненногоОМ;
				Исключение
					Менеджер = Неопределено;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли КлассОМ = "БИЗНЕСПРОЦЕСС"
	      Или КлассОМ = "БИЗНЕСПРОЦЕССЫ" Тогда
		Менеджер = БизнесПроцессы;
		
	ИначеЕсли КлассОМ = "ЗАДАЧА"
	      Или КлассОМ = "ЗАДАЧИ" Тогда
		Менеджер = Задачи;
		
	ИначеЕсли КлассОМ = "КОНСТАНТА"
	      Или КлассОМ = "КОНСТАНТЫ" Тогда
		Менеджер = Константы;
		
	ИначеЕсли КлассОМ = "ПОСЛЕДОВАТЕЛЬНОСТЬ"
	      Или КлассОМ = "ПОСЛЕДОВАТЕЛЬНОСТИ" Тогда
		Менеджер = Последовательности;
	КонецЕсли;
	
	Если Менеджер <> Неопределено Тогда
		Если ЗначениеЗаполнено(ИмяОМ) Тогда
			Попытка
				Возврат Менеджер[ИмяОМ];
			Исключение
				Менеджер = Неопределено;
			КонецПопытки;
		Иначе
			Возврат Менеджер;
		КонецЕсли;
	КонецЕсли;
	
	ВызватьИсключение СтрШаблон(НСтр("ru = 'Не удалось получить менеджер для объекта ""%1""'"), Имя);
	
КонецФункции

// Возвращает ссылку на общий модуль или модуль менеджера по имени.
//
// Параметры:
//  Имя - Строка - имя общего модуля.
//
// Возвращаемое значение:
//  ОбщийМодуль, МодульМенеджераОбъекта - общий модуль.
//
// Пример:
//	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбновлениеКонфигурации") Тогда
//		МодульОбновлениеКонфигурации = ОбщегоНазначения.ОбщийМодуль("ОбновлениеКонфигурации");
//		МодульОбновлениеКонфигурации.<Имя метода>();
//	КонецЕсли;
//
//	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолнотекстовыйПоиск") Тогда
//		МодульПолнотекстовыйПоискСервер = ОбщегоНазначения.ОбщийМодуль("ПолнотекстовыйПоискСервер");
//		МодульПолнотекстовыйПоискСервер.<Имя метода>();
//	КонецЕсли;
//
Функция ОбщийМодуль(Имя) Экспорт
	
	Если Метаданные.ОбщиеМодули.Найти(Имя) <> Неопределено Тогда
		// ВычислитьВБезопасномРежиме не используется, чтобы избежать вызова ОбщийМодуль рекурсивно.
		УстановитьБезопасныйРежим(Истина);
		Модуль = Вычислить(Имя);
	ИначеЕсли СтрЧислоВхождений(Имя, ".") = 1 Тогда
		Возврат СерверныйМодульМенеджера(Имя);
	Иначе
		Модуль = Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Модуль) <> Тип("ОбщийМодуль") Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Общий модуль ""%1"" не найден.'"), Имя);
	КонецЕсли;
	
	Возврат Модуль;
	
КонецФункции

// Возвращает серверный модуль менеджера по имени объекта.
Функция СерверныйМодульМенеджера(Имя)
	ОбъектНайден = Ложь;
	
	ЧастиИмени = СтрРазделить(Имя, ".");
	Если ЧастиИмени.Количество() = 2 Тогда
		
		ИмяВида = ВРег(ЧастиИмени[0]);
		ИмяОбъекта = ЧастиИмени[1];
		
		Если ИмяВида = ВРег("Константы") Тогда
			Если Метаданные.Константы.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("РегистрыСведений") Тогда
			Если Метаданные.РегистрыСведений.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("РегистрыНакопления") Тогда
			Если Метаданные.РегистрыНакопления.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("РегистрыБухгалтерии") Тогда
			Если Метаданные.РегистрыБухгалтерии.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("РегистрыРасчета") Тогда
			Если Метаданные.РегистрыРасчета.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("Справочники") Тогда
			Если Метаданные.Справочники.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("Документы") Тогда
			Если Метаданные.Документы.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("Отчеты") Тогда
			Если Метаданные.Отчеты.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("Обработки") Тогда
			Если Метаданные.Обработки.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("БизнесПроцессы") Тогда
			Если Метаданные.БизнесПроцессы.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("ЖурналыДокументов") Тогда
			Если Метаданные.ЖурналыДокументов.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("Задачи") Тогда
			Если Метаданные.Задачи.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("ПланыСчетов") Тогда
			Если Метаданные.ПланыСчетов.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("ПланыОбмена") Тогда
			Если Метаданные.ПланыОбмена.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("ПланыВидовХарактеристик") Тогда
			Если Метаданные.ПланыВидовХарактеристик.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		ИначеЕсли ИмяВида = ВРег("ПланыВидовРасчета") Тогда
			Если Метаданные.ПланыВидовРасчета.Найти(ИмяОбъекта) <> Неопределено Тогда
				ОбъектНайден = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ОбъектНайден Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Объект метаданных ""%1"" не найден,
			           |либо для него не поддерживается получение модуля менеджера.'"),
			Имя);
	КонецЕсли;
	
	// ВычислитьВБезопасномРежиме не используется, чтобы избежать вызова ОбщийМодуль рекурсивно.
	УстановитьБезопасныйРежим(Истина);
	Модуль = Вычислить(Имя);
	
	Возврат Модуль;
	
КонецФункции

Процедура ВыполнитьВБезопасномРежиме(Знач Алгоритм, Знач Параметры = Неопределено,
		Знач ПараметрыПроверкиАлгоритма = Неопределено) Экспорт
	
	//ПроверитьАлгоритм(Алгоритм, ПараметрыПроверкиАлгоритма);
	
	УстановитьБезопасныйРежим(Истина);
	
	//Если ПодсистемаСуществует("ТехнологияСервиса.БазоваяФункциональность") Тогда
	//	МодульРаботаВМоделиСервиса = ОбщийМодуль("РаботаВМоделиСервиса");
	//	МассивРазделителей = МодульРаботаВМоделиСервиса.РазделителиКонфигурации();
	//Иначе
		МассивРазделителей = Новый Массив;
	//КонецЕсли;
	
	Для Каждого ИмяРазделителя Из МассивРазделителей Цикл
		УстановитьБезопасныйРежимРазделенияДанных(ИмяРазделителя, Истина);
	КонецЦикла;
	
	Попытка
		Если ТранзакцияАктивна() Тогда
			Выполнить Алгоритм; // АПК:487 Код выполняется в безопасном режиме.
		Иначе
			НачатьТранзакцию(); // АПК:326 принудительная отмена транзакции
			Попытка
				Выполнить Алгоритм; // АПК:487 Код выполняется в безопасном режиме.
				ОтменитьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Уточнение = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(ИнформацияОбОшибке);
		ВызватьИсключение(Уточнение.Текст, КатегорияОшибки.ОшибкаВнешнегоИсточникаДанных ,,, ИнформацияОбОшибке);
	КонецПопытки;

КонецПроцедуры

#Область УправляемыеФормы

// Добавляет элемент условного оформления в форму.
//
// Параметры:
//   ФормаИлиСписок - ФормаКлиентскогоПриложения, ДинамическийСписок - Форма или список, 
//       в который необходимо добавить условное оформление.
//   ИменаПолей - Строка - Имена элементов формы, разделенные запятыми, для которых применяется оформление.
//       Например: "Поле1, Поле2".
//       Если необходимо чтобы условное оформление применялось ко всем строкам таблицы, тогда:
//       - Для условного оформления управляемой формы в параметре "ИменаПолей" следует передать имя таблицы формы.
//       - Для условного оформления динамического списка в параметре "ИменаПолей" следует передать пустую строку.
//
// Возвращаемое значение:
//   ЭлементУсловногоОформленияКомпоновкиДанных - Добавленный элемент условного оформления формы или списка.
//
Функция ДобавитьУсловноеОформление(ФормаИлиСписок, ИменаПолей) Экспорт
	ЭлементУсловногоОформленияКД = ФормаИлиСписок.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформленияКД.Использование = Истина;
	
	Если ТипЗнч(ИменаПолей) = Тип("Строка") Тогда
		МассивИменПолей = СтрРазделить(ИменаПолей, ",", Ложь);
	Иначе
		МассивИменПолей = ИменаПолей;
	КонецЕсли;
	Для Каждого ИмяПоля Из МассивИменПолей Цикл
		ИмяПоля = СокрЛП(ИмяПоля);
		Если ИмяПоля <> "" Тогда
			ПолеКД = ЭлементУсловногоОформленияКД.Поля.Элементы.Добавить();
			ПолеКД.Использование = Истина;
			ПолеКД.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЭлементУсловногоОформленияКД;
КонецФункции

// Устанавливает параметр условного оформления.
//
// Параметры:
//   ОформлениеКД - ЭлементУсловногоОформленияКомпоновкиДанных - Элемент условного оформления формы.
//   ИмяПараметра - Строка - Имя параметра или параметр компоновки данных, значение которого нужно установить.
//       Имена параметров см. в синтакс-помощнике: "ОформлениеКомпоновкиДанных", блок "Описание".
//       Частотные параметры: "Видимость", "Доступность", "Отображать", "Текст", "ЦветТекста", "ОтметкаНезаполненного".
//   ЗначениеПараметра - Произвольный - Значение, которое нужно установить.
//       Типы значений см. в синтакс-помощнике: "ОформлениеКомпоновкиДанных", блок "Описание".
//
Процедура УстановитьПараметрУсловногоОформления(ОформлениеКД, ИмяПараметра, ЗначениеПараметра) Экспорт
	
	ОформлениеКД.Оформление.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра), ЗначениеПараметра);
	
КонецПроцедуры

// Процедура - Установить значения параметров условного оформления
//
// Параметры:
//  ЭлементУсловногоОформления	 - 	ЭлементУсловногоОформленияКомпоновкиДанных - Элемент условного оформления, для которого необходимо установить значения параметров.
//  ПараметрыОформления			 - 	Структура - Ключ структуры содержит наименование параметра, а значение структуры присваиваемое значение.
//
Процедура УстановитьЗначенияПараметровУсловногоОформления(ЭлементУсловногоОформления, Знач ПараметрыОформления) Экспорт

	Если ТипЗнч(ЭлементУсловногоОформления) = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") И ТипЗнч(ПараметрыОформления) = Тип("Структура") Тогда
		Для Каждого Параметр Из ПараметрыОформления Цикл
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(Параметр.Ключ, Параметр.Значение);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// Процедура - Добавить элемент отбора условного оформления
//
// Параметры:
//  ЭлементУсловногоОформления	 - 	ЭлементУсловногоОформленияКомпоновкиДанных - Элемент условного оформления, в который необходимо добавить элемент отбора.
//  ЛевоеЗначение				 - 	Строка - Значение, устанавливаемое в левое поле элемента отбора для сравнения.
//  ВидСравнения				 - 	Строка - Используемый вид сравнения.
//  ПравоеЗначение				 - 	Произвольный - Значение для сравнения.
//
Процедура ДобавитьЭлементОтбораУсловногоОформления(ЭлементУсловногоОформления, Знач ЛевоеЗначение, Знач ВидСравнения = "Равно", Знач ПравоеЗначение = Истина) Экспорт
	
	Если ТипЗнч(ЭлементУсловногоОформления) = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ИначеЕсли ТипЗнч(ЭлементУсловногоОформления) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		ЭлементОтбора = ЭлементУсловногоОформления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Иначе
		Возврат;
	КонецЕсли;
	
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ЛевоеЗначение);
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных[ВидСравнения];
	ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
	ЭлементОтбора.Использование = Истина;
	
КонецПроцедуры

// Процедура - Добавить поле условного оформления
//
// Параметры:
//  ЭлементУсловногоОформления	 - 	ЭлементУсловногоОформленияКомпоновкиДанных - Элемент условного оформления, в который необходимо добавить оформляемые поля.
//  Поля						 - 	Строка - Наименование оформляемых полей, в качестве разделителя используется ", ".
//
Процедура ДобавитьПолеУсловногоОформления(ЭлементУсловногоОформления, Знач Поля) Экспорт

	Если ТипЗнч(ЭлементУсловногоОформления) = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") И ЗначениеЗаполнено(Поля) Тогда
		МассивПолей = СтрРазделить(Поля, ", ", Ложь);
		Для Каждого Поле Из МассивПолей Цикл
			ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
			ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Поле);
			ПолеОформления.Использование = Истина;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры 

#КонецОбласти