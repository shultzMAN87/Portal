
#Область ПрограммныйИнтерфейс

Функция СписокКонтактныхЛицАукциона(пАукцион) Экспорт

	Возврат Справочники.Аук_Аукционы.КонтактныеЛицаАукциона(пАукцион);
	
КонецФункции	

Функция СписокЭтаповПоУмолчаниюПоВидуАукциона(пВидАукциона) Экспорт

	Возврат Справочники.Аук_ВидыАукционов.СписокЭтаповПоУмолчаниюПоВидуАукциона(пВидАукциона);
	
КонецФункции	

Функция КонтактныеЛицаУчастникаАукциона(пАукцион, пУчастник) Экспорт

	Возврат Справочники.Аук_Аукционы.КонтактныеЛицаУчастникаАукциона(пАукцион, пУчастник);
	
КонецФункции	

Функция ТехническоеНаименованиеСоответствуетТребованиям(пТекущийОбъект, пИмяОбъектаМД) Экспорт
	
	Если Не СтрПодобнаПоРегулярномуВыражению(пТекущийОбъект.ТехническоеНаименование, Аук_ОбщийКлиентСервер.Шаблон_ТехническоеНаименованиеЭлементовПВХ()) Тогда
		
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Техническое наименование содержит недопустимые символы'"));
		
	КонецЕсли; 
	
	Если ПланыВидовХарактеристик[пИмяОбъектаМД].СуществуетЭлементСТехническимНаименованием(пТекущийОбъект) Тогда
		
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Существует элемент с таким техническим наименованием'"));
		
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();
	
КонецФункции

Функция НаименованиеСоответствуетТребованиям(пТекущийОбъект, пИмяОбъектаМД) Экспорт
	
	Если Не СтрПодобнаПоРегулярномуВыражению(пТекущийОбъект.Наименование, Аук_ОбщийКлиентСервер.Шаблон_НаименованиеЭлементовПВХ()) Тогда
		
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Наименование содержит недопустимые символы'"));
		
	КонецЕсли; 
	
	Если ПланыВидовХарактеристик[пИмяОбъектаМД].СуществуетЭлементСНаименованием(пТекущийОбъект) Тогда
		
		Возврат ЭдоОбщий._Ошибка(НСтр("ru = 'Существует элемент с таким наименованием'"));
		
	КонецЕсли;
	
	Возврат ЭдоОбщий._Успех();
	
КонецФункции

Функция СписокПредставленийПоМассивуЭлементов(пЭлементыДанных, пОтборПоТипу = Неопределено) Экспорт

	Возврат ПланыВидовХарактеристик.Аук_ЭлементыДанныхАукционов.СписокПредставленийПоМассивуЭлементов(пЭлементыДанных, пОтборПоТипу);	
	
КонецФункции

Функция ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона) Экспорт

	Возврат Справочники.Аук_ВидыАукционов.ОписаниеСтруктурыСтавкиПоВидуАукциона(пВидАукциона);
	
КонецФункции	

Функция КонтактныеЛицаУчастникаПоВидуАукциона(пУчастник, пВидАукциона) Экспорт

	Возврат Справочники.Аук_УчастникиАукционов.КонтактныеЛицаУчастникаПоВидуАукциона(пУчастник, пВидАукциона);
	
КонецФункции	

Функция КлючевыеДанныеАукциона(пАукцион) Экспорт 
	
	Возврат Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион);
	
КонецФункции

Функция КлючевойПараметрАукциона(пАукцион, пПараметр) Экспорт 
	
	Возврат Справочники.Аук_Аукционы.КлючевыеДанныеАукциона(пАукцион)[пПараметр];
	
КонецФункции

Функция ВыполнитьКонтрольКритическиВажныхНастроек(пДанныеАукциона) Экспорт
	Возврат Аук_ОбщийСервер.ВыполнитьКонтрольДанныхАукциона(пДанныеАукциона);	
КонецФункции

Функция ДанныеАукционаДляПроверки(пАукцион) Экспорт
	Возврат Справочники.Аук_Аукционы.ДанныеАукционаДляПроверки(пАукцион);	
КонецФункции

Функция УчастникиАукционаИИхКонтактныеЛицаПоВидуАукциона(пВидАукциона) Экспорт
	Возврат Справочники.Аук_УчастникиАукционов.УчастникиАукционаИИхКонтактныеЛицаПоВидуАукциона(пВидАукциона);	
КонецФункции

Функция ВидыАукционовСписком() Экспорт
	Возврат Справочники.Аук_ВидыАукционов.ВидыАукционовСписком();	
КонецФункции	

Функция ЗначенияПараметровШаблонаПоСтрокеШаблона(пСтрШаблон, пСткПараметры) Экспорт	
	масПараметрыШаблона = СтрокаНайтиВсеПоРегулярномуВыражениюТолькоЗначения(пСтрШаблон, Аук_ОбщийКлиентСервер.Шаблон_ПараметрыШаблонаСообщения(),,, Истина);
	
	Возврат Аук_ОбщийСервер.ЗначенияПараметровШаблонов(масПараметрыШаблона, пСткПараметры);
КонецФункции	

#КонецОбласти

#Область ИнтерактивныеКомандыПользователей

// Отправка сообщений участникам. Происходит создание команд и их выполнение
//
// Параметры:
//  пСткПараметры
//		*КонтактныеЛица	- Массив - элементы справочника Аук_КонтактныеЛицаУчастниковАукционов
//		*Аукцион		- Справочник.Аук_Аукционы 
//		*ТекстСообщения - Строка - отсылаемое сообщение
//		*Автоматическая - Булево - признак интеративной отправки
//		*ВидСообщения	- Перечисление.Аук_ВидыСообщенийУчастникам
//
Процедура ОтправитьСообщениеУчастникам(пСткПараметры) Экспорт
	
	// 1. Получить данные рассылки
	масСообщенияУчастникам = Аук_УправлениеАукционамиСервер.ОтправкаПростогоСообщенияУчастникам_ДанныеРассылки(пСткПараметры);
	
	пСткПараметры.Вставить("СообщенияУчастникам", масСообщенияУчастникам);
	пСткПараметры.Вставить("НомерЭтапа", 		  РегистрыСведений.Аук_ИсторияСтатусовАукционов.ТекущееСостояниеАукциона(пСткПараметры.Аукцион).НомерЭтапа);
	
	// 2. Создание команд
	Попытка
		Аук_УправлениеАукционамиСервер.ОтправитьСообщенияУчастникам_СозданиеКоманд_(пСткПараметры);	
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команды на отправку не были сформированы. Причина: %1'"), ОписаниеОшибки());
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат;	
	КонецПопытки;	
	
	// 3. Выполнить команды
	Если пСткПараметры.Свойство("КлючиКоманд") И ЗначениеЗаполнено(пСткПараметры.КлючиКоманд) Тогда
		
		масДанныеКоманд = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеКомандПоКлючам(пСткПараметры.КлючиКоманд);
		
		Для Каждого ЭлДанныеКоманды Из масДанныеКоманд Цикл
			сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(ЭлДанныеКоманды);
			
			Если Не сткРез.Успех Тогда	
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Для контактного лица <%1> создана команда отправки сообщения, но не была выполнена. Причина: %2'"),
													ЭлДанныеКоманды.КонтактноеЛицо, сткРез.Результат);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);	
			КонецЕсли;	
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

// Получение данных от участников (сообщения, ставки, отказы)
//
// Параметры:
//  пСткПараметры
//	  Обязательные:
//		*Аукцион			- Справочник.Аук_Аукционы 
//		*КонтактноеЛицо		- Справочник.Аук_КонтактныеЛицаУчастниковАукционов 
//		*Участник			- Справочник.Аук_УчастникиАукционов
//		*Данные 			- Произвольно. Может быть текст (отказ, сообщение) или массив структур (ставка)
//		*Автоматическая 	- Булево - признак интеративной отправки
//		*ВидДанных 			- Перечисление.Аук_ВидыДанных
//		*ТребуетсяПодтверждение 	- Булево - признак необходимости оповещать участника о приеме от него данных
//	  Опциональные:
//		*ВидСообщения		- Перечисление.Аук_ВидыСообщенийУчастникам - вид сообщения, которое необходимо отправить участнику (обязательно если ТребуетсяПодтверждение = Истина) 
//
Процедура ПолучитьДанныеОтУчастника(пСткПараметры) Экспорт
		
	// 1. Создание команды
	Попытка
		Аук_УправлениеАукционамиСервер.ПолучениеДанныхОтУчастника_СозданиеКоманды(пСткПараметры);	
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Данные от контактного лица <%1> не были зафиксированы. Причина: %2'"),
										пСткПараметры.КонтактноеЛицо, ОписаниеОшибки());
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);	
	КонецПопытки;
	
	// 2. Выполнение команды по ключу
	Если пСткПараметры.Свойство("КлючиКоманд") И ЗначениеЗаполнено(пСткПараметры.КлючиКоманд) Тогда	
		масДанныеКоманд = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеКомандПоКлючам(пСткПараметры.КлючиКоманд);
		
		Для Каждого ЭлДанныеКоманды Из масДанныеКоманд Цикл
			сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(ЭлДанныеКоманды);
			
			Если Не сткРез.Успех Тогда	
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Для контактного лица <%1> создана команда ввода данных, но не была выполнена. Причина: %2'"),
											пСткПараметры.КонтактноеЛицо, сткРез.Результат);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				
				Возврат;
			КонецЕсли;	
		КонецЦикла;	
	Иначе	
		Возврат;
	КонецЕсли;	
	
	// 3. Выполнить оповещение участника о приеме данных (только для ставки и отказа). Если нужно
	Если пСткПараметры.ТребуетсяПодтверждение Тогда
		масДанныеКоманд = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеДочернихКоманд(пСткПараметры.КлючиКоманд);
		
		Для Каждого ЭлДанныеКоманды Из масДанныеКоманд Цикл
			сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(ЭлДанныеКоманды);
			
			Если Не сткРез.Успех Тогда	
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Входящие данные контактного лица <%1> зафиксированы, но не выполнено оповещение. Причина: %2'"),
										пСткПараметры.КонтактноеЛицо, сткРез.Результат);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

// Перевод аукциона в работу. Происходит создание команд и их выполнение. Требуется возврат состояния
//
// Параметры:
//  пСткПараметры
//		*Аукцион		- Справочник.Аук_Аукционы
//		*Автоматическая - Булево - признак интеративной отправки
//
Функция ПеревестиАукционВРаботу(пСткПараметры) Экспорт	
	// 1. Создание команды
	Попытка
		Аук_УправлениеАукционамиСервер.НачатьАукцион_СозданиеКоманды(пСткПараметры);
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда начала аукциона не была зафиксирована. Причина: %1'"), ОписаниеОшибки());
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	КонецПопытки;
	
	// Такого вообще быть не должно, поэтому жестко кину исключение
	Если Не пСткПараметры.Свойство("КлючКоманды") Или Не ЗначениеЗаполнено(пСткПараметры.КлючКоманды) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка ключа команды'");	
	КонецЕсли;	
	
	// 2. Выполнение команды по ключу	
	сткДанныеКоманды = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеКомандПоКлючам(пСткПараметры.КлючКоманды);
	
	сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(сткДанныеКоманды);
	
	Если Не сткРез.Успех Тогда	
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда начала аукциона была сформирована, но не была выполнена. Причина: %1'"), сткРез.Результат);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	КонецЕсли;	
	
	// 3. Выполнить оповещение участников. Ошибки в оповещениях не должны тормозить аукцион!?
	масДанныеКоманд = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеДочернихКоманд(пСткПараметры.КлючКоманды);
	
	Для Каждого ЭлДанныеКоманды Из масДанныеКоманд Цикл
		сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(ЭлДанныеКоманды);
		
		Если Не сткРез.Успех Тогда	
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Аукцион начался. Контактное лицо <%1> не было проинформирвоано. Причина: %2'"),
										ЭлДанныеКоманды.КонтактноеЛицо, сткРез.Результат);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

// Перевод аукциона в архив. Происходит создание команд и их выполнение. Требуется возврат состояния
//
// Параметры:
//  пСткПараметры
//		*Аукцион		- Справочник.Аук_Аукционы
//		*Автоматическая - Булево - признак интеративной отправки
//
Функция ПоместитьАукционВАрхив(пСткПараметры) Экспорт	
	// 1. Создание команды
	Попытка
		Аук_УправлениеАукционамиСервер.ПоместитьВАрхив_СозданиеКоманды(пСткПараметры);
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда начала аукциона не была зафиксирована. Причина: %1'"), ОписаниеОшибки());
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	КонецПопытки;
	
	// Такого вообще быть не должно, поэтому жестко кину исключение
	Если Не пСткПараметры.Свойство("КлючКоманды") Или Не ЗначениеЗаполнено(пСткПараметры.КлючКоманды) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка ключа команды'");	
	КонецЕсли;	
	
	// 2. Выполнение команды по ключу	
	сткДанныеКоманды = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеКомандПоКлючам(пСткПараметры.КлючКоманды);
	
	сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(сткДанныеКоманды);
	
	Если Не сткРез.Успех Тогда	
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда начала аукциона была сформирована, но не была выполнена. Причина: %1'"), сткРез.Результат);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Перевод аукциона на новый этап. Происходит создание команд и их выполнение. Требуется возврат состояния
//
// Параметры:
//  пСткПараметры
//		*Аукцион		- Справочник.Аук_Аукционы
//		*Автоматическая - Булево - признак интеративной отправки
//
Функция ПеревестиАукционНаСледующийЭтап(пСткПараметры) Экспорт	
	// 1. Создание команды
	Попытка
		Аук_УправлениеАукционамиСервер.ПеревестиНаСледующийЭтап_СозданиеКоманды(пСткПараметры);
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда перевода на новый этап не была зафиксирована. Причина: %1'"), ОписаниеОшибки());
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	КонецПопытки;
	
	// Такого вообще быть не должно, поэтому жестко кину исключение
	Если Не пСткПараметры.Свойство("КлючКоманды") Или Не ЗначениеЗаполнено(пСткПараметры.КлючКоманды) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка ключа команды'");	
	КонецЕсли;
	
	// 2. Выполнение команды по ключу	
	сткДанныеКоманды = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеКомандПоКлючам(пСткПараметры.КлючКоманды);
	
	сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(сткДанныеКоманды);
	
	Если Не сткРез.Успех Тогда	
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда перевода аукциона на новый этапа была сформирована, но не была выполнена. Причина: %1'"), сткРез.Результат);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	КонецЕсли;	
	
	// 3. Выполнить оповещение участников.Ошибки в оповещениях не должны тормозить аукцион!?
	масДанныеКоманд = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеДочернихКоманд(пСткПараметры.КлючКоманды);
	
	Для Каждого ЭлДанныеКоманды Из масДанныеКоманд Цикл
		сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(ЭлДанныеКоманды);
		
		Если Не сткРез.Успех Тогда	
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Входящие данные контактного лица <%1> зафиксированы, но не выполнено оповещение. Причина: %2'"),
											ЭлДанныеКоманды.КонтактноеЛицо, сткРез.Результат);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Истина;
КонецФункции	

// Фиксация промежуточных итогов аукциона. Требуется возврат состояния
//
// Параметры:
//  пСткПараметры
//	  Обязательные:
//		*Аукцион			- Справочник.Аук_Аукционы 
//		*КонтактноеЛицо		- Справочник.Аук_КонтактныеЛицаУчастниковАукционов 
//		*Участник			- Справочник.Аук_УчастникиАукционов
//		*ДанныеСтавки 		- Массив структур
//			*ЭлементДанных			- Строка - тех. наименование
//			*ЗначениеЭлементаДанных - Произвольно
//		*Автоматическая 	- Булево - признак интеративной отправки
//		*НомерЭтапа 		- Число
//		*РасчетПоФормуле 	- Число
//		*ИтоговоеЗначение 	- Строка
//
Функция ЗафиксироватьПромежуточныеИтогиАукциона(пСткПараметры) Экспорт	
	// 1. Создание команды
	Попытка
		Аук_УправлениеАукционамиСервер.ЗафиксироватьПромежуточныеИтогиАукциона_СозданиеКоманды(пСткПараметры);	
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда фиксации промежуточных итогов не создана. Причина: %1'"), ОписаниеОшибки());
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;	
	КонецПопытки;
	
	// Такого вообще быть не должно, поэтому жестко кину исключение
	Если Не пСткПараметры.Свойство("КлючКоманды") Или Не ЗначениеЗаполнено(пСткПараметры.КлючКоманды) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка ключа команды'");	
	КонецЕсли;
	
	// 2. Выполнение команды по ключу
	сткДанныеКоманды = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеКомандПоКлючам(пСткПараметры.КлючКоманды);
	
	сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(сткДанныеКоманды);
	
	Если Не сткРез.Успех Тогда	
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда фиксации промежуточных итогов была сформирована, но не была выполнена. Причина: %1'"), сткРез.Результат);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;	
	КонецЕсли;
	
	// 3. Выполнить оповещение участников
	масДанныеКоманд = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеДочернихКоманд(пСткПараметры.КлючКоманды);
	
	Для Каждого ЭлДанныеКоманды Из масДанныеКоманд Цикл	
		сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(ЭлДанныеКоманды);
		
		Если Не сткРез.Успех Тогда	
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Входящие данные контактного лица <%1> зафиксированы, но не выполнено оповещение. Причина: %2'"),
									ЭлДанныеКоманды.КонтактноеЛицо, сткРез.Результат);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);		
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат Истина;
КонецФункции

// Фиксация окончательных итогов аукциона.Требуется возврат состояния
//
// Параметры:
//  пСткПараметры
//	  Обязательные:
//		*Аукцион				- Справочник.Аук_Аукционы 
//		*Автоматическая 		- Булево - признак интеративной отправки
//		*НачальныеУсловия		- Структура
//			**ОбщаяСумма		- Число
//			**ОбщийСрок			- Число
//		*ИтогиАукциона			- Массив стуктур
//			**Участник			- Справочник.Аук_УчастникиАукционов
//			**СтрокаДанных		- Число
//			**РаспределяемаяСумма	- Число
//			**ЭтоПобедитель		- Булево
//
Функция ЗафиксироватьОкончательныеИтогиАукциона(пСткПараметры) Экспорт	
	// 1. Создание команды
	Попытка
		Аук_УправлениеАукционамиСервер.ЗавершитьАукцион_СозданиеКоманды(пСткПараметры);	
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда фиксации окончательных итогов не создана. Причина: %2'"), ОписаниеОшибки());
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;	
	КонецПопытки;
	
	// Такого вообще быть не должно, поэтому жестко кину исключение
	Если Не пСткПараметры.Свойство("КлючКоманды") Или Не ЗначениеЗаполнено(пСткПараметры.КлючКоманды) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка ключа команды'");	
	КонецЕсли;
	
	// 2. Выполнение команды по ключу
	сткДанныеКоманды = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеКомандПоКлючам(пСткПараметры.КлючКоманды);
	
	сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(сткДанныеКоманды);
	
	Если Не сткРез.Успех Тогда	
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Команда фиксации окончательных была сформирована, но не была выполнена. Причина: %1'"), сткРез.Результат);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;	
	КонецЕсли;
	
	// 3. Выполнить оповещение участников
	масДанныеКоманд = РегистрыСведений.Аук_ИсторияКомандАукционов.ДанныеДочернихКоманд(пСткПараметры.КлючКоманды);
	
	Для Каждого ЭлДанныеКоманды Из масДанныеКоманд Цикл	
		сткРез = Аук_УправлениеАукционамиСервер.ПроизвестиКонтрольДанныхИВыполнитьКоманду(ЭлДанныеКоманды);
		
		Если Не сткРез.Успех Тогда	
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Входящие данные контактного лица <%1> зафиксированы, но не выполнено оповещение. Причина: %2'"),
										ЭлДанныеКоманды.КонтактноеЛицо, сткРез.Результат);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);		
		КонецЕсли;	
	КонецЦикла;		
		
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область РаботаСРегулярнымиВыражениями

Функция СтрокаПодобнаРегулярномуВыражению(пСтрока, пРегулярноеВыражение, пИгнорироватьРегистр = Ложь, пМногострочныйПоиск = Ложь) Экспорт

	Возврат СтрПодобнаПоРегулярномуВыражению(пСтрока, пРегулярноеВыражение, пИгнорироватьРегистр, пМногострочныйПоиск); 
	
КонецФункции

Функция СтрокаЗаменитьПоРегулярномуВыражению(пСтрока, пРегулярноеВыражение, пПодстрокаЗамены, пИгнорироватьРегистр = Ложь, пМногострочныйПоиск = Ложь) Экспорт

	Возврат СтрЗаменитьПоРегулярномуВыражению(пСтрока, пРегулярноеВыражение, пПодстрокаЗамены, пИгнорироватьРегистр, пМногострочныйПоиск);   
	
КонецФункции

Функция СтрокаНайтиВсеПоРегулярномуВыражению(пСтрока, пРегулярноеВыражение, пИгнорироватьРегистр = Ложь, пМногострочныйПоиск = Ложь) Экспорт
	
	масРезультат = Новый Массив;
	
	масВсеВхождения =  СтрНайтиВсеПоРегулярномуВыражению(пСтрока, пРегулярноеВыражение, пИгнорироватьРегистр, пМногострочныйПоиск);  
		
	Для Каждого ЭлВхождение Из масВсеВхождения Цикл
		
		сткЭлВхождение = Новый Структура("Значение, НачальнаяПозиция");
		
		ЗаполнитьЗначенияСвойств(сткЭлВхождение, ЭлВхождение); 
		
		масРезультат.Добавить(сткЭлВхождение);
		
	КонецЦикла;	
	
	Возврат масРезультат;
	
КонецФункции

Функция СтрокаНайтиВсеПоРегулярномуВыражениюТолькоЗначения(пСтрока, пРегулярноеВыражение, пИгнорироватьРегистр = Ложь, пМногострочныйПоиск = Ложь, пБулТолькоУникальные = Ложь) Экспорт
	
	масРезультат = Новый Массив;
	
	масВсеВхождения =  СтрНайтиВсеПоРегулярномуВыражению(пСтрока, пРегулярноеВыражение, пИгнорироватьРегистр, пМногострочныйПоиск);  
	
	Для Каждого ЭлВхождение Из масВсеВхождения Цикл 	
		Если пБулТолькоУникальные Тогда
			Если масРезультат.Найти(ЭлВхождение.Значение) = Неопределено Тогда
				масРезультат.Добавить(ЭлВхождение.Значение);	
			КонецЕсли;	
		Иначе			
			масРезультат.Добавить(ЭлВхождение.Значение);
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат масРезультат;
	
КонецФункции

#КонецОбласти

#Область ПолучениеЭлементовНастроек

Функция ЭлементНастроек_Email() Экспорт

	Возврат ПланыВидовХарактеристик.Аук_ЭлементыНастроекКаналовВзаимодействия.ЭлементНастроекПоТехническомуНаименованию("EMail");
	
КонецФункции	

#КонецОбласти 

